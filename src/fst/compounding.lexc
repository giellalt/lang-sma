! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software ; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

! ==========================================================================
!!                    South Sámi morphological analyser
!! ==========================================================================


!! Prefixing and compounding
!! =========================

LEXICON Prefixes
!! ## Lexicon `@LEXNAME@`

!! It contains only one entry:
Noerhte- ProperNoun ; !!≈ - `@CODE@`

!ov Adjective ; this renders in ovovmessie


LEXICON R
!! ## Lexicon `@LEXNAME@`

!! This lexicon is the main entry for regular compounding.
!! All entries NOT requiring a hyphen should point to it. 

!! The whole content of it is a list of flag diacritics to control compounding.

< "@P.CmpFrst.FALSE@"
 "@P.CmpPref.FALSE@" 
 "@D.CmpLast.TRUE@"  
 "@D.CmpNone.TRUE@"  
 "@U.CmpNone.FALSE@" 
 "@P.CmpOnly.TRUE@" >

Rreal ; !!≈ After the flags, we continue to the `@CODE@` lexicon, for the real compounding action.

!! It should be noted that some of the flags above require a corresponding flag in the lexicon `ENDLEX` to work properly. 

LEXICON Rreal
!! ## Lexicon `@LEXNAME@`

!! This is where the actual compounding happens.

+Cmp#:∑#                  Noun       ; ! This is the normal case.
                 @P.NeedNoun.ON@+Cmp#:@P.NeedNoun.ON@∑#   Verb       ; ! Flag diacr declares that the compound
                 @P.NeedNoun.ON@+Cmp#:@P.NeedNoun.ON@∑#   Adjective  ; ! needs a N tag later in the derivation
                       +Cmp/Hyph+Cmp#:%-∑#                Noun       ; ! These 3 cases add a hyphen, so
        @P.NeedNoun.ON@+Cmp/Hyph+Cmp#:@P.NeedNoun.ON@%-∑# Verb       ; ! Sem-julggaštus and maana-gåetie
        @P.NeedNoun.ON@+Cmp/Hyph+Cmp#:@P.NeedNoun.ON@%-∑# Adjective  ; ! are allowed.

                       +Cmp/Hyph+Cmp#:%-∑#                HyphNouns  ; ! Go to nouns requiring hyphens, like -tv- and -cd-
                       +Cmp/Hyph+Cmp#:%-∑#                Acronym    ; ! maana-tv, "lomme-cd-spelar"
                       +Cmp/Hyph+Cmp#:%-∑#                Numeral    ; ! maana-tv, "lomme-cd-spelar"
                       +Cmp/Hyph+Cmp#:%-∑#                ProperNoun ; ! Proper nouns as the 2nd part of compounds for non-hyph.
                                                                       ! words. viessu-London goes through here.

 +Cmp/SplitR+Use/SpellNoSugg+Cmp:%-                 ENDLEX     ; ! This to take care of Oahpo- ja dutkandept

LEXICON RNum
      +Cmp/Hyph+Cmp#:-∑# Noun ; ! For Num Cmp Noun, vi vil ikke ha Num Cmp Num


! This is for compounding with hyphen. All entries REQUIRING a hyphen should point to this:
LEXICON RHyph ! Recursive lexicon from all classes REQUIRING a hyphen to follow.
              ! NBNBNB! ! ! The hyphen should be added here! NOT in the other lexicons!
@P.CmpFrst.FALSE@@P.CmpPref.FALSE@@D.CmpLast.TRUE@@D.CmpNone.TRUE@@U.CmpNone.FALSE@@P.CmpOnly.TRUE@ RHyphReal ; ! Block certain types of compounds in spellers.

LEXICON RHyphReal
                     +Cmp/Hyph+Cmp#:%-∑#                Noun       ; ! This is the normal case.
      @P.NeedNoun.ON@+Cmp/Hyph+Cmp#:@P.NeedNoun.ON@%-∑# Verb       ; ! Flag diacr declares that the compound
      @P.NeedNoun.ON@+Cmp/Hyph+Cmp#:@P.NeedNoun.ON@%-∑# Adjective  ; ! needs a N tag later in the derivation

                     +Cmp/Hyph+Cmp#:%-∑#                HyphNouns  ; ! Go to words requiring hyphens, like -tv- and -cd-
                     +Cmp/Hyph+Cmp#:%-∑#                Acronym    ; ! maana-tv, "lomme-cd-spelar"
                     +Cmp/Hyph+Cmp#:%-∑#                Numeral    ; ! maana-123
                     +Cmp/Hyph+Cmp#:%-∑#                ProperNoun ; ! Proper nouns as the 2nd part of compounds for hyph-words.
                                                                        ! London-Hull is covered here, whereas Hull-viessu
                                                                        ! is covered by RHyph + Noun.

 +Cmp/SplitR+Use/SpellNoSugg+Cmp:%-                   ENDLEX     ; ! This to take care of Oahpo- ja dutkandept

! Proper noun compounds without hyphens are NOT presently covered - if they were
! to be added, they need to be marked +Err/Orth. Examples: LondonHull & viessuLondon.
