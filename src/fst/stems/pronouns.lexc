! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no



!! # South Saami pronouns
!     ====================
!     ====================
!     ====================





LEXICON Pronoun
!! The **@LEXNAME@** lexicon points to all the subgrops, presented in this order below:

Reciprocal ;    !!= * @CODE@
Personal ;	    !!= * @CODE@
Demonstrative ; !!= * @CODE@
Interrogative ; !!= * @CODE@
Indefinite ;    !!= * @CODE@
Reflexive ;	    !!= * @CODE@





!! ## The Reciprocal pronoun
!    ----------------------
!    ----------------------
!    ----------------------


LEXICON Reciprocal
sinsitniem+Pron+Recipr+Sg+Acc:sinsitniem FINAL1 ;          !!= * @CODE@
sinsitniem+Pron+Recipr+Sg+Acc+Err/Orth:sinsitnem FINAL1 ;   !! * ...
sinsitniem+Pron+Recipr+Sg+Gen:sinsitnien FINAL1 ;
sinsitniem+Pron+Recipr+Sg+Ill:sinsætnan FINAL1 ;
sinsitniem+Pron+Recipr+Sg+Ela:sinsitneste FINAL1 ;
sinsitniem+Pron+Recipr+Sg+Com:sinsitnine FINAL1 ;

sinsitniem+Pron+Recipr+Pl+Acc:sinsitnide FINAL1 ; ! Not in MMM


!! ## Personal pronouns
!    -----------------
!    -----------------
!    -----------------


!! Splitting in 1st, 2nd, 3rd
!  - - - - - - - - - - - - -

LEXICON Personal

!! New lemma form, now number as baseform, due to Oahpa

manne+Pron+Pers+Sg1+Gen:mov # ; ! Added to fill the gap, remove this or previous
manne+Pron+Pers+Sg1+Gen+Err/Orth:muv # ;
manne+Pron+Pers+Sg1:m firstperspronsg ;
månnoeh+Pron+Pers+Du1:m firstpersprondu ;
mijjieh+Pron+Pers+Pl1:m perspronpl ;
datne+Pron+Pers+Sg2:d nonfirstperspronsg ;
dåtnoeh+Pron+Pers+Du2:d nonfirstpersprondu ;
dijjieh+Pron+Pers+Pl2:d perspronpl ;
satne+Pron+Logo+Sg3:s nonfirstperspronsg ;
såtnoeh+Pron+Logo+Du3:s nonfirstpersprondu ;
sijjieh+Pron+Logo+Pl3:s perspronpl ;
DIHTE ; ! this is for personal pronoun use of dïhte


!! ### Lexica for sg
!   - - - - - - -

LEXICON firstperspronsg
!! the **@LEXNAME@** for first pers has special consonantism
:anne longproncase ;
+Ill:unnjien FINAL1 ;
+Ill+Use/NG:unnjan FINAL1 ;

LEXICON nonfirstperspronsg
!! for **@LEXNAME@** the 2nd and 3rd are identical
:atne longproncase ;
+Gen:ov FINAL1 ;
+Gen+Err/Orth:uv FINAL1 ;
+Ill:utnjien FINAL1 ;
+Ill+Use/NG:utnjan FINAL1 ;


!! ### Lexica for du
!   - - - - - - -

LEXICON firstpersprondu  !!= * @CODE@
+Nom:ånnoeh FINAL1 ;
+Nom+Err/Orth:onnoeh firstpersprondu ;
+Gen:onnen FINAL1 ;
+Gen+Err/Orth:onnoej FINAL1 ;
+Ill:onnese FINAL1 ;
+Nom+Use/NG:onne FINAL1 ;
+Acc:onnem FINAL1 ;
+Ine:onnesne FINAL1 ;
+Ela:onneste FINAL1 ;
+Com:onnine FINAL1 ;

LEXICON nonfirstpersprondu  !!= * @CODE@
+Nom:åtnoeh FINAL1 ;
+Nom+Err/Orth:otnoeh FINAL1 ;
+Gen+Err/Orth:otnoej FINAL1 ;
+Gen:otnen FINAL1 ;
+Ill:otnese FINAL1 ;
+Nom:otne FINAL1 ;
+Acc:otnem FINAL1 ;
+Ine:otnesne FINAL1 ;
+Ela:otneste FINAL1 ;
+Com:otnine FINAL1 ;

!  sotnoeh+Pron+Logo+Du3+Err/Orth:s nonfirstpersprondu ;

!! ### Lexica for pl
! - - - - - - -

LEXICON perspronpl  !!= * @CODE@
+Nom:ijjieh FINAL1 ;
+Nom+Use/NG:ijje FINAL1 ;
+Nom+Err/Orth:iejjieh FINAL1 ;
+Acc:ijjem FINAL1 ;
+Acc+Use/NG:ijjiem FINAL1 ;
+Gen:ijjen FINAL1 ;
+Gen+Use/NG:ijjien FINAL1 ;
+Ill:ijjese FINAL1 ;
+Ill+Use/NG:ijjide FINAL1 ;
+Ine:ijjesne FINAL1 ;
+Ela:ijjeste FINAL1 ;
+Ela+Use/NG:ijjijste FINAL1 ;
+Com:ijjeX2ine FINAL1 ;


!! ### Common case lexica
! - - - - - - - - -


LEXICON longproncase  !!= * @CODE@
+Nom: FINAL1 ;
+Acc:m FINAL1 ;
+Ine:sne FINAL1 ;
+Ela:ste FINAL1 ;
+Com:X2ine FINAL1 ;


!! ### dïhte
LEXICON DIHTE
!! **@LEXNAME@**  is a personal pronoun,
!! demonstrative dïhte is treated below.
dïhte+Pron+Pers+Sg3+Nom:dïhte FINAL1 ;
dïhte+Pron+Pers+Sg3+Acc:dam FINAL1 ;
dïhte+Pron+Pers+Sg3+Acc+Use/NG:altemse FINAL1 ;
dïhte+Pron+Pers+Sg3+Gen:dan FINAL1 ;
dïhte+Pron+Pers+Sg3+Gen+Use/NG:altese FINAL1 ;
dïhte+Pron+Pers+Sg3+Gen+Use/NG:elties FINAL1 ;
dïhte+Pron+Pers+Sg3+Gen+Use/NG:eltie FINAL1 ;
dïhte+Pron+Pers+Sg3+Ill:dïsse FINAL1 ;
dïhte+Pron+Pers+Sg+Ill+PxSg3:altasasse FINAL1 ; !dïhte


dïhte+Pron+Pers+Sg3+Ine:desnie FINAL1 ;
dïhte+Pron+Pers+Sg3+Ela:destie FINAL1 ;
dïhte+Pron+Pers+Sg3+Ela+Use/NG:altasistie FINAL1 ;
dïhte+Pron+Pers+Sg3+Com:dejnie FINAL1 ;
dïhte+Pron+Pers+Sg3+Com+Use/NG:altasinie FINAL1 ;

dah% guaktah+Pron+Pers+Du3+Nom:dah% guaktah FINAL1 ; ! For oahpa use

dah+Pron+Pers+Pl3+Nom:dah FINAL1 ;
dah+Pron+Pers+Pl3+Acc:dejtie FINAL1 ;
dah+Pron+Pers+Pl3+Gen+Use/NG:daj FINAL1 ;
dah+Pron+Pers+Pl3+Gen:dej FINAL1 ;
dah+Pron+Pers+Pl3+Gen+Use/NG:altese FINAL1 ;
dah+Pron+Pers+Pl3+Ill:dejtie FINAL1 ;
dïhte+Pron+Refl+Sg+Ill+PxPl3:altasissie FINAL1 ; !dïhte
dah+Pron+Pers+Pl3+Ine:dejnie FINAL1 ;
dah+Pron+Pers+Pl3+Ela:dejstie FINAL1 ;
dah+Pron+Pers+Pl3+Com:dejgujmie FINAL1 ;
dah+Pron+Pers+Pl3+Com+Use/NG:dajgujmie FINAL1 ;

dah% guaktah+Pron+Pers+Du3+Gen:altesigan FINAL1 ; ! XXX check this




!! ## Demonstrative pronouns
!    ----------------------
!    ----------------------
!    ----------------------

!! This is for:
!! the attributive forms of dïhte
!! all forms of the other pronouns

!! ### The initial demonstrative lexica
! - - - - - - - - - - - - - - - -

LEXICON Demonstrative   !!= * @CODE@
!! Same as above, with exceptions in
!! Sg Ill, Sg Ine, Sg Ela, Pl Com
!! Still open: analyse morphologically or not...

dïhte+Pron+Dem+Sg+Nom:dïhte FINAL1 ;
dïhte+Pron+Dem+Sg+Acc:dam FINAL1 ;
dïhte+Pron+Dem+Sg+Gen:dan FINAL1 ;
dïhte+Pron+Dem+Sg+Ill:dïsse FINAL1 ; !
dïhte+Pron+Dem+Sg+Ine:desnie FINAL1 ;
dïhte+Pron+Dem+Sg+Ela:destie FINAL1 ;
dïhte+Pron+Dem+Sg+Ill+Attr:dan FINAL1 ;
dïhte+Pron+Dem+Sg+Ine+Attr:dennie FINAL1 ;
dïhte+Pron+Dem+Sg+Ela+Attr:dehtie FINAL1 ;
dïhte+Pron+Dem+Sg+Com:dejnie FINAL1 ;

dïhte+Pron+Dem+Pl+Nom:dah FINAL1 ;
dïhte+Pron+Dem+Pl+Acc:dejtie FINAL1 ;
dïhte+Pron+Dem+Pl+Gen+Use/NG:daj FINAL1 ;
dïhte+Pron+Dem+Pl+Gen:dej FINAL1 ;
dïhte+Pron+Dem+Pl+Ill:dejtie FINAL1 ;
dïhte+Pron+Dem+Pl+Ine:dejnie FINAL1 ;
dïhte+Pron+Dem+Pl+Ela:dejstie FINAL1 ;
dïhte+Pron+Dem+Pl+Com:dejgujmie FINAL1 ; !Lene la til, pga MT
dïhte+Pron+Dem+Pl+Com+Attr:dej FINAL1 ; ! 
dïhte+Pron+Dem+Pl+Com+Attr+Use/NG:daj FINAL1 ;

! Here come the unambiguous demonstratives:
! Separate attributive forms are marked as such in the commentary field

!daate-Sg
daate+Pron+Dem+Sg+Nom:daate FINAL1 ;
daate+Pron+Dem+Sg+Acc:daam FINAL1 ;
daate+Pron+Dem+Sg+Gen:daan FINAL1 ;
daate+Pron+Dem+Sg+Gen+Err/Orth:daen FINAL1 ;
daate+Pron+Dem+Sg+Ill:daase FINAL1 ;
daate+Pron+Dem+Sg+Ill+Attr:daan # ; ! XXX Attr form (Gen analysis?)
daate+Pron+Dem+Sg+Ine:daesnie FINAL1 ;
daate+Pron+Dem+Sg+Ine+Attr:daennie # ; ! XXX Attr form
daate+Pron+Dem+Sg+Ela:daestie FINAL1 ;
daate+Pron+Dem+Sg+Ela+Attr:daehtie # ; ! XXX Attr form
daate+Pron+Dem+Sg+Com:daejnie FINAL1 ;
!daaate-Pl ;
daate+Pron+Dem+Pl+Nom:daah FINAL1 ;
daate+Pron+Dem+Pl+Acc:daejtie FINAL1 ;
daate+Pron+Dem+Pl+Gen:daaj FINAL1 ;
daate+Pron+Dem+Pl+Gen+Use/NG:daej FINAL1 ;
daate+Pron+Dem+Pl+Ill:daejtie FINAL1 ;
daate+Pron+Dem+Pl+Ine:daejnie FINAL1 ;
daate+Pron+Dem+Pl+Ela:daejstie FINAL1 ;
daate+Pron+Dem+Pl+Com:daajgujmie FINAL1 ;
daate+Pron+Dem+Pl+Com+Attr:daaj # ; ! XXX Attr form (Gen analysis?)
daate+Pron+Dem+Pl+Com+Attr+Use/NG:daej # ; ! XXX Attr form (Gen analysis?)
daate+Pron+Dem+Pl+Com+Use/NG:daejgujmie FINAL1 ;


!doete-Sg
doete+Pron+Dem+Sg+Nom:doete FINAL1 ;
doete+Pron+Dem+Sg+Acc:doem FINAL1 ;
doete+Pron+Dem+Sg+Gen:doen FINAL1 ;
doete+Pron+Dem+Sg+Ill:doese FINAL1 ;
doete+Pron+Dem+Sg+Ill+Attr:doen FINAL1 ;
doete+Pron+Dem+Sg+Ine:duesnie FINAL1 ;
doete+Pron+Dem+Sg+Ine+Attr:duennie # ; ! these are Attr forms, demonstr
doete+Pron+Dem+Sg+Ela:duestie FINAL1 ;
doete+Pron+Dem+Sg+Ela+Attr:duehtie # ; ! these are Attr forms, demonstr
doete+Pron+Dem+Sg+Com:duejnie FINAL1 ;
!doete-Pl
doete+Pron+Dem+Pl+Nom:doeh FINAL1 ;
doete+Pron+Dem+Pl+Acc:duejtie FINAL1 ;
doete+Pron+Dem+Pl+Gen:doej FINAL1 ;
doete+Pron+Dem+Pl+Gen+Use/NG:duej FINAL1 ;
doete+Pron+Dem+Pl+Ill:duejtie FINAL1 ;
doete+Pron+Dem+Pl+Ine:duejnie FINAL1 ;
doete+Pron+Dem+Pl+Ela:duejstie FINAL1 ;
doete+Pron+Dem+Pl+Com:duejgujmie FINAL1 ;
doete+Pron+Dem+Pl+Com+Attr:duej # ; ! XXX Attr form
doete+Pron+Dem+Pl+Com+Use/NG:doejgujmie FINAL1 ;





!dohte-Sg
dohte+Pron+Dem+Sg+Nom:dohte FINAL1 ;
dohte+Pron+Dem+Sg+Acc:dom FINAL1 ;
dohte+Pron+Dem+Sg+Gen:don FINAL1 ;
dohte+Pron+Dem+Sg+Ill:dosse FINAL1 ;
dohte+Pron+Dem+Sg+Ill+Attr:don FINAL1 ;
dohte+Pron+Dem+Sg+Ine:dusnie FINAL1 ;
dohte+Pron+Dem+Sg+Ine+Attr:dunnie # ; ! these are Attr forms, demonstr
dohte+Pron+Dem+Sg+Ela:dustie FINAL1 ;
dohte+Pron+Dem+Sg+Ela+Attr:duhtie # ; ! these are Attr forms, demonstr
dohte+Pron+Dem+Sg+Com:dujnie FINAL1 ;
!dohte-Pl
dohte+Pron+Dem+Pl+Nom:doh FINAL1 ;
dohte+Pron+Dem+Pl+Acc:dujtie FINAL1 ;
dohte+Pron+Dem+Pl+Gen:doj FINAL1 ;
dohte+Pron+Dem+Pl+Gen+Use/NG:dyj FINAL1 ;
dohte+Pron+Dem+Pl+Ill:dujtie FINAL1 ;
dohte+Pron+Dem+Pl+Ine:dujnie FINAL1 ;
dohte+Pron+Dem+Pl+Ela:dujstie FINAL1 ;
dohte+Pron+Dem+Pl+Com:dojgujmie FINAL1 ;
dohte+Pron+Dem+Pl+Com+Attr:doj # ; ! XXX Attr form
dohte+Pron+Dem+Pl+Com+Attr+Use/NG:dyj # ; ! XXX Attr form
dohte+Pron+Dem+Pl+Com+Use/NG:dyjgujmie FINAL1 ;


magkeres+Pron+Interr:magk DAGKERES ;
guktemes+Pron+Interr:gukt DAGKERES ;
dagkeres+Pron+Dem:dagk DAGKERES ;
daagkeres+Pron+Dem:daagk DAGKERES ;
doegkeres+Pron+Dem:doegk DAGKERES ;
dogkeres+Pron+Dem:dogk DAGKERES ;

! gaajhke som determinativ (+Pron+Dem). Som pronomen
! burde det kanskje ha vore kvantor, semantisk er det Def, men altså no +Indef.
        gaajhke+Pron+Dem+Attr:gaajhke   #              ; ! for MT, like sme.
          gaajhke+Pron+Dem+Sg:gaajhk    indsg-e        ;
          gaajhke+Pron+Dem+Pl:gaajhk    indpl-e        ;
             gaajhke+Pron+Dem:gaajhk    indess         ;
 gaajhke+Pron+Dem+Sg+Ela+Attr:gaajhkede #       "alle" ;
 gaajhke+Pron+Dem+Sg+Ine+Attr:gaajhkene #       "alle" ;



LEXICON DAGKERES    !!= * @CODE@
+Attr:eres # ; ! Sjekk dette paradigmet, det er bygd på m-
+Sg+Nom+Use/NG:eres FINAL1 ; ! Sub?
+Sg+Nom:ere FINAL1 ;
+Sg+Acc:erem FINAL1 ;
+Sg+Gen:eren FINAL1 ;
+Sg+Ill:arasse FINAL1 ;
+Sg+Ine+Attr:arinie FINAL1 ;
+Sg+Ine:arisnie FINAL1 ; ! sjekk ??? Ine arisnie
+Sg+Ela:aristie FINAL1 ;
+Sg+Com:arinie FINAL1 ;
+Pl+Nom:erh FINAL1 ;
+Pl+Acc:aridie FINAL1 ;
+Pl+Gen:eri FINAL1 ;
+Pl+Ill:aridie FINAL1 ;
+Pl+Ela:arijstie FINAL1 ;
+Pl+Ine:arijnie FINAL1 ;
+Pl+Com:erigujmie FINAL1 ;






!! ## Interrogative and relative pronouns
!    -----------------------------------
!    -----------------------------------
!    -----------------------------------


LEXICON Interrogative
gie+Pron+Interr: GIE ;
gie+Pron+Rel: GIE ;
mij+Pron+Interr: MIJ ;
mij+Pron+Rel: MIJ ;
guhte+Pron+Rel: GUHTE ;
guhte+Pron+Interr:  GUHTE ;
mij+Pron+Indef: MIJ ;
gie+Pron+Indef: GIE ;

gåabpa+Pron+Interr+Sg+Nom:gåabpa FINAL1 "hvilken (av to)" ;
gåabpa+Pron+Interr+Sg+Acc:gåabpam FINAL1 "hvilken (av to)" ;
gåabpa+Pron+Interr+Sg+Gen:gåabpan FINAL1 "hvilken (av to)" ;
gåabpa+Pron+Interr+Sg+Ill:gåabpese FINAL1 "hvilken (av to)" ;
gåabpa+Pron+Interr+Sg+Ine:gåabpesne FINAL1 "hvilken (av to)" ;
gåabpa+Pron+Interr+Sg+Ine+Attr:gåabpene # "hvilken (av to)" ;
gåabpa+Pron+Interr+Sg+Ela:gåabpeste FINAL1 "hvilken (av to)" ;
gåabpa+Pron+Interr+Sg+Ela+Attr:gåabpede # "hvilken (av to)" ;
gåabpa+Pron+Interr+Sg+Com:gåabpine FINAL1 "hvilken (av to)" ;

gåabpetje+Pron+Interr+Sg+Nom:gåabpetje FINAL1 "hvilken (av to)" ;
gåabpetje+Pron+Interr+Sg+Acc:gåabpetjem FINAL1 "hvilken (av to)" ;
gåabpetje+Pron+Interr+Sg+Gen:gåabpetjen FINAL1 "hvilken (av to)" ;
gåabpetje+Pron+Interr+Sg+Ill:gåabpatjasse FINAL1 "hvilken (av to)" ;
gåabpetje+Pron+Interr+Sg+Ine:gåabpatjisnie FINAL1 "hvilken (av to)" ;
gåabpetje+Pron+Interr+Sg+Ela:gåabpatjistie FINAL1 "hvilken (av to)" ;
gåabpetje+Pron+Interr+Sg+Com:gåabpatjinie FINAL1 "hvilken (av to)" ;


gåabpa+Pron+Rel+Sg+Nom:gåabpa        FINAL1 "hvilken (av to)" ;
gåabpa+Pron+Rel+Sg+Acc:gåabpam       FINAL1 "hvilken (av to)" ;
gåabpa+Pron+Rel+Sg+Gen:gåabpan       FINAL1 "hvilken (av to)" ;
gåabpa+Pron+Rel+Sg+Ill:gåabpese      FINAL1 "hvilken (av to)" ;
gåabpa+Pron+Rel+Sg+Ine:gåabpesne     FINAL1 "hvilken (av to)" ;
gåabpa+Pron+Rel+Sg+Ine+Attr:gåabpene # 		"hvilken (av to)" ;
gåabpa+Pron+Rel+Sg+Ela:gåabpeste 	 FINAL1 "hvilken (av to)" ;
gåabpa+Pron+Rel+Sg+Ela+Attr:gåabpede # 		"hvilken (av to)" ;
gåabpa+Pron+Rel+Sg+Com:gåabpine 	 FINAL1 "hvilken (av to)" ;

gåabpetje+Pron+Rel+Sg+Nom:gåabpetje     FINAL1 "hvilken (av to)" ;
gåabpetje+Pron+Rel+Sg+Acc:gåabpetjem    FINAL1 "hvilken (av to)" ;
gåabpetje+Pron+Rel+Sg+Gen:gåabpetjen    FINAL1 "hvilken (av to)" ;
gåabpetje+Pron+Rel+Sg+Ill:gåabpatjasse  FINAL1 "hvilken (av to)" ;
gåabpetje+Pron+Rel+Sg+Ine:gåabpatjisnie FINAL1 "hvilken (av to)" ;
gåabpetje+Pron+Rel+Sg+Ela:gåabpatjistie FINAL1 "hvilken (av to)" ;
gåabpetje+Pron+Rel+Sg+Com:gåabpatjinie  FINAL1 "hvilken (av to)" ; !?





LEXICON GUHTE
+Sg+Nom:guhte FINAL1 ;
+Sg+Nom+Err/Orth:guht FINAL1 ;
+Sg+Acc:guhtem FINAL1 ;
+Sg+Gen:guhten FINAL1 ;
+Sg+Ill:guhtese FINAL1 ;
+Sg+Ine:guhtesne FINAL1 ;
+Sg+Ine+Attr:guhtene # ;
+Sg+Ela:guhteste FINAL1 ;
+Sg+Ela+Attr:guhtede # ;
+Sg+Com+Err/Orth:guhtine FINAL1 ; !? !
+Pl+Nom:guhth FINAL1 ;
+Pl+Nom+Err/Orth:guht FINAL1 ;
+Pl+Acc:guhtide FINAL1 ;
+Pl+Gen:guhti FINAL1 ;
+Pl+Ill:guhtide FINAL1 ;
+Pl+Ine:guhtine FINAL1 ;
+Pl+Ela:guhtijste FINAL1 ;
+Pl+Com+Err/Orth:guhtigujmie FINAL1 ; !? !

LEXICON GIE
+Sg+Nom:gie FINAL1 ;
!+Sg+Nom+Err/Orth:gïe FINAL1 ; !spellrelax
+Sg+Acc:gïem FINAL1 ;
+Sg+Acc+Use/NG:geam FINAL1 ;
+Sg+Gen:gïen FINAL1 ;
+Sg+Gen+Use/NG:gean FINAL1 ;
+Sg+Ill:gïese FINAL1 ;
+Sg+Ill+Use/NG:gease FINAL1 ;
+Pl+Nom:gïeh FINAL1 ;
+Pl+Nom+Use/NG:geah FINAL1 ;
+Pl+Gen+Use/NG:geaj FINAL1 ;
+Pl+Com+Use/NG:geajgujmie FINAL1 ;
:gie INTERR ; !

LEXICON MIJ
+Sg+Nom:mij FINAL1 ;
+Sg+Acc:maam FINAL1 ;
+Sg+Acc+Err/Orth:maab FINAL1 ;
+Sg+Gen:man FINAL1 ;
+Sg+Ill:mïsse FINAL1 ;
+Sg+Ill+Attr:man FINAL1 ;
+Sg+Ill+Use/NG:masse FINAL1 ;
+Sg+Ess:mannasinie FINAL1 ;
+Sg+Ess+Err/Orth:mannasine FINAL1 ;
+Pl+Nom:mah FINAL1 ;
+Pl+Gen+Use/NG:maj FINAL1 ;
+Pl+Gen:mej FINAL1 ;
+Pl+Com+Use/NG:majgujmie FINAL1 ;
:me INTERR ;


LEXICON INTERR
+Sg+Ine:snie 		FINAL1 ;
+Sg+Ine+Attr:nnie FINAL1 ;
+Sg+Ela:stie        FINAL1 ;
+Sg+Ela+Attr:htie        FINAL1 ;
+Sg+Com:jnie        FINAL1 ;
+Pl+Acc:jtie        FINAL1 ;
+Pl+Gen:j           FINAL1 ;
+Pl+Ill:jtie        FINAL1 ;
+Pl+Ine:jnie        FINAL1 ;
+Pl+Ela:jstie       FINAL1 ;
+Pl+Com:jgujmie     FINAL1 ;

!! ## Indefinite pronouns
!    -------------------
!    -------------------
!    -------------------

LEXICON Indefinite   !!= * @CODE@

guhte+Pron+Indef: GUHTE ;

abpe ind_noninfl "hele" ; !probably does not have inflection, so it was removed
aktege+Pron+Indef+Attr:aktege FINAL1 ;  ! mt
dovnesh:dovnes indcoll "alle" ; ! check this
eatjah:eatj indeven-a "annen, de andre" ;
eatjah:eatj indeven-e "annen, de andre" ;
mubpie+Pron+Indef:mubp MUBPIE ;
nubpie+Pron+Indef:nubp MUBPIE ;

fïere+Pron+Indef+Attr:fïere # ; 
fïereguhte:fïereguht indeven-e "hver og en" ;
fïereguhte+Use/NG:fïere-guht indeven-e "hver og en" ; ! MMM
feareguhte:feareguht indeven-e "hver og en" ; ! MMM
feareguhte+Use/NG:feare-guht indeven-e "hver og en" ; ! MMM

fïerhte:fïerht indeven-e "hver, hverthver" ;
fïerhte+Err/Orth:fïert indeven-e "hver, hverthver" ;

gaajhke+Pron+Indef+Attr:gaajhke # ; ! for MT, like sme.
gaajhke:gaajhk indeven-e "alle" ;
gaajhke+Pron+Indef+Sg+Ela+Attr:gaajhkede # "alle" ;
gaajhke+Pron+Indef+Sg+Ine+Attr:gaajhkene # "alle" ;

gaajhkesh:gaajhkes indcoll "alle sammen" ;
gaajhkesh+Err/Orth:gaajhks indcoll "alle sammen" ;
gaajhkesh+Pron+Coll+Pl+Nom+Err/Orth:gaajhkes FINAL1 ;
gaajhkesh+Pron+Coll+Pl+Nom+Err/Orth:gaajhksh FINAL1 ;

gallesh:gallas indcoll "mange sammen" ;
!gellie ind_noninfl "mange" ; !also inessive for gellene e.g. gellene lehkesne - på många ställen. Example in Åarjelsaemien 6 (2000:6).
!gellieh ind_noninfl ; => lenger ned

! Check this:
geahken+Pron+Indef:geahk indsg-e-obl ;
geahken+Pron+Indef:geahk indpl-e ;
geahkese:geahkes indodd ;

giege+Pron+Indef+Sg+Nom:giege # ;
giege+Pron+Indef+Sg+Acc:gïemge # ;
giege+Pron+Indef+Sg+Acc+Use/NG:geamge # ;
giege+Pron+Indef+Sg+Gen:gïenge # ;
giege+Pron+Indef+Sg+Gen+Use/NG:geange # ;
giege+Pron+Indef+Sg+Ill:gïesege FINAL1 ;
giege+Pron+Indef+Sg+Ill+Use/NG:geasege FINAL1 ;
giege+Pron+Indef+Sg+Ine:giesniege 		FINAL1 ;
giege+Pron+Indef+Sg+Ine+Attr:gienniege FINAL1 ;
giege+Pron+Indef+Sg+Ela:giestiege        FINAL1 ;
giege+Pron+Indef+Sg+Ela+Attr:giehtiege        FINAL1 ;
giege+Pron+Indef+Sg+Com:giejniege        FINAL1 ;

giege+Pron+Indef+Pl+Nom:gïehge FINAL1 ;
giege+Pron+Indef+Pl+Nom+Use/NG:geahge FINAL1 ;
giege+Pron+Indef+Pl+Gen:giejge           FINAL1 ;
giege+Pron+Indef+Pl+Gen+Use/NG:geajge FINAL1 ;
giege+Pron+Indef+Pl+Acc:giejtiege        FINAL1 ;
giege+Pron+Indef+Pl+Ill:giejtiege        FINAL1 ;
giege+Pron+Indef+Pl+Ine:giejniege        FINAL1 ;
giege+Pron+Indef+Pl+Ela:giejstiege       FINAL1 ;
giege+Pron+Indef+Pl+Com:geajgujmiege FINAL1 ;


! The following paradigm is for mt use, but perhaps needed elsewhere?

gïehk+Pron+Indef+Sg+Nom:gïehk # ;
gïehk+Pron+Indef+Sg+Acc:gïemhk # ;
gïehk+Pron+Indef+Sg+Acc+Use/NG:geamhk # ;
gïehk+Pron+Indef+Sg+Gen:gïenhk # ;
gïehk+Pron+Indef+Sg+Gen+Use/NG:geanhk # ;
gïehk+Pron+Indef+Sg+Ill:gïesehk FINAL1 ;
gïehk+Pron+Indef+Sg+Ill+Use/NG:geasehk FINAL1 ;
gïehk+Pron+Indef+Sg+Ine:giesniehk 		FINAL1 ;
gïehk+Pron+Indef+Sg+Ine+Attr:gienniehk FINAL1 ;
gïehk+Pron+Indef+Sg+Ela:giestiehk        FINAL1 ;
gïehk+Pron+Indef+Sg+Ela+Attr:giehtiehk        FINAL1 ;
gïehk+Pron+Indef+Sg+Com:giejniehk        FINAL1 ;

gïehk+Pron+Indef+Pl+Nom:gïehk FINAL1 ;
gïehk+Pron+Indef+Pl+Nom+Use/NG:geahk FINAL1 ;
gïehk+Pron+Indef+Pl+Gen:giejhk           FINAL1 ;
gïehk+Pron+Indef+Pl+Gen+Use/NG:geajhk FINAL1 ;
gïehk+Pron+Indef+Pl+Acc:giejtiehk        FINAL1 ;
gïehk+Pron+Indef+Pl+Ill:giejtiehk        FINAL1 ;
gïehk+Pron+Indef+Pl+Ine:giejniehk        FINAL1 ;
gïehk+Pron+Indef+Pl+Ela:giejstiehk       FINAL1 ;
gïehk+Pron+Indef+Pl+Com:geajgujmiehk FINAL1 ;


! maamkh -- the lemma 'mijkh' is unattested, but maamkh is Acc
! The whole paradigm should be looked into

mijkh+Pron+Indef+Sg+Nom:mijkh FINAL1 ;
mijkh+Pron+Indef+Sg+Acc:maamkh FINAL1 ;
mijkh+Pron+Indef+Sg+Gen:mankh FINAL1 ;
mijkh+Pron+Indef+Sg+Ill:mïssekh FINAL1 ;
mijkh+Pron+Indef+Sg+Ine:mesniekh FINAL1 ;
mijkh+Pron+Indef+Sg+Ela:mestiekh FINAL1 ;
mijkh+Pron+Indef+Sg+Com:mejniekh FINAL1 ;
mijkh+Pron+Indef+Ess:mannasiniekh FINAL1 ;
mijkh+Pron+Indef+Pl+Nom:mahkh FINAL1 ;
mijkh+Pron+Indef+Pl+Acc:mejtiekh FINAL1 ;
mijkh+Pron+Indef+Pl+Gen:majkh FINAL1 ;
mijkh+Pron+Indef+Pl+Ill:mejtiekh FINAL1 ;
mijkh+Pron+Indef+Pl+Ine:mejniekh FINAL1 ;
mijkh+Pron+Indef+Pl+Ela:mejstiekh FINAL1 ;
mijkh+Pron+Indef+Pl+Com:mejgujmiekh FINAL1 ;


gille:gill indeven-e "noen få" ;
gillesh:gilles indcoll "noen få sammen" ;


!Guhtegh-Sg
guhtegh+Pron+Indef+Sg+Nom:guhtegh FINAL1 ;
guhtegh+Pron+Indef+Sg+Acc:guhtemgh FINAL1 ;
guhtegh+Pron+Indef+Sg+Gen:guhtengh FINAL1 ;
guhtegh+Pron+Indef+Sg+Ill:guhtesegh FINAL1 ;
guhtegh+Pron+Indef+Sg+Ine:guhtesnegh FINAL1 ;
guhtegh+Pron+Indef+Sg+Ine+Attr:guhtene # ;
guhtegh+Pron+Indef+Sg+Ela:guhtestegh FINAL1 ;
guhtegh+Pron+Indef+Sg+Ela+Attr:guhtedegh # ;
!Guhtegh-Pl	 Indef
guhtegh+Pron+Indef+Pl+Nom:guhthgh FINAL1 ;
guhtegh+Pron+Indef+Pl+Acc:guhtidegh FINAL1 ;
guhtegh+Pron+Indef+Pl+Gen:guhtigh FINAL1 ;
guhtegh+Pron+Indef+Pl+Ill:guhtidegh FINAL1 ;
guhtegh+Pron+Indef+Pl+Ine:guhtinegh FINAL1 ;
guhtegh+Pron+Indef+Pl+Ela:guhtijstegh FINAL1 ;
guhtegh+Pron+Indef+Pl+Com:guhtigujmiegh FINAL1 ; !? !



gåabpatjahkh+Pron+Indef+Sg+Ela+Attr:gåabpatjahke%>de # "både" ;
gåabpatjahkh:gåabpatjahk indeven-e "både" ;
gåabpatjahkh+Err/Orth:gåabpahtjahk indeven-e "både" ;
gåabpegh+Pron+Indef+Pl+Nom:gåabpegh FINAL1 "både" ;
gåabpegh+Pron+Indef+Pl+Acc:gåabpagidie FINAL1 ;
gåabpegh+Pron+Indef+Pl+Gen:gåabpegi FINAL1 ;
gåabpegh+Pron+Indef+Pl+Ill:gåabpagidie FINAL1 ;
gåabpegh+Pron+Indef+Pl+Ine:gåabpaginie FINAL1 ;
gåabpegh+Pron+Indef+Pl+Ela:gåabpagijstie FINAL1 ;
gåabpegh+Pron+Indef+Pl+Com:gåabpaginie FINAL1 ;

! gåabpatjahkh begge gåabpatjahken aejkien begge ganger;
! gåabpatjahkem gåetiem begge telt (objekt);
! gåabpatjahken bealan til begge sider
! gåabpatjahkide = til begge to (om personer)

!jeatja:jeatj indeven-a "annen, de andre" ;
!jeatjh:jeatj indeven-e "annen, de andre" ;
jeatjebe:jeatjab indodd "en annen, noe annet, flt. jeatjebh andre" ;
jeatjebe+Pron+Indef+Attr:jeatjah FINAL1 ;
jeatjebe+Pron+Indef+Attr+Use/NG:jeatjh FINAL1 ;
jeatjebe+Pron+Indef+Attr+Use/NG:jeatja FINAL1 ;
jeatjebe+Pron+Indef+Pl+Ill+Err/Orth:jeatjadibie FINAL1 ;
jeatjebe+Pron+Indef+Pl+Acc+Err/Orth:jeatjadibie FINAL1 ;
jeene:jeen indeven-e "mange, mye" ;
jeenje:jeenj indeven-e "mange, mye" ; !uncertain if this word can be inflected in all these cases
jeenjemes+Pron+Indef+Pl:jeenjemes indpl-e "de fleste" ;
jeenjemes+Pron+Indef:jeenjemes indess ;
jeenjemes+Pron+Indef+Nom:jeenjemes FINAL1 ; !XXX Should these include other tgs too?
jeenjemes+Pron+Indef+Acc:jeenjemese%>m FINAL1 ;
jeenjemes+Pron+Indef+Gen:jeenjemese%>n FINAL1 ;
jeenjemes+Pron+Indef+Ill:jeenjemese%>se FINAL1 ;
jeenjemes+Pron+Indef+Ine:jeenjemese%>sne FINAL1 ;
jeenjemes+Pron+Indef+Ine+Attr:jeenjemese%>ne # ; !XXX look at Attr
jeenjemes+Pron+Indef+Ela:jeenjemese%>ste FINAL1 ;
jeenjemes+Pron+Indef+Com:jeenjemes%>ine FINAL1 ;
jeenjesh:jeenjes indcoll "mange sammen" ;
jienebe:jienieb indodd "flere" ;
jïenesh+Err/Orth:jienes indcoll "mange sammen" ; !
jïjnjesh+Err/Orth:jijnjes indcoll "mange sammen" ; !
jïenesh:jïenes indcoll "mange sammen" ;
jïjnje:jïjnj indeven-e "mange, mye" ;
jïjnjesh:jïjnjes indcoll "mange sammen" ;


mij% akt+MWE+Pron+Indef+Nom:mij% akt # ;
mijakt+Pron+Indef+Nom:mij#akt # ;
mijakt+Pron+Indef+Nom+Err/Orth:mij-akt # ; !Err/Orth or norm?  (Majas Kommentar: På siste GG-seminaret i Staare, virket det som at de ønsket å normere inn språkstøtter - generelt om bindestrek :-) Altså bindesak-problematiken er vi ikke ferdig med enda i sma) 
mij% akt+MWE+Pron+Indef+Sg+Acc:maam% akt # ;
mij% akt+Pron+Indef+Sg+Acc+Err/Orth:maam#akt # ;
mij% akt+Pron+Indef+Sg+Acc+Err/Orth:maam-akt # ; !Err/Orth or norm?
mij% gih+MWE+Pron+Indef+Sg+Acc:maam% gih # ;
mij% gih+Pron+Indef+Sg+Acc+Err/Orth:maam#gih # ;
mij% gih+Pron+Indef+Sg+Acc+Err/Orth:maam-gih # ; !Err/Orth or norm?
gie% akt+MWE+Pron+Indef+Sg+Nom:gie% akt # ;
gie% akt+Pron+Indef+Sg+Nom+Err/Orth:gie#akt # ;
gie% akt+Pron+Indef+Sg+Nom+Err/Orth:gie-akt # ; !Err/Orth or norm?
gie% akt+MWE+Pron+Indef+Sg+Acc:gïem% akt # ;
gie% akt+Pron+Indef+Sg+Acc+Err/Orth:gïem#akt # ;
gie% akt+Pron+Indef+Sg+Acc+Err/Orth:gïem-akt # ; !Err/Orth or norm?
gie% akt+MWE+Pron+Indef+Sg+Acc:geam% akt # ;
gie% akt+Pron+Indef+Sg+Acc+Err/Orth:geam#akt # ;
gie% akt+Pron+Indef+Sg+Acc+Err/Orth:geam-akt # ; !Err/Orth or norm?
mij% akt+MWE+Pron+Indef+Sg+Nom+Err/Orth:mij% akth # ;
mij% akt+Pron+Indef+Sg+Nom+Err/Orth:mij#akth # ;
mij% akt+Pron+Indef+Sg+Nom+Err/Orth:mij-akth # ; !Err/Orth or norm?
mij% akt+MWE+Pron+Indef+Sg+Acc+Err/Orth:maam% akth # ;
mij% akt+Pron+Indef+Sg+Acc+Err/Orth:maam#akth # ;
mij% akt+Pron+Indef+Sg+Acc+Err/Orth:maam-akth # ; !Err/Orth or norm?
gie% akt+MWE+Pron+Indef+Sg+Nom+Err/Orth:gie% akth # ;
gie% akt+Pron+Indef+Sg+Nom+Err/Orth:gie#akth # ;
gie% akt+Pron+Indef+Sg+Nom+Err/Orth:gie-akth # ; !Err/Orth or norm?
gie% akt+MWE+Pron+Indef+Sg+Acc+Err/Orth:giem% akth # ;
gie% akt+Pron+Indef+Sg+Acc+Err/Orth:giem#akth # ;
gie% % akt+Pron+Indef+Sg+Acc+Err/Orth:giem-akth # ; !Err/Orth or norm?
gie% akt+MWE+Pron+Indef+Sg+Acc+Err/Orth:geam% akth # ;
gie% akt+Pron+Indef+Sg+Acc+Err/Orth:geam#akth # ;
gie% akt+Pron+Indef+Sg+Acc+Err/Orth:geam-akth # ; !Err/Orth or norm?


mij% akt+MWE+Pron+Indef+Sg+Ill:mïsse% akt # ;
mij% akt+MWE+Pron+Indef+Sg+Com:mejnie% akt # ;
mij% akt+MWE+Pron+Indef+Sg+Ine:mesnie% akt # ;
mij% akt+MWE+Pron+Indef+Sg+Ela:mestie% akt # ;
gie% akt+MWE+Pron+Indef+Sg+Ill:gïese% akt # ;
gie% akt+MWE+Pron+Indef+Sg+Com:giejnie% akt # ;
gie% akt+MWE+Pron+Indef+Sg+Ela:giestie% akt # ;


mij% joem+MWE+Pron+Indef+Sg+Nom:mij% joem # ;
mij% joem+MWE+Pron+Indef+Sg+Acc:maam% joem # ;
mij% joem+MWE+Pron+Indef+Sg+Ill:mïsse% joem # ;
mij% joem+MWE+Pron+Indef+Sg+Com:mejnie% joem # ;
mij% joem+MWE+Pron+Indef+Sg+Ine:mesnie% joem # ;
mij% joem+MWE+Pron+Indef+Sg+Ela:mestie% joem # ;

gie% joem+MWE+Pron+Indef+Sg+Nom:gie% joem # ;
gie% joem+MWE+Pron+Indef+Sg+Acc:gïem% joem # ;
gie% joem+MWE+Pron+Indef+Sg+Ill:gïese% joem # ;
gie% joem+MWE+Pron+Indef+Sg+Com:giejnie% joem # ;
gie% joem+MWE+Pron+Indef+Sg+Ela:giestie% joem # ;

mejtegh+Pron+Indef:mejtegh FINAL1 ; !XXX

muvhte:muvht indeven-e "noen" ;
muvhte+Err/Orth:muvht muvhtiecase "noen" ;

måadtesh:måadtas indcoll "mangen" ;
muvhtesh:muvhtes indcoll "mangen" ;
måedtie:måedt muvhtiecase "mange" ;
gellie:gell muvhtiecase "mange" ;

naakede ind_noninfl ; ! no inflection?
naakene:naaken naakenlex "noen, noe" ; !this must be pointed towards another lexicon due to it being three-syllabic
naakene+Err/Orth:naagen naakenlex ;
naan ind_noninfl "noen, noe" ;

seamma ind_noninfl "samme" ; !XXX

såamesh:såamas indcoll "noen, noe" ;
såamesh+Err/Orth:såames indcoll "noen, noe" ; !
såemies ind_noninfl "noen, noe" ;

åbpe ind_noninfl "hele" ; !probably does not have inflection, so it was removed

LEXICON indeven-e !!= * @CODE@
+Pron+Indef+Sg: indsg-e ;
+Pron+Indef+Pl: indpl-e ;
+Pron+Indef: indess ;

LEXICON indeven-a !!= * @CODE@
+Pron+Indef+Sg: indsg-a ;
+Pron+Indef+Pl: indpl-a ;
+Pron+Indef: indess ;


!! ## kongruensContlex

!! ### muvhtiecase

LEXICON muvhtiecase !!= * @CODE@
+Pron+Indef+Sg: muvhtiesg ;
+Pron+Indef+Pl: muvhtiepl ;

LEXICON muvhtiesg !!= * @CODE@
+Nom:ie FINAL1 ;
+Acc:ie%>m FINAL1 ;
+Gen:ie%>n FINAL1 ;
+Ela:%>este FINAL1 ; !Magga&Magga
+Ill+Attr:ie%>n # ; !utan attr også?
+Ine+Attr:%>ine # ;
+Ine+Attr+Use/NG:%>ene # ; ! gellene lehkesne - på många ställen. Example in Åarjelsaemien 6 (2000:6).
+Ela+Attr:%>ide # ;
+Com:%>ine FINAL1 ;

LEXICON muvhtiepl !!= * @CODE@
+Nom:ieh FINAL1 ;
+Acc:ide FINAL1 ;
+Gen:ie%>j FINAL1 ;
+Ill+Attr:ide # ;
+Ill:ide FINAL1 ;	   ! mt
+Ine:ine FINAL1 ;	   ! mt
+Ela:ijste FINAL1 ;	   ! mt
+Com:ijgujmie FINAL1 ; ! mt


!! ### naaken

LEXICON naakenlex !!= * @CODE@
+Pron+Indef+Sg+Nom+Use/NG: FINAL1 ;
+Pron+Indef+Attr:e FINAL1 ; ! mt
indodd ;
! XXX Sjekk desse tre formene. Er det rett?
! Ei spesiell attributtform heller enn td. 2x gujmie
+Pron+Indef+Pl+Ill+Attr:%>%^DISIMPi FINAL1 ; ! mt
+Pron+Indef+Pl+Ine+Attr:%>%^DISIMPi FINAL1 ; ! mt
+Pron+Indef+Pl+Ela+Attr:%>%^DISIMPi FINAL1 ; ! mt


LEXICON indodd   !!= * @CODE@
               ! This lexicon cannot be removed
               ! before lexica pointing here
               ! get another contlex!
+Pron+Indef+Sg: indsg_odd ;
+Pron+Indef+Pl: indpl_odd ;
+Pron+Indef: indess_odd ;

LEXICON indsg_odd  !!= * @CODE@
+Nom:e%^DISIMP FINAL1 ;
+Acc:e%>%^DISIMPm FINAL1 ;
+Gen:e%>%^DISIMPn FINAL1 ;
+Ill:%>asse FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
+Ine:%>isnie FINAL1 ;
+Ela:%>istie FINAL1 ;
+Com:%>inie FINAL1 ;

LEXICON indpl_odd  !!= * @CODE@
+Nom:%>%^DISIMPh FINAL1 ;
+Acc:%>idie FINAL1 ;
+Acc+Err/Orth:%>ide FINAL1 ;
+Gen:%>%^DISIMPi FINAL1 ;
+Ill:%>idie FINAL1 ;
+Ill+Err/Orth:%>ide FINAL1 ;
+Ine:%>inie FINAL1 ;
+Ela:%>ijstie FINAL1 ;
+Com:%>%^DISIMPigujmie FINAL1 ;

LEXICON indess_odd  !!= * @CODE@
+Ess:%>inie FINAL1 ;



LEXICON ind_noninfl  !!= * @CODE@
+Pron+Indef+Attr: FINAL1 ;
+Pron+Indef+Sg+Nom: FINAL1 ;



!! ### Inflecting even indefinites
!   - - - - - - - - - - - - - -

LEXICON indsg-e  !!= * @CODE@
indsg-e-nom ;
indsg-e-obl ;

LEXICON indsg-e-nom
+Nom:e FINAL1 ;

LEXICON indsg-e-obl
+Acc:e%>m FINAL1 ;
+Gen:e%>n FINAL1 ;
+Ill+Attr:e%>n # ;
+Ill:e%>se FINAL1 ;
+Ine:e%>sne FINAL1 ;
+Ine+Attr:e%>ne # ;
+Ela:e%>ste FINAL1 ;
+Ela+Attr:e%>de # ;
+Com:%>ine FINAL1 ;

LEXICON indsg-a  !!= * @CODE@
+Nom:a FINAL1 ;
+Acc:a%>m FINAL1 ;
+Gen:a%>n FINAL1 ;
+Ill:e%>se FINAL1 ;
+Ine:e%>sne FINAL1 ;
+Ine+Attr:e%>ne # ;
+Ela:e%>ste FINAL1 ;
+Com:%>ine FINAL1 ;



LEXICON indpl-e  !!= * @CODE@
+Nom:%>h FINAL1 ;
+Acc:%>ide FINAL1 ;
+Gen:%>i FINAL1 ;
+Ill:%>ide FINAL1 ;
+Ine:%>ine FINAL1 ;
+Ela:%>ijste FINAL1 ;
+Com:%>igujmie FINAL1 ;

LEXICON indpl-a  !!= * @CODE@
+Nom:a%>h FINAL1 ;
!+Nom:a%>ah FINAL1 ; !XXX check
+Acc:%>ide FINAL1 ;
+Gen:a%>j FINAL1 ;
+Ill:%>ide FINAL1 ;
+Ine:%>ine FINAL1 ;
+Ela:%>ijste FINAL1 ;
+Com:a%>jgujmie FINAL1 ;


LEXICON indess  !!= * @CODE@
+Ess:%>ine FINAL1 ;


LEXICON indcoll   !!= * @CODE@
+Pron+Coll+Pl: indpl_odd ;









!! ## Reflexive pronouns
!    ------------------
!    ------------------
!    ------------------


LEXICON Reflexive !!= * @CODE@

jïjtje+Pron+Refl+Sg+Nom:jïjtje FINAL1 ;
jïjtje+Pron+Refl+Sg+Nom+Use/NG:jeetje FINAL1 ;
jïjtje+Pron+Refl+Sg+Nom+Use/NG:ijtje FINAL1 ;

dïhte+Pron+Pers+Sg+Acc+PxSg3:altemse FINAL1 ;
dïhte+Pron+Pers+Sg+Gen+PxSg3:altese FINAL1 ;
dïhte+Pron+Pers+Sg+Gen+PxSg3+Use/NG:elties FINAL1 ;
dïhte+Pron+Pers+Sg+Gen+PxSg3+Use/NG:eltie FINAL1 ;

jïjtje+Pron+Refl:jïjtj OBLREFL ;
jïjtje+Pron+Refl:jïjt OBLREFL-NONPAL ;
jïjtje+Pron+Refl+Err/Orth:jïjtj OBLREFL-NONPAL ;
jïjtje+Pron+Refl+Use/NG:jïj OBLREFL-NONPAL2 ; !
jïjtje+Pron+Refl+Use/NG:jïjt OBLREFL-NONPAL2 ; !LA også denne finnes i korp
jïjtje+Pron+Refl+Pl+Nom:jïjtjh FINAL1 ;
jïjtje+Pron+Refl+Du+Nom:jïjtjh FINAL1 ;

LEXICON OBLREFL !!= * @CODE@
 +Acc+PxSg1:eme FINAL1 ;
 +Acc+PxSg1+Use/NG:emem FINAL1 ;
 +Acc+PxDu1:emem FINAL1 ;
 +Acc+PxDu1+Err/Orth:eme FINAL1 ; !
 +Acc+PxPl1:emem FINAL1 ;
 +Acc+PxPl1+Err/Orth:eme FINAL1 ; !

 +Acc+PxSg2:emdh FINAL1 ;
 +Acc+PxDu2:emdh FINAL1 ;
 +Acc+PxPl2:emdh FINAL1 ;

 +Acc+PxSg3:emse FINAL1 ;
 +Acc+PxDu3:emsh FINAL1 ;
 +Acc+PxDu3+Err/Orth:emse FINAL1 ; ! KB s.122 - but not sma speakers today
 +Acc+PxPl3:emsh FINAL1 ;
 +Acc+PxPl3+Err/Orth:emse FINAL1 ; ! KB s.122 - but not sma speakers today

 +Gen+PxSg1:en FINAL1 ;
 +Gen+PxSg1+Err/Orth:ene FINAL1 ; !
 +Gen+PxDu1:emh FINAL1 ;
 +Gen+PxPl1:emh FINAL1 ;

 +Gen+PxSg2:edh FINAL1 ;
 +Gen+PxDu2:edh FINAL1 ;
 +Gen+PxPl2:edh FINAL1 ;

 +Gen+PxSg3:se FINAL1 ; ! MMM
 +Gen+PxDu3:sh FINAL1 ; !
 +Gen+PxPl3:sh FINAL1 ; !

 +Gen+PxSg3+Use/NG:ese FINAL1 ; ! MMM in (...)
 +Gen+PxDu3+Use/NG:esh FINAL1 ; !
 +Gen+PxPl3+Use/NG:esh FINAL1 ; !

!+Gen+PxSg3+Use/MT:ese FINAL1 ; ! MMM...
!+Gen+PxDu3+Use/MT:esh FINAL1 ; !
!+Gen+PxPl3+Use/MT:esh FINAL1 ; !

!+Ill+PxSg123 is in OBLREFL-NONPAL below

 +Ill+PxDu1:emidie FINAL1 ; ! MMM
 +Ill+PxDu2:esadth FINAL1 ; !
!+Ill+PxDu3 is in OBLREFL-NONPAL below

 +Ill+PxPl1:emidie FINAL1 ; ! MMM
 +Ill+PxPl2:esadth FINAL1 ;
!+Ill+PxPl3 is in OBLREFL-NONPAL below


 +Ine+PxSg1:emisnie FINAL1 ;
 +Ine+PxSg2:enadth FINAL1 ;
 +Ine+PxSg3:isnie FINAL1 ;

 +Ine+PxDu1:isnie FINAL1 ;
 +Ine+PxDu2:enadth FINAL1 ;
 +Ine+PxDu3:sinie FINAL1 ;

 +Ine+PxPl1:isnie FINAL1 ;
 +Ine+PxPl2:enadth FINAL1 ;
 +Ine+PxPl3:sinie FINAL1 ;

 +Ela+PxSg1:estanne FINAL1 ; !KB
 +Ela+PxSg1+Use/NG:enistie FINAL1 ; !ÅW+HB
 +Ela+PxSg2:estadth FINAL1 ; !KB and ÅW / HB
 +Ela+PxSg3:estissie FINAL1 ; !KB

 +Ela+PxDu1:istie FINAL1 ; !MMM
 +Ela+PxDu1+Use/NG:isti FINAL1 ; !ÅW+HB
!+Ela+PxDu23 is in OBLREFL-NONPAL below

 +Ela+PxPl1:istie FINAL1 ; !MMM
 +Ela+PxPl1:isti FINAL1 ; !ÅW+HB
!+Ela+PxPl23 is in OBLREFL-NONPAL below

+Com+PxSg1:enæjnene FINAL1 ; !KB
+Com+PxSg1+Use/NG:eninie FINAL1 ; !ÅW+HB
+Com+PxSg2:enæjnedh FINAL1 ; !KB
+Com+PxSg2+Use/NG:enedh FINAL1 ; !ÅW
!+Com+PxSg3 is in OBLREFL-NONPAL below

 +Com+PxDu1:eminie FINAL1 ; !ÅW+HB
 +Com+PxDu2:enadth FINAL1 ; !ÅW+HB XXX sjekk -- feil?
!+Com+PxDu3 is in OBLREFL-NONPAL below

 +Com+PxPl1:eminie FINAL1 ; !ÅW+HB
 +Com+PxPl2:enadth FINAL1 ; !ÅW+HB XXX sjekk -- feil?
!+Com+PxPl3 is in OBLREFL-NONPAL below



LEXICON OBLREFL-NONPAL  !!= * @CODE@
+Gen+PxSg3+Use/NG:se FINAL1 ;
+Gen+PxPl3+Use/NG:sh FINAL1 ;
+Gen+PxPl3+Err/Orth:si FINAL1 ;

+Ill+PxSg1:sanne FINAL1 ; !KB
+Ill+PxSg1+Use/NG:sene FINAL1 ; !ÅW
+Ill+PxSg2:sadth FINAL1 ; ! KB, MMM
+Ill+PxSg3:sissie FINAL1 ; ! KB, MMM

+Ill+PxSg3+Use/NG:sasse FINAL1 ; ! MMM
+Ill+PxSg3+Err/Orth:sassee FINAL1 ; ! 

+Ill+PxDu3:sanne FINAL1 ; !ÅW and MMM

+Ill+PxPl3:sanne FINAL1 ; ! ÅW and MMM

+Ine+PxDu3+Use/NG:sinie FINAL1; !Acc. to ÅW+HB - might be a spellingerror
+Ine+PxPl3+Use/NG:sinie FINAL1; !Acc. to ÅW+HB - might be a spellingerror

+Ela+PxSg3+Err/Orth:sistie FINAL1 ; ! MMM
+Ela+PxPl3+Err/Orth:sistie FINAL1 ; !

+Ela+PxDu2:sijstie FINAL1 ; !ÅW+HB and MMM
+Ela+PxDu3:sistie FINAL1 ; !ÅW+HB and MMM

+Ela+PxPl2:sijstie FINAL1 ; !ÅW+HB and MMM
+Ela+PxPl3:sistie FINAL1 ; !ÅW+HB and MMM

+Com+PxSg3:sinie FINAL1 ; !KB, ÅW+HB. But MMM ok.
+Com+PxDu3:semissine FINAL1 ; !ÅW+HB and MMM
+Com+PxDu3+Err/Orth:sinie FINAL1 ; !KB, ÅW+HB! Not
+Com+PxPl3:semissine FINAL1 ; !ÅW+HB and MMM
+Com+PxPl3+Err/Orth:sinie FINAL1 ; !KB, ÅW+HB! Not

LEXICON OBLREFL-NONPAL2 !!= * @CODE@
+Ill+PxSg3:sissie FINAL1 ; !KB







! Dump

! removed from pron interr lexicon

!magkere+Pron+Interr+Attr:magkeres # ;
!magkere+Pron+Interr+Sg+Nom:magkere FINAL1 ;
!magkere+Pron+Interr+Sg+Acc:magkerem FINAL1 ;
!magkere+Pron+Interr+Sg+Gen:magkeren FINAL1 ;
!magkere+Pron+Interr+Sg+Ill:magkarasse FINAL1 ; !
!magkere+Pron+Interr+Sg+Ine+Attr:magkarinie FINAL1 ;
!magkere+Pron+Interr+Sg+Ine:magkarisnie FINAL1 ; ! sjekk
!magkere+Pron+Interr+Sg+Ela:magkaristie FINAL1 ;
!magkere+Pron+Interr+Sg+Com:magkarinie FINAL1 ;
!magkere+Pron+Interr+Pl+Nom:magkerh FINAL1 ;
!magkere+Pron+Interr+Pl+Acc:magkaridie FINAL1 ;
!magkere+Pron+Interr+Pl+Gen:magkeri FINAL1 ;
!magkere+Pron+Interr+Pl+Ill:magkaridie FINAL1 ;
!magkere+Pron+Interr+Pl+Ela:magkarijstie FINAL1 ;
!magkere+Pron+Interr+Pl+Ine:magkarijnie FINAL1 ;
!magkere+Pron+Interr+Pl+Com:magkerigujmie FINAL1 ;
