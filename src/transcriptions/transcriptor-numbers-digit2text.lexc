! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

Multichar_Symbols

  +Use/NG    ! Do not generate, for isme-ped.fst and apertium
  +String    ! Tag to denote non-numeric strings
  +NumNum    ! Tag to denote real numbers in one form or another

LEXICON Root
< %+String [a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|q|y|z|æ|ø|å|ä|ö|á|č|đ|ŋ|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Æ|Ø|Å|Ä|Ö|Á|Č|Đ|Ŋ|Š|Ŧ|Ž]* > COMMA ; ! This first line is to allow all letter strings.
!+NumNum NUMBERSECTION ;
!+NumNum COMMASECTION ;
 NUMBERSECTION ;
 COMMASECTION ;

LEXICON NUMBERSECTION
                       HUNDREDSM     ; ! 200M
             1:tjuetie HUNDREDM      ; ! 100M
 1:stoereluhkie+Use/NG HUNDREDM      ; ! 100M
1:stoerreluhkie+Use/NG HUNDREDM      ; ! 100M
                       TENSM         ; ! 20-99M
                       TEENSM        ; ! 10-19M
                       ONESM         ; ! 1-9M
                       HUNDREDST     ; ! 200000-999999
             1:tjuetie HUNDREDT      ; ! 100000-100999
                       TENST         ; ! 20000-99999,10000-10999
                       TEENST        ; ! 11000-19999
                       ONEST         ; ! 2000-9999
             1:tåvsene THOUSAND      ; ! 1000-1999
                       UNDERTHOUSAND ; ! 100-999
                       TENS          ; ! 20-99
                       TEENS         ; ! 10-19
                       ONES          ; ! 1-9

LEXICON HUNDREDSM
2:göökte   CUODIM ;
3:golme  CUODIM ;
4:njieljie  CUODIM ; ! Google: 459
4:njielje+Use/NG  CUODIM ;  ! Google: 218
4:nieljie+Use/NG  CUODIM ;  ! Google:  61
4:nielje+Use/NG  CUODIM ;  ! Google: irrelevant (20,600)
5:vïjhte  CUODIM ;
6:govhte  CUODIM ;
7:tjïjhtje  CUODIM ;
8:gaektsie  CUODIM ;
9:uktsie  CUODIM ;

LEXICON CUODIM
:tjuetie HUNDREDM ;
:stoereluhkie HUNDREDM ;
:stoerreluhkie HUNDREDM ;

LEXICON HUNDREDM
      TENSM ;
      TEENSM ;
%0:   ONESM ;
%0%0: MILJON ;

LEXICON TEENSM
1:luhkie  TEENM ;

LEXICON TEENM
1:akte      LOHKAIM ;
2:göökte    LOHKAIM ;
3:golme     LOHKAIM ;
4:njieljie  LOHKAIM ;
4:njielje+Use/NG   LOHKAIM ;
4:nieljie+Use/NG   LOHKAIM ;
4:nielje+Use/NG   LOHKAIM ;
5:vïjhte    LOHKAIM ;
6:govhte    LOHKAIM ;
7:tjïjhtje  LOHKAIM ;
8:gaektsie  LOHKAIM ;
9:uktsie    LOHKAIM ;

LEXICON LOHKAIM
MILJON ;

LEXICON TENSM
1%0:luhkie MILJON ;
2:göökte    LUHKIEM ;
3:golme     LUHKIEM ;
4:njieljie  LUHKIEM ;
4:njielje+Use/NG   LUHKIEM ;
4:nieljie+Use/NG   LUHKIEM ;
4:nielje+Use/NG   LUHKIEM ;
5:vïjhte    LUHKIEM ;
6:govhte    LUHKIEM ;
7:tjïjhtje  LUHKIEM ;
8:gaektsie  LUHKIEM ;
9:uktsie    LUHKIEM ;

LEXICON LUHKIEM
%0:luhkie  MILJON ;
:luhkie   ONESM ;

LEXICON ONESM
1:akte% miljon%   OVERTHOUSANDS ;
2:göökte          MILJON ;
3:golme           MILJON ;
4:njieljie        MILJON ;
4:njielje+Use/NG         MILJON ;
4:nieljie+Use/NG         MILJON ;
4:nielje+Use/NG         MILJON ;
5:vïjhte          MILJON ;
6:govhte          MILJON ;
7:tjïjhtje        MILJON ;
8:gaektsie        MILJON ;
9:uktsie          MILJON ;

LEXICON MILJON
:% miljona%   OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
                            HUNDREDST     ; ! x00.000.000
            1:tjuetie       HUNDREDT      ; ! 100.000.000
            1:stoereluhkie  HUNDREDT      ; ! 100.000.000
            1:stoerreluhkie HUNDREDT      ; ! 100.000.000
           %0:              TENST         ; !  20.000.000
           %0:              TEENST        ; !  10.000.000
         %0%0:              ONEST         ; !   2.000.000
        %0%01:tåvsene       THOUSAND      ; !   1.000.000
       %0%0%0:              UNDERTHOUSAND ;
     %0%0%0%0:              TENS          ;
     %0%0%0%0:              TEENS         ;
   %0%0%0%0%0:              ONES          ;
 %0%0%0%0%0%0:              COMMA         ; !  x000000

LEXICON HUNDREDST
2:göökte    CUODIT ;	! 200000-299999
3:golme     CUODIT ;	! 300000-399999
4:njieljie  CUODIT ;	! 400000-499999
4:njielje+Use/NG   CUODIT ;
4:nieljie+Use/NG   CUODIT ;
4:nielje+Use/NG   CUODIT ;
5:vïjhte    CUODIT ;	! 500000-599999
6:govhte    CUODIT ;	! 600000-699999
7:tjïjhtje  CUODIT ;	! 700000-799999
8:gaektsie  CUODIT ;	! 800000-899999
9:uktsie    CUODIT ;	! 900000-999999

LEXICON CUODIT
:tjuetie HUNDREDT ;	! 100000-199999
:stoereluhkie+Use/NG HUNDREDT ;	! 100000-199999
:stoerreluhkie+Use/NG HUNDREDT ;	! 100000-199999

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
TENST ;			! X2XYYY, X10YYY
TEENST ;		! X1XYYY
%01:akte THOUSANDS ;
%0: ONEST ;		! XX0YYY
%0%0: THOUSANDS ;	! X00YYY

LEXICON TEENST
1:luhkie   TEENT ;
1%0:luhkie THOUSANDS ;

LEXICON TEENT
1:akte      LOHKAIT ;
2:göökte    LOHKAIT ;
3:golme     LOHKAIT ;
4:njieljie  LOHKAIT ;
4:njielje+Use/NG   LOHKAIT ;
4:nieljie+Use/NG   LOHKAIT ;
4:nielje+Use/NG   LOHKAIT ;
5:vïjhte    LOHKAIT ;
6:govhte    LOHKAIT ;
7:tjïjhtje  LOHKAIT ;
8:gaektsie  LOHKAIT ;
9:uktsie    LOHKAIT ;

LEXICON LOHKAIT
THOUSANDS ;

LEXICON TENST
2:göökteluhkie    LUHKIET ;
3:golmeluhkie     LUHKIET ;
4:njieljieluhkie  LUHKIET ;
4:njieljeluhkie+Use/NG   LUHKIET ;
4:nieljieluhkie+Use/NG   LUHKIET ;
4:nieljeluhkie+Use/NG   LUHKIET ;
5:vïjhteluhkie    LUHKIET ;
6:govhteluhkie    LUHKIET ;
7:tjïjhtjeluhkie  LUHKIET ;
8:gaektsieluhkie  LUHKIET ;
9:uktsieluhkie    LUHKIET ;

LEXICON LUHKIET
%0: THOUSANDS ;
1:akte  THOUSANDS ;
ONEST ;

LEXICON ONEST
2:göökte  THOUSANDS ;
3:golme  THOUSANDS ;
4:njieljie  THOUSANDS ;
4:njielje+Use/NG  THOUSANDS ;
4:nieljie+Use/NG  THOUSANDS ;
4:nielje+Use/NG  THOUSANDS ;
5:vïjhte  THOUSANDS ;
6:govhte  THOUSANDS ;
7:tjïjhtje  THOUSANDS ;
8:gaektsie  THOUSANDS ;
9:uktsie  THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
:tåvsene  THOUSAND ;
:stoerretjuetie+Use/NG THOUSAND ;
:stoeretjuetie+Use/NG THOUSAND ;

LEXICON THOUSAND
UNDERTHOUSAND ;	   ! 1100-1999
%0: TENS ;	   ! 1020-1099
%0: TEENS ;	   ! 1010-1019
%0%0: ONES ;	   ! 1001-1009
%0%0%0: COMMA ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
1:tjuetie  HUNDRED ;
1:stoereluhkie+Use/NG  HUNDRED ;
1:stoerreluhkie+Use/NG  HUNDRED ;
1:tjuatede   CUODAAT ;
1:stoerelåhkede+Use/NG   CUODAAT ;
1:stoerrelåhkede+Use/NG   CUODAAT ;

LEXICON HUNDREDS
2:göökte    CUODI ;
3:golme     CUODI ;
4:njieljie  CUODI ;
4:njielje+Use/NG   CUODI ;
4:nieljie+Use/NG   CUODI ;
4:nielje+Use/NG   CUODI ;
5:vïjhte    CUODI ;
6:govhte    CUODI ;
7:tjïjhtje  CUODI ;
8:gaektsie  CUODI ;
9:uktsie    CUODI ;

LEXICON CUODI
:tjuetie   HUNDRED ;
:stoereluhkie+Use/NG   HUNDRED ;
:stoerreluhkie+Use/NG   HUNDRED ;
:tjuatede  CUODAAT ;
:stoerelåhkede+Use/NG  CUODAAT ;
:stoerrelåhkede+Use/NG  CUODAAT ;

LEXICON HUNDRED
      TENS ;
      TEENS ;
%0:   ONES ;
%0%0: COMMA ;

LEXICON CUODAAT
%0%0%.:    COMMA ;

LEXICON TEENS
1:luhkie  ONES ;


LEXICON TENS
1%0:luhkie  COMMA ;
1%0%.:låhkede COMMA ;
2:göökte  LUHKIE ;
3:golme  LUHKIE ;
4:njieljie  LUHKIE ;
4:njielje+Use/NG  LUHKIE ;
4:nieljie+Use/NG  LUHKIE ;
4:nielje+Use/NG  LUHKIE ;
5:vïjhte  LUHKIE ;
6:govhte  LUHKIE ;
7:tjïjhtje  LUHKIE ;
8:gaektsie  LUHKIE ;
9:uktsie  LUHKIE ;

LEXICON LUHKIE
   %0:luhkie  COMMA ;
 %0%.:låhkede COMMA ;
     :luhkie  ONES  ;

LEXICON ONES
CARDINAL ;
ORDINAL ;

LEXICON CARDINAL
 1:akte           COMMA ;
 2:göökte         COMMA ;
 3:golme          COMMA ;
 4:njieljie       COMMA ;
 4:njielje+Use/NG COMMA ;
 4:nieljie+Use/NG COMMA ;
 4:nielje+Use/NG  COMMA ;
 5:vïjhte         COMMA ;
 6:govhte         COMMA ;
 7:tjïjhtje       COMMA ;
 8:gaektsie       COMMA ;
 9:uktsie         COMMA ;

LEXICON ORDINAL
 1%.:voestes    COMMA ;
 2%.:mubpie     COMMA ;
 3%.:gåalmede   COMMA ;
 4%.:njealjede  COMMA ;
 5%.:vïjhtede   COMMA ;
 6%.:govhtede   COMMA ;
 7%.:tjïjhtjede COMMA ;
 8%.:gaaktsede  COMMA ;
 9%.:åktsede    COMMA ;

LEXICON COMMA
 ENDLEX ;
 COMMASECTION ;

LEXICON COMMASECTION  ! these are not spellchecked!!!
                  ENDLEX ;
! % komma% :,      Root ;
! % tjuohkkis% :%. Root ;
! % kolon% :%:     Root ;
! % sárggis% :%-   Root ;
 %=:% la%          Root ;
 %°:% gráda%       Root ;
 §:% paragráfa%    Root ;
! % násti% :%*     Root ;
 &:% ja%           Root ;

LEXICON ENDLEX
 # ;
