! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! Proper nouns morphology
! =======================

! Table of content
! ----------------
! Continuation lexica
! Personal names
!  First names
!  Surnames
! Place names
! ----------------


! Continuation lexica
! ===================

! Continuation lexica for personal names
! --------------------------------------


! At the moment, these continuation lexica are identical. But since I
! suspect that linguistic input will prove that the words behave
! differently, they are kept distinct.

! Names from Sweden


! Vowel final names
! - - - - - - - - -
LEXICON VNAME_ODD-plc
+N+Prop+Plc: VNAME_ODD ;

LEXICON VNAME_ODD-LOAN-plc
VNAME_ODD-LOAN ;

LEXICON VNAME_ODD-LOAN
+N+Prop+Plc+Sg+Nom:e%^DISIMP         FINAL1 ;
+N+Prop+Plc+Sg+Nom:e%^DISIMP         RHyph ;
+N+Prop+Plc: VNAME_ODD ;


LEXICON VNAME_ODD-mal
+N+Prop+Mal: VNAME_ODD ;

LEXICON VNAME_ODD-sur
+N+Prop+Sur: VNAME_ODD ;

LEXICON VNAME_ODD-org
+N+Prop+Org: VNAME_ODD ;

LEXICON VNAME_ODD-fem
+N+Prop+Fem: VNAME_ODD ;

LEXICON VNAME_ODD-obj
+N+Prop+Obj: VNAME_ODD ;

LEXICON VNAME_ODD ! Names ending in stressless vowel
+Sg: N_ODD_SG ;
+Sg+Nom:e%^DISIMP         RHyph ;
+Pl: N_ODD_PL ;
   N_ODD_ESS ;
+Attr:e%^DISIMP FINAL1 ;

LEXICON VNAME_ODD_NO_DISIMPH-mal
+N+Prop+Mal: VNAME_ODD_NO_DISIMPH ;

LEXICON VNAME_ODD_NO_DISIMPH-ani
+N+Prop+Ani: VNAME_ODD_NO_DISIMPH ;

LEXICON VNAME_ODD_NO_DISIMPH-fem
+N+Prop+Fem: VNAME_ODD_NO_DISIMPH ;

LEXICON VNAME_ODD_NO_DISIMPH-plc
+N+Prop+Plc: VNAME_ODD_NO_DISIMPH ;

LEXICON VNAME_ODD_NO_DISIMPH-org
+N+Prop+Org: VNAME_ODD_NO_DISIMPH ;

LEXICON VNAME_ODD_NO_DISIMPH-LOAN-plc
VNAME_ODD_NO_DISIMPH-LOAN ;

LEXICON VNAME_ODD_NO_DISIMPH-LOAN
+N+Prop+Plc+Sg+Nom:e         FINAL1 ;
+N+Prop+Plc: VNAME_ODD_NO_DISIMPH ;

LEXICON VNAME_ODD_NO_DISIMPH ! Names ending in stressless vowel
+Sg: N_ODD_SG_NO_DISIMPH ;
+Sg+Nom:e         RHyph ;
+Pl: N_ODD_PL_NO_DISIMPH ;
    N_ODD_ESS ;
+Attr:e FINAL1 ;

LEXICON N_ODD_SG_NO_DISIMPH
  +Nom:e         FINAL1 ;
  +Nom+PxSg1:%>amme FINAL1 ;
  +Nom+PxSg2:%>adth FINAL1 ;
  +Acc:e%>m      FINAL1 ;
  +Acc+PxSg2:%>amth FINAL1 ;
  +Gen:e%>n      FINAL1 ;
  +Gen+PxSg1:%>anne FINAL1 ;
  +Gen+PxSg2:%>anne FINAL1 ;
  +Gen+PxSg3:%>anne FINAL1 ;
 +Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
  +Ill+PxSg2:%>assedh FINAL1 ;
  +Ine:%>isnie           FINAL1 ;
  +Ela:%>istie           FINAL1 ;
  +Com:%>inie            FINAL1 ;
  +Com+PxSg2:%>æjnedh FINAL1 ;

LEXICON N_ODD_PL_NO_DISIMPH
  +Nom:%>h       FINAL1 ;
  +Acc:%>idie            FINAL1 ;
  +Gen:%>i       FINAL1 ;
  +Ill:%>idie            FINAL1 ;
  +Ine:%>inie            FINAL1 ;
  +Ela:%>ijstie          FINAL1 ;
  +Com:%>igujmie FINAL1 ;
 +Com+Use/Sub:%>igyjmie FINAL1 ; !


!LEXICON VNAME_EVEN ! Names ending in stressless vowel
!+N+Prop+Plc+Sg: NIEJTE_SG ;
!+N+Prop+Plc+Sg+Nom:e          RHyph ;
!+N+Prop+Plc+Pl: NIEJTE_PL ;
!+N+Prop+Plc:    EVEN_ESS ;
!+N+Prop+Plc+Attr:e FINAL1 ;

LEXICON VNAME-plc
+N+Prop+Plc: VNAME ;

LEXICON VNAME-surplc
+N+Prop+Plc: VNAME ;
+N+Prop+Sur: VNAME ;

LEXICON VNAME-org
+N+Prop+Org: VNAME ;

LEXICON VNAME-sur
+N+Prop+Sur: VNAME ;

LEXICON VNAME-mal
+N+Prop+Mal: VNAME ;

LEXICON VNAME-fem
+N+Prop+Fem: VNAME ;

LEXICON VNAME-obj
+N+Prop+Obj: VNAME ;

LEXICON VNAME-ani
+N+Prop+Ani: VNAME ;


LEXICON VNAME
 +Sg+Nom:          FINAL1 ;
 +Sg+Nom:          RHyph ;
 +Sg+Acc:%>m       FINAL1 ;
 +Sg+Gen:%>n       FINAL1 ;
 +Sg+Ill:%>se      FINAL1 ;
 +Sg+Ine:%>sne     FINAL1 ;
 +Sg+Ela:%>ste     FINAL1 ;
 +Sg+Com:%>jne      FINAL1 ;
 +Pl+Nom:%>h       FINAL1 ;
 +Pl+Acc:%>jde      FINAL1 ;
 +Pl+Gen:%>j       FINAL1 ;
 +Pl+Ill:%>jde      FINAL1 ;
 +Pl+Ine:%>jne      FINAL1 ;
 +Pl+Ela:%>jste    FINAL1 ;
 +Pl+Com:%>jgujmie  FINAL1 ;
 +Ess:%>jne         FINAL1 ;
 +Attr: FINAL1 ;
!SNAME_NIEJTE ; ! Even syllable as default - rename using either VNAME_ODD, or
! the appropriate even syllable lexicons.

LEXICON NYSTØ-plc
+N+Prop+Plc: NYSTØ ;

LEXICON NYSTØ-org
+N+Prop+Org: NYSTØ ;

LEXICON NYSTØ-sur
+N+Prop+Sur: NYSTØ ;

LEXICON NYSTØ-surplc
+N+Prop+Sur: NYSTØ ;
+N+Prop+Plc: NYSTØ ;

LEXICON NYSTØ-obj
+N+Prop+Obj: NYSTØ ;

LEXICON NYSTØ-mal
+N+Prop+Mal: NYSTØ ;

LEXICON NYSTØ-fem
+N+Prop+Fem: NYSTØ ;

LEXICON NYSTØ ! Names ending in stressed vowel, typically scand. compounds 
 SNAME_NIEJTE ; ! check whether this is correct!
+Attr: FINAL1 ;
+Sg+Nom:          RHyph ;


LEXICON INAME-plc
+N+Prop+Plc: INAME ;

LEXICON INAME-org
+N+Prop+Org: INAME ;

LEXICON INAME-sur
+N+Prop+Sur: INAME ;

LEXICON INAME-surplc
+N+Prop+Sur: INAME ;
+N+Prop+Plc: INAME ;

LEXICON INAME-obj
+N+Prop+Obj: INAME ;

LEXICON INAME-mal
+N+Prop+Mal: INAME ;

LEXICON INAME-fem
+N+Prop+Fem: INAME ;

LEXICON INAME ! Names ending in stressless vowel
 +Sg+Nom:i          FINAL1 ;
 +Sg+Nom:i          RHyph ;
 +Sg+Acc:i%>m       FINAL1 ;
 +Sg+Gen:i%>n       FINAL1 ;
 +Sg+Ill:e%>se      FINAL1 ;
 +Sg+Ine:e%>sne     FINAL1 ;
 +Sg+Ela:e%>ste     FINAL1 ;
 +Sg+Com:%>ine      FINAL1 ;
 +Pl+Nom:i%>h       FINAL1 ;
 +Pl+Acc:%>ide      FINAL1 ;
 +Pl+Gen:%>ij       FINAL1 ;
 +Pl+Ill:%>ide      FINAL1 ;
 +Pl+Ine:%>ine      FINAL1 ;
 +Pl+Ela:%>ijste    FINAL1 ;
 +Pl+Com:%>igujmie  FINAL1 ;
 +Ess:%>ine         FINAL1 ;
 +Attr:i FINAL1 ;



LEXICON OENAME-sur
+N+Prop+Sur: OENAME ;



LEXICON OENAME-mal
+N+Prop+Mal: OENAME ;


LEXICON OENAME
 SNAME_NIEJTE ;


LEXICON ONAME-plc
+N+Prop+Plc: ONAME ;

LEXICON ONAME-org
+N+Prop+Org: ONAME ;

LEXICON ONAME-sur
+N+Prop+Sur: ONAME ;

LEXICON ONAME-surplc
+N+Prop+Sur: ONAME ;
+N+Prop+Plc: ONAME ;

LEXICON ONAME-obj
+N+Prop+Obj: ONAME ;

LEXICON ONAME-mal
+N+Prop+Mal: ONAME ;

LEXICON ONAME-fem
+N+Prop+Fem: ONAME ;

LEXICON ONAME
  VNAME ; !until we get answer from SGM
 +Sg+Nom+Use/Sub:           FINAL1 ; ! !until we get answer from SGM
 +Sg+Nom+Use/Sub:           RHyph ; ! !until we get answer from SGM
 +Sg+Acc+Use/Sub:e%>m       FINAL1 ; ! !until we get answer from SGM
 +Sg+Gen+Use/Sub:e%>n       FINAL1 ; ! !until we get answer from SGM
 +Sg+Acc+Use/Sub:%>m        FINAL1 ; ! !until we get answer from SGM
 +Sg+Gen+Use/Sub:%>n        FINAL1 ; ! !until we get answer from SGM
 +Sg+Ill+Use/Sub:%>se       FINAL1 ; ! !until we get answer from SGM
 +Sg+Ine+Use/Sub:%>sne      FINAL1 ; ! !until we get answer from SGM
 +Sg+Ela+Use/Sub:%>ste      FINAL1 ; ! !until we get answer from SGM
 +Sg+Com+Use/Sub:%>jne      FINAL1 ; ! !until we get answer from SGM

! the sne / snie business remains to be sorted out.




LEXICON UENAME-plc
+N+Prop+Plc: UENAME ;


LEXICON UENAME-sur
+N+Prop+Sur: UENAME ;


LEXICON UENAME-mal
+N+Prop+Mal: UENAME ;


LEXICON UENAME
SNAME_NIEJTE ;

LEXICON UNAME-plc
+N+Prop+Plc: UNAME ;

LEXICON UNAME-ani
+N+Prop+Ani: UNAME ;

LEXICON UNAME-org
+N+Prop+Org: UNAME ;

LEXICON UNAME-sur
+N+Prop+Sur: UNAME ;

LEXICON UNAME-surplc
+N+Prop+Sur: UNAME ;
+N+Prop+Plc: UNAME ;

LEXICON UNAME-obj
+N+Prop+Obj: UNAME ;

LEXICON UNAME-mal
+N+Prop+Mal: UNAME ;

LEXICON UNAME-fem
+N+Prop+Fem: UNAME ;

LEXICON UNAME
 VNAME ; !until we get answer from SGM
 +Use/Sub: UNAME_EVEN ; ! !until we get answer from SGM

LEXICON UNAME_EVEN !SUB (sub lexicon names as well, or the code breaks)
 +Sg+Nom+Use/Sub:          FINAL1 ;  ! !until we get answer from SGM
 +Sg+Nom+Use/Sub:          RHyph ;  ! !until we get answer from SGM
 +Sg+Acc+Use/Sub:ve%>m       FINAL1 ; ! !until we get answer from SGM
 +Sg+Gen+Use/Sub:ve%>n       FINAL1 ; ! !until we get answer from SGM
 +Sg+Ill+Use/Sub:ve%>se       FINAL1 ; ! !until we get answer from SGM
 +Sg+Ine+Use/Sub:ve%>sne      FINAL1 ; ! !until we get answer from SGM
 +Sg+Ela+Use/Sub:ve%>ste      FINAL1 ; ! !until we get answer from SGM
 +Sg+Com+Use/Sub:ve%>jne      FINAL1 ; ! !until we get answer from SGM


LEXICON ZEENAME-plc
+N+Prop+Plc: ZEENAME ;

LEXICON ZEENAME-sur
+N+Prop+Sur: ZEENAME ;

LEXICON ZEENAME-fem
+N+Prop+Fem: ZEENAME ;

LEXICON ZEENAME
 +Sg+Nom:e          FINAL1 ;
 +Sg+Nom:e          RHyph ;
 +Sg+Acc:e%>m       FINAL1 ;
 +Sg+Gen:e%>n       FINAL1 ;
 +Sg+Ill:e%>se      FINAL1 ;
 +Sg+Ine:e%>sne     FINAL1 ;
 +Sg+Ela:e%>ste     FINAL1 ;
 +Sg+Com:e%>ine      FINAL1 ;
 +Pl+Nom:e%>h       FINAL1 ;
 +Pl+Acc:e%>ide      FINAL1 ;
 +Pl+Gen:e%>i       FINAL1 ;
 +Pl+Ill:e%>ide      FINAL1 ;
 +Pl+Ine:e%>ine      FINAL1 ;
 +Pl+Ela:e%>iste    FINAL1 ;
 +Pl+Com:e%>igujmie  FINAL1 ;
 +Ess:e%>ine         FINAL1 ;
 +Attr:e FINAL1 ;


LEXICON INAME_ODD-plc
+N+Prop+Plc: INAME_ODD ;


LEXICON INAME_ODD-sur
+N+Prop+Sur: INAME_ODD ;

LEXICON INAME_ODD-surplc
+N+Prop+Sur: INAME_ODD ;
+N+Prop+Plc: INAME_ODD ;


LEXICON INAME_ODD-mal
+N+Prop+Mal: INAME_ODD ;

LEXICON INAME_ODD-fem
+N+Prop+Fem: INAME_ODD ;


 LEXICON INAME_ODD ! Names ending in stressless vowel
  +Sg+Nom:%>i         FINAL1 ;
  +Sg+Nom:%>i         RHyph ;
 +Sg: INAME_ODD_SG ; ! name special
 +Pl: N_ODD_PL ;     ! normal noun
 :    N_ODD_ESS ;    ! normal noun
 +Attr:%>i FINAL1 ;     ! name special
  
  LEXICON INAME_ODD_SG
  +Acc:e%>%^DISIMPm      FINAL1 ;
  +Gen:e%>%^DISIMPn      FINAL1 ;
 +Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
  +Ine:%>isnie           FINAL1 ;
  +Ela:%>istie           FINAL1 ;
  +Com:%>inie            FINAL1 ;


LEXICON PERS_SNAME_ODD-mal
+N+Prop+Mal: PERS_SNAME_ODD ;

LEXICON PERS_SNAME_ODD-fem
+N+Prop+Fem: PERS_SNAME_ODD ;

LEXICON PERS_SNAME_ODD-ani
+N+Prop+Ani: PERS_SNAME_ODD ;

LEXICON PERS_SNAME_ODD
 VNAME_ODD ;
+Der1+Der/Dim+N:e%»tj   GÅATETJE ; !Åvletje


LEXICON SNAME_ODD-plc
+N+Prop+Plc: SNAME_ODD ;


LEXICON SNAME_ODD-org
+N+Prop+Org: SNAME_ODD ;

LEXICON SNAME_ODD-ani
+N+Prop+Ani: SNAME_ODD ;


LEXICON SNAME_ODD-mal
+N+Prop+Mal: SNAME_ODD ;

!LEXICON SNAME_ODD-fem
!+N+Prop+Fem: SNAME_ODD ;

LEXICON SNAME_ODD ! Southern Sami names
VNAME_ODD ;

LEXICON VAARATJE-plc
VAARATJE ;

LEXICON VAARATJE !names vowel expansion in all cases
 +N+Prop+Plc+Sg+Nom:e         FINAL1 ;
 +N+Prop+Plc+Sg+Nom:e         RHyph ;
 +N+Prop+Plc+Sg+Acc:e%>m      FINAL1 ;
 +N+Prop+Plc+Sg+Gen:e%>n      FINAL1 ;
 +N+Prop+Plc+Sg+Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
 +N+Prop+Plc+Sg+Ine:%>isnie           FINAL1 ;
 +N+Prop+Plc+Sg+Ela:%>istie           FINAL1 ;
 +N+Prop+Plc+Sg+Com:%>inie            FINAL1 ;
 +N+Prop+Plc+Pl+Nom:%>h       FINAL1 ;
 +N+Prop+Plc+Pl+Acc:%>idie            FINAL1 ;
 +N+Prop+Plc+Pl+Gen:%>i       FINAL1 ;
 +N+Prop+Plc+Pl+Ill:%>idie            FINAL1 ;
 +N+Prop+Plc+Pl+Ine:%>inie            FINAL1 ;
 +N+Prop+Plc+Pl+Ela:%>ijstie          FINAL1 ;
 +N+Prop+Plc+Pl+Com:%>igujmie FINAL1 ;
 +N+Prop+Plc+Ess:%>inie          FINAL1 ;
 +N+Prop+Plc+Attr:e FINAL1 ;

LEXICON JALKEDSEN ! Names ending in stressless vowel
 +N+Prop+Plc+Sg+Nom:e%^DISIMPn         FINAL1 ;
 +N+Prop+Plc+Sg+Nom:e%^DISIMPn         RHyph ;
 +N+Prop+Plc+Sg+Acc:e%>%^DISIMPm      FINAL1 ;
 +N+Prop+Plc+Sg+Gen:e%>%^DISIMPn      FINAL1 ;
 +N+Prop+Plc+Sg+Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
 +N+Prop+Plc+Sg+Ine:%>isnie           FINAL1 ;
 +N+Prop+Plc+Sg+Ela:%>istie           FINAL1 ;
 +N+Prop+Plc+Sg+Com:%>inie            FINAL1 ;
+N+Prop+Plc+Pl: N_ODD_PL ;
+N+Prop+Plc:    N_ODD_ESS ;
+N+Prop+Plc+Attr:e%^DISIMPn FINAL1 ;



LEXICON SNAME_ODD_PLUR ! Southern Sami names
+N+Prop+Plc+Pl: N_ODD_PL ;
+N+Prop+Plc+Pl+Gen:%>%^DISIMPi       RHyph ;
+N+Prop+Plc:    N_ODD_ESS ;
!+N+Prop+Plc+Attr:%>%^DISIMPh FINAL1 ;


LEXICON JAAVRETJEH
  +N+Prop+Plc+Sg+Nom:%>%^DISIMPe       RHyph ;
  +N+Prop+Plc+Pl+Nom:%>%^DISIMPeh       FINAL1 ;
  +N+Prop+Plc+Pl+Acc:%>idie            FINAL1 ;
  +N+Prop+Plc+Pl+Gen:%>%^DISIMPi       FINAL1 ;
  +N+Prop+Plc+Pl+Gen:%>%^DISIMPi       RHyph ;
  +N+Prop+Plc+Pl+Ill:%>idie            FINAL1 ;
  +N+Prop+Plc+Pl+Ine:%>inie            FINAL1 ;
  +N+Prop+Plc+Pl+Ela:%>ijstie          FINAL1 ;
  +N+Prop+Plc+Pl+Com:%>%^DISIMPigujmie FINAL1 ;
 +N+Prop+Plc+Pl+Com+Use/Sub:%>%^DISIMPigyjmie FINAL1 ; !
  +N+Prop+Plc:    N_ODD_ESS ;
!+N+Prop+Plc+Attr:e%>%^DISIMPh FINAL1 ;


LEXICON RITCHIE-plc
+N+Prop+Plc: RITCHIE ;

LEXICON RITCHIE-sur
+N+Prop+Sur: RITCHIE ;


LEXICON RITCHIE-mal
+N+Prop+Mal: RITCHIE ;

LEXICON RITCHIE-fem
+N+Prop+Fem: RITCHIE ;


LEXICON RITCHIE ! Ritchie:Ritchijasse
+Sg+Nom:e%^DISIMP          FINAL1 ;
+Sg+Nom:e%^DISIMP          RHyph ;
+Sg+Acc:je%>%^DISIMPm      FINAL1 ;
+Sg+Gen:je%>%^DISIMPn      FINAL1 ;
 +Sg+Ill:j%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
+Sg+Ine:j%>isnie           FINAL1 ;
+Sg+Ela:j%>istie           FINAL1 ;
+Sg+Com:j%>inie            FINAL1 ;
+Pl:j N_ODD_PL ;
:j    N_ODD_ESS ;
+Attr:e%^DISIMP FINAL1 ;


LEXICON PN_IE-plc
+N+Prop+Plc: PN_IE ;

LEXICON PN_IE-sur
+N+Prop+Sur: PN_IE ;

LEXICON PN_IE-org
+N+Prop+Mal: PN_IE ;



! Even syllable Sámi names:
LEXICON PN_IE ! as N_IE, but with Prop tag
+Sg: N_IE_SG ;
+Sg+Nom:ie                  RHyph ;
+Pl: N_IE_PL ;
:    N_IE_ESS ;
+Attr:ie FINAL1 ;

LEXICON MEHKIEN ! as N_IE, but with Prop tag
+N+Prop+Plc+Sg+Nom:ie%>n               FINAL1 ;
+N+Prop+Plc+Sg+Nom:ie%>n               RHyph ;
+N+Prop+Plc+Sg+Acc:ie%>m               FINAL1 ;
+N+Prop+Plc+Sg+Gen:ie%>n               FINAL1 ;
! +N+Prop+Plc+Sg+Ill:%>an                FINAL1 ; !SUB - is this possible? IllSg without Uml in -ie?
+N+Prop+Plc+Sg+Ill+Uml:%>%^1UMLan      FINAL1 ;
+N+Prop+Plc+Sg+Ine:e%>sne              FINAL1 ;
+N+Prop+Plc+Sg+Ela:e%>ste              FINAL1 ;
+N+Prop+Plc+Sg+Com:%>ine               FINAL1 ;
+N+Prop+Plc+Sg+Com+Uml:%>%^iUMLine     FINAL1 ;
+N+Prop+Plc+Pl: N_IE_PL ;
+N+Prop+Plc:    N_IE_ESS ;
+N+Prop+Plc+Attr:ie%>n FINAL1 ;


! Even syllable Sámi names:
LEXICON PN_IE_PLUR ! as N_IE, but with Prop tag
+N+Prop+Plc+Pl: N_IE_PL ;
+N+Prop+Plc+Pl+Gen:ie%>j               RHyph ;
+N+Prop+Plc+Pl+Gen+Uml+Use/NG:%>%^iUMLi       RHyph ;
+N+Prop+Plc:    N_IE_ESS ;
!+N+Prop+Plc+Attr:ie%>h FINAL1 ;


! LEXICON PN_OE_UML ! as N_OE_UML, but with Prop tag ! (still) not in use
!  +N+Prop+Plc: N_OE_SGILL_UML ;
!           PN_OE ;

LEXICON PN_OE-plc
+N+Prop+Plc: PN_OE ;

LEXICON PN_OE-mal
+N+Prop+Mal: PN_OE ;

LEXICON PN_OE ! as N_OE, but with Prop tag
+Sg: N_OE_SG ;
+Sg+Nom:oe          RHyph ;
+Pl: N_OE_PL ;
 N_OE_ESS ;
+Attr:oe FINAL1 ;


LEXICON PERS_SNAME_MAANA-fem
+N+Prop+Fem: PERS_SNAME_MAANA ;

LEXICON PERS_SNAME_MAANA-mal
+N+Prop+Mal: PERS_SNAME_MAANA ;

LEXICON PERS_SNAME_MAANA-ani
+N+Prop+Ani: PERS_SNAME_MAANA ;


LEXICON PERS_SNAME_MAANA
SNAME_MAANA ;
+Der1+Der/Dim+N:e%»tj   GÅATETJE ; !Åvletje

LEXICON SNAME_MAANA-fem
+N+Prop+Fem: SNAME_MAANA ;

LEXICON SNAME_MAANA-sur
+N+Prop+Sur: SNAME_MAANA ;

LEXICON SNAME_MAANA-mal
+N+Prop+Mal: SNAME_MAANA ;

LEXICON SNAME_MAANA-ani
+N+Prop+Ani: SNAME_MAANA ;

LEXICON SNAME_MAANA-plc
+N+Prop+Plc: SNAME_MAANA ;

LEXICON SNAME_MAANA-org
+N+Prop+Org: SNAME_MAANA ;

LEXICON SNAME_MAANA-obj
+N+Prop+Obj: SNAME_MAANA ;

LEXICON SNAME_MAANA-surplc
+N+Prop+Plc: SNAME_MAANA ;
+N+Prop+Sur: SNAME_MAANA ;



LEXICON SNAME_MAANA ! as MAANA, but with Prop tag
+Sg: MAANASGNOM ;
+Sg+Acc:a%>m       FINAL1 ;
+Sg+Gen:a%>n       FINAL1 ;
+Sg+Ill:e%>se      FINAL1 ;
+Sg+Ine:e%>sne     FINAL1 ;
+Sg+Ela:e%>ste     FINAL1 ;
+Sg+Com:%>ine      FINAL1 ;
+Sg+Nom:a          RHyph ;
+Pl: MAANA_PL ;
 EVEN_ESS ;
+Attr:a FINAL1 ;

LEXICON SNAME_MAANA-LOAN ! as MAANA, but with Prop tag
 +N+Prop+Plc+Sg+Nom:a          FINAL1 ;
 +N+Prop+Plc+Sg+Nom+PxSg1+Use/Sub:%>eme FINAL1 ;
 +N+Prop+Plc+Sg+Nom+PxSg2+Use/Sub:%>edh FINAL1 ;
 +N+Prop+Plc+Sg+Nom+PxSg3+Use/Sub:e%>se FINAL1 ;
 +N+Prop+Plc+Sg+Acc:a%>m       FINAL1 ;
 +N+Prop+Plc+Sg+Acc+PxSg1+Use/Sub:%>eme FINAL1 ;
 +N+Prop+Plc+Sg+Acc+PxSg1+Use/NG+Use/Sub:%>emem FINAL1 ;
 +N+Prop+Plc+Sg+Acc+PxSg2+Use/Sub:%>emdh FINAL1 ;
 +N+Prop+Plc+Sg+Acc+PxSg3+Use/Sub:e%>mse FINAL1 ;
 +N+Prop+Plc+Sg+Gen:a%>n       FINAL1 ;
 +N+Prop+Plc+Sg+Gen+PxSg1+Use/Sub:%>ene FINAL1 ;
 +N+Prop+Plc+Sg+Ill:e%>se      FINAL1 ;
 +N+Prop+Plc+Sg+Ill+PxSg1+Use/Sub:%>asanne FINAL1 ;
 +N+Prop+Plc+Sg+Ill+PxSg2+Use/Sub:%>asadth FINAL1 ;
 +N+Prop+Plc+Sg+Ine:e%>sne     FINAL1 ;
 +N+Prop+Plc+Sg+Ela:e%>ste     FINAL1 ;
 +N+Prop+Plc+Sg+Ela+PxSg1+Use/Sub:%>astanne FINAL1 ;
 +N+Prop+Plc+Sg+Ela+PxSg1+Use/NG+Use/Sub:%>astadth FINAL1 ;
 +N+Prop+Plc+Sg+Com:%>ine      FINAL1 ;
 +N+Prop+Plc+Sg+Com+PxSg2+Use/Sub:%>anadth FINAL1 ;
 +N+Prop+Plc+Sg+Nom:a          RHyph ;
 +N+Prop+Plc+Pl: MAANA_PL ;
 +N+Prop+Plc: EVEN_ESS ;
 +N+Prop+Plc+Attr:a FINAL1 ;


LEXICON SNAME_MAANA_PLUR ! as MAANA, but with Prop tag
+N+Prop+Plc+Pl: MAANA_PL ;
+N+Prop+Plc+Pl+Gen:a%>j       RHyph ;
+N+Prop+Plc:    EVEN_ESS ;
!+N+Prop+Plc+Attr:a%>h FINAL1 ;

LEXICON JÆLLAN ! as MAANA, but with Prop tag
+N+Prop+Plc+Sg+Nom:a%>n       FINAL1 ;
+N+Prop+Plc+Sg+Nom:a%>n       RHyph ;
+N+Prop+Plc+Sg+Acc:a%>m       FINAL1 ;
+N+Prop+Plc+Sg+Gen:a%>n       FINAL1 ;
+N+Prop+Plc+Sg+Ill:e%>se      FINAL1 ;
+N+Prop+Plc+Sg+Ine:e%>sne     FINAL1 ;
+N+Prop+Plc+Sg+Ela:e%>ste     FINAL1 ;
+N+Prop+Plc+Sg+Com:%>ine      FINAL1 ;
+N+Prop+Plc+Pl: MAANA_PL ;
+N+Prop+Plc:    EVEN_ESS ;
+N+Prop+Plc+Attr:a%>n FINAL1 ;

LEXICON PERS_SNAME_NIEJTE-mal
+N+Prop+Mal: PERS_SNAME_NIEJTE ;
+N+Prop+Mal+Der1+Der/Dim+N:e%»tj   GÅATETJE ; !Åvletje

LEXICON PERS_SNAME_NIEJTE-fem
+N+Prop+Fem: PERS_SNAME_NIEJTE ;
+N+Prop+Fem+Der1+Der/Dim+N:e%»tj   GÅATETJE ; !Åvletje

LEXICON PERS_SNAME_NIEJTE-ani
+N+Prop+Ani: PERS_SNAME_NIEJTE ;

LEXICON PERS_SNAME_NIEJTE
 SNAME_NIEJTE ;


LEXICON SNAME_NIEJTE-plc
+N+Prop+Plc: SNAME_NIEJTE ;

LEXICON SNAME_NIEJTE-ani
+N+Prop+Ani: SNAME_NIEJTE ;

LEXICON SNAME_NIEJTE-mal
+N+Prop+Mal: SNAME_NIEJTE ;

LEXICON SNAME_NIEJTE-fem
+N+Prop+Fem: SNAME_NIEJTE ;

LEXICON SNAME_NIEJTE-sur
+N+Prop+Sur: SNAME_NIEJTE ;

LEXICON SNAME_NIEJTE-surplc
+N+Prop+Sur: SNAME_NIEJTE ;
+N+Prop+Plc: SNAME_NIEJTE ;


LEXICON SNAME_NIEJTE-org
+N+Prop+Org: SNAME_NIEJTE ;

LEXICON SNAME_NIEJTE-obj
+N+Prop+Obj: SNAME_NIEJTE ;


LEXICON SNAME_NIEJTE ! as NIEJTE, but with Prop tag
+Sg: NIEJTE_SG ;
+Sg+Nom:e          RHyph ;
+Pl: NIEJTE_PL ;
:    EVEN_ESS ;
+Attr:e FINAL1 ;

LEXICON SNAME_NIEJTE-LOAN-plc
SNAME_NIEJTE-LOAN ; ! To be fixed

LEXICON SNAME_NIEJTE-LOAN ! as NIEJTE, but with Prop tag
  +N+Prop+Plc+Sg+Nom:e          FINAL1 ;
  +N+Prop+Plc+Sg+Nom+PxSg1+Use/Sub:%>eme FINAL1 ;
  +N+Prop+Plc+Sg+Nom+PxSg2+Use/Sub:%>edh FINAL1 ;
  +N+Prop+Plc+Sg+Nom+PxSg3+Use/Sub:e%>se FINAL1 ;
  +N+Prop+Plc+Sg+Acc:e%>m       FINAL1 ;
  +N+Prop+Plc+Sg+Acc+PxSg1+Use/Sub:%>eme FINAL1 ;
  +N+Prop+Plc+Sg+Acc+PxSg1+Use/NG+Use/Sub:%>emem FINAL1 ;
  +N+Prop+Plc+Sg+Acc+PxSg2+Use/Sub:%>emdh FINAL1 ;
  +N+Prop+Plc+Sg+Acc+PxSg3+Use/Sub:e%>mse FINAL1 ;
  +N+Prop+Plc+Sg+Gen:e%>n       FINAL1 ;
  +N+Prop+Plc+Sg+Gen+PxSg1+Use/Sub:%>ene FINAL1 ;
  +N+Prop+Plc+Sg+Ill:e%>se      FINAL1 ;
  +N+Prop+Plc+Sg+Ill+PxSg1+Use/Sub:%>esanne FINAL1 ;
  +N+Prop+Plc+Sg+Ill+PxSg2+Use/Sub:%>esadth FINAL1 ;
  +N+Prop+Plc+Sg+Ine:e%>sne     FINAL1 ;
  +N+Prop+Plc+Sg+Ela:e%>ste     FINAL1 ;
  +N+Prop+Plc+Sg+Ela+PxSg1+Use/Sub:%>estanne FINAL1 ;
  +N+Prop+Plc+Sg+Ela+PxSg1+Use/NG+Use/Sub:%>estadth FINAL1 ;
  +N+Prop+Plc+Sg+Com:%>ine      FINAL1 ;
  +N+Prop+Plc+Sg+Com+PxSg2+Use/Sub:%>enadth FINAL1 ;
+N+Prop+Plc+Sg+Nom:e          RHyph ;
+N+Prop+Plc+Pl: NIEJTE_PL ;
+N+Prop+Plc:    EVEN_ESS ;
+N+Prop+Plc+Attr:e FINAL1 ;


LEXICON TJØNNIN ! as NIEJTE, but with Prop tag
+N+Prop+Plc+Sg+Nom:i%>n          FINAL1 ;
+N+Prop+Plc+Sg+Nom:i%>n          RHyph ;
+N+Prop+Plc+Sg+Acc:i%>m       FINAL1 ;
+N+Prop+Plc+Sg+Gen:i%>n       FINAL1 ;
+N+Prop+Plc+Sg+Ill:e%>se      FINAL1 ;
+N+Prop+Plc+Sg+Ine:e%>sne     FINAL1 ;
+N+Prop+Plc+Sg+Ela:e%>ste     FINAL1 ;
+N+Prop+Plc+Sg+Com:%>ine      FINAL1 ;
+N+Prop+Plc+Pl+Nom:i%>h        FINAL1 ;
+N+Prop+Plc+Pl+Acc:%>ide      FINAL1 ;
+N+Prop+Plc+Pl+Gen:%>i        FINAL1 ;
+N+Prop+Plc+Pl+Ill:%>ide      FINAL1 ;
+N+Prop+Plc+Pl+Ine:%>ine      FINAL1 ;
+N+Prop+Plc+Pl+Ela:%>ijste    FINAL1 ;
+N+Prop+Plc+Pl+Com:%>igujmie  FINAL1 ;
 +N+Prop+Plc+Pl+Com+Use/Sub:%>igyjmie  FINAL1 ; !
+N+Prop+Plc:    EVEN_ESS ;
+N+Prop+Plc+Attr:i%>n FINAL1 ;


LEXICON SNAME_NIEJTE_PLUR ! as NIEJTE, but with Prop tag
+N+Prop+Plc+Pl: NIEJTE_PL ;
+N+Prop+Plc+Pl+Gen:%>i        RHyph ;
+N+Prop+Plc:    EVEN_ESS ;
!+N+Prop+Plc+Attr:%>h FINAL1 ;

LEXICON STAABREH ! as NIEJTE, but with Prop tag
  +N+Prop+Plc+Sg+Nom:e%>        RHyph ;
  +N+Prop+Plc+Pl+Nom:e%>h        FINAL1 ;
  +N+Prop+Plc+Pl+Acc:%>ide      FINAL1 ;
  +N+Prop+Plc+Pl+Gen:%>i        FINAL1 ;
  +N+Prop+Plc+Pl+Gen:%>i        RHyph ;
  +N+Prop+Plc+Pl+Ill:%>ide      FINAL1 ;
  +N+Prop+Plc+Pl+Ine:%>ine      FINAL1 ;
  +N+Prop+Plc+Pl+Ela:%>ijste    FINAL1 ;
  +N+Prop+Plc+Pl+Com:%>igujmie  FINAL1 ;
  +N+Prop+Plc:    EVEN_ESS ;
!+N+Prop+Plc+Attr:e%>h FINAL1 ;


LEXICON AEKTUVE
+N+Prop+Plc+Sg: BÅERUVE_SG ;
+N+Prop+Plc+Sg+Nom:uve%^DISIMP         RHyph ;
+N+Prop+Plc+Pl: BÅERUVE_PL ;
+N+Prop+Plc:    BÅERUVE_ESS ;
+N+Prop+Plc+Attr:uve FINAL1 ;

LEXICON LAANTE-surplc
+N+Prop+Plc:land CNAME_EVENLEX ; ! Småland
+N+Prop+Plc:laant SNAME_NIEJTE ; ! Smålaante
+N+Prop+Sur:land CNAME_EVENLEX ; ! Småland
+N+Prop+Sur:laant SNAME_NIEJTE ; ! Smålaante

LEXICON LAANTE-plc
+N+Prop+Plc:land CNAME_EVENLEX ; ! Småland
+N+Prop+Plc:laant SNAME_NIEJTE ; ! Smålaante

LEXICON LAANTE-sur
+N+Prop+Sur:land CNAME_EVENLEX ; ! Småland
+N+Prop+Sur:laant SNAME_NIEJTE ; ! Smålaante

LEXICON NAME_KONTO
+N+Prop+Org: KONTODECL ;


LEXICON ELV-plc
:^elv CNAME_EVEN-plc ;
:^elva SNAME_MAANA-plc ;
:^elven CNAME_ODD-plc ;

! Consonant final names
! - - - - - - - - -

 LEXICON SAAMI_CNAME_ODD ! Names ending in stressless vowel
 +N+Prop+Plc+Sg: SAAMI_CNAME_ODD_SG ; ! name special
 +N+Prop+Plc+Pl: N_ODD_PL ;     ! normal noun
 +N+Prop+Plc:    N_ODD_ESS ;    ! normal noun
 +N+Prop+Plc+Attr: FINAL1 ;     ! name special
  
  LEXICON SAAMI_CNAME_ODD_SG
  +Nom:         FINAL1 ; ! Herodes, Aasen
  +Nom:         RHyph ;
  +Acc:e%>%^DISIMPm      FINAL1 ;
  +Gen:e%>%^DISIMPn      FINAL1 ;
 +Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
  +Ine:%>isnie           FINAL1 ;
  +Ela:%>istie           FINAL1 ;
  +Com:%>inie            FINAL1 ;

LEXICON CNAME_ODD-mal
+N+Prop+Mal: CNAME_ODD ;  

LEXICON CNAME_ODD-fem
+N+Prop+Fem: CNAME_ODD ;  

LEXICON CNAME_ODD-plc
+N+Prop+Plc: CNAME_ODD ;  

LEXICON CNAME_ODD-sur
+N+Prop+Sur: CNAME_ODD ;  

LEXICON CNAME_ODD-obj
+N+Prop+Obj: CNAME_ODD ;  

LEXICON CNAME_ODD-ani
+N+Prop+Ani: CNAME_ODD ;  

LEXICON CNAME_ODD-org
+N+Prop+Org: CNAME_ODD ;  

LEXICON CNAME_ODD-surplc
+N+Prop+Sg: CNAME_ODD ;  
+N+Prop+Plc: CNAME_ODD ;  

 LEXICON CNAME_ODD ! Names ending in stressless vowel
 +Sg: CNAME_ODD_SG ; ! name special
 +Pl: CNAME_ODD_PL ;     ! name special
     N_ODD_ESS ;    ! normal noun
 +Attr: FINAL1 ;     ! name special
  
  LEXICON CNAME_ODD_SG
  +Nom:         FINAL1 ;
  +Nom:         RHyph ;
  +Acc:e%>m      FINAL1 ;
  +Gen:e%>n      FINAL1 ;
 +Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
  +Ine:%>isnie           FINAL1 ;
  +Ela:%>istie           FINAL1 ;
  +Com:%>inie            FINAL1 ;

LEXICON CNAME_ODD_PL
  +Nom:%>h       FINAL1 ;
  +Acc:%>idie            FINAL1 ;
  +Gen:%>i       FINAL1 ;
  +Ill:%>idie            FINAL1 ;
  +Ine:%>inie            FINAL1 ;
  +Ela:%>ijstie          FINAL1 ;
  +Com:%>igujmie FINAL1 ;
 +Com+Use/Sub:%>igyjmie FINAL1 ; !
 

 
LEXICON CNAME_ODD-LOAN ! Names ending in stressless vowel
 +N+Prop+Plc+Sg+Nom:         FINAL1 ;
 +N+Prop+Plc+Sg+Nom:         RHyph ;
 +N+Prop+Plc+Sg+Acc:e%>m      FINAL1 ;
 +N+Prop+Plc+Sg+Gen:e%>n      FINAL1 ;
 +N+Prop+Plc+Sg+Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
 +N+Prop+Plc+Sg+Ine:%>isnie           FINAL1 ;
 +N+Prop+Plc+Sg+Ela:%>istie           FINAL1 ;
 +N+Prop+Plc+Sg+Com:%>inie            FINAL1 ;
 +N+Prop+Plc+Pl: CNAME_ODD_PL ;     ! name special
 +N+Prop+Plc:    N_ODD_ESS ;    ! normal noun
 +N+Prop+Plc+Attr: FINAL1 ;     ! name special


LEXICON CNAME_EVEN-sur ! generates
+N+Prop+Sur: CNAME_EVENLEX ;

LEXICON CNAME_EVEN-mal ! generates
+N+Prop+Mal: CNAME_EVENLEX ;

LEXICON CNAME_EVEN-fem ! generates
+N+Prop+Fem: CNAME_EVENLEX ;

LEXICON CNAME_EVEN-org
+N+Prop+Org: CNAME_EVENLEX ;

LEXICON CNAME_EVEN-plc
+N+Prop+Plc: CNAME_EVENLEX ;

LEXICON CNAME_EVEN-obj
+N+Prop+Obj: CNAME_EVENLEX ;

LEXICON CNAME_EVEN-ani
+N+Prop+Ani: CNAME_EVENLEX ;

LEXICON CNAME_EVEN-surplc
+N+Prop+Plc: CNAME_EVENLEX ;
+N+Prop+Sur: CNAME_EVENLEX ;

LEXICON CNAME_EVEN ! Does not generate
+N+Prop: CNAME_EVENLEX ;

LEXICON CNAME_EVENLEX
+Sg: CNAME_EVEN_SG ;
+Pl: NIEJTE_PL ;
     EVEN_ESS ;
+Attr: FINAL1 ;

  LEXICON CNAME_EVEN_SG
  +Nom:          FINAL1 ;
  +Nom:          RHyph ;
  +Acc:e%>m       FINAL1 ;
  +Gen:e%>n       FINAL1 ;
  +Ill:e%>se      FINAL1 ;
  +Ine:e%>sne     FINAL1 ;
  +Ela:e%>ste     FINAL1 ;
  +Com:%>ine      FINAL1 ;


LEXICON CNAME_EVEN-LOAN
+N+Prop+Plc+Sg+Nom:          FINAL1 ;
+N+Prop+Plc+Sg+Nom:          RHyph ;
+N+Prop+Plc+Sg+Acc:e%>m       FINAL1 ;
+N+Prop+Plc+Sg+Gen:e%>n       FINAL1 ;
+N+Prop+Plc+Sg+Ill:e%>se      FINAL1 ;
+N+Prop+Plc+Sg+Ine:e%>sne     FINAL1 ;
+N+Prop+Plc+Sg+Ela:e%>ste     FINAL1 ;
+N+Prop+Plc+Sg+Com:%>ine      FINAL1 ;
+N+Prop+Plc+Pl: NIEJTE_PL ;
+N+Prop+Plc:     EVEN_ESS ;
+N+Prop+Plc+Attr: FINAL1 ;

LEXICON SAAMI_CNAME_EVEN ! Names ending in stressless vowel
+N+Prop+Sur+Sg: SAAMI_CNAME_EVEN_SG ;
+N+Prop+Sur+Pl: NIEJTE_PL ;
+N+Prop+Sur:    EVEN_ESS ;
+N+Prop+Sur+Attr: FINAL1 ;

  LEXICON SAAMI_CNAME_EVEN_SG
  +Nom:          FINAL1 ;
  +Nom:          RHyph ;
  +Nom:e          FINAL1 ;
  +Nom:e          RHyph ;
  +Acc:e%>m       FINAL1 ;
  +Gen:e%>n       FINAL1 ;
  +Ill:e%>se      FINAL1 ;
  +Ine:e%>sne     FINAL1 ;
  +Ela:e%>ste     FINAL1 ;
  +Com:%>ine      FINAL1 ;


LEXICON DÅERIES
 +N+Prop+Plc+Sg+Nom:ies             FINAL1 ;
 +N+Prop+Plc+Sg+Nom:ies             RHyph ;
 +N+Prop+Plc+Sg+Acc:%^1UMLas%>am      FINAL1 ;
 +N+Prop+Plc+Sg+Gen:%^1UMLas%>an      FINAL1 ;
 +N+Prop+Plc+Sg+Ill:%^1UMLas%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
 +N+Prop+Plc+Sg+Ine:%^1UMLas%>isnie           FINAL1 ;
 +N+Prop+Plc+Sg+Ela:%^1UMLas%>istie           FINAL1 ;
 +N+Prop+Plc+Sg+Com:%^1UMLas%>inie            FINAL1 ;
 +N+Prop+Plc+Pl+Nom:%^1UMLas%>h       FINAL1 ;
 +N+Prop+Plc+Pl+Acc:%^1UMLas%>idie            FINAL1 ;
 +N+Prop+Plc+Pl+Gen:%^1UMLas%>i       FINAL1 ;
 +N+Prop+Plc+Pl+Ill:%^1UMLas%>idie            FINAL1 ;
 +N+Prop+Plc+Pl+Ine:%^1UMLas%>inie            FINAL1 ;
 +N+Prop+Plc+Pl+Ela:%^1UMLas%>ijstie          FINAL1 ;
 +N+Prop+Plc+Pl+Com:%^1UMLas%>igujmie FINAL1 ;
 +N+Prop+Plc:ies    N_ODD_ESS ;    ! normal noun
 +N+Prop+Plc+Attr:ies FINAL1 ;     ! name special

LEXICON TJIRREDS
+N+Prop+Plc+Sg+Nom:d%>%^DISIMPs             FINAL1 ;
+N+Prop+Plc+Sg+Nom:d%>%^DISIMPs             RHyph ;
+A+Sg+Acc:de%>%^DISIMPm      FINAL1 ;
+A+Sg+Gen:de%>%^DISIMPn      FINAL1 ;
 +A+Sg+Ill:d%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
+A+Sg+Ine:d%>isnie           FINAL1 ;
+A+Sg+Ela:d%>istie           FINAL1 ;
+A+Sg+Com:d%>inie            FINAL1 ;
+A+Pl:d N_ODD_PL ;
+A:d    N_ODD_ESS ;
 +N+Prop+Plc+Attr:d%>%^DISIMPs FINAL1 ;     ! name special



