
! Proper nouns morphology
! =======================

! Table of content
! ----------------
! Continuation lexica
! Personal names
!  First names
!  Surnames
! Place names
! ----------------


! Continuation lexica
! ===================

! Continuation lexica for personal names
! --------------------------------------


! At the moment, these continuation lexica are identical. But since I
! suspect that linguistic input will prove that the words behave
! differently, they are kept distinct.

! Names from Sweden

LEXICON NAME ! these are the non-vow-final names from Sweden
CNAME_EVEN ;

! Vowel final names
! - - - - - - - - -

LEXICON VNAME_ODD ! Names ending in stressless vowel
+N+Prop+Sg: N_ODD_SG ;
+N+Prop+Pl: N_ODD_PL ;
+N+Prop:    N_ODD_ESS ;
+N+Prop+Attr: FINAL1 ;

LEXICON VNAME_EVEN ! Names ending in stressless vowel
+N+Prop+Sg: NIEJTE_SG ;
+N+Prop+Pl: NIEJTE_PL ;
+N+Prop:    EVEN_ESS ;
+N+Prop+Attr: FINAL1 ;


! Consonant final names
! - - - - - - - - -

 LEXICON CNAME_ODD ! Names ending in stressless vowel
  +N+Prop+Sg: CNAME_ODD_SG ; ! name special
  +N+Prop+Pl: N_ODD_PL ;     ! normal noun
  +N+Prop:    N_ODD_ESS ;    ! normal noun
  +N+Prop+Attr: FINAL1 ;     ! name special
  
  LEXICON CNAME_ODD_SG
  +Nom:         FINAL1 ;
  +Acc:e%>%^DISIMPm      FINAL1 ;
  +Gen:e%>%^DISIMPn      FINAL1 ;
  +Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
  +Ine:%>isnie           FINAL1 ;
  +Ela:%>istie           FINAL1 ;
  +Com:%>inie            FINAL1 ;



LEXICON CNAME_EVEN ! Names ending in stressless vowel
+N+Prop+Sg: CNAME_EVEN_SG ;
+N+Prop+Pl: NIEJTE_PL ;
+N+Prop:    EVEN_ESS ;
+N+Prop+Attr: FINAL1 ;

  LEXICON CNAME_EVEN_SG
  +Nom:          FINAL1 ;
  +Acc:e%>m       FINAL1 ;
  +Gen:e%>n       FINAL1 ;
  +Ill:e%>se      FINAL1 ;
  +Ine:e%>sne     FINAL1 ;
  +Ela:e%>ste     FINAL1 ;
  +Com:%>ine      FINAL1 ;


LEXICON SAAMI_CNAME_EVEN ! Names ending in stressless vowel
+N+Prop+Sg: SAAMI_CNAME_EVEN_SG ;
+N+Prop+Pl: NIEJTE_PL ;
+N+Prop:    EVEN_ESS ;
+N+Prop+Attr: FINAL1 ;

  LEXICON SAAMI_CNAME_EVEN_SG
  +Nom:          FINAL1 ;
  +Nom:e          FINAL1 ;
  +Acc:e%>m       FINAL1 ;
  +Gen:e%>n       FINAL1 ;
  +Ill:e%>se      FINAL1 ;
  +Ine:e%>sne     FINAL1 ;
  +Ela:e%>ste     FINAL1 ;
  +Com:%>ine      FINAL1 ;


LEXICON NYSTØ ! Names ending in stressed vowel, typically scand. compounds 
VNAME ; ! check whether this is correct!


LEXICON SNAME_ODD ! Southern Sami names
VNAME_ODD ;

! Even syllable Sámi names:
LEXICON PN_IE ! as N_IE, but with Prop tag
+N+Prop+Sg: N_IE_SG ;
+N+Prop+Pl: N_IE_PL ;
+N+Prop:    N_IE_ESS ;
+N+Prop+Attr: FINAL1 ;

! LEXICON PN_OE_UML ! as N_OE_UML, but with Prop tag ! (still) not in use
!  +N+Prop: N_OE_SGILL_UML ;
!           PN_OE ;

LEXICON PN_OE ! as N_OE, but with Prop tag
+N+Prop+Sg: N_OE_SG ;
+N+Prop+Pl: N_OE_PL ;
+N+Prop:    N_OE_ESS ;
+N+Prop+Attr: FINAL1 ;

LEXICON SNAME_MAANA ! as MAANA, but with Prop tag
+N+Prop+Sg: MAANA_SG ;
+N+Prop+Pl: MAANA_PL ;
+N+Prop:    EVEN_ESS ;
+N+Prop+Attr: FINAL1 ;

LEXICON SNAME_NIEJTE ! as NIEJTE, but with Prop tag
+N+Prop+Sg: NIEJTE_SG ;
+N+Prop+Pl: NIEJTE_PL ;
+N+Prop:    EVEN_ESS ;
+N+Prop+Attr: FINAL1 ;


! Old names, for backwards compatibility until all entries have got new names
! ---------------------------------------------------------------------------

LEXICON CNAME
CNAME_EVEN ; ! Even syllable as default - rename using either CNAME_EVEN or
! CNAME_ODD.

LEXICON VNAME
SNAME_NIEJTE ; ! Even syllable as default - rename using either VNAME_ODD, or
! the appropriate even syllable lexicons.

LEXICON INAME ! Names ending in stressless vowel
 +N+Prop+Sg+Nom:i          FINAL1 ;
 +N+Prop+Sg+Acc:i%>m       FINAL1 ;
 +N+Prop+Sg+Gen:i%>n       FINAL1 ;
 +N+Prop+Sg+Ill:e%>se      FINAL1 ;
 +N+Prop+Sg+Ine:e%>sne     FINAL1 ;
 +N+Prop+Sg+Ela:e%>ste     FINAL1 ;
 +N+Prop+Sg+Com:%>ine      FINAL1 ;
 +N+Prop+Pl+Nom:i%>h       FINAL1 ;
 +N+Prop+Pl+Acc:%>ide      FINAL1 ;
 +N+Prop+Pl+Gen:%>ij       FINAL1 ;
 +N+Prop+Pl+Ill:%>ide      FINAL1 ;
 +N+Prop+Pl+Ine:%>ine      FINAL1 ;
 +N+Prop+Pl+Ela:%>ijste    FINAL1 ;
 +N+Prop+Pl+Com:%>igujmie  FINAL1 ;
 +N+Prop+Ess:%>ine         FINAL1 ;
 +N+Prop+Attr:i FINAL1 ;

LEXICON ONAME
  +Nom:          FINAL1 ;
  +Acc:e%>m       FINAL1 ;
  +Gen:e%>n       FINAL1 ;
  +Acc:%>m       FINAL1 ;
  +Gen:%>n       FINAL1 ;
  +Ill:%>se       FINAL1 ;
  +Ine:%>sne      FINAL1 ;
  +Ela:%>ste      FINAL1 ;
  +Com:%>jne      FINAL1 ;

! the sne / snie business remains to be sorted out.

LEXICON UNAME
UNAME_EVEN ;

LEXICON UNAME_EVEN
  +Nom:          FINAL1 ;
  +Acc:ve%>m       FINAL1 ;
  +Gen:ve%>n       FINAL1 ;
  +Ill:ve%>se       FINAL1 ;
  +Ine:ve%>sne      FINAL1 ;
  +Ela:ve%>ste      FINAL1 ;
  +Com:ve%>jne      FINAL1 ;

 LEXICON INAME_ODD ! Names ending in stressless vowel
  +N+Prop+Sg+Nom:%>i         FINAL1 ;
  +N+Prop+Sg: INAME_ODD_SG ; ! name special
  +N+Prop+Pl: N_ODD_PL ;     ! normal noun
  +N+Prop:    N_ODD_ESS ;    ! normal noun
  +N+Prop+Attr:%>i FINAL1 ;     ! name special
  
  LEXICON INAME_ODD_SG
  +Acc:e%>%^DISIMPm      FINAL1 ;
  +Gen:e%>%^DISIMPn      FINAL1 ;
  +Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
  +Ine:%>isnie           FINAL1 ;
  +Ela:%>istie           FINAL1 ;
  +Com:%>inie            FINAL1 ;


