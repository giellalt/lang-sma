Multichar_Symbols

  A1 A2 A3 A4 E1 E2 I1 I2 I3 I4 O1 O2 U1 U2 Æ Å1 Å2 Å3 Å4 D1
  +N +Sg +Pl +Nom +Acc +Gen +Ine +Ela +Ill +Ess +Kom


! The root lexicon
! ================

LEXICON Root
  0:#			Noun	;



! Nominal inflection sublexica
! ============================


! Inflection for nouns ending in '-ie'
! ------------------------------------

LEXICON N_IE
  +N+Sg+Ill:%>A1n	FINAL1	;	! Special Ill
  +N+Sg+Ess:%>inI1	FINAL1	;	! Special Ess
  +N: J-LESS-CASE		;	! cases without -i/j-
  +N: I-CLASS			;	! cases with -i-


! Inflection for nouns ending in oe
! ---------------------------------

LEXICON N_OE
  +N+Sg+Ill:%>A1sD1e	FINAL1	;	! Causes Umlaut
  +N+Sg+Ill:%>se	FINAL1	;	! does NOT cause Umlaut
  +N+Sg+Ess:%>jnI1	FINAL1	;	! cases without -i/j-
  +N: J-LESS-CASE		;	! cases with -j-
  +N: I-CLASS			;


! Inflection for all other nouns
! ------------------------------

LEXICON N_OTHERS
  +N+Sg+Ill:%>A1sD1e	FINAL1	;	! Special Ill
  +N+Sg+Ess:%>inI1	FINAL1	;	! Special Ess
  +N: J-LESS-CASE		;	! cases without -i/j-
  +N: I-CLASS			;	! cases with -i-



! Sublexica to handle the i/j difference between oe nouns and other
! -----------------------------------------------------------------

LEXICON I-CLASS
  +N+Pl+Ela:%>ijstI1	FINAL1	;	! Ela has -iste
  :%>i			IJ-CASE ;	! others have -ine etc.

LEXICON J-CLASS
  +N+Pl+Ela:%>jstI1	FINAL1	;	! Ela has -ijste
  :%>j			IJ-CASE ;	! others have -jne etc.



! Remaining grammatical case lexica for the nouns
! ===============================================


LEXICON J-LESS-CASE
  +Sg+Nom:0		FINAL1	;
  +Sg+Acc:%>m		FINAL1	;
  +Sg+Gen:%>n		FINAL1	;
  +Sg+Ine:%>snI1	FINAL1	;
  +Sg+Ela:%>stI1	FINAL1	;
  +Pl+Nom:%>h		FINAL1	;
  +Pl+Gen:%>I3		FINAL1	;
  +Kom:%>I3+gujmie	FINAL1	;

LEXICON IJ-CASE
  +Sg+Kom:nI1		FINAL1	;
  +Pl+Acc:dI1		FINAL1	;
  +Pl+Ill:dI1		FINAL1	;
  +Pl+Ine:nI1		FINAL1	;




! Final lexicon
! =============


LEXICON FINAL1
  0:#			#	;
