Multichar_Symbols

  +N +Sg +Pl +Nom +Acc +Gen +Ine +Ela +Ill +Com +Ess
  +PxSg1 +PxSg2 +PxSg3
  +PxDu1 +PxDu2 +PxDu3
  +PxPl1 +PxPl2 +PxPl3

  +V +Prs +Prt
  +Sg1 +Sg2 +Sg3
  +Du1 +Du2 +Du3
  +Pl1 +Pl2 +Pl3

  +Inf +Neg  +PrfPrc
! +VGen +Ger
  +Ind
  +Imp +ImpII !Den siste berre nektingsverbet

! Berre hjelpeverbet/nektingsverbet (Pot. for andre V?):
  +Pot +Cond  ! +Opt (Hasselbrink 48.3.I, 48.14, 48.15, 48.21)

! Frå nordsamisk:
! +Actor +Clt
! +A
  +Comp +Superl
  +Attr

  +Pron +Pers +Dem +Adp +Po +Adv +Interj +Num
  +CS +CC +Pcle +CLB +PUNCT +LEFT +RIGHT

  A1 A2 A3 A4 E1 I1 I2 I3 I4 O1 O2 U1 U2 Æ Å1 Å2 Å3 Å4 D1
  X2
  E2 ! insert e ! Fjern denne.
  %^DISIMP %^1UML %^2UML %^3UML %^3sUML %^3dUML %^iæUML

! ================
! The root lexicon
! ================

LEXICON Root
  0:#			Noun	;
  0:#			ProperNoun	;  
  0:#			Verb	;
  0:#			Pronoun	;
  0:#			Adverb	;
  0:#			Subjunction	;
  0:#			Conjunction	;
  0:#			Particle	;
  0:#			Adposition	;
  0:#			Punctuation	;  
  0:#			Interjection	;  
  0:#			Numeral ;


! ============================
! Nominal inflection sublexica
! ============================



! Inflection for nouns ending in '-ie'
! ------------------------------------

LEXICON N_IE
  +N+Sg+Ill:%>A1n	FINAL1	;	! Special Ill
  +N+Ess:%>inI1		FINAL1	;	! Special Ess
  +N: J-LESS-CASE		;	! cases without -i/j-
  +N: I-CLASS			;	! cases with -i-


! Inflection for nouns ending in oe
! ---------------------------------

LEXICON N_OE
  +N+Sg+Ill:%>A1sD1e	FINAL1	;	! Causes Umlaut
  +N+Sg+Ill:%>se	FINAL1	;	! does NOT cause Umlaut
  +N+Ess:%>jnI1		FINAL1	;	! cases without -i/j-
  +N: J-LESS-CASE		;	! cases with -j-
  +N: J-CLASS			;


! Inflection for all other nouns
! ------------------------------

LEXICON N_OTHERS
  +N+Sg+Ill:%>A1sD1e	FINAL1	;	! Special Ill
  +N+Sg+Ess:%>inI1	FINAL1	;	! Special Ess
  +N: J-LESS-CASE		;	! cases without -i/j-
  +N: I-CLASS			;	! cases with -i-




! Sublexica to handle the i/j difference between oe nouns and other
! -----------------------------------------------------------------

LEXICON I-CLASS
  +Pl+Ela:%>ijstI1	FINAL1	;	! Ela has -iste
  :%>i			IJ-CASE ;	! others have -ine etc.

LEXICON J-CLASS
  +Pl+Ela:%>jstI1	FINAL1	;	! Ela has -ijste
  :%>j			IJ-CASE ;	! others have -jne etc.



! Remaining grammatical case lexica for the nouns
! ===============================================


LEXICON J-LESS-CASE
  +Sg+Nom:  	    FINAL1  ; 
  +Sg+Acc:%>m		FINAL1	;
  +Sg+Gen:%>n		FINAL1	;
  +Sg+Ine:%>snI1	FINAL1	;
  +Sg+Ela:%>stI1	FINAL1	;
  +Pl+Nom:%>h		FINAL1	;
  +Pl+Gen:%>I3		FINAL1	;
  +Pl+Com:%>I3+gujmie	FINAL1	;

LEXICON IJ-CASE
  +Sg+Com:nI1		FINAL1	;
  +Pl+Acc:dI1		FINAL1	;
  +Pl+Ill:dI1		FINAL1	;
  +Pl+Ine:nI1		FINAL1	;







! ===========================
! Verbal inflection sublexica
! ===========================


! The auxiliaries
! ===============

LEXICON LEA

+Ind+Prs:l LEA-PRES	;
+Ind+Prt:l LEA-PRET	;

LEXICON LEA-PRES
+Sg1:eam		FINAL1	;
+Sg2:eah		FINAL1	;
+Sg3:ea			FINAL1	;
+Du1:ean		FINAL1	;
+Du2:idien		FINAL1	;
+Du3:ægan		FINAL1	;
+Du3:æban		FINAL1	; !SUB
+Pl1:ibie		FINAL1	;
+Pl2:idie		FINAL1	;
+Pl3:eah		FINAL1	;

LEXICON LEA-PRET
+Sg1:im		FINAL1	;
+Sg2:ih		FINAL1	;
+Sg3:ij		FINAL1	;
+Du1:imen	FINAL1	;
+Du2:iden	FINAL1	;
+Du3:igan	FINAL1	;
+Pl1:imh	FINAL1	;
+Pl2:idh	FINAL1	;
+Pl3:in		FINAL1	;


! The negative verb
! =================

LEXICON NEG
+Ind+Prs:	IJ-PRES ;
+Ind+Prt:idtj	VSUF-PRT ;

LEXICON IJ-PRES
+Sg1:im		FINAL1	;
+Sg2:ih		FINAL1	;
+Sg3:ij		FINAL1	;
+Du1:ean	FINAL1	;
+Du2:idien	FINAL1	;
+Du3:eakan	FINAL1	;
+Pl1:ibie	FINAL1	;
+Pl2:idie	FINAL1	;
+Pl3:eah	FINAL1	;



! Odd-syllable verbs
! ==================

! Odd syllable verbs differ in Prt Sg3.
! This form is treated separately, and
! the rest of the paradigm is conflated.

LEXICON GOLTELIDH ! one Verbh class
+V+Ind+Prt+Sg3:%>e FINAL1 ;
              :%>  V-ODD ;

LEXICON DÅERIEDIDH ! The other 4 Verbh classes
+V+Ind+Prt+Sg3:%>i FINAL1 ;
              :%>  V-ODD ;


LEXICON V-ODD

! Finite forms
! ------------
 +V+Ind+Prs:%> VSUF-ODD-PRS ;
 +V+Ind+Prt:%> VSUF-ODD-PRT ;
 +V+Imp:%>     VSUF-ODD-IMP ;

! Infinite forms
! --------------
 +V+Inf:idh		        FINAL1 ;
 +V+VGen:%^DISIMPeminie	FINAL1 ; ! GH Modalis, KB måtesform
		                 		 ! GH Voluntativ
 +V+Act:amme		    FINAL1 ; ! GH verbalsubst, KB nomen actionis
 +V+Ger:%^DISIMPeminie	FINAL1 ;
 +V+PrsPrc:ije	        FINAL1 ; ! must go to nominal lexicon instead
				                 ! Ptz.Fut b'ïssejässaa
 +V+PrfPrc:amme	        FINAL1 ;
 +V+ConNeg:%^DISIMPh    FINAL1 ;
 +V+VAbess:h		    FINAL1 ; ! Check


! Contract verbs
! ==============


LEXICON BILLIJIDH
 +V+Ind+Prs:%> VSUF-CONTR-PRS ;
 +V+Ind+Prt:%> VSUF-CONTR-PRT ;
 +V+Imp:%>     VSUF-CONTR-IMP ;

! Infinite forms
! --------------
 +V+Inf:%>ijidh		    FINAL1 ;
 +V+VGen:%>eminie	    FINAL1 ; ! GH Modalis, KB måtesform
            		    		 ! GH Voluntativ
 +V+Act:%>eme		    FINAL1 ; ! GH verbalsubst, KB nomen actionis
 +V+Ger:%>eminie	    FINAL1 ;
 +V+PrsPrc:%>ije	    FINAL1 ; ! must go to nominal lexicon instead
				                 ! Ptz.Fut b'ïssejässaa
 +V+PrfPrc:%>eme	    FINAL1 ;
 +V+ConNeg:%^DISIMP%>h	FINAL1 ;
 +V+VAbess:%>h		    FINAL1 ; ! Check



! Even-syllable verbs
! ===================

LEXICON BÅETEDH ! row A
 +V+Ind+Prs:%>%^1UMLa         V-I-PRS-SG ; ! class I special
 +V+Ind+Prs:%>                V-EVEN-PRS-DUPL ;
 +V+Ind+Prt:%>                VSUF-PRT ;
 +V+Imp:%>                    VSUF-EVEN-IMP ;
 +V+Inf:%>edh                 FINAL1 ;
 +V+Ger:%>ieminie             FINAL1 ;
 +V+PrfPrc:%>%^1UMLeme        FINAL1 ;
 +V+ConNeg:%>ieh              FINAL1 ;
 ! row F trigger for Umlaut Prt

LEXICON TJEARODH ! row C
 +V+Ind+Prs:%>          V-II-PRS-SG ; ! class II special
 +V+Ind+Prs:%>          V-II-PRS-DUPL ;
 +V+Ind+Prt:%>oej       VSUF-PRT ;
 +V+Imp:%>              VSUF-II-EVEN-IMP ;
 +V+Inf:%>odh           FINAL1 ;
 +V+Ger:%>oeminie       FINAL1 ;
 +V+PrfPrc:%>%^2UMLeme  FINAL1 ;
 +V+ConNeg:%>oeh        FINAL1 ;

LEXICON GUARKEDH ! row B
 +V+Ind+Prs:%>    V-III-PRS-SG ; ! class III special
 +V+Ind+Prs:%>	  V-III-PRS-DUPL ;
 +V+Ind+Prt:%>aj  VSUF-PRT ;
 +V+Imp:%>        VSUF-III-EVEN-IMP ;
 +V+Inf:%>adh     FINAL1 ;
 +V+Ger:%>minI1   FINAL1 ;
 +V+PrfPrc:%>me   FINAL1 ;
 +V+ConNeg:%>0h   FINAL1 ;

LEXICON TJOEHPEDH ! row D
 +V+Ind+Prs:%>     V-IV-EVEN-PRS ; 
 +V+Ind+Prt:%>     VSUF-PRT ;
 +V+Imp:%>         VSUF-IV-EVEN-IMP ;
 +V+Inf:%>edh      FINAL1 ;
 +V+Ger:%>eminie   FINAL1 ;
 +V+PrfPrc:%>eme   FINAL1 ;
 +V+ConNeg:%>0h    FINAL1 ;

LEXICON VÅÅJNEDH ! row E
 +V+Ind+Prs:%>   VSUF-V-EVEN-PRS ; 
 +V+Ind+Prt:%>   VSUF-PRT ;
 +V+Imp:%>       VSUF-EVEN-IMP ;
 +V+Inf:%>edh    FINAL1 ;
 +V+Ger:%>eminie FINAL1 ;
 +V+PrfPrc:%>eme FINAL1 ;
 +V+ConNeg:%>h   FINAL1 ;

LEXICON GÖÖLEDH ! row F
 +V+Ind+Prs:%>    VSUF-VI-EVEN-PRS ; 
 +V+Ind+Prt:%>    VSUF-PRT ;
 +V+Imp:%>        VSUF-EVEN-IMP ;
 +V+Inf:%>edh     FINAL1 ;
 +V+Ger:%>minie   FINAL1 ;
 +V+PrfPrc:%>eme  FINAL1 ;
 +V+ConNeg:%>h    FINAL1 ;






! Verbal affixes
! ==============

! Finite forms
! ------------

! Even

LEXICON V-I-PRS-SG ! Merge with V-EVEN-PRS if nothing special here.
 V-PRS-SG-12 ;
 V-PRS-SG-3 ;

LEXICON V-II-PRS-SG
 :oe       V-PRS-SG-12 ;
 :%^2UMLe  V-PRS-SG-3 ; ! ?? parallels the byörkesne case

LEXICON V-II-PRS-DUPL
 :o			    VSUF-EVEN-PRS-DUPL ;
! +Du1:oen      FINAL1	;
! +Du2:oden		FINAL1	;
! +Du3:oejægan	FINAL1	;
! +Du3:oeben	FINAL1	; !SUB
! +Pl1:obe		FINAL1	;
! +Pl2:ode		FINAL1	;
! +Pl3:oeh		FINAL1	;

LEXICON V-III-PRS-SG
 :a  V-PRS-SG-12 ; ! This should give guarka
 :oe V-PRS-SG-3 ; ! this is wrong, gives *guarka pro guarkoe
! perhaps use A2 as 0 rule, triggers deletion in front of aff border
! but then oe aff must come afterwards
 :oe FINAL1 ; ! well, perhaps a more archi-looking affix ?

LEXICON V-III-PRS-DUPL
 :e			VSUF-EVEN-PRS-DUPL ;
! +Du1:an       FINAL1	; !e>a
! +Du2:eden		FINAL1	;
! +Du3:ajægan	FINAL1	; !e>a 
! +Du3:aben	FINAL1	; !SUB !e>a
! +Pl1:ebe		FINAL1	;
! +Pl2:ede		FINAL1	;
! +Pl3:ah		FINAL1	; !e>a

LEXICON VSUF-V-EVEN-PRS
 :e  V-PRS-SG-1 ;
     V-PRS-SG-2 ;
 :oe V-PRS-SG-3 ;
 :e				VSUF-EVEN-PRS-DUPL ;
! +Du1:oen       FINAL1	; !ee>oe
! +Du2:eden		FINAL1	;
! +Du3:ijægan	FINAL1	; !ee>i
! +Du3:iben	    FINAL1	; !SUB !ee>i
! +Pl1:ebe		FINAL1	;
! +Pl2:ede		FINAL1	;
! +Pl3:oeh		FINAL1	; !ee>oe



!! LEXICON V-EVEN-PRS
!!   V-PRS-SG-12 ;
!!   V-PRS-SG-3 ;
!!    V-EVEN-PRS-DUPL ;
   
LEXICON V-EVEN-PRS-DUPL
 :i			VSUF-EVEN-PRS-DUPL ;
! +Du1:ien       FINAL1	;
! +Du2:eden		FINAL1	; !i>e
! +Du3:iejægan	FINAL1	; 
! +Du3:ieben	FINAL1	; !SUB
! +Pl1:ebe		FINAL1	; !i>e
! +Pl2:ede		FINAL1	; !i>e
! +Pl3:ieh		FINAL1	;

LEXICON VSUF-EVEN-PRS-DUPL
 +Du1:en        FINAL1	;
 +Du2:den		FINAL1	;
 +Du3:ejægan	FINAL1	;
 +Du3:eben	    FINAL1	; !SUB
 +Pl1:be		FINAL1	;
 +Pl2:de		FINAL1	;
 +Pl3:eh		FINAL1	;


LEXICON VSUF-EVEN-IMP
 +Sg2:ieh	    FINAL1  ;
 +Du2:eden      FINAL1  ;
 +Pl2:ede       FINAL1  ;


!LEXICON V-II-EVEN-PRS-DUPL
! +Du1:oen       FINAL1	;
! +Du2:oden		FINAL1	;
! +Du3:oejægan	FINAL1	;
! +Pl1:obe		FINAL1	;
! +Pl2:ode		FINAL1	;
! +Pl3:oeh		FINAL1	;


LEXICON VSUF-II-EVEN-IMP
 +Sg2:oeh	    FINAL1  ;
 +Du2:oden      FINAL1  ;
 +Pl2:ode      FINAL1  ;

LEXICON VSUF-III-EVEN-IMP
 +Sg2:ah	    FINAL1  ;
 +Du2:eden      FINAL1  ;
 +Pl2:ede      FINAL1  ;


LEXICON V-IV-EVEN-PRS
 :e		          V-PRS-SG-1 ;
 		          V-PRS-SG-2 ;
 +Sg3:%^3sUMLa	  FINAL1 ;
 +Du1:%^3dUMLien  FINAL1	;
 +Du2:eden		  FINAL1	;
 +Du3:ijægan	  FINAL1	;
 +Du3:iben	      FINAL1	;
 +Pl1:ebe		  FINAL1	;
 +Pl2:ede		  FINAL1	;
 +Pl3:%^3dUMLieh  FINAL1	;


LEXICON VSUF-IV-EVEN-IMP
 +Sg2:h		    FINAL1  ;
 +Du2:eden      FINAL1  ;
 +Pl2:ede      FINAL1  ;



LEXICON VSUF-VI-EVEN-PRS
 :e		          V-PRS-SG-1 ;
 		          V-PRS-SG-2 ;
 +Sg3:ie     	  FINAL1 ;
 +Du1:ien         FINAL1	;
 +Du2:eden		  FINAL1	; !i>e
 +Du3:ijægan	  FINAL1	; !e>0
 +Du3:iben	      FINAL1	; !e>0
 +Pl1:ebe		  FINAL1	; !i>e
 +Pl2:ede		  FINAL1	;
 +Pl3:ieh         FINAL1	;



! Odd

LEXICON VSUF-ODD-PRS
 :%^DISIMPe V-PRS-SG-1 ;
 :%^DISIMP  V-PRS-SG-2 ;
 :%^DISIMPe V-PRS-SG-3 ;
            V-ODD-PRS-DUPL ;


LEXICON V-ODD-PRS-DUPL
 +Du1:ien		FINAL1	;
 +Du2:idien		FINAL1	;
 +Du3:ægan		FINAL1	;
 +Du3:æban		FINAL1	; !SUB
 +Pl1:ibie		FINAL1	;
 +Pl2:idie		FINAL1	;
 +Pl3:ieh		FINAL1	;


LEXICON VSUF-ODD-PRT
VSUF-PRT-SG-12 ;
!+Sg3 was treated in GOLTELIDH / DÅERIEDIDH wrt. i/e
VSUF-PRT-DUPL ;

LEXICON VSUF-ODD-IMP
 +Sg2:%^DISIMPh	FINAL1  ;
 +Du2:idien       FINAL1  ;
 +Pl2:idie        FINAL1  ;


! Contract


LEXICON VSUF-CONTR-PRS
     :e  V-PRS-SG-1 ;
     :   V-PRS-SG-2 ;
     :ie V-PRS-SG-3 ;
           VSUF-CONTR-DUPL ;

LEXICON VSUF-CONTR-DUPL
 +Du1:ien		FINAL1	;
 +Du2:eden		FINAL1	;
 +Du3:ijægan	FINAL1	;
 +Du3:iben	FINAL1	; !SUB
 +Pl1:ebe		FINAL1	;
 +Pl2:ede		FINAL1	;
 +Pl3:ieh		FINAL1	;

LEXICON VSUF-CONTR-PRT
+Sg3:iji FINAL1 ;
           VSUF-PRT ;

LEXICON VSUF-CONTR-IMP
 +Sg2:h	 FINAL1  ;
 +Du2:eden FINAL1  ;
 +Pl2:ede  FINAL1  ;


! Common even-odd-contr
! ---------------------

LEXICON V-PRS-SG-12 ! Kutt denne viss ikkje ref til
 V-PRS-SG-1 ;
 V-PRS-SG-2 ;

LEXICON V-PRS-SG-1 
 +Sg1:m			FINAL1	; ! Border in prev lex

LEXICON V-PRS-SG-2 
 +Sg2:h		FINAL1	;

LEXICON V-PRS-SG-3
 +Sg3:  		FINAL1	;

! LEXICON V-PRS-DUPL
!  +Du1:n         FINAL1	;
!  +Du2:dI1n		FINAL1	;
!  +Du3:gan		FINAL1	;
!  +Pl1:bI1		FINAL1	;
!  +Pl2:dI1		FINAL1	;
!  +Pl3:h		    FINAL1	;

LEXICON VSUF-PRT
 VSUF-PRT-SG-12 ;
 VSUF-PRT-SG-3 ;
 VSUF-PRT-DUPL ;

LEXICON VSUF-PRT-SG-12 
+Sg1:im		FINAL1	;
+Sg2:ih		FINAL1	;

LEXICON VSUF-PRT-SG-3 
+Sg3:i		FINAL1	;

LEXICON VSUF-PRT-DUPL 
+Du1:imen		FINAL1	;
+Du2:iden		FINAL1	;
+Du3:igan		FINAL1	;
+Pl1:imh		FINAL1	;
+Pl2:idh		FINAL1	;
+Pl3:in		    FINAL1	;








! Final lexicon
! =============


LEXICON FINAL1
  0:#			#	;
