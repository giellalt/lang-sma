Multichar_Symbols

  +N +Sg +Pl +Nom +Acc +Gen +Ine +Ela +Ill +Com +Ess
  +PxSg1 +PxSg2 +PxSg3
  +PxDu1 +PxDu2 +PxDu3
  +PxPl1 +PxPl2 +PxPl3
  
  +V +Prs +Prt
  +Sg1 +Sg2 +Sg3
  +Du1 +Du2 +Du3
  +Pl1 +Pl2 +Pl3

  +Inf +Neg  +PrfPrc
! +VGen +Ger
  +Ind
  +Imp +ImpII !Den siste berre nektingsverbet
  +Coll ! Collective numeral

! Berre hjelpeverbet/nektingsverbet (Pot. for andre V?):
  +Pot +Cond  ! +Opt (Hasselbrink 48.3.I, 48.14, 48.15, 48.21)

! Frå nordsamisk:
! +Actor +Clt
  +A
  +Pred
  +Comp +Superl
  +Attr
  +Dirty ! Only until we have adj grammar in place - this tags mark adjectives
  		 ! with missing grammatical description

  +Uml   ! A tag to indicate realised or potential Umlaut
  +Hyph  ! A tag to indicate that a hyphen was used when compounding
  +Pron +Pers +Dem +Adp +Po +Adv +Interj +Num +Ord
  +Prop +Mal +Fem +Sur
  +CS +CC +Pcle +CLB +PUNCT +LEFT +RIGHT
  +SgNomCmp

  X2 X3
  E2 ! insert e ! Fjern denne.

  !Umlaut and diphthong simplification triggers:
  %^DISIMP
  %^DISIMP2
  %^1UML  ! a-uml, like 1sg prs, perf.part of båetedh/V-I, and ill sg of -ie nouns
  %^2UML  ! dark e, as 3sg prs & perf.part of tjearodh/V-II, and ill sg of -oe nouns
  %^3UML  ! not used
  %^3sUML ! a-uml in 3sg prs of V-IV (roehtedh - ruahta)
  %^3dUML ! ie-uml in 1du & 3pl prs of V-IV (roehtedh - ruehtien)
  %^iæUML ! not used
  %^iUML  ! i-uml in pret of V-I (båetedh - böötim)
  %^PASSUML ! Short passive Umlaut Rx->R5
  %^didhUML !Der/didh Umlaut for GUARKEDH-words
  %^htjidhUML !Umlaut für der/htjidh derivations
  %^adteUML !Umlaut für Der/adte and Der/alle derivations
  %^aLATUS  ! Latus-Umlaut for -ie stems
  %^uLATUS  ! Latus-Umlaut for -oe stems

! Der#begin
! Derivation position in a derivation row:  Affix and:
! 1              2               3                4                         POS type
+Der1           +Der2           +Der3            +Der4
! Der#1
! Der#2
! Der#3
! Der#4
! Der#other
								+Der/PassL                                  ! VV - long only
								+Der/PassS                                  ! VV - short only
+Der/htalle                                                                 ! VV - Passive
+Der/lgidh                                                                  ! VV - Passive
+Der/NomAg                                                                  ! VN
+Der/ijes                                                                    ! NA
+Der/NomAct                                                                 ! VN
+Der/les                                                                    !VA
+Der/ldihkie                                                                !VA
+Der/ldahke                                                                 !VA
                +Der/voete                                                  !AN
+Der/htidh                                                                  ! VV - Causative
+Der/dehte                                                                  ! VV - Causative
+Der/lidh                                                                   !VV - Subitive
+Der/stidh                                                                  !VV - Diminutive
+Der/didh                                                                  !VV - Continuative
+Der/htjidh                                                                  !Dim-cont.
+Der/htovve                                                                  !AV 
+Der/htoeve                                                                  !AV 
+Der/htehte                                                                  !AV
                                +Der/ahtje                                  !VV - Inchoative
                                +Der/gåete                                 ! VV - Inchoative
+Der/Dim                                                                    ! NN - Diminutive
                +Der/ehte                                                     !VV - Causative
                +Der/adte                                                 !VV - Frequentative
                +Der/alle                                                   !VV - Frequentative
                +Der/eds
! Der#end

! ================
! The root lexicon
! ================

LEXICON Root
  0:#			Noun	;
  0:#			ProperNoun	;
  0:#			Verb	;
  0:#			Adjective	;
  0:#			Pronoun	;
  0:#			Adverb	;
  0:#			Subjunction	;
  0:#			Conjunction	;
  0:#			Particle	;
  0:#			Adposition	;
  0:#			Punctuation	;
  0:#			Interjection	;
  0:#			Numeral ;
  0:#			Abbreviation ;
  0:#			Acronym ;


! =============================
! Compounding - copied from SMJ
! =============================
LEXICON R
 # Noun ;                           ! This is the normal case.		         ^C^
 #@P.NeedNoun.ON@  Verb ;      ! Flag diacr declares that the compound   ^C^
 #@P.NeedNoun.ON@  Adjective ; ! needs a +N tag later in the derivation  ^C^
 +Hyph#:%- Noun ;                        ! These 3 cases add a hyphen, so     ^C^
 +Hyph#:%-@P.NeedNoun.ON@  Verb ;        ! Sem-julggaštus and the like	      ^C^
 +Hyph#:%-@P.NeedNoun.ON@  Adjective ;   ! is allowed.			              ^C^
! # ProperNoun ;     ! Now, let us allow proper nouns as the 2nd part of com- ^C^
 #:%- ProperNoun ;  ! pounds. Thus: London-Hull, LondonHull, Hull-viessu      ^C^
                    ! and viessuLondon are now all ok. Some refinement could  ^C^
                    ! perhaps be done, but corpus research is needed.	      ^C^
 +Cmpnd:%- # ;     ! This to take care of Oahpo- ja dutkandept - want this in speller

LEXICON RAbbr !From abbreviations and acronyms to avoid two hyphens: a-- and ...
 # Noun ;                              ! This is the normal case.               ^C^
! # NounSecond ;                       ! Testing some quasisuff, cf. noun file  ^C^
 #@P.NeedNoun.ON@  Verb ;              ! Flag diacr declares that the compound  ^C^
! #@P.NeedNoun.ON@  AdjectiveRoot ;    ! needs a +N tag later in the derivation ^C^
 +Hyph#:%- Noun ;                      ! These 3 cases add a hyphen, so         ^C^
 +Hyph#:%-@P.NeedNoun.ON@  Verb ;      ! Sem-julggaštus and the like	        ^C^
 +Hyph#:%-@P.NeedNoun.ON@  Adjective ; ! is allowed.                            ^C^
!# ProperNoun ;     ! Now, let us allow proper nouns as the 2nd part of com-    ^C^
!#:%- ProperNoun ;  ! pounds. Thus: London-Hull, LondonHull, Hull-viessu        ^C^
                    ! and viessuLondon are now all ok. Some refinement could    ^C^
                    ! perhaps be done, but corpus research is needed.	        ^C^
 +Cmpnd: # ;        ! This to take care of Oahpo- ja dutkandept - want this in speller

! ============================
! Nominal inflection sublexica
! ============================

! Inflection for odd-syllable nouns
! ---------------------------------
LEXICON N_ODD
 +N+Sg: N_ODD_SG ;
 +N+Pl: N_ODD_PL ;
 +N:    N_ODD_ESS ;
  +N+SgNomCmp:            R ;
  +N+SgGenCmp:e%>n        R ;
  +N+PlGenCmp:%>%^DISIMPi R ;
  +N+Der1+Der/Dim:%»atj GIERIEHTSATJE ;


LEXICON N_ODD_SG
  +Nom:e%^DISIMP         FINAL1 ;
  +Acc:e%>%^DISIMPm      FINAL1 ;
  +Gen:e%>%^DISIMPn      FINAL1 ;
  +Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
  +Ine:%>isnie           FINAL1 ;
  +Ela:%>istie           FINAL1 ;
  +Com:%>inie            FINAL1 ;

LEXICON N_ODD_PL
  +Nom:%>%^DISIMPh       FINAL1 ;
  +Acc:%>idie            FINAL1 ;
  +Gen:%>%^DISIMPi       FINAL1 ;
  +Ill:%>idie            FINAL1 ;
  +Ine:%>inie            FINAL1 ;
  +Ela:%>ijstie          FINAL1 ;
  +Com:%>%^DISIMPigujmie FINAL1 ;

LEXICON N_ODD_ESS
  +Ess:%>inie          FINAL1 ;


! Inflection for nouns ending in '-ie'
! ------------------------------------

LEXICON N_IE
  +N+Sg: N_IE_SG ;
  +N+Pl: N_IE_PL ;
  +N:    N_IE_ESS ;
  +N+SgNomCmp:ie              R ;
  +N+SgGenCmp:ie%>n           R ;
  +N+PlGenCmp:ie%>j           R ;
  +N+PlGenCmp+Uml:ie%>%^iUMLi R ;
  +N+Der1+Der/Dim:%^aLATUS%»tj GÅATETJE ;
  +N+Der1+Der/ijes+A+Attr:%»%^1UMLijes ATTR-0 ;
  +N+Der1+Der/ijes+A+Pred:%»%^1UMLijes FINAL1 ;

LEXICON N_IE_SG
  +Nom:ie                  FINAL1 ;
  +Acc:ie%>m               FINAL1 ;
  +Gen:ie%>n               FINAL1 ;
! +Ill:%>an                FINAL1 ; !SUB - is this possible? IllSg without Uml in -ie?
  +Ill+Uml:%>%^1UMLan      FINAL1 ;
  +Ine:e%>sne              FINAL1 ;
  +Ela:e%>ste              FINAL1 ;
  +Com:%>ine               FINAL1 ;
  +Com+Uml:%>%^iUMLine     FINAL1 ;

LEXICON N_IE_PL
  +Nom:ie%>h               FINAL1 ;
  +Acc:%>ide               FINAL1 ;
  +Acc+Uml:%>%^iUMLide     FINAL1 ;
  +Gen:ie%>j               FINAL1 ;
  +Gen+Uml:%>%^iUMLi       FINAL1 ;
  +Ill:%>ide               FINAL1 ;
  +Ill+Uml:%>%^iUMLide     FINAL1 ;
  +Ine:%>ine               FINAL1 ;
  +Ine+Uml:%>%^iUMLine     FINAL1 ;
  +Ela:%>ijste             FINAL1 ;
  +Ela+Uml:%>%^iUMLijste   FINAL1 ;
  +Com:ie%>jgujmie         FINAL1 ;
  +Com+Uml:%>%^iUMLigujmie FINAL1 ;

LEXICON N_IE_ESS
  +Ess:%>ine                  FINAL1 ;
  +Ess+Uml:%>%^iUMLine        FINAL1 ;


! Inflection for nouns ending in oe
! ---------------------------------
LEXICON N_OE_UML
  +N+Sg: N_OE_SGILL_UML ; ! Causes Umlaut
         N_OE ;

LEXICON N_OE_SGILL_UML
  +Ill+Uml:e%>%^2UMLse	FINAL1      ; ! Causes Umlaut

LEXICON N_OE
  +N+Sg: N_OE_SG  ;
  +N+Pl: N_OE_PL  ;
  +N: N_OE_ESS ;
  +N+SgNomCmp:oe    R ;
  +N+SgGenCmp:oe%>n R ;
  +N+PlGenCmp:oe%>j R ;
  +N+Der1+Der/Dim:e%»%^2UMLtj GÅATETJE ;

LEXICON N_OE_SG
  +Nom:oe          FINAL1 ;
  +Acc:oe%>m       FINAL1 ;
  +Gen:oe%>n       FINAL1 ;
  +Ill:o%>se       FINAL1 ;
  +Ine:o%>sne      FINAL1 ;
  +Ela:o%>ste      FINAL1 ;
  +Com:o%>jne      FINAL1 ;

LEXICON N_OE_PL
  +Nom:oe%>h       FINAL1 ;
  +Acc:o%>jde      FINAL1 ;
  +Gen:oe%>j       FINAL1 ;
  +Ill:o%>jde      FINAL1 ;
  +Ine:o%>jne      FINAL1 ;
  +Ela:o%>jste     FINAL1 ;
  +Com:oe%>jgujmie FINAL1 ;

LEXICON N_OE_ESS
  +Ess:o%>jne         FINAL1 ;

! Inflection for maana-type nouns
! -------------------------------
LEXICON MAANA
  +N+Sg: MAANA_SG  ;
  +N+Pl: MAANA_PL  ;
  +N:    EVEN_ESS ;
  +N+SgNomCmp:a    R ;
  +N+SgGenCmp:a%>n R ;
  +N+PlGenCmp:a%>j R ;
  +N+Der1+Der/Dim:%^aLATUS%»tj GÅATETJE ;

LEXICON MAANA_SG
  +Nom:a          FINAL1 ;
  +Acc:a%>m       FINAL1 ;
  +Gen:a%>n       FINAL1 ;
  +Ill:e%>se      FINAL1 ;
  +Ine:e%>sne     FINAL1 ;
  +Ela:e%>ste     FINAL1 ;
  +Com:%>ine      FINAL1 ;

LEXICON MAANA_PL
  +Nom:a%>h       FINAL1 ;
  +Acc:%>ide      FINAL1 ;
  +Gen:a%>j       FINAL1 ;
  +Ill:%>ide      FINAL1 ;
  +Ine:%>ine      FINAL1 ;
  +Ela:%>ijste    FINAL1 ;
  +Com:a%>jgujmie FINAL1 ;

! Inflection for nïejte-type nouns
! --------------------------------
LEXICON NIEJTE
  +N+Sg:           NIEJTE_SG  ;
  +N+Pl:           NIEJTE_PL  ;
  +N:              EVEN_ESS ;
  +N+SgNomCmp:e    R ;
  +N+SgGenCmp:e%>n R ;
  +N+PlGenCmp:%>i  R ;
  +N+Der1+Der/Dim:e%»tj   GÅATETJE ;

LEXICON NIEJTE_SG
  +Nom:e          FINAL1 ;
  +Acc:e%>m       FINAL1 ;
  +Gen:e%>n       FINAL1 ;
  +Ill:e%>se      FINAL1 ;
  +Ine:e%>sne     FINAL1 ;
  +Ela:e%>ste     FINAL1 ;
  +Com:%>ine      FINAL1 ;

LEXICON NIEJTE_PL
  +Nom:%>h        FINAL1 ;
  +Acc:%>ide      FINAL1 ;
  +Gen:%>i        FINAL1 ;
  +Ill:%>ide      FINAL1 ;
  +Ine:%>ine      FINAL1 ;
  +Ela:%>ijste    FINAL1 ;
  +Com:%>igujmie  FINAL1 ;

! Sub-paradigm inflections:
! -------------------------

LEXICON EVEN_ESS
  +Ess:%>ine         FINAL1 ;


! Old, not used, retained at the moment to remember the history:
!LEXICON I-CLASS ! Links from propernoun (revise?)
!  :%>i			IJ-CASE ;	! others have -ine etc.
!                I-EVEN ;
!                I-ODD ;
!
!LEXICON I-EVEN
!  +Pl+Ela:%>ijste	FINAL1	;	! Ela has -iste
!
!LEXICON I-ODD
!  +Pl+Ela:%>ijstie	FINAL1	;	! Ela has -iste



! Special for cns-final names, due to nomsg
! =========================================

! LEXICON CNAME_ODD_SG !(still) not in use
!  NOMSG ;
!  OBLIQUE_COMMON ;
!  OBLIQUE_ODD ;
!
!LEXICON CNAME_EVEN_SG !(still) not in use
! NOMSG ;
! OBLIQUE_COMMON ;
! OBLIQUE_EVEN ;


! LEXICON NOMSG !(still) not in use
!   +Sg+Nom:  	        FINAL1 ;
! 
! LEXICON OBLIQUE_COMMON !(still) not in use
!   +Sg+Acc:e%>m      FINAL1 ;
!   +Sg+Gen:e%>n      FINAL1 ;
! 
! LEXICON OBLIQUE_ODD !(still) not in use
!   +Sg+Ine:e%>snie   FINAL1 ;
!   +Sg+Ela:e%>stie   FINAL1 ;
!   +Sg+Com:e%>nie    FINAL1 ;

! LEXICON OBLIQUE_EVEN !(still) not in use
!   +Sg+Ine:e%>sne    FINAL1 ;
!   +Sg+Ela:e%>ste    FINAL1 ;
!   +Sg+Com:e%>ne     FINAL1 ;

! ---- Nominal derivation sublexica ----

LEXICON GIERIEHTSATJE
  +Sg:           NIEJTE_SG  ;
  +Pl:           NIEJTE_PL  ;
                 EVEN_ESS ;
  +SgNomCmp:e    R ;
  +SgGenCmp:e%>n R ;
  +PlGenCmp:%>i  R ;

LEXICON GÅATETJE
 +Sg: N_ODD_SG ;
 +Pl: N_ODD_PL ;
      N_ODD_ESS ;
 +SgNomCmp:            R ;
 +SgGenCmp:e%>n        R ;
 +PlGenCmp:%>%^DISIMPi R ;

LEXICON MES
  +Sg+Nom:          FINAL1 ;
  +Sg+Acc:e%>m       FINAL1 ;
  +Sg+Gen:e%>n       FINAL1 ;
  +Sg+Ill:e%>se      FINAL1 ;
  +Sg+Ine:e%>sne     FINAL1 ;
  +Sg+Ela:e%>ste     FINAL1 ;
  +Sg+Com:%>ine      FINAL1 ;
  +Pl:           NIEJTE_PL  ;
                 EVEN_ESS ;
  +SgNomCmp:e    R ;
  +SgGenCmp:e%>n R ;
  +PlGenCmp:%>i  R ;


LEXICON ÅBPOE
  +Sg: ÅBPOE_SGILL_UML ; ! Causes Umlaut
         ÅBPOE_N_OE ;

LEXICON ÅBPOE_SGILL_UML
  +Ill+Uml:e%>%^2UMLse	FINAL1      ; ! Causes Umlaut

LEXICON ÅBPOE_N_OE
  +Sg: ÅBPOE_N_OE_SG  ;
  +Pl: ÅBPOE_N_OE_PL  ;
  : ÅBPOE_N_OE_ESS ;
  +SgNomCmp:oe    R ;
  +SgGenCmp:oe%>n R ;
  +PlGenCmp:oe%>j R ;

LEXICON ÅBPOE_N_OE_SG
  +Nom:oe          FINAL1 ;
  +Acc:oe%>m       FINAL1 ;
  +Gen:oe%>n       FINAL1 ;
  +Ill:o%>se       FINAL1 ;
  +Ine:o%>sne      FINAL1 ;
  +Ela:o%>ste      FINAL1 ;
  +Com:o%>jne      FINAL1 ;

LEXICON ÅBPOE_N_OE_PL
  +Nom:oe%>h       FINAL1 ;
  +Acc:o%>jde      FINAL1 ;
  +Gen:oe%>j       FINAL1 ;
  +Ill:o%>jde      FINAL1 ;
  +Ine:o%>jne      FINAL1 ;
  +Ela:o%>jste     FINAL1 ;
  +Com:oe%>jgujmie FINAL1 ;

LEXICON ÅBPOE_N_OE_ESS
  +Ess:o%>jne         FINAL1 ;


LEXICON OMMES
 +Sg+Nom:%^DISIMP         FINAL1 ;
 +Sg+Acc:e%>%^DISIMPm      FINAL1 ;
 +Sg+Gen:e%>%^DISIMPn      FINAL1 ;
 +Sg+Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
 +Sg+Ine:%>isnie           FINAL1 ;
 +Sg+Ela:%>istie           FINAL1 ;
 +Sg+Com:%>inie            FINAL1 ;
 +Pl: N_ODD_PL ;
      N_ODD_ESS ;
 +SgNomCmp:            R ;
 +SgGenCmp:e%>n        R ;
 +PlGenCmp:%>%^DISIMPi R ;

! ===========================================
! Adjectives: Adjectival inflection sublexica
! ===========================================

! Basic adjectival lexica, infl types
! -----------------------------------

! even stems
! - - - - - 

! type 1

!!1a: Vowel stem in a and p, c = be
!
!    a = V, p = V, c = Vbe
!    plaave måvhkah, pred : måvhkah leah plaave
!    plaave, plaave ("blå")
!    plaavebe  / plååvebe
!    plaavemes

!LEXICON NULL-NULL-EVEN
!     ATTR-0 ;
!     PRED-0 ;
!+A:  EVENCOMP ;

!!1b: -s in a and p, c = be/hkåbpoe
!
!     a = s, p = s, c = suffixes (be/hkåbpoe)
! 
! Subgroups:
! 
! * Vokalstamme med omlyd i komparativ
! * Vokalstamme med eller utan omlyd i komparativ
! * Vokalstamme utan omlyd i komparativ
! * Konsonantstamme

LEXICON NULL-NULL-EVEN
     ATTR-0 ;
     PRED-0 ;
+A:  EVENCOMP ; ! check this!

!LEXICON N-N-EVEN
!     ATTR-N ;
!     PRED-N ;
!+A:  EVENCOMP ;


! type 2
LEXICON NULL-S-EVEN
 :e    ATTR-0 ;
 :e    PRED-S ;
     NIEJTE ;
+A:  EVENCOMP-UML ;

LEXICON NULL-N-EVEN
     ATTR-0 ;
     PRED-N ;
+A:  EVENCOMP ;

!LEXICON E-ES-EVEN ! not in use
!:e   ATTR-0 ;
!:e   PRED-S ;
!+A:  EVENCOMP-UML ;


! type 3
LEXICON S-NULL-EVEN
     ATTR-S ;
     PRED-0 ;
+A:  EVENCOMP ;



!---- EVEN-stems (even stem declension)
! =====================================


!--- S-EVENs---
! -------------

LEXICON S-S-EVEN
     ATTR-S ;
     PRED-S ;
+A:  EVENCOMP ; ! check this!




!--- ie-stems---

!! not used: LEXICON NJALHKEDIHKS
!! not used:       ATTR-S ;
!! not used: !     AELHKIEScase ;   !have they got case?
!! not used: +A+Pred:ie FINAL1 ;
!! not used: !+A:e  EVENCOMP ; !these words do not seem to have comparation

LEXICON DIHKS
          ATTR-S ;
!         AELHKIEScase ;  !have they got case?
+A+Pred:s FINAL1 ;
! +A:e   EVENCOMP ; !these words do not seem to have comparation

LEXICON AELHKIES! attr= s, predikat = Ø, comp jih case: even (ebe/emes/esne) 
 :ie  ATTR-S ;
     AELHKIEScase ;
+A+Pred:ie FINAL1 ;
+A:e  EVENCOMP ;

LEXICON GAERTJIES! Attr=s, Prediakt = s jih Ø, Comp jih caese= even (ebe/emes/esne) 
 :ie  ATTR-S ;! (tsåekies, tsåekie/tsåekies) ved adverbial= substantiv/adjektiv
      AELHKIEScase ;
+A+Pred:ie FINAL1 ;
+A+Pred:ies FINAL1 ;
+A+SgNomCmp:ies R ;
+A:e  EVENCOMP ;

!! not used: LEXICON TSÅEKIES ! Attr= s, pred=s jih Ø, Comp = EVEN jih ODD(åbpoe, ommes) Case = even =faavroe 
!! not used:  :ie  ATTR-S ;
!! not used:       AELHKIEScase ;
!! not used: +A+Pred:ies FINAL1 ;
!! not used: +A+Pred:ie FINAL1 ;
!! not used: +A+SgNomCmp:ies R ;
!! not used: +A:e  EVENCOMP ;
!! not used: +A:ies  ODDCOMP ;


!--- oe-stems---

LEXICON FAAVROES! attr= s, pred= Ø, Comp= EVEN(ebe, emes) case/substantiven hammoe= Even
 :oe  ATTR-S ;
     FAAVROEScase ;
 :oe PRED-0 ;
+A:e  EVENCOMP ;

LEXICON OEH-OEH-ODD ! Attr=pred
:oe ATTR-H ;
:oe PRED-H ;
+A:a ODDCOMP ;
+A:oe ODDCOMP ;

LEXICON OEH-OEH-AN-ODD ! Attr=oeh, Pred=oeh og an 
:oe ATTR-H ;
:oe PRED-H ;
  PRED-AN ; 
+A:a ODDCOMP ;
+A:oe ODDCOMP ; 

LEXICON H-AN-ODD ! 
    ATTR-H ;
    PRED-H ;
+A: ODDCOMP ; 




LEXICON N-N-ODD ! attr=n, pred= n
:a ATTR-N ;
:a  PRED-N ;
+A:a ODDCOMP ; 
 ATTR-N ;
 PRED-N ;
! Comparative missing


!--- a-stems---

LEXICON FRASKAS! attr= s, pred= Ø, comp=EVEN(ebe,emes), Case/Substantvien hammoe= EVEN
 :a  ATTR-S ;
     FRASKAScase ;
+A+Pred:a FINAL1 ;
+A:e  EVENCOMP ;


!--- e-stems---

LEXICON NJOEKTSES
 :e  ATTR-S ;! Attr=s, Pred= Ø, Comp= EVEN(ebe, emes), case/substantiv= EVEN 
     NJOEKTSEScase ;
+A+Pred:e FINAL1 ;
+A:e  EVENCOMP ;

LEXICON RAAJNES
! Attr: s jih attr=Ø,  Pred= s jih Ø,Comp=even(ebe, emes), Case/substantiv= even  
 :e  ATTR-S ;
 :e  ATTR-0 ;
     NJOEKTSEScase ;
+A+Pred:es FINAL1 ;
+A+Pred:e FINAL1 ;
+A+SgNomCmp:es R ;
+A:e  EVENCOMP ;


!--- S-EVEN attr-lexicas---
! -------------------------


LEXICON GIJMIES
     ATTR-S ;! Attr= s , Comp=Even, 
+A:  EVENCOMP ;


!--- H-EVENs---
! -------------

!--- oe-stems---

!LEXICON SPÆNTJOEH ! Attr= h, Pred= Ø, Comp= EVEN, Case/Substantiv= EVEN
! :oe       ATTR-H ;
!           FAAVROEScase ;
!+A+Pred:oe FINAL1 ;
!+A:e       EVENCOMP ;


!--- a-stems---

LEXICON JÅABPAH! Attr= h, Pred=Ø, Comp=EVEN, Case/Substantiv= EVEN
 :a  ATTR-H ;
     FRASKAScase ;
+A+Pred:a FINAL1 ;
+A:e  EVENCOMP ;

! LEXICON ÅAJVAH! Attr=h, Pred=h, Comp=EVEN, Case/substantiv=EVEN
!:a  ATTR-H ;
!     FRASKAScase ;
!+A+Pred:ah FINAL1 ;
!+A:e  EVENCOMP ;



!--- H-EVEN attr-lexicas---
! -------------------------


LEXICON GÆHTJAH ! Attr= h, Comp= Even(klåtnjah)
    ATTR-H ;
+A: EVENCOMP ;


LEXICON IJTJE-RAADH! Attr= h, Comp =Even( Jïjtje-raarh) 
    ATTR-H ;
+A:e  EVENCOMP ;



!--- N-EVENs---
! -------------

!LEXICON STIJVEN! attr=n, pred=Ø, Comp=Even, Case= even(
! :e  ATTR-N ;
!     NJOEKTSEScase ;
!+A+Pred:e FINAL1 ;
!+A:e  EVENCOMP ;


!--- NULL-EVENs---
! ----------------

!--- e-stems---

LEXICON NÅAKE! attr=Ø, pred=Ø, comp=even, case= EVEN   (VOKAL=VOKAL) = E-stamme 
  :e      ATTR-0 ;
          NJOEKTSEScase ;
+A+Pred:e FINAL1 ;
+A:e      EVENCOMP ;


!--- a-stems---

!LEXICON SAATNA! attr= Ø, Pred = Ø, =A-stamme, Comp= even, case= even 
!  :a      ATTR-0 ;
!          FRASKAScase ;
!+A+Pred:a FINAL1 ;
!+A:e      EVENCOMP ;


!--- ie-stems---

!! not used: LEXICON GELJIE! attr= Ø, pred=Ø, Comp=EVEN, case=EVEN - 
!! not used:   :ie      ATTR-0 ;
!! not used:            AELHKIEScase ;
!! not used: +A+Pred:ie FINAL1 ;
!! not used: +A:e       EVENCOMP ;

LEXICON MASTEN
     ATTR-S ;
     ATTR-H ;
  :e ATTR-N ;
  :e PRED-N ;


!LEXICON H-NULL-EVEN ! attr= h, pred=Ø, Comp=EVEN,
!     ATTR-H ;
!     PRED-0 ;
!+A:  EVENCOMP ;

LEXICON H-H-EVEN ! Attr= h, pred= h, comp=even, case= even
     ATTR-H ;
     PRED-H ;
+A:  EVENCOMP ;


! type 4
LEXICON S-N-EVEN
     ATTR-S ;
     PRED-N ;
+A:  EVENCOMP ; ! check this!




!--- NULL-EVEN attr-lexicas---
! -------------------------


LEXICON JEARKOE
     ATTR-0 ;
+A:  EVENCOMP ;




! ODD-stems (odd stem declension)
! ===============================


! type 1
LEXICON NULL-NULL-ODD
     ATTR-0 ;
     PRED-0 ;
+A:  ODDCOMP ;

LEXICON H-H-ODD
     ATTR-H ;
     PRED-H ;
+A:  ODDCOMP ;

LEXICON E-E-ODD !bårreske
 :e     ATTR-0 ;
 :e     PRED-0 ;
 +A:  ODDCOMP ;

LEXICON S-S-ODD !
     ATTR-S ;
     PRED-S ;
+A:ahk ODDCOMP ;
+A:oehk ODDCOMP ; !XXX check this o

LEXICON ES-ES-ODD 
:e     ATTR-S ;
:e     PRED-S ;
+A:ahk ODDCOMP ;
+A:oehk ODDCOMP ; !XXX check this o



!LEXICON BAENEHTS
!:eht     ATTR-S ;
!:iehtadtje     PRED-S ;
!+A:iehtadtje  ODDCOMP ;
!+A+Der1+Der/htovve:%»%^1UMLahtovv SOVVEDH ;
!+A+Der1+Der/htoeve:%»%^1UMLahtoev SOVVEDH ;
!+A+Der1+Der/htehte:%»%^1UMLahteht ÅARAJEHTEDH ;

!LEXICON TJOHKETS
!:eht     ATTR-S ;
!:ehtadtje     PRED-S ;
!+A:ehtadtje  ODDCOMP ;
!+A+Der1+Der/htovve:%»ehtovv SOVVEDH ;
!+A+Der1+Der/htoeve:%»ehtoev SOVVEDH ;
!+A+Der1+Der/htehte:%»ehteht ÅARAJEHTEDH ;

LEXICON EDS-EDS/EDE
  :%^DISIMP   ATTR-S ;
  :%^DISIMP   PRED-S ;
  :%^DISIMPe   PRED-0 ;
+A:  ODDCOMP ;

LEXICON EDS-EDS
  :%^DISIMP   ATTR-S ;
  :%^DISIMP   PRED-S ;
+A:  ODDCOMP ;


! type 2
LEXICON NULL-S-ODD
     ATTR-0 ;    
:e   PRED-S ;
+A:  ODDCOMP ;

! type 3
LEXICON S-NULL-ODD
     ATTR-S ;
:e   PRED-0 ;
+A:  ODDCOMP ;

LEXICON S-N-ODD
   ATTR-S ;
   PRED-N ;
+A:  ODDCOMP ;

LEXICON ES-E-ODD
:e     ATTR-S;
:e     PRED-0;
+A:    ODDCOMP ; 

LEXICON ES-E-ES-E-ODD! as ES-ES-E-ODD, but with ATTR-0
       ATTR-0 ;
       ES-ES-E-ODD ;
       
LEXICON ES-ES-E-ODD
:e     ATTR-S;
:e     PRED-S;
:e     PRED-0;
+A:    ODDCOMP ; 

LEXICON E-S-ODD
:e     ATTR-S;
:s     PRED-0;
+A:    ODDCOMP ; 


! type 4

LEXICON H-N-ODD
 ATTR-H ;
 PRED-N ;
 ! no comparative forms???
+A:  ODDCOMP ; 

LEXICON OEH-AN-ODD
:oe   ATTR-H ;
:a    PRED-N ;
+A:ahk ODDCOMP ;
+A:oehk ODDCOMP ; !XXX check this o


!--- NULL-ODDs---
! ---------------

LEXICON DOLVE
  :e ATTR-0 ;! attr=Ø, pred=s, comp= EVEN, Case:EVEN
  :es  DOLVEcase ;
+A+Pred:es FINAL1 ;
+A:es  EVENCOMP ;

LEXICON DOLVEs
   ATTR-S ; ! attr=s, pred=s, comp= ODD, Case:ODD
  :es  DOLVEcase ;
  :e   PRED-0 ;
+A:es  ODDCOMP ;

LEXICON JIEGKE!attr=Ø, Pred=s, Comp=even jih ODD, Case= ODD
  :e ATTR-0 ;
  :es  DOLVEcase ;
+A+Pred:es FINAL1 ;
+A:es  ODDCOMP ;
+A:e  EVENCOMP ;

LEXICON LYÖVLEHKE! Attr=Ø, Pred=Ø, ComP=ODD, case= ODD
  :e ATTR-0 ;
     DOLVEcase ;
+A+Pred:e FINAL1 ;
+A:  ODDCOMP ;

LEXICON VYRTEHKE! Attr=Ø jih s, Pred= Ø jih s, Comp= ODD, Case= ODD
     ATTR-S ;
  :e ATTR-0 ;
     DOLVEcase ;
+A+Pred:e FINAL1 ;
+A+Pred:s FINAL1 ;
+A:  ODDCOMP ;








! This lexicon is for the adjs we
! have not classified yet
LEXICON ADJ-DIRTY
+A+Attr: # ;
+A+Pred: # ;
+A+Dirty: # ;

! Attribute lexica
! ----------------

LEXICON ATTR-0
+A+Attr: FINAL1 ;
+A+Attr+Der2+Der/voete:%»voet GIERIEHTSATJE ;

LEXICON ATTR-S
+A+Attr:%>s FINAL1 ;
+A+Attr+Der2+Der/voete:%>s%»voet GIERIEHTSATJE ;

LEXICON ATTR-H
+A+Attr:%>h   FINAL1 ;
+A+Attr+Der2+Der/voete:%>h%»voet GIERIEHTSATJE ;

LEXICON ATTR-N
+A+Attr:%>n FINAL1 ;
+A+Attr+Der2+Der/voete:%>n%»voet GIERIEHTSATJE ;



! Predicative lexica
! ------------------

LEXICON AELHKIEScase
  +A+Sg: N_IE_SG ;
  +A+Pl: N_IE_PL ;
  +A:    N_IE_ESS ;
  +A+SgNomCmp:ie              R ;
  +A+SgGenCmp:ie%>n           R ;
  +A+PlGenCmp:ie%>j           R ;
  +A+PlGenCmp+Uml:ie%>%^iUMLi R ;
  +A+Der1+Der/Dim+Pred:%^aLATUS%»tje   FINAL1 ;
  +A+Der1+Der/Dim:%^aLATUS%»tj GÅATETJE ;
 
LEXICON FAAVROEScase
  +A+Sg: N_OE_SGILL_UML ; ! Causes Umlaut
  +A+Sg: N_OE_SG  ;
  +A+Pl: N_OE_PL  ;
  +A: N_OE_ESS ;
  +A+SgNomCmp:oe    R ;
  +A+SgGenCmp:oe%>n R ;
  +A+PlGenCmp:oe%>j R ;
  +A+Der1+Der/Dim+Pred:e%»%^2UMLtje   FINAL1 ;
  +A+Der1+Der/Dim:e%»%^2UMLtj GÅATETJE ;
  
 LEXICON FRASKAScase
  +A+Sg: MAANA_SG  ;
  +A+Pl: MAANA_PL  ;
  +A:    EVEN_ESS ;
  +A+SgNomCmp:a    R ;
  +A+SgGenCmp:a%>n R ;
  +A+PlGenCmp:a%>j R ;
  +A+Der1+Der/Dim+Pred:%^aLATUS%»tje   FINAL1 ;
  +A+Der1+Der/Dim:%^aLATUS%»tj GÅATETJE ;

 LEXICON NJOEKTSEScase
  +A+Sg:           NIEJTE_SG  ;
  +A+Pl:           NIEJTE_PL  ;
  +A:              EVEN_ESS ;
  +A+SgNomCmp:e    R ;
  +A+SgGenCmp:e%>n R ;
  +A+PlGenCmp:%>i  R ;
  +A+Der1+Der/Dim+Pred:e%»tje   FINAL1 ;
  +A+Der1+Der/Dim:e%»tj   GÅATETJE ;

 LEXICON DOLVEcase
 +A+Sg: N_ODD_SG ;
 +A+Pl: N_ODD_PL ;
 +A:    N_ODD_ESS ;
  +A+SgNomCmp:            R ;
  +A+SgGenCmp:e%>n        R ;
  +A+PlGenCmp:%>%^DISIMPi R ;
  +A+Der1+Der/Dim:%»atj GIERIEHTSATJE ;


 
 LEXICON PRED-0
+A+Pred: FINAL1 ; ! Awaiting full adj analysis 
+A:   RELEVANTNOUNLEX ;


LEXICON PRED-S
+A+Pred:%>s FINAL1 ; ! Awaiting full adj analysis 
+A:%>s   RELEVANTNOUNLEX ;

LEXICON PRED-H
+A+Pred:%>h FINAL1 ; ! Awaiting full adj analysis 
+A:%>h   RELEVANTNOUNLEX ; ! check whether needed! XXX


LEXICON PRED-N
+A+Pred:%>n FINAL1 ; ! Awaiting full adj analysis 
+A:%>n   RELEVANTNOUNLEX ;

LEXICON PRED-AN
+A+Pred:%>an FINAL1 ; ! Awaiting full adj analysis 
+A:%>an   RELEVANTNOUNLEX ;



! Predicative forms, case inflected forms
! ---------------------------------------

LEXICON RELEVANTNOUNLEX
+Sg+Nom: FINAL1 ; ! Awaiting the noun fix


! Comparative forms
! -----------------

LEXICON EVENCOMP
 +Comp+Attr:%>%^DISIMP2be FINAL1 ;
 +Comp+Pred:%>%^DISIMP2be FINAL1 ;
 +Comp:%>%^DISIMP2b     GÅATETJE ;
 +Superl+Attr:%>%^DISIMP2mes FINAL1 ;
 +Superl+Pred:%>%^DISIMP2mes FINAL1 ;
 +Superl:%>%^DISIMP2mes    MES ;
 +Comp+Der1+Der/Dim+Attr:%>b%»adtje FINAL1 ;
 +Comp+Der1+Der/Dim+Attr:%>b%»odtje FINAL1 ;
 +Comp+Der1+Der/Dim+Pred:%>b%»adtje FINAL1 ;
 +Comp+Der1+Der/Dim+Pred:%>b%»odtje FINAL1 ;
 +Comp+Der1+Der/Dim:%>b%»adtj GIERIEHTSATJE ;
 +Comp+Der1+Der/Dim:%>b%»odtj GIERIEHTSATJE ;


LEXICON EVENCOMP-UML
 +Comp+Attr:%>%^DISIMP2be FINAL1 ;
 +Comp+Pred:%>%^DISIMP2be FINAL1 ;
 +Comp:%>%^DISIMP2be     FINAL1 ;
 +Superl+Attr:%>%^DISIMP2mes FINAL1 ;
 +Superl+Pred:%>%^DISIMP2mes FINAL1 ;
 +Superl:%>%^DISIMP2mes    FINAL1 ;
 +Comp+Der1+Der/Dim+Attr:%>b%»adtje FINAL1 ;
 +Comp+Der1+Der/Dim+Attr:%>b%»odtje FINAL1 ;
 +Comp+Der1+Der/Dim+Pred:%>b%»adtje FINAL1 ;
 +Comp+Der1+Der/Dim+Pred:%>b%»odtje FINAL1 ;
 +Comp+Der1+Der/Dim:%>b%»adtj GIERIEHTSATJE ;
 +Comp+Der1+Der/Dim:%>b%»odtj GIERIEHTSATJE ;

LEXICON ODDCOMP
 +Comp+Attr:%>åbpoe FINAL1 ;
 +Comp+Pred:%>åbpoe FINAL1 ;
 +Comp:%>åbp ÅBPOE ;
 +Superl+Attr:%>ommes FINAL1 ;
 +Superl+Pred:%>ommes FINAL1 ;
 +Superl:%>ommes  OMMES ;
 +Comp+Der1+Der/Dim+Attr:%>åbpoe%»tje FINAL1 ;
 +Comp+Der1+Der/Dim+Pred:%>åbpoe%»tje FINAL1 ;
 +Comp+Der1+Der/Dim:%>åbpoe%»tj  GÅATETJE ;




! ===========================
! Verbal inflection sublexica
! ===========================


! The auxiliaries
! ===============

LEXICON LEA

!+Inf:lea	  FINAL1 ; ! Uses årrodh
+Ind+Prs:l    LEA-PRES	;
+Ind+Prt:l    LEA-PRET	;
+Imp:l		  LEA-IMP ;
+ConNeg:leah  FINAL1 ;

LEXICON LEA-PRES
+Sg1:eam    FINAL1	;
+Sg2:eah    FINAL1	;
+Sg3:ea     FINAL1	;
+Du1:ean    FINAL1	;
+Du2:idien  FINAL1	;
+Du3:ægan   FINAL1	;
+Du3:æban   FINAL1	; !SUB
+Pl1:ibie   FINAL1	;
+Pl2:idie   FINAL1	;
+Pl3:eah    FINAL1	;

LEXICON LEA-PRET
+Sg1:im     FINAL1	;
+Sg2:ih     FINAL1	;
+Sg3:ij     FINAL1	;
+Du1:imen   FINAL1	;
+Du2:iden   FINAL1	;
+Du3:igan   FINAL1	;
+Pl1:imh    FINAL1	;
+Pl2:idh    FINAL1	;
+Pl3:in     FINAL1	;

LEXICON LEA-IMP
+Sg2:eah    FINAL1 ;
+Du2:idien  FINAL1 ;
+Pl2:idie   FINAL1 ;


! The negative verb
! =================

LEXICON NEG
+Ind+Prs:      IJ-PRES ;
+Ind+Prt:idtj  VSUF-PRT ;
+Imp:aell      NEGIMP ;
+Imp:oll       OLLE ;

LEXICON OLLE
+Sg1:em     FINAL1 ;
+Sg2:h      FINAL1 ;
+Sg3:es     FINAL1 ;
+Du1:en     FINAL1 ;
+Du2:eden   FINAL1 ;
+Pl1:ebe    FINAL1 ;
+Pl2:ede    FINAL1 ;
+Pl3:em     FINAL1 ;


LEXICON NEGIMP
+Sg2:ieh    FINAL1 ;
+Du2:eden   FINAL1 ;
+Pl2:ede    FINAL1 ;

LEXICON IJ-PRES
+Sg1:im     FINAL1	;
+Sg2:ih     FINAL1	;
+Sg3:ij     FINAL1	;
+Du1:ean    FINAL1	;
+Du2:idien  FINAL1	;
+Du3:eakan  FINAL1	;
+Pl1:ibie   FINAL1	;
+Pl2:idie   FINAL1	;
+Pl3:eah    FINAL1	;

LEXICON EDTJEDH
+Ind+Prs:       ED-PRES ;
+Ind+Prt:       ED-PRET ;
+Imp:           ED-IMP  ;
+Inf:edh        FINAL1	;
+VGen:eminie    FINAL1	;
+Act:eme        FINAL1	;
+ConNeg:h       FINAL1	;
+VAbess:h       FINAL1	;
+PrfPrc:eme     FINAL1  ;

LEXICON ED-PRES
+Sg1:em         FINAL1	;
+Sg2:h          FINAL1	;
+Sg3:a          FINAL1	;
+Du1:ien        FINAL1	;
+Du2:eden       FINAL1	;
+Du3:ijægan     FINAL1	;
+Pl1:ebe        FINAL1	;
+Pl2:ede        FINAL1	;
+Pl3:ieh        FINAL1	;

LEXICON ED-PRET
+Sg1:im     FINAL1	;
+Sg2:ih     FINAL1	;
+Sg3:i      FINAL1	;
+Du1:imen   FINAL1	;
+Du2:iden   FINAL1	;
+Du3:igan   FINAL1	;
+Pl1:imh    FINAL1	;
+Pl2:idh    FINAL1	;
+Pl3:ih     FINAL1	;

LEXICON ED-IMP
 +Sg2:h      FINAL1  ;
 +Du2:eden   FINAL1  ;
 +Pl2:ede    FINAL1  ;




! Odd-syllable verbs
! ==================

! Odd syllable verbs differ in Prt Sg3.
! This form is treated separately, and
! the rest of the paradigm is conflated.

! According to Errata för Verbh this is not the case.
! Thus, the form in -e is wrong, and we wil replace DÅERIEDIDH 
! WITH GOLTELIDH.

!LEXICON DÅERIEDIDH ! This one is commented out now that GOLTELIDH works
! GOLTELIDH ;       ! Next step is to remove it.

!LEXICON GOLTELIDH ! one Verbh class
!+V+Ind+Prt+Sg3:%>i FINAL1 ;
!              :%>  V-ODD ;

!LEXICON DÅERIEDIDH ! The other 4 Verbh classes
!+V+Ind+Prt+Sg3:%>i FINAL1 ;
!              :%>  V-ODD ;


!LEXICON V-ODD

LEXICON GOLTELIDH !odd-syll with -adte- as Der2
 COMMON-ODD ;
+V+Der2+Der/adte:%»%^adteUMLadt ÅARAJEHTEDH ;

!LEXICON AERVIEDIDH !odd-syll with -adte- as Der2 and eds-derivation
! COMMON-ODD ;
!+V+Der2+Der/adte:%»%^adteUMLadt ÅARAJEHTEDH ;
!+V+Der2+Der/eds:%^DISIMPs NULL-NULL-ODD ;


LEXICON DÅAJVOEHTIDH  !for stems ending -t-: dåajvoeht-, odd-syll with -alle- as Der2
 COMMON-ODD ;
+V+Der2+Der/alle:%»%^adteUMLall ÅARAJEHTEDH ;



LEXICON COMMON-ODD

! Finite forms
! ------------
 +V+Ind+Prs:%> VSUF-ODD-PRS ;
 +V+Ind+Prt:%> VSUF-ODD-PRT ;
 +V+Imp:%>     VSUF-ODD-IMP ;

! Infinite forms
! --------------
 +V+Inf:%>idh             FINAL1 ;
 +V+ConNeg:%>%^DISIMPh    FINAL1 ;
 +V+VGen:%>%^DISIMPen     FINAL1 ; ! = GH Modalis, KB måtesform, GH Voluntativ
 +V+Act:%>immie           FINAL1 ; ! = GH verbalsubst, KB nomen actionis
 +V+Ger:%>%^DISIMPeminie  FINAL1 ; ! = GH Gerundium
 +V+PrsPrc:%>æjja         FINAL1 ; ! = GH Ptz.Pr. => must go to nominal lexicon instead
 +V+A+Der/æjja+Attr:%>æjja     FINAL1 ; !XXX Testing, thinking aloud
 +V+A+Der/ihks+Attr:%>ihks     FINAL1 ; !XXX Testing, thinking aloud
 +V+PrfPrc:%>amme         FINAL1 ; ! = GH Ptz.Pt.
                                   !   GH Ptz.Fut. b'ïssejässaa missing here
 
!+V+VAbess:%>h            FINAL1 ; 
! On the issue on a possible Verbal abessive (karitiv), cf Hasselbrink:
! GH s. 139, V-Adj: präd -k(e)h , attr -k(e)s
! äbrekh ohne zu regnen, äbreks der nicht regnet, baal'eks der nicht gräbt, b'üdnekh ungezwirnt
! vädd'ekh ungegeben, ohne zu geben

! Derivations
! -----------
 +V+Der1+Der/NomAg:%»æjj     MAANA ;
 +V+Der1+Der/NomAct:%»imm    N_IE ;
! +V+Der3+Der/PassL:e%»sovv SOVVEDH ; ! XXX - the stem is quite varying for passives of this class
 +V+Der3+Der/ahtje:%»ahtj AHTJE ;
 +V+Der2+Der/ehte:%»eht ÅARAJEHTEDH ;




! Even-syllable verbs
! ===================

LEXICON BÅETEDH ! row A - Group I
 +V+Ind+Prs+Uml:%>%^1UMLa   V-I-PRS-SG ; ! class I special
 +V+Ind+Prs:%>ie        VSUF-EVEN-PRS-DUPL ;
 +V+Ind+Prt:%>iej        VSUF-ODD-PRT ;
 +V+Ind+Prt+Uml:%>%^iUML    VSUF-PRT ;
 +V+Imp:%>              VSUF-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh           FINAL1 ;
 +V+ConNeg:%>ieh        FINAL1 ;
 +V+VGen:%>ien          FINAL1 ;
 +V+Act:%>eme           FINAL1 ;
 +V+Ger:%>ieminie       FINAL1 ;
 +V+PrsPrc:%>ije        FINAL1 ;
 +V+PrsPrc:%>ijes        FINAL1 ;
 +V+PrfPrc+Uml:%>%^1UMLeme  FINAL1 ;
 ! row F trigger for Umlaut Prt
! Derivations
! -----------
 +V+Der1+Der/NomAg:%»ij     N_ODD ;
 +V+Der1+Der/NomAct:%»iem    N_ODD ;
 +V+Der1+Der/les:%»%^1UMLales NULL-NULL-ODD ;
 +V+Der3+Der/PassL+Uml:%»%^1UMLasovv SOVVEDH ;
 +V+Der3+Der/PassS+Uml:%»%^PASSUML VÅÅJNEDH ;
 +V+Der3+Der/gåete:%»iegået GÅETEDH ;
 +V+Der1+Der/stidh:%»iest DÅAJVOEHTIDH ;
 +V+Der1+Der/lidh:%»iel GOLTELIDH ;
 +V+Der1+Der/htjidh:%»%^htjidhUMLehtj GOLTELIDH ;
 +V+Der1+Der/htidh:%»ieht DÅAJVOEHTIDH ;
 +V+Der1+Der/dehte:%»iedeht ÅARAJEHTEDH ;
 +V+Der3+Der/ahtje:%»iejahtj AHTJE ;
 +V+Der1+Der/htalle:%»%^1UMLahtall SOVVEDH ; !Passive, only from transitive verbs
 +V+Der1+Der/lgidh:%»%^1UMLalg LGIDH ; !Passive, only from transitive verbs
 +V+Der1+Der/ldihkie:%»%^1UMLaldihkie NULL-NULL-EVEN ;
 +V+Der1+Der/ldahke:%»%^1UMLaldahke NULL-NULL-EVEN ;

LEXICON TJEARODH ! row C - Group II
 +V+Ind+Prs:%>          V-II-PRS-SG ; ! class II special
 +V+Ind+Prs:%>oe        VSUF-EVEN-PRS-DUPL ;
 +V+Ind+Prt:%>oej       VSUF-PRT ;
 +V+Imp:%>              VSUF-II-EVEN-IMP ;
 +V+Ind+Prt+Sg3:%>oej FINAL1 ;
! Infinite forms
! --------------
 +V+Inf:%>odh           FINAL1 ;
 +V+ConNeg:%>oeh        FINAL1 ;
 +V+VGen:%>oen          FINAL1 ;
 +V+Act:%>eme           FINAL1 ;
 +V+Ger:%>oeminie       FINAL1 ;
 +V+PrsPrc:%>oje        FINAL1 ;
 +V+PrsPrc:%>ojes        FINAL1 ;
 +V+PrfPrc+Uml:%>%^2UMLeme  FINAL1 ;
! Derivations
! -----------
 +V+Der1+Der/NomAg:%»oej     N_ODD ;
 +V+Der1+Der/NomAct:%»oem    N_ODD ;
 +V+Der1+Der/les:%»%^PASSUMLeles NULL-NULL-ODD ;
 +V+Der3+Der/PassL+Uml:%»%^2UMLesovv SOVVEDH ;
 +V+Der3+Der/PassS+Uml:%»%^PASSUML VÅÅJNEDH ;
 +V+Der3+Der/gåete:%»oegået GÅETEDH ;
 +V+Der1+Der/stidh:%»oest DÅAJVOEHTIDH ;
 +V+Der1+Der/lidh:%»oel GOLTELIDH ;
 +V+Der1+Der/htidh:%»oeht DÅAJVOEHTIDH ;
 +V+Der1+Der/dehte:%»oedeht ÅARAJEHTEDH ;
 +V+Der3+Der/ahtje:%»oejahtj AHTJE ;
 +V+Der1+Der/didh:%»%^PASSUMLed TJYÖREDIDH ;
 +V+Der1+Der/htalle:%»%^PASSUMLehtall SOVVEDH ; !Passive, only from transitive verbs
 +V+Der1+Der/lgidh:%»%^PASSUMLelg LGIDH ; !Passive, only from transitive verbs
 +V+Der1+Der/ldihkie:%»%^PASSUMLeldihkie NULL-NULL-EVEN ;
 +V+Der1+Der/ldahke:%»%^PASSUMLeldahke NULL-NULL-EVEN ;

 

LEXICON TSEAHKODH ! row C - Group II    these have (lexicalized) diminutives on -estit, and passives on -algidh
 +V+Ind+Prs:%>          V-II-PRS-SG ; ! class II special
 +V+Ind+Prs:%>oe        VSUF-EVEN-PRS-DUPL ;
 +V+Ind+Prt:%>oej       VSUF-PRT ;
 +V+Imp:%>              VSUF-II-EVEN-IMP ;
 +V+Ind+Prt+Sg3:%>oej FINAL1 ;
! Infinite forms
! --------------
 +V+Inf:%>odh           FINAL1 ;
 +V+ConNeg:%>oeh        FINAL1 ;
 +V+VGen:%>oen          FINAL1 ;
 +V+Act:%>eme           FINAL1 ;
 +V+Ger:%>oeminie       FINAL1 ;
 +V+PrsPrc:%>oje        FINAL1 ;
 +V+PrsPrc:%>ojes        FINAL1 ;
 +V+PrfPrc+Uml:%>%^2UMLeme  FINAL1 ;
! Derivations
! -----------
 +V+Der1+Der/NomAg:%»oej     N_ODD ;
 +V+Der1+Der/NomAct:%»oem    N_ODD ;
 +V+Der1+Der/les:%»%^PASSUMLeles NULL-NULL-ODD ;
 +V+Der3+Der/PassL+Uml:%»%^2UMLesovv SOVVEDH ;
 +V+Der3+Der/PassS+Uml:%»%^PASSUML VÅÅJNEDH ;
 +V+Der3+Der/gåete:%»oegået GÅETEDH ;
 +V+Der1+Der/lidh:%»oel GOLTELIDH ;
 +V+Der1+Der/htidh:%»oeht DÅAJVOEHTIDH ;
 +V+Der1+Der/dehte:%»oedeht ÅARAJEHTEDH ;
 +V+Der3+Der/ahtje:%»oejahtj AHTJE ;
 +V+Der1+Der/didh:%»%^PASSUMLed TJYÖREDIDH ;
 +V+Der1+Der/htalle:%»%^PASSUMLehtall SOVVEDH ; !Passive, only from transitive verbs
 +V+Der1+Der/lgidh:%»alg LGIDH ; !Passive, only from transitive verbs
 +V+Der1+Der/ldihkie:%»%^PASSUMLeldihkie NULL-NULL-EVEN ;
 +V+Der1+Der/ldahke:%»%^PASSUMLeldahke NULL-NULL-EVEN ;




LEXICON GUARKEDH ! row B - Group III
 +V+Ind+Prs:%>    V-III-PRS-SG ; ! class III special
 +V+Ind+Prs:%>a	  VSUF-EVEN-PRS-DUPL ;
 +V+Ind+Prt:%>aj  VSUF-PRT ;
 +V+Imp:%>        VSUF-III-EVEN-IMP ;
 +V+Ind+Prt+Sg3:%>aj FINAL1 ;
! Infinite forms
! --------------
 +V+Inf:%>edh     FINAL1 ;
 +V+VGen:%>an     FINAL1 ;
 +V+Act:%>eme     FINAL1 ;
 +V+Ger:%>aminie  FINAL1 ;
 +V+PrsPrc:%>ije  FINAL1 ;
 +V+PrsPrc:%>ijes  FINAL1 ;
 +V+PrfPrc:%>eme  FINAL1 ;
 +V+ConNeg:%>0h   FINAL1 ;
! Derivations
! -----------
 +V+Der1+Der/NomAg:%»ij     N_ODD ;
 +V+Der1+Der/NomAct:%»em    N_ODD ;
 +V+Der1+Der/les:a%»les NULL-NULL-ODD ;
 +V+Der3+Der/PassL:a%»sovv SOVVEDH ;
 +V+Der3+Der/PassS+Uml:%»%^PASSUML VÅÅJNEDH ;
 +V+Der3+Der/gåete:%»agået GÅETEDH ;
 +V+Der1+Der/stidh:%»ast DÅAJVOEHTIDH ;
 +V+Der1+Der/lidh:%»al GOLTELIDH ;
 +V+Der1+Der/htidh:%»aht DÅAJVOEHTIDH ;
 +V+Der1+Der/dehte:%»adeht ÅARAJEHTEDH ;
 +V+Der3+Der/ahtje:%»ajahtj AHTJE ;
 +V+Der1+Der/didh:%»%^didhUMLed TJYÖREDIDH ;
 +V+Der1+Der/htalle:%»ahtall SOVVEDH ; !Passive, only from transitive verbs
 +V+Der1+Der/lgidh:%»alg LGIDH ; !Passive, only from transitive verbs
 +V+Der1+Der/ldihkie:%»aldihkie NULL-NULL-EVEN ;
 +V+Der1+Der/ldahke:%»aldahke NULL-NULL-EVEN ;



LEXICON TJOEHPEDH ! row D - Group IV
 +V+Ind+Prs:%>     V-IV-EVEN-PRS ;
 +V+Ind+Prt:%>     VSUF-PRT ;
 +V+Imp:%>         VSUF-IV-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh      FINAL1 ;
 +V+VGen:%>en      FINAL1 ;
 +V+Act:%>eme      FINAL1 ;
 +V+Ger:%>eminie   FINAL1 ;
 +V+PrsPrc:%>ije   FINAL1 ;
 +V+PrsPrc:%>ijes  FINAL1 ;
 +V+PrfPrc:%>eme   FINAL1 ;
 +V+ConNeg:%>0h    FINAL1 ;
! Derivations
! -----------
 +V+Der1+Der/NomAg:%»ij     N_ODD ;
 +V+Der1+Der/NomAct:%»em    N_ODD ;
 +V+Der1+Der/les:e%»les NULL-NULL-ODD ;
 +V+Der3+Der/PassL:e%»sovv SOVVEDH ;
 +V+Der3+Der/PassS+Uml:%»%^PASSUML VÅÅJNEDH ;
 +V+Der3+Der/gåete:%»egået GÅETEDH ;
 +V+Der1+Der/stidh:%»est DÅAJVOEHTIDH ;
 +V+Der1+Der/lidh:%»el GOLTELIDH ;
 +V+Der1+Der/htidh:%»eht DÅAJVOEHTIDH ;
 +V+Der1+Der/dehte:%»edeht ÅARAJEHTEDH ;
 +V+Der3+Der/ahtje:%»ijahtj AHTJE ;
 +V+Der1+Der/didh:%»ed TJYÖREDIDH ;
 +V+Der1+Der/htalle:%»ehtall SOVVEDH ; !Passive, only from transitive verbs
 +V+Der1+Der/lgidh:%»elg LGIDH ; !Passive, only from transitive verbs
 +V+Der1+Der/ldihkie:%»eldihkie NULL-NULL-EVEN ;
 +V+Der1+Der/ldahke:%»eldahke NULL-NULL-EVEN ;




LEXICON VÅÅJNEDH ! row E - Group V
 +V+Ind+Prs:%>   VSUF-V-EVEN-PRS ;
 +V+Ind+Prt:%>   VSUF-PRT ;
 +V+Imp:%>       VSUF-IV-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh    FINAL1 ;
 +V+VGen:%>en    FINAL1 ;
 +V+Act:%>eme    FINAL1 ;
 +V+Ger:%>eminie FINAL1 ;
 +V+PrsPrc:%>ije FINAL1 ;
 +V+PrsPrc:%>ijes  FINAL1 ;
 +V+PrfPrc:%>eme FINAL1 ;
 +V+ConNeg:%>h   FINAL1 ;
! Derivations
! -----------
 +V+Der1+Der/NomAg:%»ij     N_ODD ;
 +V+Der1+Der/NomAct:%»em    N_ODD ;
 +V+Der1+Der/les:e%»les NULL-NULL-ODD ;
 +V+Der3+Der/PassL:e%»sovv SOVVEDH ;
! +V+Der3+Der/PassS+Uml:%»%^PASSUML VÅÅJNEDH ; ! No short passives in Group V
 +V+Der3+Der/gåete:%»egået GÅETEDH ;
 +V+Der1+Der/stidh:%»est DÅAJVOEHTIDH ;
 +V+Der1+Der/lidh:%»el GOLTELIDH ;
 +V+Der1+Der/htidh:%»eht DÅAJVOEHTIDH ;
 +V+Der1+Der/dehte:%»edeht ÅARAJEHTEDH ;
 +V+Der3+Der/ahtje:%»ejahtj AHTJE ;
 +V+Der1+Der/didh:%»ed TJYÖREDIDH ;
 +V+Der1+Der/htalle:%»ehtall SOVVEDH ; !Passive, only from transitive verbs
 +V+Der1+Der/lgidh:%»elg LGIDH ; !Passive, only from transitive verbs
 +V+Der1+Der/ldihkie:%»eldihkie NULL-NULL-EVEN ;
 +V+Der1+Der/ldahke:%»eldahke NULL-NULL-EVEN ;





LEXICON GÖÖLEDH ! row F - Group VI
 +V+Ind+Prs:%>   VSUF-VI-EVEN-PRS ;
 +V+Ind+Prt:%>   VSUF-PRT ;
 +V+Imp:%>       VSUF-IV-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh    FINAL1 ;
 +V+VGen:%>en    FINAL1 ;
 +V+Act:%>eme    FINAL1 ;
 +V+Ger:%>minie  FINAL1 ;
 +V+PrsPrc:%>ije FINAL1 ;
 +V+PrsPrc:%>ijes FINAL1 ;
 +V+PrfPrc:%>eme FINAL1 ;
 +V+ConNeg:%>h   FINAL1 ;
! Derivations
! -----------
 +V+Der1+Der/NomAg:%»ij     N_ODD ;
 +V+Der1+Der/NomAct:%»em    N_ODD ;
 +V+Der1+Der/les:e%»les NULL-NULL-ODD ;
 +V+Der3+Der/PassL:e%»sovv SOVVEDH ;
! +V+Der3+Der/PassS+Uml:%»%^PASSUML VÅÅJNEDH ; ! No short passives in Group VI
 +V+Der3+Der/gåete:%»egået GÅETEDH ;
 +V+Der1+Der/stidh:%»est DÅAJVOEHTIDH ;
 +V+Der1+Der/lidh:%»el GOLTELIDH ;
 +V+Der1+Der/htidh:%»eht DÅAJVOEHTIDH ;
 +V+Der1+Der/dehte:%»edeht ÅARAJEHTEDH ;
 +V+Der3+Der/ahtje:%»ijahtj AHTJE ;
 +V+Der1+Der/didh:%»ed TJYÖREDIDH ;
 +V+Der1+Der/htalle:%»ehtall SOVVEDH ; !Passive, only from transitive verbs
 +V+Der1+Der/lgidh:%»elg LGIDH ; !Passive, only from transitive verbs
 +V+Der1+Der/ldihkie:%»eldihkie NULL-NULL-EVEN ;
 +V+Der1+Der/ldahke:%»eldahke NULL-NULL-EVEN ;





! ---- Verbal derivation sublexica ----

LEXICON ÅARAJEHTEDH ! row A - Group I
 +V+Ind+Prs+Uml:%>%^1UMLa   V-I-PRS-SG ; ! class I special
 +V+Ind+Prs:%>ie        VSUF-EVEN-PRS-DUPL ;
 +V+Ind+Prt+Uml:%>%^iUML    VSUF-PRT ;
 +V+Imp:%>              VSUF-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh           FINAL1 ;
 +V+ConNeg:%>ieh        FINAL1 ;
 +V+VGen:%>ien          FINAL1 ;
 +V+Act:%>eme           FINAL1 ;
 +V+Ger:%>ieminie       FINAL1 ;
 +V+PrsPrc:%>ije        FINAL1 ;
 +V+PrsPrc:%>ijes        FINAL1 ;
 +V+PrfPrc+Uml:%>%^1UMLeme  FINAL1 ;
 ! row F trigger for Umlaut Prt
! Derivations
! -----------
 +V+Der1+Der/NomAg:%»ij     N_ODD ;
 +V+Der1+Der/NomAct:%»iem    N_ODD ;
 +V+Der3+Der/PassL+Uml:%»%^1UMLasovv SOVVEDH ;
 +V+Der3+Der/PassS+Uml:%»%^PASSUML VÅÅJNEDH ;
 +V+Der3+Der/gåete:%»iegået GÅETEDH ;
 +V+Der3+Der/ahtje:%»iejahtj AHTJE ;


LEXICON GÅETEDH ! from Der/gåete
 +V+Ind+Prs+Uml:%>%^1UMLa   V-I-PRS-SG ; ! class I special
 +V+Ind+Prs:%>ie        VSUF-EVEN-PRS-DUPL ;
 +V+Ind+Prt+Uml:%>%^iUML    VSUF-PRT ;
 +V+Imp:%>              VSUF-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh           FINAL1 ;
 +V+ConNeg:%>ieh        FINAL1 ;
 +V+VGen:%>ien          FINAL1 ;
 +V+Act:%>eme           FINAL1 ;
 +V+Ger:%>ieminie       FINAL1 ;
 +V+PrfPrc+Uml:%>%^1UMLeme  FINAL1 ;
 ! row F trigger for Umlaut Prt

LEXICON AHTJE ! row D - Group IV
 +V+Ind+Prs:%>     V-IV-EVEN-PRS ;
 +V+Ind+Prt:%>     VSUF-PRT ;
 +V+Imp:%>         VSUF-IV-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh      FINAL1 ;
 +V+VGen:%>en      FINAL1 ;
 +V+Act:%>eme      FINAL1 ;
 +V+Ger:%>eminie   FINAL1 ;
 +V+PrfPrc:%>eme   FINAL1 ;
 +V+ConNeg:%>0h    FINAL1 ;

LEXICON SOVVEDH ! row D - Group IV
 +V+Ind+Prs:%>     V-IV-EVEN-PRS ;
 +V+Ind+Prt:%>     VSUF-PRT ;
 +V+Imp:%>         VSUF-IV-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh      FINAL1 ;
 +V+VGen:%>en      FINAL1 ;
 +V+Act:%>eme      FINAL1 ;
 +V+Ger:%>eminie   FINAL1 ;
 +V+PrsPrc:%>ije   FINAL1 ;
 +V+PrsPrc:%>ijes   FINAL1 ;
 +V+PrfPrc:%>eme   FINAL1 ;
 +V+ConNeg:%>0h    FINAL1 ;
! Derivations
! -----------
 +V+Der1+Der/NomAg:%»ij     N_ODD ;
 +V+Der1+Der/NomAct:%»em    N_ODD ;
 +V+Der3+Der/gåete:%»egået GÅETEDH ;
 +V+Der3+Der/ahtje:%»ijahtj AHTJE ;

LEXICON TJYÖREDIDH

! Finite forms
! ------------
 +V+Ind+Prs:%> VSUF-ODD-PRS ;
 +V+Ind+Prt:%> VSUF-ODD-PRT ;
 +V+Imp:%>     VSUF-ODD-IMP ;

! Infinite forms
! --------------
 +V+Inf:%>idh             FINAL1 ;
 +V+ConNeg:%>%^DISIMPh    FINAL1 ;
 +V+VGen:%>%^DISIMPen     FINAL1 ; ! = GH Modalis, KB måtesform, GH Voluntativ
 +V+Act:%>immie           FINAL1 ; ! = GH verbalsubst, KB nomen actionis
 +V+Ger:%>%^DISIMPeminie  FINAL1 ; ! = GH Gerundium
 +V+PrsPrc:%>æjja         FINAL1 ; ! = GH Ptz.Pr. => must go to nominal lexicon instead
 +V+PrfPrc:%>amme         FINAL1 ; ! = GH Ptz.Pt.
                                   !   GH Ptz.Fut. b'ïssejässaa missing here
 
!+V+VAbess:%>h            FINAL1 ; 
! On the issue on a possible Verbal abessive (karitiv), cf Hasselbrink:
! GH s. 139, V-Adj: präd -k(e)h , attr -k(e)s
! äbrekh ohne zu regnen
! äbreks der nicht regnet
! baal'eks der nicht gräbt
! b'üdnekh ungezwirnt
! vädd'ekh ungegeben, ohne zu geben

! Derivations
! -----------
 +V+Der1+Der/NomAg:%»æjj     MAANA ;
 +V+Der1+Der/NomAct:%»imm    N_IE ;
! +V+Der3+Der/PassL:e%»sovv SOVVEDH ; ! XXX - the stem is quite varying for passives of this class
 +V+Der3+Der/ahtje:%»ahtj AHTJE ;

LEXICON LGIDH

! Finite forms
! ------------
 +V+Ind+Prs:%> VSUF-ODD-PRS ;
 +V+Ind+Prt:%> VSUF-ODD-PRT ;
 +V+Imp:%>     VSUF-ODD-IMP ;

! Infinite forms
! --------------
 +V+Inf:%>idh             FINAL1 ;
 +V+ConNeg:%>%^DISIMPh    FINAL1 ;
 +V+VGen:%>%^DISIMPen     FINAL1 ; ! = GH Modalis, KB måtesform, GH Voluntativ
 +V+Act:%>immie           FINAL1 ; ! = GH verbalsubst, KB nomen actionis
 +V+Ger:%>%^DISIMPeminie  FINAL1 ; ! = GH Gerundium
 +V+PrsPrc:%>æjja         FINAL1 ; ! = GH Ptz.Pr. => must go to nominal lexicon instead
 +V+PrfPrc:%>amme         FINAL1 ; ! = GH Ptz.Pt.
                                   !   GH Ptz.Fut. b'ïssejässaa missing here
 
!+V+VAbess:%>h            FINAL1 ; 
! On the issue on a possible Verbal abessive (karitiv), cf Hasselbrink:
! GH s. 139, V-Adj: präd -k(e)h , attr -k(e)s
! äbrekh ohne zu regnen
! äbreks der nicht regnet
! baal'eks der nicht gräbt
! b'üdnekh ungezwirnt
! vädd'ekh ungegeben, ohne zu geben

! Derivations
! -----------
 +V+Der1+Der/NomAg:%»æjj     MAANA ;
 +V+Der1+Der/NomAct:%»imm    N_IE ;
 +V+Der3+Der/ahtje:%»ahtj AHTJE ;


! Verbal affixes
! ==============

! Finite forms
! ------------

! Even

LEXICON V-I-PRS-SG ! Merge with V-EVEN-PRS if nothing special here.
 V-PRS-SG-12 ;
 V-PRS-SG-3 ;

LEXICON V-II-PRS-SG
 :oe       V-PRS-SG-12 ;
 +Uml:%^2UMLe  V-PRS-SG-3 ; ! ?? parallels the byörkesne case

LEXICON V-III-PRS-SG
 :a  V-PRS-SG-12 ; ! This should give guarka
 :oe V-PRS-SG-3 ; ! this is wrong, gives *guarka pro guarkoe
! perhaps use A2 as 0 rule, triggers deletion in front of aff border
! but then oe aff must come afterwards
 :oe FINAL1 ; ! well, perhaps a more archi-looking affix ?

LEXICON VSUF-V-EVEN-PRS
 :e  V-PRS-SG-1 ;
     V-PRS-SG-2 ;
 :oe V-PRS-SG-3 ;
 :'e			VSUF-EVEN-PRS-DUPL ;


LEXICON VSUF-EVEN-PRS-DUPL
 +Du1:X2n        		FINAL1	; !'e>oe
 +Du2:jidien            FINAL1	; !SUB Long form
 +Du2:d%^DISIMPen		FINAL1	; !oe>o a>e ie>e
 +Du3:X3jægan			FINAL1	; !'e>0i
 +Du3:b%^DISIMPen	    FINAL1	; !SUB !'e>0i
 +Pl1:b%^DISIMPe		FINAL1	; !oe>o a>e ie>e
 +Pl1:jibie		        FINAL1	; !SUB Long form
 +Pl2:jidie		        FINAL1	; !SUB Long form
 +Pl2:d%^DISIMPe		FINAL1	; !oe>o a>e ie>e
 +Pl3:X2h				FINAL1	; !'e>oe


LEXICON VSUF-EVEN-IMP
 +Sg2:ieh	    FINAL1  ;
 +Du2:eden      FINAL1  ;
 +Pl2:ede       FINAL1  ;


LEXICON VSUF-II-EVEN-IMP
 +Sg2:oeh	    FINAL1  ;
 +Du2:oden      FINAL1  ;
 +Pl2:ode       FINAL1  ;

LEXICON VSUF-III-EVEN-IMP
 +Sg2:ah	    FINAL1  ;
 +Du2:eden      FINAL1  ;
 +Pl2:ede       FINAL1  ;


LEXICON V-IV-EVEN-PRS
 :e		          V-PRS-SG-1 ;
 		          V-PRS-SG-2 ;
 +Sg3+Uml:%^3sUMLa	  FINAL1 ;
 +Du1+Uml:%^3dUMLien  FINAL1	;
 +Du2:eden		  FINAL1	;
 +Du3:ijægan	  FINAL1	;
 !+Du3:iben	      FINAL1	;
 +Pl1:ebe		  FINAL1	;
 +Pl2:ede		  FINAL1	;
 +Pl3+Uml:%^3dUMLieh  FINAL1	;


LEXICON VSUF-IV-EVEN-IMP
 +Sg2:h		    FINAL1  ;
 +Du2:eden      FINAL1  ;
 +Pl2:ede       FINAL1  ;



LEXICON VSUF-VI-EVEN-PRS
 :e	          V-PRS-SG-1 ;
 	          V-PRS-SG-2 ;
 +Sg3:ie     	  FINAL1 ;
! :ie		  VSUF-EVEN-PRS-DUPL ;
 +Du1:ien         FINAL1	;
 +Du2:eden	  FINAL1	; !i>0
 +Du3:ijægan	  FINAL1	; !e>0
 +Du3:iben	  FINAL1	; !SUB !i>0 ??
 +Pl1:ebe	  FINAL1	; !i>0
 +Pl2:ede	  FINAL1	; !i>0
 +Pl3:ieh         FINAL1	;



! Odd

LEXICON VSUF-ODD-PRS
    V-ODD-PRS-SG ;
    V-ODD-PRS-DUPL ;

LEXICON V-ODD-PRS-SG
 :%^DISIMPe V-PRS-SG-1 ;
 :%^DISIMP  V-PRS-SG-2 ;
 :%^DISIMPe V-PRS-SG-3 ;

LEXICON V-ODD-PRS-DUPL
 ODD_PRS_NON_DU3 ;
 ODD_PRS_DU3 ;

LEXICON ODD_PRS_NON_DU3
 +Du1:ien		FINAL1	;
 +Du2:idien		FINAL1	;
 +Pl1:ibie		FINAL1	;
 +Pl2:idie		FINAL1	;
 +Pl3:ieh		FINAL1	;

LEXICON ODD_PRS_DU3
 +Du3:ægan		FINAL1	;
 +Du3:æban		FINAL1	; !SUB

LEXICON VSUF-ODD-PRT
VSUF-PRT-SG-12 ;
VSUF-PRT-SG-3 ;
VSUF-PRT-DUPL ;

LEXICON VSUF-ODD-IMP
 +Sg2:%^DISIMPh	FINAL1  ;
 +Du2:idien       FINAL1  ;
 +Pl2:idie        FINAL1  ;


! Contract


! LEXICON VSUF-CONTR-PRS
!      :e  V-PRS-SG-1 ;
!      :   V-PRS-SG-2 ;
!      :ie V-PRS-SG-3 ;
!            VSUF-CONTR-DUPL ;
! 
! LEXICON VSUF-CONTR-DUPL
!  +Du1:ien		FINAL1	;
!  +Du2:eden		FINAL1	;
! ! Dual third does not get an +CTR tag
!  +Du3:eben		FINAL1	; !SUB
!  +Pl1:ebe		FINAL1	;
!  +Pl2:ede		FINAL1	;
!  +Pl3:ieh		FINAL1	;
! 
! LEXICON VSUF-CONTR-PRT
!          VSUF-PRT-SG-12 ;
!          VSUF-PRT-DUPL ;
! 
! LEXICON VSUF-CONTR-IMP
!  +Sg2:h    FINAL1  ;
!  +Du2:eden FINAL1  ;
!  +Pl2:ede  FINAL1  ;


! Common even-odd-contr
! ---------------------

LEXICON V-PRS-SG-12 ! Kutt denne viss ikkje ref til
 V-PRS-SG-1 ;
 V-PRS-SG-2 ;

LEXICON V-PRS-SG-1
 +Sg1:m			FINAL1	; ! Border in prev lex

LEXICON V-PRS-SG-2
 +Sg2:h		FINAL1	;

LEXICON V-PRS-SG-3
 +Sg3:  		FINAL1	;

! LEXICON V-PRS-DUPL
!  +Du1:n         FINAL1	;
!  +Du2:dI1n		FINAL1	;
!  +Du3:gan		FINAL1	;
!  +Pl1:bI1		FINAL1	;
!  +Pl2:dI1		FINAL1	;
!  +Pl3:h		    FINAL1	;

LEXICON VSUF-PRT
 VSUF-PRT-SG-12 ;
 VSUF-PRT-SG-3 ;
 VSUF-PRT-DUPL ;

LEXICON VSUF-PRT-SG-12
+Sg1:im		FINAL1	;
+Sg2:ih		FINAL1	;

LEXICON VSUF-PRT-SG-3
+Sg3:i		FINAL1	;

LEXICON VSUF-PRT-DUPL
+Du1:imen		FINAL1	;
+Du2:iden		FINAL1	;
+Du3:igan		FINAL1	;
+Pl1:imh		FINAL1	;
+Pl2:idh		FINAL1	;
+Pl3:in		    FINAL1	;




!LEXICON V-II-PRS-DUPL
! :oe		    VSUF-EVEN-PRS-DUPL ;
! +Du1:oen      FINAL1	;
! +Du2:oden		FINAL1	; !e>0
! +Du3:oejægan	FINAL1	;
! +Du3:oeben	FINAL1	; !SUB
! +Pl1:obe		FINAL1	; !e>0
! +Pl2:ode		FINAL1	; !e>0
! +Pl3:oeh		FINAL1	;

!LEXICON V-III-PRS-DUPL
! :a			VSUF-EVEN-PRS-DUPL ;
! +Du1:an       FINAL1	;
! +Du2:eden		FINAL1	; !a>e
! +Du3:ajægan	FINAL1	;
! +Du3:aben	FINAL1	; !SUB
! +Pl1:ebe		FINAL1	; !a>e
! +Pl2:ede		FINAL1	; !a>e
! +Pl3:ah		FINAL1	;

! From V verbs
! +Du1:oen       FINAL1	; !'e>oe
! +Du2:eden		FINAL1	;
! +Du3:ijægan	FINAL1	; !e>i
! +Du3:iben	    FINAL1	; !SUB !e>i
! +Pl1:ebe		FINAL1	;
! +Pl2:ede		FINAL1	;
! +Pl3:oeh		FINAL1	; !'e>oe

!! LEXICON V-EVEN-PRS
!!   V-PRS-SG-12 ;
!!   V-PRS-SG-3 ;
!!    V-EVEN-PRS-DUPL ;

!LEXICON V-EVEN-PRS-DUPL
! :ie		VSUF-EVEN-PRS-DUPL ;
! +Du1:ien       FINAL1	;
! +Du2:eden		FINAL1	; !i>0
! +Du3:iejægan	FINAL1	;
! +Du3:ieben	FINAL1	; !SUB
! +Pl1:ebe		FINAL1	; !i>0
! +Pl2:ede		FINAL1	; !i>0
! +Pl3:ieh		FINAL1	;






! Final lexicon
! =============


LEXICON FINAL1
  0:#			#	;
