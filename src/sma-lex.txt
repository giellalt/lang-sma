Multichar_Symbols

  +N +Sg +Pl +Nom +Acc +Gen +Ine +Ela +Ill +Com +Ess
  +PxSg1 +PxSg2 +PxSg3
  +PxDu1 +PxDu2 +PxDu3
  +PxPl1 +PxPl2 +PxPl3

  +V +Prs +Prt
  +Sg1 +Sg2 +Sg3
  +Du1 +Du2 +Du3
  +Pl1 +Pl2 +Pl3

  +Inf +Neg +VGen +Ger +Ptc
  +Ind
  +Imp +ImpII !Den siste berre nektingsverbet

! Berre hjelpeverbet/nektingsverbet (Pot. for andre V?):
  +Pot +Cond  ! +Opt (Hasselbrink 48.3.I, 48.14, 48.15, 48.21)

! Frå nordsamisk:
! +Actor +Clt
  +A +Comp +Superl
  +Attr

  +Pron +Pers +Dem +Adp +Po +Adv +Interj
  +CS +CC +Pcle +CLB

  A1 A2 A3 A4 E1 I1 I2 I3 I4 O1 O2 U1 U2 Æ Å1 Å2 Å3 Å4 D1
  X2


! ================
! The root lexicon
! ================

LEXICON Root
  0:#			Noun	;
  0:#			ProperNoun	;  
  0:#			Verb	;
  0:#			Pronoun	;
  0:#			Adverb	;
  0:#			Subjunction	;
  0:#			Conjunction	;
  0:#			Particle	;
  0:#			Adposition	;
  0:#			Punctuation	;  
  0:#			Interjection	;  

! ============================
! Nominal inflection sublexica
! ============================



! Inflection for nouns ending in '-ie'
! ------------------------------------

LEXICON N_IE
  +N+Sg+Ill:%>A1n	FINAL1	;	! Special Ill
  +N+Ess:%>inI1	FINAL1	;	! Special Ess
  +N: J-LESS-CASE		;	! cases without -i/j-
  +N: I-CLASS			;	! cases with -i-


! Inflection for nouns ending in oe
! ---------------------------------

LEXICON N_OE
  +N+Sg+Ill:%>A1sD1e	FINAL1	;	! Causes Umlaut
  +N+Sg+Ill:%>se	FINAL1	;	! does NOT cause Umlaut
  +N+Ess:%>jnI1	FINAL1	;	! cases without -i/j-
  +N: J-LESS-CASE		;	! cases with -j-
  +N: J-CLASS			;


! Inflection for all other nouns
! ------------------------------

LEXICON N_OTHERS
  +N+Sg+Ill:%>A1sD1e	FINAL1	;	! Special Ill
  +N+Sg+Ess:%>inI1	FINAL1	;	! Special Ess
  +N: J-LESS-CASE		;	! cases without -i/j-
  +N: I-CLASS			;	! cases with -i-




! Sublexica to handle the i/j difference between oe nouns and other
! -----------------------------------------------------------------

LEXICON I-CLASS
  +Pl+Ela:%>ijstI1	FINAL1	;	! Ela has -iste
  :%>i			IJ-CASE ;	! others have -ine etc.

LEXICON J-CLASS
  +Pl+Ela:%>jstI1	FINAL1	;	! Ela has -ijste
  :%>j			IJ-CASE ;	! others have -jne etc.



! Remaining grammatical case lexica for the nouns
! ===============================================


LEXICON J-LESS-CASE
  +Sg+Nom:0		FINAL1	;
  +Sg+Acc:%>m		FINAL1	;
  +Sg+Gen:%>n		FINAL1	;
  +Sg+Ine:%>snI1	FINAL1	;
  +Sg+Ela:%>stI1	FINAL1	;
  +Pl+Nom:%>h		FINAL1	;
  +Pl+Gen:%>I3		FINAL1	;
  +Pl+Com:%>I3+gujmie	FINAL1	;

LEXICON IJ-CASE
  +Sg+Com:nI1		FINAL1	;
  +Pl+Acc:dI1		FINAL1	;
  +Pl+Ill:dI1		FINAL1	;
  +Pl+Ine:nI1		FINAL1	;







! ===========================
! Verbal inflection sublexica
! ===========================


! The auxiliaries
! ===============

LEXICON LEA

+Ind+Prs:l LEA-PRES	;
+Ind+Prt:l LEA-PRET	;

LEXICON LEA-PRES
+Sg1:eam		FINAL1	;
+Sg2:eah		FINAL1	;
+Sg3:ea			FINAL1	;
+Du1:ean		FINAL1	;
+Du2:idien		FINAL1	;
+Du3:ægan		FINAL1	;
+Pl1:ibie		FINAL1	;
+Pl2:idie		FINAL1	;
+Pl3:eah		FINAL1	;

LEXICON LEA-PRET
+Sg1:im		FINAL1	;
+Sg2:ih		FINAL1	;
+Sg3:ij		FINAL1	;
+Du1:imen	FINAL1	;
+Du2:iden	FINAL1	;
+Du3:igan	FINAL1	;
+Pl1:imh	FINAL1	;
+Pl2:idh	FINAL1	;
+Pl3:in		FINAL1	;


! The negative verb
! =================

LEXICON NEG
+Ind+Prs:	IJ-PRES ;
+Ind+Prt:idtj	VSUF-PRT ;

LEXICON IJ-PRES
+Sg1:im		FINAL1	;
+Sg2:ih		FINAL1	;
+Sg3:ij		FINAL1	;
+Du1:ean	FINAL1	;
+Du2:idien	FINAL1	;
+Du3:eakan	FINAL1	;
+Pl1:ibie	FINAL1	;
+Pl2:idie	FINAL1	;
+Pl3:eah	FINAL1	;



! Odd-syllable verbs
! ==================

LEXICON DÅERIEDIDH

! Finite forms
! ------------
 +V+Ind+Prs: VSUF-PRS ;
 +V+Ind+Prt: VSUF-PRT ;

! Infinite forms
! --------------
 +V+Inf:%>0dh		FINAL1 ;
 +V+VGen:%>minie	FINAL1 ; ! GH Modalis, KB måtesform
				 ! GH Voluntativ
 +V+Act:%>me		FINAL1 ; ! GH verbalsubst, KB nomen actionis
 +V+Ger:%>minie		FINAL1 ;
 +V+PrsPrc:%>ije	FINAL1 ; ! muzt go to nominal lexicon instead
				 ! Ptz.Fut b'ïssejässaa
 +V+PrtPrc:%>me		FINAL1 ;
 +V+ConNeg:%>		FINAL1 ;
 +V+VAbess:%>		FINAL1 ;


! Even-syllable verbs
! ===================

LEXICON BÅETEDH ! row A
 +V+Prs: V-I-PRS-SG ; ! class I special
 +V+Prs:  V-PRS-DUPL ;
 +V+Prt:j  VSUF-PRT ;
 +V+Inf:%>dh FINAL1 ;
 +V+Ger:%>minI1 FINAL1 ;
 +V+PrtPrc:%>me FINAL1 ;
 +V+ConNeg:%>0h  FINAL1 ;
 ! row F trigger for Umlaut Prt

LEXICON TJEARODH ! row C
 +V+Prs:  V-II-PRS-SG ; ! class II special
 +V+Prs:  V-PRS-DUPL ;
 +V+Prt:j  VSUF-PRT ;

LEXICON GUARKEDH ! row B
 +V+Prs: V-III-PRS-SG ; ! class III special
 +V+Prt:j  VSUF-PRT ;

LEXICON TJOEHPEDH ! row D
 +V+Prs:  VSUF-PRS ; 
 +V+Prt:  VSUF-PRT ;

LEXICON VÅÅJNEDH ! row E
 +V+Prs:  VSUF-PRS ; 
 +V+Prt:  VSUF-PRT ;

LEXICON GÖÖLEDH ! row F
 +V+Prs:  VSUF-PRS ; 
 +V+Prt:  VSUF-PRT ;


! Verbal sublexica
! ================


LEXICON V-I-PRS-SG
 :A1 V-PRS-SG-12 ;
 :A1 V-PRS-SG-3 ;

LEXICON V-II-PRS-SG
 V-PRS-SG-12 ;
 :A1 V-PRS-SG-3 ; ! ?? parallels the byörkesne case

LEXICON V-III-PRS-SG
 V-PRS-SG-12 ; ! This should give guarka
! V-PRS-SG-3 ; ! this is wrong, gives *guarka pro guarkoe
! perhaps use A2 as 0 rule, triggers deletion in front of aff border
! but then oe aff must come afterwards
 :%>oe FINAL1 ; ! well, perhaps a more archi-looking affix ?



! Verbal affixes
! ==============

! Finite forms
! ------------

LEXICON VSUF-PRS
 V-PRS-SG-12 ;
 V-PRS-SG-3 ;
 V-PRS-DUPL ;

LEXICON V-PRS-SG-12
 +Sg1:%>m		FINAL1	;
 +Sg2:%>h		FINAL1	;

LEXICON V-PRS-SG-3
 +Sg3:%>		FINAL1	;

LEXICON V-PRS-DUPL
 +Du1:%>n		FINAL1	;
 +Du2:%>dI1n	FINAL1	;
 +Du3:%>gan	FINAL1	;
 +Pl1:%>bI1	FINAL1	;
 +Pl2:%>dI1	FINAL1	;
 +Pl3:%>h		FINAL1	;

LEXICON VSUF-PRT
+Sg1:%>im		FINAL1	;
+Sg2:%>ih		FINAL1	;
+Sg3:%>i		FINAL1	;
+Du1:%>imen	FINAL1	;
+Du2:%>iden	FINAL1	;
+Du3:%>igan	FINAL1	;
+Pl1:%>imh	FINAL1	;
+Pl2:%>idh	FINAL1	;
+Pl3:%>in		FINAL1	;







! Final lexicon
! =============


LEXICON FINAL1
  0:#			#	;
