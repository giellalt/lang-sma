Multichar_Symbols

  +N +Sg +Pl +Nom +Acc +Gen +Ine +Ela +Ill +Com +Ess
  +PxSg1 +PxSg2 +PxSg3
  +PxDu1 +PxDu2 +PxDu3
  +PxPl1 +PxPl2 +PxPl3
  
  +V +Prs +Prt
  +Sg1 +Sg2 +Sg3
  +Du1 +Du2 +Du3
  +Pl1 +Pl2 +Pl3

  +Inf +Neg  +PrfPrc
! +VGen +Ger
  +Ind
  +Imp +ImpII !Den siste berre nektingsverbet

! Berre hjelpeverbet/nektingsverbet (Pot. for andre V?):
  +Pot +Cond  ! +Opt (Hasselbrink 48.3.I, 48.14, 48.15, 48.21)

! Frå nordsamisk:
! +Actor +Clt
  +A
  +Comp +Superl
  +Attr
  +Dirty ! Only until we have adj grammar in place - this tags mark adjectives
  		 ! with missing grammatical description

  +Uml   ! A tag to indicate realised or potential Umlaut
  +Hyph  ! A tag to indicate that a hyphen was used when compounding
  +Pron +Pers +Dem +Adp +Po +Adv +Interj +Num
  +Prop +Mal +Fem +Sur
  +CS +CC +Pcle +CLB +PUNCT +LEFT +RIGHT

  +CTR +ODD

  X2 X3
  E2 ! insert e ! Fjern denne.
  !Umlaut and diphthong simplification triggers:
  %^DISIMP
  %^1UML  ! a-uml, like 1sg prs, perf.part of båetedh/V-I, and ill sg of -ie nouns
  %^2UML  ! dark e, as 3sg prs & perf.part of tjearodh/V-II, and ill sg of -oe nouns
  %^3UML  ! not used
  %^3sUML ! a-uml in 3sg prs of V-IV (roehtedh - ruahta)
  %^3dUML ! ie-uml in 1du & 3pl prs of V-IV (roehtedh - ruehtien)
  %^iæUML ! not used
  %^iUML  ! i-uml in pret of V-I (båetedh - böötim)

! Der#begin
! Derivation position in a derivation row:  Affix and:
! 1              2               3                4                         POS type
+Der1           +Der2           +Der3            +Der4
! Der#1
+Der/Pass                                                                   ! VV - long only
+Der/NomAg
+Der/NomAct
! Der#end

! ================
! The root lexicon
! ================

LEXICON Root
  0:#			Noun	;
  0:#			ProperNoun	;
  0:#			Verb	;
  0:#			Adjective	;
  0:#			Pronoun	;
  0:#			Adverb	;
  0:#			Subjunction	;
  0:#			Conjunction	;
  0:#			Particle	;
  0:#			Adposition	;
  0:#			Punctuation	;
  0:#			Interjection	;
  0:#			Numeral ;


! =============================
! Compounding - copied from SMJ
! =============================
LEXICON R
 # Noun ;                           ! This is the normal case.		         ^C^
 #@P.NeedNoun.ON@  Verb ;      ! Flag diacr declares that the compound   ^C^
 #@P.NeedNoun.ON@  Adjective ; ! needs a +N tag later in the derivation  ^C^
 +Hyph#:%- Noun ;                        ! These 3 cases add a hyphen, so     ^C^
 +Hyph#:%-@P.NeedNoun.ON@  Verb ;        ! Sem-julggaštus and the like	     ^C^
 +Hyph#:%-@P.NeedNoun.ON@  Adjective ;   ! is allowed.			             ^C^
! # ProperNoun ;     ! Now, let us allow proper nouns as the 2nd part of com-^C^
 #:%- ProperNoun ;  ! pounds. Thus: London-Hull, LondonHull, Hull-viessu     ^C^
                    ! and viessuLondon are now all ok. Some refinement could ^C^
                    ! perhaps be done, but corpus research is needed.	     ^C^
 +Cmpnd:%- # ;     ! This to take care of Oahpo- ja dutkandept - want this in speller

LEXICON RAbbr !From abbreviations and acronyms to avoid two hyphens: a-- and ...
 # Noun ;                             ! This is the normal case.		       ^C^
! # NounSecond ;			          ! Testing some quasisuff, cf. noun file  ^C^
 #@P.NeedNoun.ON@  Verb ;         ! Flag diacr declares that the compound  ^C^
! #@P.NeedNoun.ON@  AdjectiveRoot ;   ! needs a +N tag later in the derivation ^C^
 +Hyph#:%- Noun ;                          ! These 3 cases add a hyphen, so         ^C^
 +Hyph#:%-@P.NeedNoun.ON@  Verb ;      ! Sem-julggaštus and the like	           ^C^
 +Hyph#:%-@P.NeedNoun.ON@  Adjective ; ! is allowed.			                   ^C^
!# ProperNoun ;     ! Now, let us allow proper nouns as the 2nd part of com-   ^C^
!#:%- ProperNoun ;  ! pounds. Thus: London-Hull, LondonHull, Hull-viessu       ^C^
                    ! and viessuLondon are now all ok. Some refinement could   ^C^
                    ! perhaps be done, but corpus research is needed.	       ^C^
 +Cmpnd: # ;        ! This to take care of Oahpo- ja dutkandept - want this in speller

! ============================
! Nominal inflection sublexica
! ============================

! Inflection for odd-syllable nouns
! ---------------------------------
LEXICON N_ODD
 +N+Sg: N_ODD_SG ;
 +N+Pl: N_ODD_PL ;
 +N:    N_ODD_ESS ;
  +N+SgNomCmp:            R ;
  +N+SgGenCmp:e%>n        R ;
  +N+PlGenCmp:%>%^DISIMPi R ;

LEXICON N_ODD_SG
  +Nom:e%^DISIMP         FINAL1 ;
  +Acc:e%>%^DISIMPm      FINAL1 ;
  +Gen:e%>%^DISIMPn      FINAL1 ;
  +Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
  +Ine:%>isnie           FINAL1 ;
  +Ela:%>istie           FINAL1 ;
  +Com:%>inie            FINAL1 ;

LEXICON N_ODD_PL
  +Nom:%>%^DISIMPh       FINAL1 ;
  +Acc:%>idie            FINAL1 ;
  +Gen:%>%^DISIMPi       FINAL1 ;
  +Ill:%>idie            FINAL1 ;
  +Ine:%>inie            FINAL1 ;
  +Ela:%>ijstie          FINAL1 ;
  +Com:%>%^DISIMPigujmie FINAL1 ;

LEXICON N_ODD_ESS
  +Ess:%>inie          FINAL1 ;


! Inflection for nouns ending in '-ie'
! ------------------------------------

LEXICON N_IE
  +N+Sg: N_IE_SG ;
  +N+Pl: N_IE_PL ;
  +N:    N_IE_ESS ;
  +N+SgNomCmp:ie              R ;
  +N+SgGenCmp:ie%>n           R ;
  +N+PlGenCmp:ie%>j           R ;
  +N+PlGenCmp+Uml:ie%>%^iUMLi R ;

LEXICON N_IE_SG
  +Nom:ie                  FINAL1 ;
  +Acc:ie%>m               FINAL1 ;
  +Gen:ie%>n               FINAL1 ;
! +Ill:%>an                FINAL1 ; !SUB - is this possible? IllSg without Uml in -ie?
  +Ill+Uml:%>%^1UMLan      FINAL1 ;
  +Ine:e%>sne              FINAL1 ;
  +Ela:e%>ste              FINAL1 ;
  +Com:%>ine               FINAL1 ;
  +Com+Uml:%>%^iUMLine     FINAL1 ;

LEXICON N_IE_PL
  +Nom:ie%>h               FINAL1 ;
  +Acc:%>ide               FINAL1 ;
  +Acc+Uml:%>%^iUMLide     FINAL1 ;
  +Gen:ie%>j               FINAL1 ;
  +Gen+Uml:%>%^iUMLi       FINAL1 ;
  +Ill:%>ide               FINAL1 ;
  +Ill+Uml:%>%^iUMLide     FINAL1 ;
  +Ine:%>ine               FINAL1 ;
  +Ine+Uml:%>%^iUMLine     FINAL1 ;
  +Ela:%>ijste             FINAL1 ;
  +Ela+Uml:%>%^iUMLijste   FINAL1 ;
  +Com:ie%>jgujmie         FINAL1 ;
  +Com+Uml:%>%^iUMLigujmie FINAL1 ;

LEXICON N_IE_ESS
  +N+Ess:%>ine                  FINAL1 ;
  +N+Ess+Uml:%>%^iUMLine        FINAL1 ;


! Inflection for nouns ending in oe
! ---------------------------------
LEXICON N_OE_UML
  +N+Sg: N_OE_SGILL_UML ; ! Causes Umlaut
         N_OE ;

LEXICON N_OE_SGILL_UML
  +Ill+Uml:e%>%^2UMLse	FINAL1      ; ! Causes Umlaut

LEXICON N_OE
  +N+Sg: N_OE_SG  ;
  +N+Sg: N_OE_PL  ;
  +N+Sg: N_OE_ESS ;
  +N+SgNomCmp:oe    R ;
  +N+SgGenCmp:oe%>n R ;
  +N+PlGenCmp:oe%>j R ;

LEXICON N_OE_SG
  +Nom:oe          FINAL1 ;
  +Acc:oe%>m       FINAL1 ;
  +Gen:oe%>n       FINAL1 ;
  +Ill:o%>se       FINAL1 ;
  +Ine:o%>sne      FINAL1 ;
  +Ela:o%>ste      FINAL1 ;
  +Com:o%>jne      FINAL1 ;

LEXICON N_OE_PL
  +Nom:oe%>h       FINAL1 ;
  +Acc:o%>jde      FINAL1 ;
  +Gen:oe%>j       FINAL1 ;
  +Ill:o%>jde      FINAL1 ;
  +Ine:o%>jne      FINAL1 ;
  +Ela:o%>jste     FINAL1 ;
  +Com:oe%>jgujmie FINAL1 ;

LEXICON N_OE_ESS
  +Ess:o%>jne         FINAL1 ;

! Inflection for maana-type nouns
! -------------------------------
LEXICON MAANA
  +N+Sg: MAANA_SG  ;
  +N+Pl: MAANA_PL  ;
  +N:    EVEN_ESS ;
  +N+SgNomCmp:a    R ;
  +N+SgGenCmp:a%>n R ;
  +N+PlGenCmp:a%>j R ;

LEXICON MAANA_SG
  +Nom:a          FINAL1 ;
  +Acc:a%>m       FINAL1 ;
  +Gen:a%>n       FINAL1 ;
  +Ill:e%>se      FINAL1 ;
  +Ine:e%>sne     FINAL1 ;
  +Ela:e%>ste     FINAL1 ;
  +Com:%>ine      FINAL1 ;

LEXICON MAANA_PL
  +Nom:a%>h       FINAL1 ;
  +Acc:%>ide      FINAL1 ;
  +Gen:a%>j       FINAL1 ;
  +Ill:%>ide      FINAL1 ;
  +Ine:%>ine      FINAL1 ;
  +Ela:%>ijste    FINAL1 ;
  +Com:a%>jgujmie FINAL1 ;

! Inflection for nïejte-type nouns
! --------------------------------
LEXICON NIEJTE
  +N+Sg: NIEJTE_SG  ;
  +N+Sg: NIEJTE_PL  ;
  +N:    EVEN_ESS ;
  +N+SgNomCmp:e    R ;
  +N+SgGenCmp:e%>n R ;
  +N+PlGenCmp:%>i  R ;

LEXICON NIEJTE_SG
  +Nom:e          FINAL1 ;
  +Acc:e%>m       FINAL1 ;
  +Gen:e%>n       FINAL1 ;
  +Ill:e%>se      FINAL1 ;
  +Ine:e%>sne     FINAL1 ;
  +Ela:e%>ste     FINAL1 ;
  +Com:%>ine      FINAL1 ;

LEXICON NIEJTE_PL
  +Nom:%>h        FINAL1 ;
  +Acc:%>ide      FINAL1 ;
  +Gen:%>i        FINAL1 ;
  +Ill:%>ide      FINAL1 ;
  +Ine:%>ine      FINAL1 ;
  +Ela:%>ijste    FINAL1 ;
  +Com:%>igujmie  FINAL1 ;

! Sub-paradigm inflections:
! -------------------------

LEXICON EVEN_ESS
  +Ess:%>ine         FINAL1 ;


! Old, not used, retained at the moment to remember the history:
!LEXICON I-CLASS ! Links from propernoun (revise?)
!  :%>i			IJ-CASE ;	! others have -ine etc.
!                I-EVEN ;
!                I-ODD ;
!
!LEXICON I-EVEN
!  +Pl+Ela:%>ijste	FINAL1	;	! Ela has -iste
!
!LEXICON I-ODD
!  +Pl+Ela:%>ijstie	FINAL1	;	! Ela has -iste



! Special for cns-final names, due to nomsg
! =========================================

LEXICON CNAME_ODD_SG
 NOMSG ;
 OBLIQUE_COMMON ;
 OBLIQUE_ODD ;

LEXICON CNAME_EVEN_SG
 NOMSG ;
 OBLIQUE_COMMON ;
 OBLIQUE_EVEN ;


LEXICON NOMSG
  +Sg+Nom:  	        FINAL1 ;

LEXICON OBLIQUE_COMMON
  +Sg+Acc:e%>m      FINAL1 ;
  +Sg+Gen:e%>n      FINAL1 ;

LEXICON OBLIQUE_ODD
  +Sg+Ine:e%>snie   FINAL1 ;
  +Sg+Ela:e%>stie   FINAL1 ;
  +Sg+Com:e%>nie    FINAL1 ;

LEXICON OBLIQUE_EVEN
  +Sg+Ine:e%>sne    FINAL1 ;
  +Sg+Ela:e%>ste    FINAL1 ;
  +Sg+Com:e%>ne     FINAL1 ;







! ===============================
! Adjectival inflection sublexica
! ===============================


LEXICON null-null-even
ATTR0 ;
PRED0 ;
+A:      evencomp ;

LEXICON s-null-even
ATTR0 ;
PRED0 ;
+A:         evencomp ;

LEXICON null-s-even
ATTR0 ;
PREDS ;
+A:        evencomp ;

LEXICON h-n-even
+A+Attr:%>h   FINAL1 ;
+A:%>n     RELEVANTNOUNLEX ;
+A:        evencomp ;

LEXICON null-null-odd
ATTR0 ;
PRED0 ;
+A:        oddcomp ;

LEXICON s-null-odd
ATTRS ;
PRED0 ;
+A:         oddcomp ;


LEXICON null-s-odd
ATTR0 ;
PREDS ;
+A:        oddcomp ;

LEXICON s-s-odd
ATTRS ;
PREDS ;
+A:        oddcomp ;


! Attribute lexica

LEXICON ATTRS
+A+Attr:%>s FINAL1 ;

LEXICON ATTR0
+A+Attr: FINAL1 ;


! Predicative lexica

LEXICON PRED0
+A:      RELEVANTNOUNLEX ;

LEXICON PREDS
+A:%>s     RELEVANTNOUNLEX ;


LEXICON adj-dirty
+A+Dirty # ;


! Predicative forms, case inflected forms

LEXICON RELEVANTNOUNLEX
+Nom: FINAL1 ; ! Awaiting the noun fix

! Comparative forms

LEXICON evencomp
+Comp:%>be      FINAL1 ;
+Superl:%>mes   FINAL1 ;

LEXICON oddcomp
+Comp:%>åbpoe    FINAL1 ;
+Superl:%>ommes  FINAL1 ;

! ===========================
! Verbal inflection sublexica
! ===========================


! The auxiliaries
! ===============

LEXICON LEA

!+Inf:lea	FINAL1 ; ! Uses årrodh
+Ind+Prs:l LEA-PRES	;
+Ind+Prt:l LEA-PRET	;
+Imp:l		LEA-IMP ;
+ConNeg:leah	FINAL1 ;

LEXICON LEA-PRES
+Sg1:eam		FINAL1	;
+Sg2:eah		FINAL1	;
+Sg3:ea			FINAL1	;
+Du1:ean		FINAL1	;
+Du2:idien		FINAL1	;
+Du3:ægan		FINAL1	;
+Du3:æban		FINAL1	; !SUB
+Pl1:ibie		FINAL1	;
+Pl2:idie		FINAL1	;
+Pl3:eah		FINAL1	;

LEXICON LEA-PRET
+Sg1:im		FINAL1	;
+Sg2:ih		FINAL1	;
+Sg3:ij		FINAL1	;
+Du1:imen	FINAL1	;
+Du2:iden	FINAL1	;
+Du3:igan	FINAL1	;
+Pl1:imh	FINAL1	;
+Pl2:idh	FINAL1	;
+Pl3:in		FINAL1	;

LEXICON LEA-IMP
+Sg2:eah	FINAL1 ;
+Du2:idien	FINAL1 ;
+Pl2:idie	FINAL1 ;


! The negative verb
! =================

LEXICON NEG
+Ind+Prs:	IJ-PRES ;
+Ind+Prt:idtj	VSUF-PRT ;
+Imp:aell  NEGIMP ;


LEXICON NEGIMP
+Sg2:ieh    FINAL1 ;
+Du2:eden   FINAL1 ;
+Pl2:ede    FINAL1 ;

LEXICON IJ-PRES
+Sg1:im		FINAL1	;
+Sg2:ih		FINAL1	;
+Sg3:ij		FINAL1	;
+Du1:ean	FINAL1	;
+Du2:idien	FINAL1	;
+Du3:eakan	FINAL1	;
+Pl1:ibie	FINAL1	;
+Pl2:idie	FINAL1	;
+Pl3:eah	FINAL1	;

LEXICON EDTJEDH
+Ind+Prs: ED-PRES ;
+Ind+Prt: ED-PRET ;
+Imp:     ED-IMP  ;
+Inf:edh	FINAL1	;
+VGen:eminie	FINAL1	;
+Act:eme	FINAL1	;
+ConNeg:h	FINAL1	;
+VAbess:h	FINAL1	;
+PrfPrc:eme	FINAL1  ;

LEXICON ED-PRES
+Sg1:em		FINAL1	;
+Sg2:h		FINAL1	;
+Sg3:a			FINAL1	;
+Du1:ien		FINAL1	;
+Du2:eden		FINAL1	;
+Du3:ijægan		FINAL1	;
+Pl1:ebe		FINAL1	;
+Pl2:ede		FINAL1	;
+Pl3:ieh		FINAL1	;

LEXICON ED-PRET
+Sg1:im		FINAL1	;
+Sg2:ih		FINAL1	;
+Sg3:i		FINAL1	;
+Du1:imen	FINAL1	;
+Du2:iden	FINAL1	;
+Du3:igan	FINAL1	;
+Pl1:imh	FINAL1	;
+Pl2:idh	FINAL1	;
+Pl3:ih 	FINAL1	;

LEXICON ED-IMP
 +Sg2:h		FINAL1  ;
 +Du2:eden      FINAL1  ;
 +Pl2:ede       FINAL1  ;




! Odd-syllable verbs
! ==================

! Odd syllable verbs differ in Prt Sg3.
! This form is treated separately, and
! the rest of the paradigm is conflated.

! According to Errata för Verbh this is not the case.
! Thus, the form in -e is wrong, and we wil replace DÅERIEDIDH 
! WITH GOLTELIDH.

!LEXICON DÅERIEDIDH ! This one is commented out now that GOLTELIDH works
! GOLTELIDH ;       ! Next step is to remove it.

!LEXICON GOLTELIDH ! one Verbh class
!+V+Ind+Prt+Sg3:%>i FINAL1 ;
!              :%>  V-ODD ;

!LEXICON DÅERIEDIDH ! The other 4 Verbh classes
!+V+Ind+Prt+Sg3:%>i FINAL1 ;
!              :%>  V-ODD ;


!LEXICON V-ODD
LEXICON GOLTELIDH

! Finite forms
! ------------
 +V+Ind+Prs:%> VSUF-ODD-PRS ;
 +V+Ind+Prt:%> VSUF-ODD-PRT ;
 +V+Imp:%>     VSUF-ODD-IMP ;

! Infinite forms
! --------------
 +V+Inf:%>idh             FINAL1 ;
 +V+ConNeg:%>%^DISIMPh    FINAL1 ;
 +V+VGen:%>%^DISIMPen     FINAL1 ; ! = GH Modalis, KB måtesform, GH Voluntativ
 +V+Act:%>immie           FINAL1 ; ! = GH verbalsubst, KB nomen actionis
 +V+Ger:%>%^DISIMPeminie  FINAL1 ; ! = GH Gerundium
 +V+PrsPrc:%>æjja         FINAL1 ; ! = GH Ptz.Pr. => must go to nominal lexicon instead
 +V+PrfPrc:%>amme         FINAL1 ; ! = GH Ptz.Pt.
                                   ! GH Ptz.Fut. b'ïssejässaa missing here
 
!+V+VAbess:%>h            FINAL1 ; 
! On the issue on a possible Verbal abessive (karitiv), cf Hasselbrink:
! GH s. 139, V-Adj: präd -k(e)h , attr -k(e)s
! äbrekh ohne zu regnen
! äbreks der nicht regnet
! baal'eks der nicht gräbt
! b'üdnekh ungezwirnt
! vädd'ekh ungegeben, ohne zu geben

! Derivations
! -----------
 +V+Der/NomAg:%»æjj     MAANA ;
 +V+Der/NomAct:%»imm    N_IE ;


! Contract verbs
! ==============

! It is unclear whether there is such a stem class, or whether these verbs
! are actually odd-syllable verbs.
! Thus, we divide the contract verbforms in two:
! Some would be identically inflected as contract and odd; they are unmarked
! In the other cases, we give two versions, marked with +CTR and +ODD, 
! respectively, so that we can trace their usage in corpora. 
! The goal is then to choose between the two (or to have a normative decision).

LEXICON BILLIJIDH
 +V+CTR+Ind+Prs:%>       VSUF-CONTR-PRS ;  ! All except Du3
 +V+ODD+Ind+Prs:%>ij     V-ODD-PRS-SG ;    ! All sg
 +V+ODD+Ind+Prs:%>ij     ODD_PRS_NON_DU3 ; ! Al except Du3
 +V+Ind+Prs+Du3:%>ijægan FINAL1 ;          ! Prs Du3 identical
 +V+Ind+Prt+Sg3:%>iji    FINAL1 ;          ! Prt Sg3 identical
 +V+CTR+Ind+Prt:%>       VSUF-CONTR-PRT ;  ! Not to Prt Sg3
 +V+ODD+Ind+Prt:%>ij     VSUF-PRT-SG-12 ;  ! Also not to Prt Sg3
 +V+ODD+Ind+Prt:%>ij     VSUF-PRT-DUPL ;   

 +V+CTR+Imp:%>           VSUF-CONTR-IMP ;
 +V+ODD+Imp:%>ij         VSUF-ODD-IMP ;

! Infinite forms
! --------------
 +V+Inf:%>ijidh		        FINAL1 ;
 +V+CTR+ConNeg:%^DISIMP%>h  FINAL1 ;
 +V+ODD+ConNeg:%>ijh        FINAL1 ;
 +V+CTR+VGen:%>eminie       FINAL1 ; ! GH Modalis, KB måtesform ! GH Voluntativ
 +V+ODD+VGen:%>ijen         FINAL1 ; 
 +V+CTR+Act:%>eme           FINAL1 ; ! GH verbalsubst, KB nomen actionis
 +V+ODD+Act:%>immie         FINAL1 ; ! GH verbalsubst, KB nomen actionis
 +V+CTR+Ger:%>eminie        FINAL1 ;
 +V+ODD+Ger:%>ijeminie      FINAL1 ;
 +V+CTR+PrsPrc:%>ije        FINAL1 ; ! must go to nominal lexicon instead
 +V+ODD+PrsPrc:%>æjja       FINAL1 ; ! must go to nominal lexicon instead
 +V+CTR+PrfPrc:%>eme        FINAL1 ;
 +V+ODD+PrfPrc:%>jamme      FINAL1 ;
! +V+VAbess:%>h		    FINAL1 ; ! This one is not given in Verbh.
! Derivations
! -----------
 +V+CTR+Der/NomAg:%»iej     N_ODD ;
 +V+ODD+Der/NomAg:%»æjj     MAANA ;
 +V+CTR+Der/NomAct:%»iem    N_ODD ;
 +V+ODD+Der/NomAct:%»imm    N_IE ;



! Even-syllable verbs
! ===================

LEXICON BÅETEDH ! row A
 +V+Ind+Prs:%>%^1UMLa   V-I-PRS-SG ; ! class I special
 +V+Ind+Prs:%>ie        VSUF-EVEN-PRS-DUPL ;
 +V+Ind+Prt:%>%^iUML    VSUF-PRT ;
 +V+Imp:%>              VSUF-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh           FINAL1 ;
 +V+ConNeg:%>ieh        FINAL1 ;
 +V+VGen:%>ien          FINAL1 ;
 +V+Act:%>eme           FINAL1 ;
 +V+Ger:%>ieminie       FINAL1 ;
 +V+PrsPrc:%>ije        FINAL1 ;
 +V+PrfPrc:%>%^1UMLeme  FINAL1 ;
 ! row F trigger for Umlaut Prt
! Derivations
! -----------
 +V+Der/NomAg:%»iej     N_ODD ;
 +V+Der/NomAct:%»iem    N_ODD ;


LEXICON TJEARODH ! row C
 +V+Ind+Prs:%>          V-II-PRS-SG ; ! class II special
 +V+Ind+Prs:%>oe        VSUF-EVEN-PRS-DUPL ;
 +V+Ind+Prt:%>oej       VSUF-PRT ;
 +V+Imp:%>              VSUF-II-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>odh           FINAL1 ;
 +V+ConNeg:%>oeh        FINAL1 ;
 +V+VGen:%>oen          FINAL1 ;
 +V+Act:%>eme           FINAL1 ;
 +V+Ger:%>oeminie       FINAL1 ;
 +V+PrsPrc:%>oje        FINAL1 ;
 +V+PrfPrc:%>%^2UMLeme  FINAL1 ;
! Derivations
! -----------
 +V+Der/NomAg:%»oej     N_ODD ;
 +V+Der/NomAct:%»oem    N_ODD ;

LEXICON GUARKEDH ! row B
 +V+Ind+Prs:%>    V-III-PRS-SG ; ! class III special
 +V+Ind+Prs:%>a	  VSUF-EVEN-PRS-DUPL ;
 +V+Ind+Prt:%>aj  VSUF-PRT ;
 +V+Imp:%>        VSUF-III-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh     FINAL1 ;
 +V+VGen:%>an     FINAL1 ;
 +V+Act:%>eme     FINAL1 ;
 +V+Ger:%>aminie  FINAL1 ;
 +V+PrsPrc:%>eje  FINAL1 ;
 +V+PrfPrc:%>eme  FINAL1 ;
 +V+ConNeg:%>0h   FINAL1 ;
! Derivations
! -----------
 +V+Der/NomAg:%»ej     N_ODD ;
 +V+Der/NomAct:%»em    N_ODD ;

LEXICON TJOEHPEDH ! row D
 +V+Ind+Prs:%>     V-IV-EVEN-PRS ;
 +V+Ind+Prt:%>     VSUF-PRT ;
 +V+Imp:%>         VSUF-IV-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh      FINAL1 ;
 +V+VGen:%>en      FINAL1 ;
 +V+Act:%>eme      FINAL1 ;
 +V+Ger:%>eminie   FINAL1 ;
 +V+PrsPrc:%>eje   FINAL1 ;
 +V+PrfPrc:%>eme   FINAL1 ;
 +V+ConNeg:%>0h    FINAL1 ;
! Derivations
! -----------
 +V+Der/NomAg:%»ej     N_ODD ;
 +V+Der/NomAct:%»em    N_ODD ;

LEXICON VÅÅJNEDH ! row E
 +V+Ind+Prs:%>   VSUF-V-EVEN-PRS ;
 +V+Ind+Prt:%>   VSUF-PRT ;
 +V+Imp:%>       VSUF-IV-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh    FINAL1 ;
 +V+VGen:%>en    FINAL1 ;
 +V+Act:%>eme    FINAL1 ;
 +V+Ger:%>eminie FINAL1 ;
 +V+PrsPrc:%>eje FINAL1 ;
 +V+PrfPrc:%>eme FINAL1 ;
 +V+ConNeg:%>h   FINAL1 ;
! Derivations
! -----------
 +V+Der/NomAg:%»ej     N_ODD ;
 +V+Der/NomAct:%»em    N_ODD ;

LEXICON GÖÖLEDH ! row F
 +V+Ind+Prs:%>   VSUF-VI-EVEN-PRS ;
 +V+Ind+Prt:%>   VSUF-PRT ;
 +V+Imp:%>       VSUF-IV-EVEN-IMP ;
! Infinite forms
! --------------
 +V+Inf:%>edh    FINAL1 ;
 +V+VGen:%>en    FINAL1 ;
 +V+Act:%>eme    FINAL1 ;
 +V+Ger:%>minie  FINAL1 ;
 +V+PrsPrc:%>eje FINAL1 ;
 +V+PrfPrc:%>eme FINAL1 ;
 +V+ConNeg:%>h   FINAL1 ;
! Derivations
! -----------
 +V+Der/NomAg:%»ej     N_ODD ;
 +V+Der/NomAct:%»em    N_ODD ;




! Verbal affixes
! ==============

! Finite forms
! ------------

! Even

LEXICON V-I-PRS-SG ! Merge with V-EVEN-PRS if nothing special here.
 V-PRS-SG-12 ;
 V-PRS-SG-3 ;

LEXICON V-II-PRS-SG
 :oe       V-PRS-SG-12 ;
 :%^2UMLe  V-PRS-SG-3 ; ! ?? parallels the byörkesne case

LEXICON V-III-PRS-SG
 :a  V-PRS-SG-12 ; ! This should give guarka
 :oe V-PRS-SG-3 ; ! this is wrong, gives *guarka pro guarkoe
! perhaps use A2 as 0 rule, triggers deletion in front of aff border
! but then oe aff must come afterwards
 :oe FINAL1 ; ! well, perhaps a more archi-looking affix ?

LEXICON VSUF-V-EVEN-PRS
 :e  V-PRS-SG-1 ;
     V-PRS-SG-2 ;
 :oe V-PRS-SG-3 ;
 :'e			VSUF-EVEN-PRS-DUPL ;


LEXICON VSUF-EVEN-PRS-DUPL
 +Du1:X2n        		FINAL1	; !'e>oe
 +Du2:d%^DISIMPen		FINAL1	; !oe>o a>e ie>e
 +Du3:X3jægan			FINAL1	; !'e>0i
 +Du3:b%^DISIMPen	    FINAL1	; !SUB !'e>0i
 +Pl1:b%^DISIMPe		FINAL1	; !oe>o a>e ie>e
 +Pl2:d%^DISIMPe		FINAL1	; !oe>o a>e ie>e
 +Pl3:X2h				FINAL1	; !'e>oe


LEXICON VSUF-EVEN-IMP
 +Sg2:ieh	    FINAL1  ;
 +Du2:eden      FINAL1  ;
 +Pl2:ede       FINAL1  ;


LEXICON VSUF-II-EVEN-IMP
 +Sg2:oeh	    FINAL1  ;
 +Du2:oden      FINAL1  ;
 +Pl2:ode       FINAL1  ;

LEXICON VSUF-III-EVEN-IMP
 +Sg2:ah	    FINAL1  ;
 +Du2:eden      FINAL1  ;
 +Pl2:ede       FINAL1  ;


LEXICON V-IV-EVEN-PRS
 :e		          V-PRS-SG-1 ;
 		          V-PRS-SG-2 ;
 +Sg3:%^3sUMLa	  FINAL1 ;
 +Du1:%^3dUMLien  FINAL1	;
 +Du2:eden		  FINAL1	;
 +Du3:ijægan	  FINAL1	;
 +Du3:iben	      FINAL1	;
 +Pl1:ebe		  FINAL1	;
 +Pl2:ede		  FINAL1	;
 +Pl3:%^3dUMLieh  FINAL1	;


LEXICON VSUF-IV-EVEN-IMP
 +Sg2:h		    FINAL1  ;
 +Du2:eden      FINAL1  ;
 +Pl2:ede       FINAL1  ;



LEXICON VSUF-VI-EVEN-PRS
 :e	          V-PRS-SG-1 ;
 	          V-PRS-SG-2 ;
 +Sg3:ie     	  FINAL1 ;
! :ie		  VSUF-EVEN-PRS-DUPL ;
 +Du1:ien         FINAL1	;
 +Du2:eden	  FINAL1	; !i>0
 +Du3:ijægan	  FINAL1	; !e>0
 +Du3:iben	  FINAL1	; !SUB !i>0 ??
 +Pl1:ebe	  FINAL1	; !i>0
 +Pl2:ede	  FINAL1	; !i>0
 +Pl3:ieh         FINAL1	;



! Odd

LEXICON VSUF-ODD-PRS
    V-ODD-PRS-SG ;
    V-ODD-PRS-DUPL ;

LEXICON V-ODD-PRS-SG
 :%^DISIMPe V-PRS-SG-1 ;
 :%^DISIMP  V-PRS-SG-2 ;
 :%^DISIMPe V-PRS-SG-3 ;

LEXICON V-ODD-PRS-DUPL
 ODD_PRS_NON_DU3 ;
 ODD_PRS_DU3 ;

LEXICON ODD_PRS_NON_DU3
 +Du1:ien		FINAL1	;
 +Du2:idien		FINAL1	;
 +Pl1:ibie		FINAL1	;
 +Pl2:idie		FINAL1	;
 +Pl3:ieh		FINAL1	;

LEXICON ODD_PRS_DU3
 +Du3:ægan		FINAL1	;
 +Du3:æban		FINAL1	; !SUB

LEXICON VSUF-ODD-PRT
VSUF-PRT-SG-12 ;
VSUF-PRT-SG-3 ;
VSUF-PRT-DUPL ;

LEXICON VSUF-ODD-IMP
 +Sg2:%^DISIMPh	FINAL1  ;
 +Du2:idien       FINAL1  ;
 +Pl2:idie        FINAL1  ;


! Contract


LEXICON VSUF-CONTR-PRS
     :e  V-PRS-SG-1 ;
     :   V-PRS-SG-2 ;
     :ie V-PRS-SG-3 ;
           VSUF-CONTR-DUPL ;

LEXICON VSUF-CONTR-DUPL
 +Du1:ien		FINAL1	;
 +Du2:eden		FINAL1	;
! Dual third does not get an +CTR tag
 +Du3:eben		FINAL1	; !SUB
 +Pl1:ebe		FINAL1	;
 +Pl2:ede		FINAL1	;
 +Pl3:ieh		FINAL1	;

LEXICON VSUF-CONTR-PRT
         VSUF-PRT-SG-12 ;
         VSUF-PRT-DUPL ;

LEXICON VSUF-CONTR-IMP
 +Sg2:h    FINAL1  ;
 +Du2:eden FINAL1  ;
 +Pl2:ede  FINAL1  ;


! Common even-odd-contr
! ---------------------

LEXICON V-PRS-SG-12 ! Kutt denne viss ikkje ref til
 V-PRS-SG-1 ;
 V-PRS-SG-2 ;

LEXICON V-PRS-SG-1
 +Sg1:m			FINAL1	; ! Border in prev lex

LEXICON V-PRS-SG-2
 +Sg2:h		FINAL1	;

LEXICON V-PRS-SG-3
 +Sg3:  		FINAL1	;

! LEXICON V-PRS-DUPL
!  +Du1:n         FINAL1	;
!  +Du2:dI1n		FINAL1	;
!  +Du3:gan		FINAL1	;
!  +Pl1:bI1		FINAL1	;
!  +Pl2:dI1		FINAL1	;
!  +Pl3:h		    FINAL1	;

LEXICON VSUF-PRT
 VSUF-PRT-SG-12 ;
 VSUF-PRT-SG-3 ;
 VSUF-PRT-DUPL ;

LEXICON VSUF-PRT-SG-12
+Sg1:im		FINAL1	;
+Sg2:ih		FINAL1	;

LEXICON VSUF-PRT-SG-3
+Sg3:i		FINAL1	;

LEXICON VSUF-PRT-DUPL
+Du1:imen		FINAL1	;
+Du2:iden		FINAL1	;
+Du3:igan		FINAL1	;
+Pl1:imh		FINAL1	;
+Pl2:idh		FINAL1	;
+Pl3:in		    FINAL1	;




!LEXICON V-II-PRS-DUPL
! :oe		    VSUF-EVEN-PRS-DUPL ;
! +Du1:oen      FINAL1	;
! +Du2:oden		FINAL1	; !e>0
! +Du3:oejægan	FINAL1	;
! +Du3:oeben	FINAL1	; !SUB
! +Pl1:obe		FINAL1	; !e>0
! +Pl2:ode		FINAL1	; !e>0
! +Pl3:oeh		FINAL1	;

!LEXICON V-III-PRS-DUPL
! :a			VSUF-EVEN-PRS-DUPL ;
! +Du1:an       FINAL1	;
! +Du2:eden		FINAL1	; !a>e
! +Du3:ajægan	FINAL1	;
! +Du3:aben	FINAL1	; !SUB
! +Pl1:ebe		FINAL1	; !a>e
! +Pl2:ede		FINAL1	; !a>e
! +Pl3:ah		FINAL1	;

! From V verbs
! +Du1:oen       FINAL1	; !'e>oe
! +Du2:eden		FINAL1	;
! +Du3:ijægan	FINAL1	; !e>i
! +Du3:iben	    FINAL1	; !SUB !e>i
! +Pl1:ebe		FINAL1	;
! +Pl2:ede		FINAL1	;
! +Pl3:oeh		FINAL1	; !'e>oe

!! LEXICON V-EVEN-PRS
!!   V-PRS-SG-12 ;
!!   V-PRS-SG-3 ;
!!    V-EVEN-PRS-DUPL ;

!LEXICON V-EVEN-PRS-DUPL
! :ie		VSUF-EVEN-PRS-DUPL ;
! +Du1:ien       FINAL1	;
! +Du2:eden		FINAL1	; !i>0
! +Du3:iejægan	FINAL1	;
! +Du3:ieben	FINAL1	; !SUB
! +Pl1:ebe		FINAL1	; !i>0
! +Pl2:ede		FINAL1	; !i>0
! +Pl3:ieh		FINAL1	;






! Final lexicon
! =============


LEXICON FINAL1
  0:#			#	;
