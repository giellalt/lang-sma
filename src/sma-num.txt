!Distributed under the terms of the GNU General Public License version 2
! or any later version.


LEXICON Root
< [a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|q|y|z|æ|ø|å|ä|ö|á|č|đ|ŋ|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Æ|Ø|Å|Ä|Ö|Á|Č|Đ|Ŋ|Š|Ŧ|Ž]* > COMMA ; ! This first line is to allow all letter strings.
NUMBERSECTION ;
COMMASECTION ;

LEXICON NUMBERSECTION
HUNDREDSM ;		! 200M
tjuetie:1 HUNDREDM ;	! 100M
TENSM ;			! 20-99M
TEENSM ;		! 10-19M
ONESM ;			! 1-9M
HUNDREDST ;		! 200000-999999
tjuetie:1 HUNDREDT ;	! 100000-100999
TENST ;			! 20000-99999,10000-10999
TEENST ;		! 11000-19999
ONEST ;			! 2000-9999
tåvsene:1 THOUSAND ;	! 1000-1999
UNDERTHOUSAND ;		! 100-999
TENS ;			! 20-99
TEENS ;			! 10-19
ONES ;			! 1-9

LEXICON HUNDREDSM
göökte:2  CUODIM ;
golme:3 CUODIM ;
njieljie:4 CUODIM ;
njielje:4 CUODIM ;
nieljie:4 CUODIM ;
nielje:4 CUODIM ;
vïjhte:5 CUODIM ;
govhte:6 CUODIM ;
tjïjhtje:7 CUODIM ;
gaektsie:8 CUODIM ;
uktsie:9 CUODIM ;

LEXICON CUODIM
tjuetie: HUNDREDM ;

LEXICON HUNDREDM
      TENSM ;
      TEENSM ;
:%0   ONESM ;
:%0%0 MILJON ;

LEXICON TEENSM
luhkie:1 TEENM ;

LEXICON TEENM
akte:1     LOHKAIM ;
göökte:2   LOHKAIM ;
golme:3    LOHKAIM ;
njieljie:4 LOHKAIM ;
njielje:4  LOHKAIM ;
nieljie:4  LOHKAIM ;
nielje:4   LOHKAIM ;
vïjhte:5   LOHKAIM ;
govhte:6   LOHKAIM ;
tjïjhtje:7 LOHKAIM ;
gaektsie:8 LOHKAIM ;
uktsie:9   LOHKAIM ;

LEXICON LOHKAIM
MILJON ;

LEXICON TENSM
luhkie:1%0 MILJON ;
göökte:2   LUHKIEM ;
golme:3    LUHKIEM ;
njieljie:4 LUHKIEM ;
njielje:4  LUHKIEM ;
nieljie:4  LUHKIEM ;
nielje:4   LUHKIEM ;
vïjhte:5   LUHKIEM ;
govhte:6   LUHKIEM ;
tjïjhtje:7 LUHKIEM ;
gaektsie:8 LUHKIEM ;
uktsie:9   LUHKIEM ;

LEXICON LUHKIEM
luhkie:%0 MILJON ;
luhkie:   ONESM ;

LEXICON ONESM
akte% miljon% :1 OVERTHOUSANDS ;
göökte:2         MILJON ;
golme:3          MILJON ;
njieljie:4       MILJON ;
njielje:4        MILJON ;
nieljie:4        MILJON ;
nielje:4         MILJON ;
vïjhte:5         MILJON ;
govhte:6         MILJON ;
tjïjhtje:7       MILJON ;
gaektsie:8       MILJON ;
uktsie:9         MILJON ; 

LEXICON MILJON
% miljona% : OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		! x00.000.000
tjuetie:1 HUNDREDT ;	! 100.000.000
:%0 TENST ;		!  20.000.000
:%0 TEENST ;		!  10.000.000
:%0%0 ONEST ;		!   2.000.000
tåvsene:%0%01 THOUSAND ;	!   1.000.000
:%0%0%0 UNDERTHOUSAND ;	
:%0%0%0%0 TENS ;
:%0%0%0%0 TEENS ;
:%0%0%0%0%0 ONES ;
:%0%0%0%0%0%0 COMMA ;	!  x000000

LEXICON HUNDREDST
göökte:2   CUODIT ;	! 200000-299999
golme:3    CUODIT ;	! 300000-399999
njieljie:4 CUODIT ;	! 400000-499999
vïjhte:5   CUODIT ;	! 500000-599999
govhte:6   CUODIT ;	! 600000-699999
tjïjhtje:7 CUODIT ;	! 700000-799999
gaektsie:8 CUODIT ;	! 800000-899999
uktsie:9   CUODIT ;	! 900000-999999

LEXICON CUODIT
tjuetie: HUNDREDT ;	! 100000-199999

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
TENST ;			! X2XYYY, X10YYY
TEENST ;		! X1XYYY
akte:%01 THOUSANDS ;
:%0 ONEST ;		! XX0YYY
:%0%0 THOUSANDS ;	! X00YYY

LEXICON TEENST
luhkie:1   TEENT ;
luhkie:1%0 THOUSANDS ;

LEXICON TEENT
akte:1     LOHKAIT ;
göökte:2   LOHKAIT ;
golme:3    LOHKAIT ;
njieljie:4 LOHKAIT ;
njielje:4  LOHKAIT ;
nieljie:4  LOHKAIT ;
nielje:4   LOHKAIT ;
vïjhte:5   LOHKAIT ;
govhte:6   LOHKAIT ;
tjïjhtje:7 LOHKAIT ;
gaektsie:8 LOHKAIT ;
uktsie:9   LOHKAIT ;

LEXICON LOHKAIT
THOUSANDS ;

LEXICON TENST
göökteluhkie:2   LUHKIET ;
golmeluhkie:3    LUHKIET ;
njieljieluhkie:4 LUHKIET ;
njieljeluhkie:4  LUHKIET ;
nieljieluhkie:4  LUHKIET ;
nieljeluhkie:4   LUHKIET ;
vïjhteluhkie:5   LUHKIET ;
govhteluhkie:6   LUHKIET ;
tjïjhtjeluhkie:7 LUHKIET ;
gaektsieluhkie:8 LUHKIET ;
uktsieluhkie:9   LUHKIET ;

LEXICON LUHKIET
:%0 THOUSANDS ;
akte:1 THOUSANDS ;
ONEST ;

LEXICON ONEST
göökte:2 THOUSANDS ;
golme:3 THOUSANDS ;
njieljie:4 THOUSANDS ;
njielje:4 THOUSANDS ;
nieljie:4 THOUSANDS ;  
nielje:4 THOUSANDS ;   
vïjhte:5 THOUSANDS ;
govhte:6 THOUSANDS ;
tjïjhtje:7 THOUSANDS ;
gaektsie:8 THOUSANDS ;
uktsie:9 THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
tåvsene: THOUSAND ;
stoerretjuetie: THOUSAND ; 

LEXICON THOUSAND
UNDERTHOUSAND ;	   ! 1100-1999
:%0 TENS ;	   ! 1020-1099
:%0 TEENS ;	   ! 1010-1019
:%0%0 ONES ;	   ! 1001-1009
:%0%0%0 COMMA ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
tjuetie:1 HUNDRED ;
tjuatede:1  CUODAAT ;

LEXICON HUNDREDS
göökte:2   CUODI ;
golme:3    CUODI ;
njieljie:4 CUODI ;
njielje:4  CUODI ;
nieljie:4  CUODI ;  
nielje:4   CUODI ; 
vïjhte:5   CUODI ;
govhte:6   CUODI ;
tjïjhtje:7 CUODI ;
gaektsie:8 CUODI ;
uktsie:9   CUODI ;

LEXICON CUODI
tjuetie:   HUNDRED ;
tjuatede:  CUODAAT ;

LEXICON HUNDRED
      TENS ;
      TEENS ;
:%0   ONES ;
:%0%0 COMMA ;

LEXICON CUODAAT
:%0%0%.    COMMA ;

LEXICON TEENS
luhkie:1 ONES ;


LEXICON TENS
luhkie:1%0 COMMA ;
låhkede:1%0%. COMMA ;
göökte:2 LUHKIE ;
golme:3 LUHKIE ;
njieljie:4 LUHKIE ;
njielje:4 LUHKIE ;
nieljie:4 LUHKIE ;  
nielje:4 LUHKIE ; 
vïjhte:5 LUHKIE ;
govhte:6 LUHKIE ;
tjïjhtje:7 LUHKIE ;
gaektsie:8 LUHKIE ;
uktsie:9 LUHKIE ;

LEXICON LUHKIE
luhkie:%0 COMMA ;
luhkede:%0%. COMMA ;
luhkie: ONES ;

LEXICON ONES
CARDINAL ;
ORDINAL ;

LEXICON CARDINAL
akte:1 COMMA ;
göökte:2 COMMA ;
golme:3 COMMA ;
njieljie:4 COMMA ;
njielje:4 COMMA ;
nieljie:4 COMMA ;  
nielje:4 COMMA ; 
vïjhte:5 COMMA ;
govhte:6 COMMA ;
tjïjhtje:7 COMMA ;
gaektsie:8 COMMA ;
uktsie:9 COMMA ;

LEXICON ORDINAL
voestes:1%.     COMMA ;
mubpie:2%.   COMMA ;
gåalmede:3%.   COMMA ;
njealjede:4%. COMMA ;
vijhtede:5%.   COMMA ;
govhtede:6%.   COMMA ;
tjïjhtjede:7%.    COMMA ;
gaaktsede:8%.   COMMA ;
åktsede:9%.    COMMA ;

LEXICON COMMA
 ENDLEX ;
 COMMASECTION ;

LEXICON COMMASECTION  ! these are not spellchecked!!!
                  ENDLEX ;
! % komma% :,      Root ;
! % tjuohkkis% :%. Root ;
! % kolon% :%:     Root ;
! % sárggis% :%-   Root ; 
 % la% :%=         Root ; 
 % gráda% :%°      Root ; 
 % paragráfa% :§   Root ; 
! % násti% :%*     Root ; 
 % ja% :&          Root ; 

LEXICON ENDLEX
 # ;
