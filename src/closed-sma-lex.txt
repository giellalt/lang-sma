!This file contains the following closed parts of speech

!Pronoun
!Subjunction
!Conjunction
!Particle
!Interjections



! Pronoun
! =======

!Pointing to all the subgrops, presented in this order below.
LEXICON Pronoun
 Personal ;
 Demonstrative ;
 Interrogative ;
 Indefinite ;
 Reflexive ;

! Personal pronouns
! -----------------


! Personal pronouns, splitting in 1st, 2nd, 3rd
! - - - - - - - - - - - - - - - - - - - - - - - 

LEXICON Personal
mån+Pron+Pers+Sg1:m firstperspronsg ;
mån+Pron+Pers+Du1:m firstpersprondu ;
mån+Pron+Pers+Pl1:m perspronpl ;
dån+Pron+Pers+Sg2:d nonfirstperspronsg ;
dån+Pron+Pers+Du2:d nonfirstpersprondu ;
dån+Pron+Pers+Pl2:d perspronpl ;
sån+Pron+Pers+Sg3:s nonfirstperspronsg ;
sån+Pron+Pers+Du3:s nonfirstpersprondu ;
sån+Pron+Pers+Pl3:s perspronpl ;


! Lexica for sg
! - - - - - - - 

LEXICON firstperspronsg		! First pers has special consonantism
 shortproncase		;
 :anne longproncase	;
 +Ill:unnjien	FINAL1	;
 +Ill:unnjan		FINAL1	;

LEXICON nonfirstperspronsg	! 2nd and 3rd are identical
 shortproncase		;
 :atne longproncase	;
 +Ill:utnjien		FINAL1	;
 +Ill:utnjan		FINAL1	;


! Lexica for du
! - - - - - - - 

LEXICON firstpersprondu
 +Nom:in	FINAL1	;
 +Nom:ånnoeh	FINAL1	;
 +Nom:onnoej	FINAL1	;
 +Gen:onnen	FINAL1	;
 +Ill:onnese	FINAL1	;
 :onne longproncase	;

LEXICON nonfirstpersprondu
 +Nom:in	FINAL1	;
 +Nom:åtnoeh	FINAL1	;
 +Nom:otnoej	FINAL1	;
 +Gen:otnen	FINAL1	;
 +Ill:otnese	FINAL1	;
 :onne longproncase	;


! Lexica for pl
! - - - - - - - 

LEXICON perspronpl
 +Nom:ih	FINAL1	;
 +Nom:ijjieh	FINAL1	;
 +Gen:ijjen	FINAL1	;
 +Ill:ijjese	FINAL1	;
 +Ill:ijjide	FINAL1	; 
 :ijje longproncase	;


! Common case lexica
! - - - - - - - - - 

LEXICON shortproncase
 +Gen:ov		FINAL1	;
 +Nom:ån		FINAL1	;
 +Nom:an		FINAL1	;

LEXICON longproncase
 +Nom:		FINAL1	;
 +Acc:m		FINAL1	;
 +Ine:sne	FINAL1	;
 +Ela:ste	FINAL1	;
 +Com:X2ine	FINAL1	;



! Demonstrative pronouns
! ----------------------

! The initial demonstrative lexica
! - - - - - - - - - - - - - - - - 

LEXICON Demonstrative
daate+Pron+Dem+Sg+Nom:daate FINAL1 ; ! ideosyncratic nominative
daate+Pron+Dem+Sg+Ill:daase FINAL1 ; ! ideosyncratic illative
daate+Pron+Dem+Pl+Com:daajgujmie FINAL1 ; ! ideosyncratic comitative
daate+Pron+Dem:daa DEM1 ;	     ! points to case endings for the first stem
daate+Pron+Dem:dae DEM2 ;	     ! points to case endings for the second stem

dïhte+Pron+Dem+Sg+Nom:dïhte FINAL1 ; ! The same pattern is repeated for each
dïhte+Pron+Dem+Sg+Ill:dïsse FINAL1 ; ! demonstrative pronoun
dïhte+Pron+Dem+Pl+Com:dajgujmie FINAL1 ;
dïhte+Pron+Dem+Pl+Com:dejgujmie FINAL1 ;
dïhte+Pron+Dem+Pl+Gen:dej FINAL1 ;
dïhte+Pron+Dem:da DEM1 ;
dïhte+Pron+Dem:de DEM2 ;

doete+Pron+Dem+Sg+Nom:doete FINAL1 ;
doete+Pron+Dem+Sg+Ill:doese FINAL1 ;
doete+Pron+Dem+Pl+Com:doejgujmie FINAL1 ;
doete+Pron+Dem:doe DEM1 ;
doete+Pron+Dem:due DEM2 ;

dohte+Pron+Dem+Sg+Nom:dohte FINAL1 ;
dohte+Pron+Dem+Sg+Ill:dosse FINAL1 ;
dohte+Pron+Dem+Pl+Com:dojgujmie FINAL1 ;
dohte+Pron+Dem+Pl+Com:dyjgujmie FINAL1 ;
dohte+Pron+Dem:do DEM1 ;
dohte+Pron+Dem:du DEM2 ;


! The two sublexica, one for each stem
! - - - - - - - - - - - - - - - - - - 

LEXICON DEM1
+Sg+Acc:m	FINAL1	;
+Sg+Gen:n	FINAL1	;
+Pl+Nom:h	FINAL1	;
+Pl+Gen:j	FINAL1	;
+Pl+Com:j	FINAL1	;

LEXICON DEM2
+Sg+Ine:snie	FINAL1	;
+Sg+Ine:nnie	FINAL1	;	! Attributive form
+Sg+Ela:stie	FINAL1	;
+Sg+Ela:htie	FINAL1	;	! Attributive form
+Sg+Com:jnie	FINAL1	;
+Pl+Acc:jtie	FINAL1	;
+Pl+Ill:jtie	FINAL1	;
+Pl+Ine:jnie	FINAL1	;
+Pl+Ela:jstie	FINAL1	;


LEXICON Interrogative
gie+Pron+Interr+Sg+Nom:gie		FINAL1	;
gie+Pron+Interr+Sg+Acc:gïem		FINAL1	;
gie+Pron+Interr+Sg+Acc:geam		FINAL1	;
gie+Pron+Interr+Sg+Gen:gïen		FINAL1	;
gie+Pron+Interr+Sg+Gen:gean		FINAL1	;
gie+Pron+Interr+Sg+Ill:gïese		FINAL1	;
gie+Pron+Interr+Sg+Ill:gease		FINAL1	;
gie+Pron+Interr+Pl+Nom:gïeh		FINAL1	;
gie+Pron+Interr+Pl+Nom:geah		FINAL1	;
gie+Pron+Interr+Pl+Gen:geaj		FINAL1	;
gie+Pron+Interr+Pl+Com:geajgujmie	FINAL1	;
gie+Pron+Interr:gie			INTERR	;
mij+Pron+Interr+Sg+Nom:mij		FINAL1	;
mij+Pron+Interr+Sg+Acc:maam		FINAL1	;
mij+Pron+Interr+Sg+Gen:man		FINAL1	;
mij+Pron+Interr+Sg+Ill:mïse		FINAL1	;
mij+Pron+Interr+Sg+Ill:masse		FINAL1	;
mij+Pron+Interr+Pl+Nom:mah		FINAL1	;
mij+Pron+Interr+Pl+Gen:maj		FINAL1	;
mij+Pron+Interr+Pl+Com:majgujmie	FINAL1	;
mij+Pron+Interr:me			INTERR	;

LEXICON INTERR
+Sg+Ine:snie	FINAL1	;
+Sg+Ela:stie	FINAL1	;
+Sg+Com:jnie	FINAL1	;
+Pl+Acc:jtie	FINAL1	;
+Pl+Gen:j	FINAL1	;
+Pl+Ill:jtie	FINAL1	;
+Pl+Ine:jnie	FINAL1	;
+Pl+Ela:jstie	FINAL1	;
+Pl+Com:jgujmie	FINAL1	;



! Indefinite pronouns
! -------------------

LEXICON Indefinite

naan ind ;

LEXICON ind
+Pron+Indef:0 # ;



! Reflexive pronouns
! ------------------

LEXICON Reflexive
+Pron+Refl+Nom+Sg:jijtje	#	;
+Pron+Refl+Nom+Sg:jeetje	#	;
+Pron+Refl+Nom+Sg:ijtje		#	;
+Pron+Refl+Nom+Pl:jijtjh	#	;

+Pron+Refl:jijtj OBLREFL ;
+Pron+Refl:jijt OBLREFL-NONPAL ;

LEXICON OBLREFL
+Acc+Sg1:eme # ;
+Acc+Sg1:emem # ;
+Acc+Sg2:emdh # ;
+Acc+Sg3:emse # ;

+Gen+Sg1:ene # ;
+Gen+Sg1:en # ;
+Gen+Sg2:edh # ;
+Gen+Sg3:ese # ;
+Gen+Sg3:se # ;

+Gen+Pl1:emh # ;
+Gen+Pl2:edh # ;
+Gen+Pl3:esh # ;
+Gen+Pl3:sh # ;

+Ill+Sg1:sanne # ;
+Ill+Sg2:sadth # ;
+Ill+Sg3:sissie # ;

+Ela+Sg1:estanne # ;
+Ela+Sg2:estadth # ;
+Ela+Sg3:estissie # ;

+Com+Sg1:enæjnene # ;
+Com+Sg2:enæjnedh # ;


+Ill+Pl1:sanne # ;
+Ill+Pl2:sadth # ;
+Ill+Pl3:sissie # ;

+Ela+Pl1:estanne # ;
+Ela+Pl2:estadth # ;
+Ela+Pl3:estissie # ;

+Com+Pl1:enæjnene # ;
+Com+Pl2:enæjnedh # ;



LEXICON OBLREFL-NONPAL
+Ill+Sg3:sasse # ;
+Ela+Sg3:sistie # ;
+Ill+Pl3:sasse # ;
+Ela+Pl3:sistie # ;
+Com+Pl3:sinie # ;



! Subjunction
! ===========

LEXICON Subjunction
ahte	Cs ;
dastegh	Cs ;
dellie	Cs ;
dle	Cs ;
dlie	Cs ;
eannan	Cs ;
goh	Cs ;
goske	Cs ;
gosse	Cs ;
guktie	Cs ;
ihke	Cs ;
ihkie	Cs ;
jalts	Cs ;
jis	Cs ;
juktie	Cs ;
juktie	Cs ;
jus	Cs ;
maaje	Cs ;
mearan	Cs ;
mearas	Cs ;
men	Cs ;


LEXICON Cs 
+CS:0 # ;



! Conjunction
! ===========


LEXICON Conjunction

jih	Cc ;
jah	Cc ;
jallh	Cc ;
jïllh	Cc ;
aaj	Cc ;

LEXICON Cc
+CC:0 # ;



! Particle
! ========

LEXICON Particle
amma	pcle	;
badth	pcle	;
bist	pcle	;
dagke	pcle	;
darhkan	pcle	;
gan	pcle	;
gih	pcle	;
goh	pcle	;
golh	pcle	;
gujht	pcle	;
gænnah	pcle	;
havkh	pcle	;
hijven	pcle	;
hov	pcle	;
hævvi	pcle	;
jaavoe	pcle	;
jam	pcle	;
jeavoe	pcle	;
jis	pcle	;
maa	pcle	;
mah	pcle	;
mejtie	pcle	;
mihk	pcle	;
nagke	pcle	;
nov% gujht	pcle	;
ojhte	pcle	;
ov	pcle	;
vaajt	pcle	;
vaejtie	pcle	;
van	pcle	;
vuj	pcle	;
vååjt	pcle	;

LEXICON pcle
+Pcle:0 #	;

! Interjections

LEXICON Interjection

åå intj ;
au intj ;
tvi intj ;
jøø intj ;
fysjh intj ;
hurra intj ;
jaa intj ;
haaj intj ;
ho intj ;
nåå intj ;


LEXICON intj

+Interj:0 # ;
