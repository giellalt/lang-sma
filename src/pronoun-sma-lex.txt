! Pronoun
! =======

!Pointing to all the subgrops, presented in this order below.
LEXICON Pronoun

 Reciprocal ;
 Personal ;
 Demonstrative ;
 Interrogative ;
 Indefinite ;
 Reflexive ;

! The Reciprocal pronoun

LEXICON Reciprocal
sinsitnie+Pron+Reci+Sg+Ack:sinsitniem  FINAL1 ;
sinsitnie+Pron+Reci+Sg+Gen:sinsitnien  FINAL1 ;
sinsitnie+Pron+Reci+Sg+Ill:sinsætnan   FINAL1 ;
sinsitnie+Pron+Reci+Sg+Ela:sinsitneste FINAL1 ;
sinsitnie+Pron+Reci+Sg+Com:sinsitnine  FINAL1 ;

! Personal pronouns
! -----------------
  
! Personal pronouns, splitting in 1st, 2nd, 3rd
! - - - - - - - - - - - - - - - - - - - - - - - 

LEXICON Personal
manne+Pron+Pers+Sg1:m firstperspronsg ;
manne+Pron+Pers+Du1:m firstpersprondu ;
manne+Pron+Pers+Pl1:m perspronpl ;
datne+Pron+Pers+Sg2:d nonfirstperspronsg ;
datne+Pron+Pers+Du2:d nonfirstpersprondu ;
datne+Pron+Pers+Pl2:d perspronpl ;
satne+Pron+Pers+Sg3:s nonfirstperspronsg ;
satne+Pron+Pers+Du3:s nonfirstpersprondu ;
satne+Pron+Pers+Pl3:s perspronpl ;

!mån+Pron+Pers+Sg1:m firstperspronsg ;
!mån+Pron+Pers+Du1:m firstpersprondu ;
!mån+Pron+Pers+Pl1:m perspronpl ;
!dån+Pron+Pers+Sg2:d nonfirstperspronsg ;
!dån+Pron+Pers+Du2:d nonfirstpersprondu ;
!dån+Pron+Pers+Pl2:d perspronpl ;
!sånPron+Pers+Sg3:s nonfirstperspronsg ;
!sån+Pron+Pers+Du3:s nonfirstpersprondu ;
!sån+Pron+Pers+Pl3:s perspronpl ;

!these forms will need to be included in the analasys eventually.

! Lexica for sg
! - - - - - - - 

LEXICON firstperspronsg		! First pers has special consonantism
 shortproncase		;
 :anne longproncase	;
 +Ill:unnjien	FINAL1	;
 +Ill:unnjan		FINAL1	;

LEXICON nonfirstperspronsg	! 2nd and 3rd are identical
 shortproncase		;
 :atne longproncase	;
 +Ill:utnjien		FINAL1	;
 +Ill:utnjan		FINAL1	;


! Lexica for du
! - - - - - - - 

LEXICON firstpersprondu
 +Nom:in	FINAL1	;
 +Nom:ånnoeh	FINAL1	;
 +Nom:onnoej	FINAL1	;
 +Gen:onnen	FINAL1	;
 +Ill:onnese	FINAL1	;
 :onne longproncase	;

LEXICON nonfirstpersprondu
 +Nom:in	FINAL1	;
 +Nom:åtnoeh	FINAL1	;
 +Nom:otnoej	FINAL1	;
 +Gen:otnen	FINAL1	;
 +Ill:otnese	FINAL1	;
 +Ela:otneste	 FINAL1	;
 :onne longproncase	;


! Lexica for pl
! - - - - - - - 

LEXICON perspronpl
 +Nom:ih	FINAL1	;
 +Nom:ijjieh	FINAL1	;
 +Gen:ijjen	FINAL1	;
 +Ill:ijjese	FINAL1	;
 +Ill:ijjide	FINAL1	; 
 :ijje longproncase	;


! Common case lexica
! - - - - - - - - - 

LEXICON shortproncase
 +Gen:ov		FINAL1	;
 +Nom:ån		FINAL1	;
 +Nom:an		FINAL1	;

LEXICON longproncase
 +Nom:		FINAL1	;
 +Acc:m		FINAL1	;
 +Ine:sne	FINAL1	;
 +Ela:ste	FINAL1	;
 +Com:X2ine	FINAL1	;



! Demonstrative pronouns
! ----------------------

! The initial demonstrative lexica
! - - - - - - - - - - - - - - - - 

LEXICON Demonstrative
daate+Pron+Dem+Sg+Nom:daate FINAL1 ; ! ideosyncratic nominative
daate+Pron+Dem+Sg+Ill:daase FINAL1 ; ! ideosyncratic illative
daate+Pron+Dem+Pl+Com:daajgujmie FINAL1 ; ! ideosyncratic comitative
daate+Pron+Dem:daa DEM1 ;	     ! points to case endings for the first stem
daate+Pron+Dem:dae DEM2 ;	     ! points to case endings for the second stem

dïhte+Pron+Dem+Sg+Nom:dïhte FINAL1 ; ! The same pattern is repeated for each
dïhte+Pron+Dem+Sg+Ill:dïsse FINAL1 ; ! demonstrative pronoun
dïhte+Pron+Dem+Pl+Com:dajgujmie FINAL1 ;
dïhte+Pron+Dem+Pl+Com:dejgujmie FINAL1 ;
dïhte+Pron+Dem+Pl+Gen:dej FINAL1 ;
dïhte+Pron+Dem:da DEM1 ;
dïhte+Pron+Dem:de DEM2 ;

doete+Pron+Dem+Sg+Nom:doete FINAL1 ;
doete+Pron+Dem+Sg+Ill:doese FINAL1 ;
doete+Pron+Dem+Pl+Com:doejgujmie FINAL1 ;
doete+Pron+Dem:doe DEM1 ;
doete+Pron+Dem:due DEM2 ;

dohte+Pron+Dem+Sg+Nom:dohte FINAL1 ;
dohte+Pron+Dem+Sg+Ill:dosse FINAL1 ;
dohte+Pron+Dem+Pl+Com:dojgujmie FINAL1 ;
dohte+Pron+Dem+Pl+Com:dyjgujmie FINAL1 ;
dohte+Pron+Dem:do DEM1 ;
dohte+Pron+Dem:du DEM2 ;

gåabpegh+Pron+Dem+Pl+Nom:gåabpegh FINAL1 ; ! XXX Comment this set in or out.
!gåabpegh+Pron+Dem+Pl+Acc:gåabpagidie
!gåabpegh+Pron+Dem+Pl+Gen:gåabpegi
!gåabpegh+Pron+Dem+Pl+Ill:gåabpagidie
!gåabpegh+Pron+Dem+Pl+Ine:gåabpaginie
!gåabpegh+Pron+Dem+Pl+Ela:gåabpagijstie
!gåabpegh+Pron+Dem+Pl+Com:gåabpaginie


! The two sublexica, one for each stem
! - - - - - - - - - - - - - - - - - - 

LEXICON DEM1
+Sg+Acc:m	FINAL1	;
+Sg+Gen:n	FINAL1	;
+Pl+Nom:h	FINAL1	;
+Pl+Gen:j	FINAL1	;
+Pl+Com:j	FINAL1	;

LEXICON DEM2
+Pl+Gen:j	FINAL1	;
+Sg+Ine:snie	FINAL1	;
+Sg+Ine:nnie	FINAL1	;	! Attributive form
+Sg+Ela:stie	FINAL1	;
+Sg+Ela:htie	FINAL1	;	! Attributive form
+Sg+Com:jnie	FINAL1	;
+Pl+Acc:jtie	FINAL1	;
+Pl+Ill:jtie	FINAL1	;
+Pl+Ine:jnie	FINAL1	;
+Pl+Ela:jstie	FINAL1	;


LEXICON Interrogative
gie+Pron+Interr+Sg+Nom:gie		FINAL1	;
gie+Pron+Interr+Sg+Acc:gïem		FINAL1	;
gie+Pron+Interr+Sg+Acc:geam		FINAL1	;
gie+Pron+Interr+Sg+Gen:gïen		FINAL1	;
gie+Pron+Interr+Sg+Gen:gean		FINAL1	;
gie+Pron+Interr+Sg+Ill:gïese		FINAL1	;
gie+Pron+Interr+Sg+Ill:gease		FINAL1	;
gie+Pron+Interr+Pl+Nom:gïeh		FINAL1	;
gie+Pron+Interr+Pl+Nom:geah		FINAL1	;
gie+Pron+Interr+Pl+Gen:geaj		FINAL1	;
gie+Pron+Interr+Pl+Com:geajgujmie	FINAL1	;
gie+Pron+Interr:gie			INTERR	;

guhte+Pron+Interr+Sg+Nom:guhte          FINAL1  ;
gusnie+Pron+Interr+Sg+Ine:gusnie        FINAL1  ;
gustie+Pron+Interr+Sg+Ela:gustie        FINAL1  ;

gubpede+Pron+Interr+sg+Ela:gubpede  FINAL1 ;
gubpene+Pron+Interr+sg+Ine:gubpene  FINAL1 ;


mij+Pron+Interr+Sg+Nom:mij		FINAL1	;
mij+Pron+Interr+Sg+Acc:maam		FINAL1	;
mij+Pron+Interr+Sg+Gen:man		FINAL1	;
mij+Pron+Interr+Sg+Ill:mïsse		FINAL1	;
mij+Pron+Interr+Sg+Ill:masse		FINAL1	;
mij+Pron+Interr+Sg+Ine:mannasinie		FINAL1	;
mij+Pron+Interr+Pl+Nom:mah		FINAL1	;
mij+Pron+Interr+Pl+Gen:maj		FINAL1	;
mij+Pron+Interr+Pl+Com:majgujmie	FINAL1	;
mij+Pron+Interr:me			INTERR	;

magkere+Pron+Interr+Sg+Nom+Attr:magkere  FINAL1 ;
magkere+Pron+Interr+Sg+Nom+Pred:magkeres  FINAL1 ;
magkere+Pron+Interr+Sg+Com:magkarinie  FINAL1 ;
magkere+Pron+Interr+Pl+Nom:magkerh  FINAL1 ;


gogkoe+Pron+Interr:gogkoe FINAL1 ;
gosse+Pron+Interr:gosse FINAL1 ;
gåabph+Pron+Interr:gåabph FINAL1 ;
guktie+Pron+Interr:guktie FINAL1 ;
gåabpa+Pron+Interr:gåabpa FINAL1 ;



gåabpoeh+Pron+Interr:gåabpoeh INTERR ;
gåabpoeh+Pron+Interr:gåabpoeh FINAL1 ;

LEXICON INTERR
+Sg+Ine:snie	FINAL1	;
+Sg+Ine:nnie    FINAL1  ;
+Sg+Ela:stie	FINAL1	;
+Sg+Com:jnie	FINAL1	;
+Pl+Acc:jtie	FINAL1	;
+Pl+Gen:j	FINAL1	;
+Pl+Ill:jtie	FINAL1	;
+Pl+Ine:jnie	FINAL1	;
+Pl+Ela:jstie	FINAL1	;
+Pl+Com:jgujmie	FINAL1	;


! Indefinite pronouns
! -------------------

LEXICON Indefinite

naan ind_noninfl ;
seamma ind_noninfl ; !XXX
muvhtie indeven ;
muvhte:muvht indeven ; 
gellie ind_noninfl ; !also inessive for gellene e.g. gellene lehkesne - på många ställen. Example in Åarjelsaemien 6 (2000:6).
gellieh ind_noninfl ; 
måedtie indeven ;
måadtesh indeven ;
jïjnje:jïjnj 	 indeven ;
jeenje:jeenj indeven ; !It's uncertain if this word can be inflected in all these cases
jeene:jeen	 indeven ;
jienebe:jienieb indodd ;
jijnjh indeven ;!SUB
jïjnjh indeven ;
jeenjemes indeven ;
jeenjesh:jeenjes indcoll ;
jïjnjesh:jïjnjesh indcoll ;
gaajhke:gaajhk indeven ;
jeatjh:jeatjah indeven ; 
gaajhke+Pron+Ind+Sg+Ine:gaajhkene FINAL1 ;
gaajhkesh:gaajhkes indcoll ;
gallesh:gallesh indcoll ;
såamesh:såamesh indcoll ; !SUB
naakene:naaken indodd ; !this most be pointed towards another lexicon due to it being three-syllabic
dovne indeven ;
dovnesh:dovnes indcoll ;
åbpe ind_noninfl ; !probably does not have inflection, so it was removed
abpe ind_noninfl ; !probably does not have inflection, so it was removed
såemies ind_noninfl ;
såamesh indcoll ;
fïereguhte:fïereguht indeven ; !this means basicly each and every, but I'm unsure what kind of pronoun it is, so I leave it here (David)
fïerhte indeven ;
fïerhte+Pron+Ind+Sg+Gen:fïerhten FINAL1 ;

LEXICON indeven
+Pron+Indef+Sg: indsg ;
+Pron+Indef+Pl: indpl ;
+Pron+Indef:    indess ;

LEXICON indodd ! This lexicon cannot be removed before lexica pointing here
               ! get another contlex!
+Pron+Indef+Sg: indsg_odd ;
+Pron+Indef+Pl: indpl_odd ;
+Pron+Indef:    indess_odd ;

LEXICON ind_noninfl
+Pron+Indef: # ;

LEXICON indsg
+Nom:e       # ;
+Acc:e%>m    # ;
+Gen:e%>n    # ;
+Ill:e%>se   # ;
+Ine:e%>sne  # ;
+Ela:e%>ste  # ;
+Com:%>ine # ;

LEXICON indess
+Ess:e%>jne     # ;

LEXICON indpl
+Nom:%>h  # ;
+Acc:%>ide # ;
+Gen:%>i  # ;
+Ill:%>ide # ;
+Ine:%>ine # ;
+Ela:%>ijste   # ;
+Com:%>igujmie  # ;

LEXICON indsg_odd
  +Nom:e%^DISIMP         FINAL1 ;
  +Acc:e%>%^DISIMPm      FINAL1 ;
  +Gen:e%>%^DISIMPn      FINAL1 ;
  +Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
  +Ine:%>isnie           FINAL1 ;
  +Ela:%>istie           FINAL1 ;
  +Com:%>inie            FINAL1 ;

LEXICON indpl_odd
  +Nom:%>%^DISIMPh       FINAL1 ;
  +Acc:%>idie            FINAL1 ;
  +Gen:%>%^DISIMPi       FINAL1 ;
  +Ill:%>idie            FINAL1 ;
  +Ine:%>inie            FINAL1 ;
  +Ela:%>ijstie          FINAL1 ;
  +Com:%>%^DISIMPigujmie FINAL1 ;

LEXICON indess_odd
  +Ess:%>inie          FINAL1 ;

LEXICON indcoll
+Pron+Coll+Pl: indpl_odd ;

! Reflexive pronouns
! ------------------

LEXICON Reflexive
+Pron+Refl+Nom+Sg:jïjtje	#	;
+Pron+Refl+Nom+Sg:jeetje	#	;
+Pron+Refl+Nom+Sg:ijtje		#	;
+Pron+Refl+Nom+Pl:jïjtjh	#	;

+Pron+Refl:jïjtj OBLREFL ;
+Pron+Refl:jïjt OBLREFL-NONPAL ;

LEXICON OBLREFL
+Acc+Sg1:eme # ;
+Acc+Sg1:emem # ;
+Acc+Sg2:emdh # ;
+Acc+Sg3:emse # ;

+Gen+Sg1:ene # ;
+Gen+Sg1:en # ; !SUB
+Gen+Sg2:edh # ;
+Gen+Sg3:ese # ; !SUB
+Gen+Pl1:emh # ;
+Gen+Pl2:edh # ;
+Gen+Pl3:esh # ; !SUB

+Ela+Sg1:estanne # ;
+Ela+Sg2:estadth # ;
+Ela+Sg3:estissie # ;

+Com+Sg1:enæjnene # ;
+Com+Sg2:enæjnedh # ;

+Ill+Pl1:sanne # ;
+Ill+Pl2:sadth # ;
+Ill+Pl3:sissie # ;

+Ela+Pl1:estanne # ;
+Ela+Pl2:estadth # ;
+Ela+Pl3:estissie # ;

+Com+Pl1:enæjnene # ;
+Com+Pl2:enæjnedh # ;

LEXICON OBLREFL-NONPAL
+Ill+Sg1:sanne # ;
+Ill+Sg2:sadth # ;
+Ill+Sg3:sissie # ;
+Ill+Sg3:sasse # ;
+Gen+Sg3:se # ;
+Gen+Pl3:sh # ; 
+Ela+Sg3:sistie # ; !SUB
+Ill+Pl3:sasse # ; !SUB
+Ela+Pl3:sistie # ; !SUB
+Com+Pl3:sinie # ; !SUB
