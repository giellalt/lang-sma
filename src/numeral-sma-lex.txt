LEXICON Numeral
NUMBERWORDS ;     ! the sma part
                  ! These are from the sme file
ARABIC ;          ! for the arabic numerals        !^C^
ROMAN ;           ! for the roman numerals         !^C^
NUM-PREFIXES ;    ! for §34 etc.                   !^C^
ISOLATED-NUMEXP ; ! for ½ etc.                     !^C^
NUMBERLIKE ;      ! a dustbin



LEXICON NUMBERWORDS
nulle:null NUMTAGe ;
akte:akt NUMTAGe ;
akti-akti+Num+Pl+Gen:akti-akti FINAL1 ;
aktesi-aktesi+Num+Pl+Gen:akti-akti FINAL1 ;
göökti-göökti+Num+Pl+Gen:akti-akti FINAL1 ;
gööktesi-gööktesi+Num+Pl+Gen:akti-akti FINAL1 ;
göökte:göökt NUMTAGe ;
golme:golm NUMTAGe ;
njieljie:njielj NUMTAGie ;
njielje:njielj NUMTAGe ; !Saemesth amma
nieljie:nielj NUMTAGie ;
nielje:nielj NUMTAGe ; 
vïjhte:vïjht NUMTAGe ;
govhte:govht NUMTAGe ;
tjïjhtje:tjïjhtj NUMTAGe ;
gaektsie:gaekts NUMTAGe ;
uktsie:ukts NUMTAGie ;
luhkie:luhk NUMTAGie ;

veete+SgNomCmp:veete R ;


akte+SgNomCmp:akte R ;
göökte+SgNomCmp:göökte R ;
golme+SgNomCmp:golme R ;
njieljie+SgNomCmp:njieljie R ;
njielje+SgNomCmp:njielje R ; !Saemesth amma
nieljie+SgNomCmp:njieljie R ;
nielje+SgNomCmp:njielje R ; 
vïjhte+SgNomCmp:vïjhte R ;
govhte+SgNomCmp:govhte R ;
tjïjhtje+SgNomCmp:tjïjhtje R ;
gaektsie+SgNomCmp:gaektsie R ;
uktsie+SgNomCmp:uktsie R ;
luhkie+SgNomCmp:luhkie R ;

akth	  ORDTAG1 ;
ikth	  ORDTAG1 ;
göökth	  ORDTAG1 ;
gulmh	  ORDTAG1 ;
njeeljh	  ORDTAG1 ;
vijhth	  ORDTAG1 ;
guvhth	  ORDTAG1 ;
tjijhtjh  ORDTAG1 ;
geektsh	  ORDTAG1 ;
utksh	  ORDTAG1 ;
luhkh	  ORDTAG1 ;

voestes+A+Ord+Sg+Nom:voestes         FINAL1 ; !XXX check this
voestegh+A+Ord+Adv:voestegh          FINAL1 ; !this form is in my point of view (David) an adverb, so perhaps it should be relocated, on the other hand it is a form of voeste-
voestemes+A+Ord+Adv:voestemes        FINAL1 ; !this form is in my point of view (David) an adverb, so perhaps it should be relocated, on the other hand it is a form of voeste-
mubpie:mubp           MUBPIE ;

gåalmede:gåalmad ORDTAG ;
njieljede:njieljed ORDTAG ;
njealjede:njealjad ORDTAG ;
nealjede:nealjad ORDTAG ;
nieljede:nieljed ORDTAG ;
vïjhtede:vïjhted ORDTAG ;
govhtede:govhted ORDTAG ;
tjïjhtjhede:tjïjhtjed ORDTAG ;
gaaktsede:gaaktsad ORDTAG ;
åktsede:åktsad ORDTAG ;
låhkede:låhkad ORDTAG ;
tjuatede:tjuatad ORDTAG ;
tovsene:tovsen ORDTAG ;
tåvsene:tåvsan ORDTAG ;

gööktesh COLLTAG ;
golmesh COLLTAG ;
njealjesh COLLTAG ;
vïjhtesh COLLTAG ;
govhtesh COLLTAG ;
tjïjhtjesh COLLTAG ;
gaaktsesh COLLTAG ;
åktsesh COLLTAG ;
låhkesh COLLTAG ;

LEXICON NUMTAGe ! akte:akt
+Num+Sg+Nom:e # ;
+Num+Sg+Acc:%>em # ;
+Num+Sg+Gen:%>en # ;
+Num+Sg+Ill:%>ese # ;
+Num+Sg+Ine:%>esne # ;
+Num+Sg+Ine+Attr:%>ene # ;
+Num+Sg+Ela+Attr:%>ede # ;
+Num+Sg+Ela:%>este # ;
+Num+Sg+Com:%>ine # ;
+Num+Ess:%>ijne # ;
+Num+Pl+Nom:%>eh # ;
+Num+Pl+Acc:%>ide # ;
+Num+Pl+Gen:%>i # ;
+Num+Pl+Ill:%>ide # ;
+Num+Pl+Ine:%>ene # ;
+Num+Pl+Ela:%>ijste # ; !
+Num+Pl+Com:%>ijgujme # ;

LEXICON NUMTAGie !njieljie:njielj
+Num+Sg+Nom:ie # ;
+Num+Sg+Acc:%>em    # ;
+Num+Sg+Gen:%>en    # ;
+Num+Sg+Ill+Uml:%>%^1UMLane   # ;
+Num+Sg+Ine:%>esne  # ;
+Num+Sg+Ine+Attr:%>ine  # ;
+Num+Sg+Ela+Attr:%>ede  # ;
+Num+Sg+Ela:%>este  # ;
+Num+Sg+Com:%>ine # ;
+Num+Ess:%>ijne    # ;
+Num+Pl+Nom:%>ieh  # ;
+Num+Pl+Acc:%>ide # ;
+Num+Pl+Gen:%>i  # ;
+Num+Pl+Ill:%>ide # ;
+Num+Pl+Ine:%>ne # ;
+Num+Pl+Ela:%>ijste   # ; !
+Num+Pl+Com:%>jgujme  # ;


!LEXICON NUMTAG
!+Num+Sg+Nom:       # ;
!+Num+Sg+Acc:%>m    # ;
!+Num+Sg+Gen:%>n    # ;
!+Num+Sg+Ill:%>se   # ;
!+Num+Sg+Ine:%>sne  # ;
!+Num+Sg+Ine+Attr:%>ne  # ;
!+Num+Sg+Ela+Attr:%>de  # ;
!+Num+Sg+Ela:%>ste  # ;
!+Num+Sg+Com:%>ine # ;
!+Num+Ess:%>jne    # ;
!+Num+Pl+Nom:%>h  # ;
!+Num+Pl+Acc:%>ide # ;
!+Num+Pl+Gen:%>i  # ;
!+Num+Pl+Ill:%>ide # ;
!+Num+Pl+Ine:%>ne # ;
!+Num+Pl+Ela:%>ijste   # ; !
!+Num+Pl+Com:%>jgujme  # ;


LEXICON ORDTAG1
+A+Ord: # ;

LEXICON ORDTAG
 +Num+Sg: N_ODD_SG ;
! +Num+Pl: N_ODD_PL ; unsure if this rule is necessary for numerals
 +Num:    N_ODD_ESS ;

LEXICON COLLTAG
+Num+Coll: # ; !Case should be added, I think,

LEXICON MUBPIE
  +A+Ord+Sg+Nom:ie                  FINAL1 ;
  +A+Ord+Sg+Acc:ie%>m               FINAL1 ;
  +A+Ord+Sg+Gen:ie%>n               FINAL1 ;
! +A+Ord+Sg+Ill:%>an                FINAL1 ; !SUB - is this possible? IllSg without Uml in -ie?
  +A+Ord+Sg+Ill+Uml:%>%^1UMLan      FINAL1 ;
  +A+Ord+Sg+Ine:e%>sne              FINAL1 ;
  +A+Ord+Sg+Ine+Attr:e%>ne FINAL1 ;
  +A+Ord+Sg+Ela+Attr:e%>de              FINAL1 ;
  +A+Ord+Sg+Ela:e%>ste              FINAL1 ;
  +A+Ord+Sg+Com:%>ine               FINAL1 ;
  +A+Ord+Sg+Com+Uml:%>%^iUMLine     FINAL1 ;
  +A+Ord+Pl+Nom:ie%>h               FINAL1 ;
  +A+Ord+Pl+Acc:%>ide               FINAL1 ;
  +A+Ord+Pl+Acc+Uml:%>%^iUMLide     FINAL1 ;
  +A+Ord+Pl+Gen:ie%>j               FINAL1 ;
  +A+Ord+Pl+Gen+Uml:%>%^iUMLi       FINAL1 ;
  +A+Ord+Pl+Ill:%>ide               FINAL1 ;
  +A+Ord+Pl+Ill+Uml:%>%^iUMLide     FINAL1 ;
  +A+Ord+Pl+Ine:%>ine               FINAL1 ;
  +A+Ord+Pl+Ine+Uml:%>%^iUMLine     FINAL1 ;
  +A+Ord+Pl+Ela:%>ijste             FINAL1 ;
  +A+Ord+Pl+Ela+Uml:%>%^iUMLijste   FINAL1 ;
  +A+Ord+Pl+Com:ie%>jgujmie         FINAL1 ;
  +A+Ord+Pl+Com+Uml:%>%^iUMLigujmie FINAL1 ;
  +A+Ord+Ess:%>ine                  FINAL1 ;
  +A+Ord+Ess+Uml:%>%^iUMLine        FINAL1 ;






! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES                         !^C^
 §    ARABIC ;   ! §24                                    !^C^
 §§    ARABIC ;  ! §§24                                   !^C^
 §%     ARABIC ; ! § 24                                   !^C^
 §§%     ARABIC ; ! §§ 24                                  !^C^
 %-    ARABIC ;  ! -24                                    !^C^
 %‒    ARABIC ;  ! -24 U+2012 Figure line                 !^C^
 %–    ARABIC ;  ! -24 U+2013 en dash                     !^C^
 %—    ARABIC ;  ! -24 U+2014 em dash                     !^C^
 %―    ARABIC ;  ! -24 U+2015 horizontal l                !^C^
 %-%     ARABIC ;! - 24                                  !^C^
 %‒%     ARABIC ;! - 24 2012                             !^C^
 %–%     ARABIC ;! - 24 2013                             !^C^
 %—%     ARABIC ;! - 24 2014                             !^C^
 %―%     ARABIC ;! - 24 2015                             !^C^
 %*    ARABIC ;  ! *24                                    !^C^
 %$    ARABIC ;  ! $24  ! And the Euro?                   !^C^
 %<    ARABIC ;  ! <24                                    !^C^
 %>:%>7    ARABIC ;  ! >24                                !^C^
 %.    ARABIC ;  ! s.24                                  !^C^
! %+%     ARABIC ;! + 24                                  !^C^
! %+    ARABIC ; ! +24 ! out bec. of %+:0 in twol file   !^C^
! If lack of analysis for +34 etc becomes a problem, we will need
! a new border symbol in twol instead of +.



LEXICON ARABIC  !^C^
< [%0|1|2|3|4|5|6|7|8|9] >  ARABICLOOP ; !^C^

LEXICON ARABICLOOP      !^C^
         ARABIC ;        !^C^
 %-      ARABIC ;          ! 1-2    multipart numbers         	!SUB                 !^C^
 %.%-    ARABIC ;          ! 1.-2   multipart numbers           !SUB                 !^C^
 %-%     ARABIC ;          ! 1- 2   multipart numbers        	!SUB                 !^C^
 % %-    ARABIC ;          ! 1 -2   multipart numbers        	!SUB                 !^C^
 % %-%   ARABIC ;          ! 1 - 2  multipart numbers        	!SUB                 !^C^
 % %-%-%  ARABIC ;          ! 1 -- 2 multipart numbers       	!SUB                  !^C^
 % %-    ARABICDELIMITER ; ! 1 -    multipart numbers   - "1 - :s" - do we find it?  !SUB	!^C^
 % %-%-  ARABICDELIMITER ; ! 1 --   multipart numbers        	!SUB                 !^C^
 %.      ARABIC ;                                            	!SUB                 !^C^
 %,      ARABIC ;                                            	!SUB                 !^C^
 %:      ARABIC ;                                            	!SUB                 !^C^
 %       ARABIC ;    ! Allowing for "23 500".                	!SUB                 !^C^
         ARABIC ;    ! Allowing for "23 500" w/nbsp (init char is alt-space)         !^C^
 %/      ARABIC ;    ! 24/12                                                         !^C^
 %/%     ARABIC ;    ! 24/ 12                                                        !^C^
 % %/    ARABIC ;    ! 24 /12                                                        !^C^
 +A+Ord:%.   # ;                                                                     !^C^
 ,%-+Num:,%-       ARABICCASE ;      ! 10,-                                          !SUB !^C^
 ,%-+Num:.%-       ARABICCASE ;      ! 10.- It is wrong, but written.                !SUB !^C^
 %%+Num+Nom:%%     ARABICDELIMITER ; ! 50%. It is wrong, but 75 % of us write "75%". !SUB !^C^
 % %%+Num+Nom:% %% ARABICDELIMITER ; ! the correct ones as well... 50 % etc.         !SUB !^C^
 +Num:             ARABICDELIMITER ; ! list of number-case delimiters                !SUB !^C^


LEXICON ARABICDELIMITER ! blank + 3 delim, what does the lg counc prefer? !SUB !^C^
       ARABICCASE ; ! This is the 1984s case.     	!SUB                       !^C^
 :%-   ARABICCASE ; ! This is the 1984-s case.   	!SUB                       !^C^



LEXICON ARABICCASE                      !SUB              !^C^
 +Nom:f   # ;     ! s. 123f.          	!SUB                   !^C^
 +Nom:ff   # ;     ! s. 123ff.        	!SUB                   !^C^
 +Sg+Nom:b # ;     ! § 123b         	!SUB                   !^C^
 +Sg+Nom:c # ;     ! § 123c         	!SUB                   !^C^
 +Sg+Nom:d # ;     ! § 123d         	!SUB                   !^C^
 +Sg+Nom:e # ;     ! § 123e         	!SUB                   !^C^
 +Sg+Nom:  # ;                        	!SUB                  !^C^

! The Roman numerals !
! ------------------ !

LEXICON ROMAN                                !^C^
 ROM-THOUSAND ;                             !^C^
 ROM-HUNDRED ;                             !^C^
 ROM-TEN ;                                 !^C^
 ROM-ONE ;                                 !^C^

LEXICON ROM-THOUSAND                          !^C^
 M ROM-THOUSAND-TAG ;                     !^C^
 MM ROM-THOUSAND-TAG ;                     !^C^
 MMM ROM-THOUSAND-TAG ;                     !^C^
 MMMM ROM-THOUSAND-TAG ;                 !^C^
 MMMMM ROM-THOUSAND-TAG ;                 !^C^

LEXICON ROM-THOUSAND-TAG                    !^C^
  ROMNUMTAG ;                             !^C^
 ROM-HUNDRED ;                             !^C^
 ROM-TEN ;				!^C^
 ROM-ONE ;				!^C^
 ROM-SPLIT ;                             !^C^

LEXICON ROM-HUNDRED                            !^C^
 C ROM-HUNDRED-TAG ;                     !^C^
 CC ROM-HUNDRED-TAG ;                     !^C^
 CCC ROM-HUNDRED-TAG ;                     !^C^
 CD ROM-HUNDRED-TAG ;                     !^C^
 D ROM-HUNDRED-TAG ;                     !^C^
 DC ROM-HUNDRED-TAG ;                     !^C^
 DCC ROM-HUNDRED-TAG ;                     !^C^
 DCCC ROM-HUNDRED-TAG ;                     !^C^
 CM ROM-HUNDRED-TAG ;                     !^C^

LEXICON ROM-HUNDRED-TAG                        !^C^
  ROMNUMTAG ;                             !^C^
 ROM-TEN ;                                 !^C^
 ROM-ONE ;				!^C^
 ROM-SPLIT ;                             !^C^

LEXICON ROM-TEN                                !^C^
 X ROM-TEN-TAG ;                         !^C^
 XX ROM-TEN-TAG ;                         !^C^
 XXX ROM-TEN-TAG ;                         !^C^
 XL ROM-TEN-TAG ;                         !^C^
 L ROM-TEN-TAG ;                         !^C^
 LX ROM-TEN-TAG ;                         !^C^
 LXX ROM-TEN-TAG ;                         !^C^
 LXXX ROM-TEN-TAG ;                         !^C^
 XC ROM-TEN-TAG ;                         !^C^

LEXICON ROM-TEN-TAG                            !^C^
  ROMNUMTAG ;                             !^C^
 ROM-ONE ;                                 !^C^
 ROM-SPLIT ;                             !^C^

LEXICON ROM-ONE                                !^C^
 I ROM-ONE-TAG ;                         !^C^
 II ROM-ONE-TAG ;                         !^C^
 III ROM-ONE-TAG ;                         !^C^
 IV  ROM-ONE-TAG ;                         !^C^
 V ROM-ONE-TAG ;                         !^C^
 VI ROM-ONE-TAG ;                         !^C^
 VII ROM-ONE-TAG ;                         !^C^
 VIII ROM-ONE-TAG ;                         !^C^
 IX ROM-ONE-TAG ;                         !^C^

LEXICON ROM-ONE-TAG                            !^C^
  ROMNUMTAG ;                             !^C^
! +N: ROMNUMTAG ;                         !^C^ !The Olav viđeš fix (Roman V now gets A)
 ROM-SPLIT ;                             !^C^

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the
! trick.

LEXICON ROM-SPLIT                            !^C^
 :%- 2ROMAN ; ! II-VI, etc.                    !^C^


! Here goes loop 2.

LEXICON 2ROMAN                                !^C^
 2ROM-THOUSAND ;                         !^C^
 2ROM-HUNDRED ;                             !^C^
 2ROM-TEN ;                                  !^C^
 2ROM-ONE ;                                  !^C^

LEXICON 2ROM-THOUSAND                       !^C^
 M 2ROM-THOUSAND-TAG ;                     !^C^
 MM 2ROM-THOUSAND-TAG ;                     !^C^
 MMM 2ROM-THOUSAND-TAG ;                 !^C^
 MMMM 2ROM-THOUSAND-TAG ;                 !^C^
 MMMMM 2ROM-THOUSAND-TAG ;                 !^C^

LEXICON 2ROM-THOUSAND-TAG                    !^C^
  ROMNUMTAG ;                             !^C^
 2ROM-HUNDRED ;                             !^C^
 2ROM-TEN ;				   !^C^
 2ROM-ONE ;				!^C^

LEXICON 2ROM-HUNDRED                        !^C^
 C 2ROM-HUNDRED-TAG ;                     !^C^
 CC 2ROM-HUNDRED-TAG ;                     !^C^
 CCC 2ROM-HUNDRED-TAG ;                     !^C^
 CD 2ROM-HUNDRED-TAG ;                     !^C^
 D 2ROM-HUNDRED-TAG ;                     !^C^
 DC 2ROM-HUNDRED-TAG ;                     !^C^
 DCC 2ROM-HUNDRED-TAG ;                     !^C^
 DCCC 2ROM-HUNDRED-TAG ;                 !^C^
 CM 2ROM-HUNDRED-TAG ;                     !^C^

LEXICON 2ROM-HUNDRED-TAG                    !^C^
  ROMNUMTAG ;                             !^C^
 2ROM-TEN ;                                 !^C^
 2ROM-ONE ;				!^C^
LEXICON 2ROM-TEN                               !^C^
 X 2ROM-TEN-TAG ;                           !^C^
 XX 2ROM-TEN-TAG ;                          !^C^
 XXX 2ROM-TEN-TAG ;                         !^C^
 XL 2ROM-TEN-TAG ;                          !^C^
 L 2ROM-TEN-TAG ;                           !^C^
 LX 2ROM-TEN-TAG ;                          !^C^
 LXX 2ROM-TEN-TAG ;                         !^C^
 LXXX 2ROM-TEN-TAG ;                        !^C^
 XC 2ROM-TEN-TAG ;                          !^C^

LEXICON 2ROM-TEN-TAG                           !^C^
  ROMNUMTAG ;                               !^C^
 2ROM-ONE ;                                 !^C^

LEXICON 2ROM-ONE                               !^C^
 I 2ROM-ONE-TAG ;                           !^C^
 II 2ROM-ONE-TAG ;                          !^C^
 III 2ROM-ONE-TAG ;                         !^C^
 IV  2ROM-ONE-TAG ;                         !^C^
 V 2ROM-ONE-TAG ;                           !^C^
 VI 2ROM-ONE-TAG ;                          !^C^
 VII 2ROM-ONE-TAG ;                         !^C^
 VIII 2ROM-ONE-TAG ;                        !^C^
 IX 2ROM-ONE-TAG ;                          !^C^

LEXICON 2ROM-ONE-TAG                           !^C^
  ROMNUMTAG ;                               !^C^

LEXICON ROMNUMTAG                              !^C^
 +Num+Nom:%> # ;                              !^C^
 +Num+Gen:%> # ;                              !^C^
 +Num+Acc:%> # ;                              !^C^
 +A+Ord:%.%> # ;                              !^C^ !III. as ordinal.
 +A+Ord:%> # ;                                !^C^ !Looking for trouble...
 +Num:%: ROMNUMTAGOBL ;                     !^C^
 +Num:'7 ROMNUMTAGOBL ;                     !^C^
 +Num:%- ROMNUMTAGOBL ;                     !^C^
 +Num:%´ ROMNUMTAGOBL ;                     !^C^
! +Num:   ROMNUMTAGOBL ;                     !^C^

LEXICON ROMNUMTAGOBL                           !^C^
+Gen:%>n # ; !SUB ! But here 123a 123+Num+Gen      !^C^
+Acc:%>m # ; !SUB                                 !^C^
+Ill:%>ese # ;                                  !^C^ 
+Ine:%>esne # ;                                  !^C^
+Ine+Attr:%>ene # ;                                  !^C^
+Ela+Attr:%>ede # ;                                  !^C^
+Ela:%>este # ;                                  !^C^
+Com:%>ine # ;                                 !^C^
+Ess:%>in # ;                                  !^C^

LEXICON NUMBERLIKE
gellie+Num+Sg:gell           N_IE_SG ; ! as sme máŋga
gellie+Num+Sg+Ine+Attr:gelle%>ne FINAL1 ;
gellie+Num+Sg+Ela+Attr:gelle%>de              FINAL1 ;


! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !


LEXICON ISOLATED-NUMEXP                        !^C^
½+Num:½ # ;                                 !^C^
¹+Num:¹ # ;                                 !^C^
²+Num:² # ;                                 !^C^
³+Num:³ # ;                                 !^C^
¼+Num:¼ # ;                                 !^C^
¾+Num:¾ # ;

