!Distributed under the terms of the GNU General Public License version 2 
! or any later version.

! ############################## !
!This file contains the numerals !
! ############################## !




! Numerals
! ========

! The initial lexica
! ------------------

LEXICON Numeral
MILLIJOVNE ;       ! a noun of its own
OVERTÅVSENE ;       ! for generator over 1000
JUSTTÅVSENE ;       ! going via 1000
UNDERTÅVSENE ;      ! for generator under 1000
!num-imprecise ;                              
!COLLECTIVE ;                            
ARABIC ;           ! for the arabic numerals !^C^
ROMAN ;            ! for the roman numerals!^C^
NUM-PREFIXES ;     ! for §34 etc.!^C^
ISOLATED-NUMEXP ;  ! for ½ etc.!^C^
NUMBERLIKE ;      ! a dustbin







! We now run through the alphabetic numerals, ending up with the case
! inflection of the final number.

! Million, a noun of its own
! --------------------------

LEXICON MILLIJOVNE
millijovne:millijovn NUMTAGe ;
millijovnede+A+Ord:millijovned ORDTAGODD ;
millijarde:millijard NUMTAGe ;! Num or N?
millijardede+A+Ord:millijarded ORDTAGODD ;

stoere#luhkie:stoere#luhk NUMTAGie ;

stoere#tjuetie:stoere#tjuet NUMTAGie ;

stoere#nïmme:stoere#nïmm NUMTAGe ;
stoere#nymme:stoere#nymm NUMTAGe ;
stoere#nomme:stoere#nomm NUMTAGe ;


! OVERTÅVSENE, for the numerals over 1000
! --------------------------------------

LEXICON OVERTÅVSENE
O-AKTEF ;      ! 1000
O-2TO9F ;      ! 2000, 3000, ...
O-JUSTLUHKIEF ; ! 10000
O-2TO9LUHKIE ;    ! 20000, 30000, ...
O-TJUETIE ;    ! 100000
O-2TO9TJUETIE ;   ! 200000, 300000, ...

! All the child lexica of OVERTUVSÁN have the prefix O-.
! They are directed via their respective numerals to the lexicon JUSTTÅVSENE.


! Thousands

LEXICON O-AKTEF
akte# JUSTTOVSENE ;
JUSTTOVSENE ;

LEXICON O-X1000
akte# JUSTTÅVSENE ;

LEXICON O-2TO9F                               
göökte#  JUSTTÅVSENE ;       
golme#  JUSTTÅVSENE ;  
njieljie# JUSTTÅVSENE ;   
njielje# JUSTTÅVSENE ; !Saemesth amma
nieljie# JUSTTÅVSENE ;
nielje# JUSTTÅVSENE ; 
vïjhte#  JUSTTÅVSENE ;       
govhte#  JUSTTÅVSENE ;             
tjïjhtje#  JUSTTÅVSENE ;
gaektsie#  JUSTTÅVSENE ;         
uktsie#   JUSTTÅVSENE ;            

! Tens of thousands

LEXICON O-JUSTLUHKIEF
luhkie# JUSTTÅVSENE ;
! This lexicon is for the number 10000 only. it is separated from the
! rest to avoid forms like *luhkieluhkietåvsene, etc.


LEXICON O-LUHKIEF     
luhkie# JUSTTÅVSENE ;
luhkie# O-X1000 ;
luhkie# O-2TO9F ;
luhkie# JUSTTÅVSENE ;
luhkie# O-X1000 ;
luhkie# O-2TO9F ;
! this lexicon is accessed only via other O-lexica, and not directly 
! from OVERTUVSÁN. Thus, *luhkieluhkietåvsene, etc. is avoided.


LEXICON O-2TO9LUHKIE  
göökte# O-LUHKIEF ;
golme# O-LUHKIEF ;
njieljie# O-LUHKIEF ;
njielje# O-LUHKIEF ;
nieljie# O-LUHKIEF ;
nielje# O-LUHKIEF ; 
vïjhte# O-LUHKIEF ;
govhte# O-LUHKIEF ;
tjïjhtje# O-LUHKIEF ;
gaektsie# O-LUHKIEF ;
uktsie# O-LUHKIEF ;




! Hundreds of thousands

LEXICON O-TJUETIE
tjuetie# JUSTTÅVSENE ; ! 100000
tjuetie# O-X1000 ;    ! 101000
tjuetie# O-2TO9F ;    ! 102000, 103000, ...
tjuetie# O-LUHKIEF ;   ! 110000, 113000, ...
tjuetie# O-2TO9LUHKIE ;  ! 120000, 123000, ...
!tjuetie# O-LÅGENAN ;  ! 115000, ...


LEXICON O-TJUETIE2
tjuetie# JUSTTÅVSENE ;  ! 200000
tjuetie# O-X1000 ;     ! 201000
tjuetie# O-2TO9F ;     ! 202000, 203000, ...
tjuetie# O-LUHKIEF ;    ! 210000, 213000, ...
tjuetie# O-2TO9LUHKIE ;   ! 220000, 223000, ...
!tjuetie# O-LÅGENAN ;   ! 215000, ...


LEXICON O-2TO9TJUETIE
akte# O-TJUETIE ;       ! #one# hundred...
göökte# O-TJUETIE2 ;
golme# O-TJUETIE2 ;
njieljie# O-TJUETIE2 ;
njielje# O-TJUETIE2 ;
nieljie# O-TJUETIE2 ;
nielje# O-TJUETIE2 ; 
vïjhte# O-TJUETIE2 ;
govhte# O-TJUETIE2 ;
tjïjhtje# O-TJUETIE2 ;
gaektsie# O-TJUETIE2 ;
uktsie# O-TJUETIE2 ;


LEXICON JUSTTOVSENE
tåvsene# UNDERTÅVSENE ;
tåvsene ORDTAGEVEN ;                    ! Case infl of tuvsán
tåvsenede+A+Ord:tåvsened ORDTAGEVEN ;

! Justtuvsán, for numerals going via 1000
! -------------------------------------

LEXICON JUSTTÅVSENE
tåvsene# UNDERTÅVSENE ;             ! Thousand-and-something
tåvsene ORDTAGEVEN ;                   ! Case infl of tuvsán
tåvsenede+A+Ord:tåvsened ORDTAGEVEN ;








LEXICON UNDERTÅVSENE
AKTEF ;     ! 1
2TO9F ;     ! 2-9
LUHKIEF ;     ! 10
!LÅGENAN ;   ! 11-19             
2TO9LUHKIE ;   ! 20-99
TJUETIE ;     ! 100-199
2TO9TJUETIE ;   ! 200-999

LEXICON AKTEF
akte:akt NUMTAGe ;        
voestes+A+Ord+Sg+Nom:voestes         FINAL1 ; !XXX check this


LEXICON 2TO9F
göökte:guokt NUMTAGe ;
göökte:göökt NUMTAGe ;
golme:golm NUMTAGe ;
njieljie:njielj NUMTAGie ;
njielje:njielj NUMTAGe ; !Saemesth amma
nieljie:nielj NUMTAGie ;
nielje:nielj NUMTAGe ; 
vïjhte:vïjht NUMTAGe ;
govhte:govht NUMTAGe ;
tjïjhtje:tjïjhtj NUMTAGe ;
gaektsie:gaekts NUMTAGie ;
uktsie:ukts NUMTAGie ;
mubpie+A+Ord:mubp           MUBPIE ;
nubpie+A+Ord:nubp           MUBPIE ;
gåalmede+A+Ord:gåalmad ORDTAGODD ;
njieljede+A+Ord:njieljied ORDTAGODD ;
njealjede+A+Ord:njealjad ORDTAGODD ;
nealjede+A+Ord:nealjad ORDTAGODD ;
nieljede+A+Ord:nieljied ORDTAGODD ;
vïjhtede+A+Ord:vïjhted ORDTAGODD ;
govhtede+A+Ord:govhted ORDTAGODD ;
tjïjhtjede+A+Ord:tjïjhtjed ORDTAGODD ;
gaaktsede+A+Ord:gaaktsad ORDTAGODD ;
åktsede+A+Ord:åktsad ORDTAGODD ;
gööktesh:gööktes COLLTAG ;
golmesh:golmes COLLTAG ;
njealjesh:njealjas COLLTAG ;
vïjhtesh:vïjhtes COLLTAG ;
govhtesh:govhtes COLLTAG ;
tjïjhtjesh:tjïjhtjes COLLTAG ;
gaaktsesh:gaaktsas COLLTAG ;
åktsesh:åktsas COLLTAG ;

! Case forms of the basic digits
! ------------------------------

LEXICON NUMTAGe ! akte:akt
+Num+Sg+Nom:e # ;
+Num+Sg+Acc:%>em # ;
+Num+Sg+Gen:%>en # ;
+Num+Sg+Ill:%>ese # ;
+Num+Sg+Ine:%>esne # ;
+Num+Sg+Ine+Attr:%>ene # ; !XXX Attr forms
+Num+Sg+Ela+Attr:%>ede # ; !XXX Attr forms
+Num+Sg+Ela:%>este # ;
+Num+Sg+Com:%>ine # ;
+Num+Ess:%>ijne # ;
+Num+Pl+Nom:%>h # ;
+Num+Pl+Acc:%>ide # ;
+Num+Pl+Gen:%>i # ;
+Num+Pl+Ill:%>ide # ;
+Num+Pl+Ine:%>ine # ;
+Num+Pl+Ela:%>ijste # ;
+Num+Pl+Com:%>ijgujme # ;

LEXICON NUMTAGie !njieljie:njielj
+Num+Sg+Nom:ie # ;
+Num+Sg+Acc:%>em    # ;
+Num+Sg+Gen:%>en    # ;
+Num+Sg+Ill+Uml:%>%^1UMLane   # ;
+Num+Sg+Ine:%>esne  # ;
+Num+Sg+Ine+Attr:%>ine  # ;
+Num+Sg+Ela+Attr:%>ede  # ;
+Num+Sg+Ela:%>este  # ;
+Num+Sg+Com:%>ine # ;
+Num+Ess:%>ijne    # ;
+Num+Pl+Nom:%>ieh  # ;
+Num+Pl+Acc:%>ide # ;
+Num+Pl+Gen:%>i  # ;
+Num+Pl+Ill:%>ide # ;
+Num+Pl+Ine:%>ne # ;
+Num+Pl+Ela:%>ijste   # ; !
+Num+Pl+Com:%>jgujme  # ;


LEXICON LUHKIEF
luhkie:luhk NUMTAGie ;
låhkede+A+Ord:låhkad ORDTAGODD ;
låhkesh:låhkas COLLTAG ;


LEXICON LUHKIE-
luhkie# AKTEF ;
luhkie# 2TO9F ;
låhkede+A+Ord:låhkad ORDTAGODD ;
låhkesh:låhkas COLLTAG ;

! Ordinal numbers
! ---------------

LEXICON MUBPIE
  +Sg+Nom:ie                  FINAL1 ;
  +Sg+Acc:ie%>m               FINAL1 ;
  +Sg+Gen:ie%>n               FINAL1 ;
! +Sg+Ill:%>an                FINAL1 ; !SUB - is this possible? IllSg without Uml in -ie?
  +Sg+Ill+Uml:%>%^1UMLan      FINAL1 ;
  +Sg+Ine:e%>sne              FINAL1 ;
  +Sg+Ine+Attr:e%>ne FINAL1 ;
  +Sg+Ela+Attr:e%>de              FINAL1 ;
  +Sg+Ela:e%>ste              FINAL1 ;
  +Sg+Com:%>ine               FINAL1 ;
  +Sg+Com+Uml:%>%^iUMLine     FINAL1 ;
  +Pl+Nom:ie%>h               FINAL1 ;
  +Pl+Acc:%>ide               FINAL1 ;
  +Pl+Acc+Uml:%>%^iUMLide     FINAL1 ;
  +Pl+Gen:ie%>j               FINAL1 ;
  +Pl+Gen+Uml:%>%^iUMLi       FINAL1 ;
  +Pl+Ill:%>ide               FINAL1 ;
  +Pl+Ill+Uml:%>%^iUMLide     FINAL1 ;
  +Pl+Ine:%>ine               FINAL1 ;
  +Pl+Ine+Uml:%>%^iUMLine     FINAL1 ;
  +Pl+Ela:%>ijste             FINAL1 ;
  +Pl+Ela+Uml:%>%^iUMLijste   FINAL1 ;
  +Pl+Com:ie%>jgujmie         FINAL1 ;
  +Pl+Com+Uml:%>%^iUMLigujmie FINAL1 ;
  +Ess:%>ine                  FINAL1 ;
  +Ess+Uml:%>%^iUMLine        FINAL1 ;


LEXICON ORDTAGODD
+Sg+Nom:e%^DISIMP         # ;
+Sg+Acc:e%>%^DISIMPm      # ;
+Sg+Gen:e%>%^DISIMPn      # ;
+Sg+Ill:%>asse            # ; !Rule to delete Jesuse%>asse > Jesusasse
+Sg+Ine+Attr:%>inie  # ;
+Sg+Ela+Attr:%>ihtie  # ;
+Sg+Ine:%>isnie           # ;
+Sg+Ela:%>istie           # ;
+Sg+Com:%>inie            # ;
+Pl+Nom:%>%^DISIMPh       # ;
+Pl+Acc:%>idie            # ;
+Pl+Gen:%>%^DISIMPi       # ;
+Pl+Ill:%>idie            # ;
+Pl+Ine:%>inie            # ;
+Pl+Ela:%>ijstie          # ;
+Pl+Com:%>%^DISIMPigujmie # ;
+Pl+Com:%>%^DISIMPigyjmie # ; !SUB
+Ess:%>inie          # ;

LEXICON ORDTAGEVEN
+Sg+Nom:e # ;
+Sg+Acc:%>em # ;
+Sg+Gen:%>en # ;
+Sg+Ill:%>ese # ;
+Sg+Ine:%>esne # ;
+Sg+Ine+Attr:%>ene # ;
+Sg+Ela+Attr:%>ede # ;
+Sg+Ela:%>este # ;
+Sg+Com:%>ine # ;
+Ess:%>ine # ;
+Pl+Nom:%>h # ;
+Pl+Acc:%>ide # ;
+Pl+Gen:%>i # ;
+Pl+Ill:%>ide # ;
+Pl+Ine:%>ine # ;
+Pl+Ela:%>ijste # ; !
+Pl+Com:%>ijgujme # ; 
+Pl+Com:%>igyjmie  FINAL1 ; !SUB


 
! Collection numbers
! ---------------



LEXICON COLLTAG
+Num+Coll+Pl+Nom:%>%^DISIMPh       # ;
+Num+Coll+Pl+Acc:%>idie            # ;
+Num+Coll+Pl+Gen:%>%^DISIMPi       # ;
+Num+Coll+Pl+Ill:%>idie            # ;
+Num+Coll+Pl+Ine:%>inie            # ;
+Num+Coll+Pl+Ela:%>ijstie          # ;
+Num+Coll+Pl+Com:%>%^DISIMPigujmie # ;
+Num+Coll+Pl+Com:%>%^DISIMPigyjmie # ; !SUB
+Num+Coll+Ess:%>inie          # ;

! Arabic numerals
! ---------------



LEXICON NUM-PREFIXES                         !^C^
 §    ARABIC ;   ! §24                                    !^C^
 §§    ARABIC ;  ! §§24                                   !^C^
 §%     ARABIC ; ! § 24                                   !^C^
 §§%     ARABIC ; ! §§ 24                                  !^C^
 %-    ARABIC ;  ! -24                                    !^C^
 %‒    ARABIC ;  ! -24 U+2012 Figure line                 !^C^
 %–    ARABIC ;  ! -24 U+2013 en dash                     !^C^
 %—    ARABIC ;  ! -24 U+2014 em dash                     !^C^
 %―    ARABIC ;  ! -24 U+2015 horizontal l                !^C^
 %-%     ARABIC ;! - 24                                  !^C^
 %‒%     ARABIC ;! - 24 2012                             !^C^
 %–%     ARABIC ;! - 24 2013                             !^C^
 %—%     ARABIC ;! - 24 2014                             !^C^
 %―%     ARABIC ;! - 24 2015                             !^C^
 %*    ARABIC ;  ! *24                                    !^C^
 %$    ARABIC ;  ! $24  ! And the Euro?                   !^C^
 %<    ARABIC ;  ! <24                                    !^C^
 %>:%>7    ARABIC ;  ! >24                                !^C^
 %.    ARABIC ;  ! s.24                                  !^C^
! %+%     ARABIC ;! + 24                                  !^C^
! %+    ARABIC ; ! +24 ! out bec. of %+:0 in twol file   !^C^
! If lack of analysis for +34 etc becomes a problem, we will need
! a new border symbol in twol instead of +.



LEXICON ARABIC  !^C^
< [%0|1|2|3|4|5|6|7|8|9] >  ARABICLOOP ; !^C^

LEXICON ARABICLOOP      !^C^
         ARABIC ;        !^C^
 %-      ARABIC ;          ! 1-2    multipart numbers         !SUB                 !^C^
 %.%-    ARABIC ;          ! 1.-2   multipart numbers           !SUB                 !^C^
 %-%     ARABIC ;          ! 1- 2   multipart numbers        !SUB                 !^C^
 % %-    ARABIC ;          ! 1 -2   multipart numbers        !SUB                 !^C^
 % %-%   ARABIC ;          ! 1 - 2  multipart numbers        !SUB                 !^C^
 % %-%-%  ARABIC ;          ! 1 -- 2 multipart numbers       !SUB                  !^C^
 % %-    ARABICDELIMITER ; ! 1 -    multipart numbers   - "1 - :s" - do we find it?  !SUB!^C^
 % %-%-  ARABICDELIMITER ; ! 1 --   multipart numbers        !SUB                 !^C^
 %.      ARABIC ;                                            !SUB                 !^C^
 %,      ARABIC ;                                            !SUB                 !^C^
 %:      ARABIC ;                                            !SUB                 !^C^
 %       ARABIC ;    ! Allowing for "23 500".                !SUB                 !^C^
         ARABIC ;    ! Allowing for "23 500" w/nbsp (init char is alt-space)         !^C^
 %/      ARABIC ;    ! 24/12                                                         !^C^
 %/%     ARABIC ;    ! 24/ 12                                                        !^C^
 % %/    ARABIC ;    ! 24 /12                                                        !^C^
 +A+Ord:%.   # ;                                                                     !^C^
 ,%-+Num:,%-       ARABICCASE ;      ! 10,-                                          !SUB !^C^
 ,%-+Num:.%-       ARABICCASE ;      ! 10.- It is wrong, but written.                !SUB !^C^
 %%+Num+Nom:%%     ARABICDELIMITER ; ! 50%. It is wrong, but 75 % of us write "75%". !SUB !^C^
 % %%+Num+Nom:% %% ARABICDELIMITER ; ! the correct ones as well... 50 % etc.         !SUB !^C^
 +Num:             ARABICDELIMITER ; ! list of number-case delimiters                !SUB !^C^


LEXICON ARABICDELIMITER ! blank + 3 delim, what does the lg counc prefer? !SUB !^C^
       ARABICCASE ; ! This is the 1984s case.     !SUB                       !^C^
 :%-   ARABICCASE ; ! This is the 1984-s case.   !SUB                       !^C^



LEXICON ARABICCASE                      !SUB              !^C^
 +Nom:f   # ;     ! s. 123f.          !SUB                   !^C^
 +Nom:ff   # ;     ! s. 123ff.        !SUB                   !^C^
 +Sg+Nom:b # ;     ! § 123b         !SUB                   !^C^
 +Sg+Nom:c # ;     ! § 123c         !SUB                   !^C^
 +Sg+Nom:d # ;     ! § 123d         !SUB                   !^C^
 +Sg+Nom:e # ;     ! § 123e         !SUB                   !^C^
 +Sg+Nom:  # ;                        !SUB                  !^C^

! The Roman numerals !
! ------------------ !

LEXICON ROMAN                                !^C^
 ROM-THOUSAND ;                             !^C^
 ROM-HUNDRED ;                             !^C^
 ROM-TEN ;                                 !^C^
 ROM-ONE ;                                 !^C^

LEXICON ROM-THOUSAND                          !^C^
 M ROM-THOUSAND-TAG ;                     !^C^
 MM ROM-THOUSAND-TAG ;                     !^C^
 MMM ROM-THOUSAND-TAG ;                     !^C^
 MMMM ROM-THOUSAND-TAG ;                 !^C^
 MMMMM ROM-THOUSAND-TAG ;                 !^C^

LEXICON ROM-THOUSAND-TAG                    !^C^
  ROMNUMTAG ;                             !^C^
 ROM-HUNDRED ;                             !^C^
 ROM-TEN ;!^C^
 ROM-ONE ;!^C^
 ROM-SPLIT ;                             !^C^

LEXICON ROM-HUNDRED                            !^C^
 C ROM-HUNDRED-TAG ;                     !^C^
 CC ROM-HUNDRED-TAG ;                     !^C^
 CCC ROM-HUNDRED-TAG ;                     !^C^
 CD ROM-HUNDRED-TAG ;                     !^C^
 D ROM-HUNDRED-TAG ;                     !^C^
 DC ROM-HUNDRED-TAG ;                     !^C^
 DCC ROM-HUNDRED-TAG ;                     !^C^
 DCCC ROM-HUNDRED-TAG ;                     !^C^
 CM ROM-HUNDRED-TAG ;                     !^C^

LEXICON ROM-HUNDRED-TAG                        !^C^
  ROMNUMTAG ;                             !^C^
 ROM-TEN ;                                 !^C^
 ROM-ONE ;!^C^
 ROM-SPLIT ;                             !^C^

LEXICON ROM-TEN                                !^C^
 X ROM-TEN-TAG ;                         !^C^
 XX ROM-TEN-TAG ;                         !^C^
 XXX ROM-TEN-TAG ;                         !^C^
 XL ROM-TEN-TAG ;                         !^C^
 L ROM-TEN-TAG ;                         !^C^
 LX ROM-TEN-TAG ;                         !^C^
 LXX ROM-TEN-TAG ;                         !^C^
 LXXX ROM-TEN-TAG ;                         !^C^
 XC ROM-TEN-TAG ;                         !^C^

LEXICON ROM-TEN-TAG                            !^C^
  ROMNUMTAG ;                             !^C^
 ROM-ONE ;                                 !^C^
 ROM-SPLIT ;                             !^C^

LEXICON ROM-ONE                                !^C^
 I ROM-ONE-TAG ;                         !^C^
 II ROM-ONE-TAG ;                         !^C^
 III ROM-ONE-TAG ;                         !^C^
 IV  ROM-ONE-TAG ;                         !^C^
 V ROM-ONE-TAG ;                         !^C^
 VI ROM-ONE-TAG ;                         !^C^
 VII ROM-ONE-TAG ;                         !^C^
 VIII ROM-ONE-TAG ;                         !^C^
 IX ROM-ONE-TAG ;                         !^C^

LEXICON ROM-ONE-TAG                            !^C^
  ROMNUMTAG ;                             !^C^
! +N: ROMNUMTAG ;                         !^C^ !The Olav viđeš fix (Roman V now gets A)
 ROM-SPLIT ;                             !^C^

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the
! trick.

LEXICON ROM-SPLIT                            !^C^
 :%- 2ROMAN ; ! II-VI, etc.                    !^C^


! Here goes loop 2.

LEXICON 2ROMAN                                !^C^
 2ROM-THOUSAND ;                         !^C^
 2ROM-HUNDRED ;                             !^C^
 2ROM-TEN ;                                  !^C^
 2ROM-ONE ;                                  !^C^

LEXICON 2ROM-THOUSAND                       !^C^
 M 2ROM-THOUSAND-TAG ;                     !^C^
 MM 2ROM-THOUSAND-TAG ;                     !^C^
 MMM 2ROM-THOUSAND-TAG ;                 !^C^
 MMMM 2ROM-THOUSAND-TAG ;                 !^C^
 MMMMM 2ROM-THOUSAND-TAG ;                 !^C^

LEXICON 2ROM-THOUSAND-TAG                    !^C^
  ROMNUMTAG ;                             !^C^
 2ROM-HUNDRED ;                             !^C^
 2ROM-TEN ;   !^C^
 2ROM-ONE ;!^C^

LEXICON 2ROM-HUNDRED                        !^C^
 C 2ROM-HUNDRED-TAG ;                     !^C^
 CC 2ROM-HUNDRED-TAG ;                     !^C^
 CCC 2ROM-HUNDRED-TAG ;                     !^C^
 CD 2ROM-HUNDRED-TAG ;                     !^C^
 D 2ROM-HUNDRED-TAG ;                     !^C^
 DC 2ROM-HUNDRED-TAG ;                     !^C^
 DCC 2ROM-HUNDRED-TAG ;                     !^C^
 DCCC 2ROM-HUNDRED-TAG ;                 !^C^
 CM 2ROM-HUNDRED-TAG ;                     !^C^

LEXICON 2ROM-HUNDRED-TAG                    !^C^
  ROMNUMTAG ;                             !^C^
 2ROM-TEN ;                                 !^C^
 2ROM-ONE ;!^C^
LEXICON 2ROM-TEN                               !^C^
 X 2ROM-TEN-TAG ;                           !^C^
 XX 2ROM-TEN-TAG ;                          !^C^
 XXX 2ROM-TEN-TAG ;                         !^C^
 XL 2ROM-TEN-TAG ;                          !^C^
 L 2ROM-TEN-TAG ;                           !^C^
 LX 2ROM-TEN-TAG ;                          !^C^
 LXX 2ROM-TEN-TAG ;                         !^C^
 LXXX 2ROM-TEN-TAG ;                        !^C^
 XC 2ROM-TEN-TAG ;                          !^C^

LEXICON 2ROM-TEN-TAG                           !^C^
  ROMNUMTAG ;                               !^C^
 2ROM-ONE ;                                 !^C^

LEXICON 2ROM-ONE                               !^C^
 I 2ROM-ONE-TAG ;                           !^C^
 II 2ROM-ONE-TAG ;                          !^C^
 III 2ROM-ONE-TAG ;                         !^C^
 IV  2ROM-ONE-TAG ;                         !^C^
 V 2ROM-ONE-TAG ;                           !^C^
 VI 2ROM-ONE-TAG ;                          !^C^
 VII 2ROM-ONE-TAG ;                         !^C^
 VIII 2ROM-ONE-TAG ;                        !^C^
 IX 2ROM-ONE-TAG ;                          !^C^

LEXICON 2ROM-ONE-TAG                           !^C^
  ROMNUMTAG ;                               !^C^

LEXICON ROMNUMTAG                              !^C^
 +Num+Nom:%> # ;                              !^C^
 +Num+Gen:%> # ;                              !^C^
 +Num+Acc:%> # ;                              !^C^
 +A+Ord:%.%> # ;                              !^C^ !III. as ordinal.
 +A+Ord:%> # ;                                !^C^ !Looking for trouble...
 +Num:%: ROMNUMTAGOBL ;                     !^C^
 +Num:'7 ROMNUMTAGOBL ;                     !^C^
 +Num:%- ROMNUMTAGOBL ;                     !^C^
 +Num:%´ ROMNUMTAGOBL ;                     !^C^
! +Num:   ROMNUMTAGOBL ;                     !^C^

LEXICON ROMNUMTAGOBL                           !^C^
+Gen:%>n # ; !SUB ! But here 123a 123+Num+Gen      !^C^
+Acc:%>m # ; !SUB                                 !^C^
+Ill:%>ese # ;                                  !^C^ 
+Ine:%>esne # ;                                  !^C^
+Ine+Attr:%>ene # ;                                  !^C^
+Ela+Attr:%>ede # ;                                  !^C^
+Ela:%>este # ;                                  !^C^
+Com:%>ine # ;                                 !^C^
+Ess:%>in # ;                                  !^C^

LEXICON NUMBERLIKE
gellie+Num+Sg:gell           N_IE_SG ; ! as sme máŋga
gellie+Num+Sg+Ine+Attr:gelle%>ne FINAL1 ;
gellie+Num+Sg+Ela+Attr:gelle%>de              FINAL1 ;


! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !


LEXICON ISOLATED-NUMEXP                        !^C^
½+Num:½ # ;                                 !^C^
¹+Num:¹ # ;                                 !^C^
²+Num:² # ;                                 !^C^
³+Num:³ # ;                                 !^C^
¼+Num:¼ # ;                                 !^C^
¾+Num:¾ # ;





LEXICON 2TO9LUHKIE
göökte# LUHKIE- ;
golme# LUHKIE- ;
njieljie# LUHKIE- ;
njielje# LUHKIE- ;
nieljie# LUHKIE- ;
nielje# LUHKIE- ;
vïjhte# LUHKIE- ;
govhte# LUHKIE- ;
tjïjhtje# LUHKIE- ;
gaektsie# LUHKIE- ;
uktsie# LUHKIE- ;

LEXICON 2TO9TJUETIE
göökte# GTJUETIE ;
golme# GTJUETIE ;
njieljie# GTJUETIE ;
njielje# GTJUETIE ;
nieljie# GTJUETIE ;
nielje# GTJUETIE ;
vïjhte# GTJUETIE ;
govhte# GTJUETIE ;
tjïjhtje# GTJUETIE ;
gaektsie# GTJUETIE ;
uktsie# GTJUETIE ;

LEXICON GTJUETIE
tjuetie LUHKIEF ;
tjuetie# 2TO9LUHKIE ;
tjuetie# AKTEF ;
tjuetie# 2TO9F ;
!tjuetie# LÅGENAN ;
tjuetie:tjuet NUMTAGie ;
tjuatede+A+Ord:tjuatad ORDTAGODD ;

LEXICON TJUETIE
tjuetie:tjuet NUMTAGie ;
tjuetie# LUHKIEF ;
tjuetie# LUHKIE- ;
tjuetie# 2TO9LUHKIE ;
tjuetie# AKTEF ;
tjuetie# 2TO9F ;
!tjuetie# LÅGENAN ;
tjuatede+A+Ord:tjuatad ORDTAGODD ;
akte#tjuetie:akte#tjuet NUMTAGie ; ;    
akte#tjuohte# LUHKIEF ;
akte#tjuohte# LUHKIE- ;
akte#tjuohte# 2TO9LUHKIE ;
akte#tjuohte# AKTEF ;
akte#tjuohte# 2TO9F ;
!akte#tjuohte# LÅGENAN ;




  



!LEXICON COLLECTIVE    !Some collective numbers that do not fit in anywhere else



                  
                                      
!LEXICON NUMERALCOMPOUNDS  ! numeral as first part 
! NAMÁK ;   ! tuvsánjienat, logigielat, etc. 
! SASJ ; 
! NounSecond ;! golmmačiegahas
! geardásaš:geardás DABALAS ;   
! !tuvsán#geardásaš 
! jahkásaš:jahkás DABALAS ; 





! Imprecise numbers
! -----------------

!LEXICON num-imprecise
