! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software ; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ========================================================================== !
!                     South Sámi morphological analyser                      !
! ========================================================================== !


! ===========================================
! Adjectives: Adjectival inflection sublexica
! ===========================================

! Basic adjectival lexica, infl types
! -----------------------------------
        
        !***** Adjektiver som skiller seg ut fra systemet****! 
            
LEXICON REAKTOE
  :rïektes ATTR_0 ;! rett, riktig
  :reakt   oeCASE ;
  
LEXICON JAEDTUVES
 :uves ATTR_0 ;
 +A+Sg: ÅERUVE_SG ;
 +A+Pl: ÅERUVE_PL ;
 +A:    ÅERUVE_ESS ;
  +A+SgNomCmp:uve%^DISIMP R ;
  +A+SgGenCmp:uve%>%^DISIMPn R ;
  +A+PlGenCmp:uv%>%^DISIMPi R ;
 :iev ODDCOMP ;
 
LEXICON VIELIEH
:lieh ATTR_0 ;
:lieh PRED_0 ;

LEXICON h_H_EVENNOCOMP 
     ATTR_H ;
     PRED_H ;
:ege PRED_0 ; 
!Denne skal kanskje ikke plasseres under spesielle lexicons.

     !**SLUTT**Spesielle Adjektiver som skiller seg ut fra systemet**SLUTT**! 

! VOKALUTVIDELSE på 2-stavelsesadjektiv, Fortsatt KLASSE1

   
   ! Attribute lexica  
! ----------------

LEXICON ATTR_0
+A+Attr: ATTRCONT ;

LEXICON ATTR_0_ODD
+A+Attr:e ATTRCONT ;
 ODDCASEOBL ;
 ODDCOMP ;

LEXICON EENSI
+A+Attr:i ATTRCONT ;
:ig ODDCOMP ;

LEXICON YÖLELH
+A+Attr:h ATTRCONT ;
: ODDCOMP ;

LEXICON ATTR_S
+A+Attr:%>s ATTRCONT ;

!Skal jeg legge til nouncase for alle stamme-attributtene? se nedenunder forslag til leksikon: 
!LEXI ATTR_IES_EVEN
!+A+Attr:ies ATTRCONT ; 
!N_IE ; 
!
!LEXI ATTR_OES_EVEN
!+A+Attr:oes ATTRCONT ;
!N_OE_UML ;
!
!LEXI ATTR_OES_EVEN_UML
!+A+Attr:oes ATTRCONT ;
!N_OE ;
!
!LEXI ATTR_AS_EVEN
!+A+Attr:as ATTRCONT ;
!MAANA ;
!
!LEXI ATTR_ES_EVEN
!+A+Attr:es ATTRCONT ;
!NIEJTE ;

LEXICON ATTR_H
+A+Attr:%>h   ATTRCONT ;

LEXICON ATTR_N
+A+Attr:%>n ATTRCONT ;

!LEX ije_ODDNOCOMP ! BÅETIJE, da dette er handlernomen, nomen agentis
! ATTR_0 ;

LEXICON ATTRCONT
 FINAL1 ;
 R ;
+Der2+Der/voete+Use/Sub:%»voet GIERIEHTSADTJE ;
LAAKAN ;


             ! *Slutt* BARE ATTRIBUTTER  *Slutt* !
   
                     ! Predicative lexica
! ------------------
 
LEXICON PRED_0
 +A+Sg+Nom: FINAL1 ; !    

LEXICON PRED_S
 +A+Sg+Nom:%>s FINAL1 ; ! Delle denne opp i EVEN og ODD -adjektiver 

LEXICON PRED_H
 +A+Sg+Nom:%>h FINAL1 ; ! Del OPP! 


LEXICON PRED_NE_ODD
 +A+Sg+Nom:%>ne FINAL1 ; !
 n: ODDCASEOBL ;
 n: ODDCOMP ;

LEXICON PRED_N
 +A+Sg+Nom:%>n FINAL1 ; !

LEXICON PRED_AN
 +A+Sg+Nom:%>an FINAL1 ; ! DEL OPP!
 

                ! *SLUTT* BARE PRED-former  *SLUTT*   !

 
  



!   *** KLASSE 1, ie,oe,e og a-stammer med 1) attribut og prediaktiv -s, 2) uten attr/pred-s     ***   !
 
!D:1
LEXICON e_E_EVEN!!≈ !!!@CODE@
!! The __@LEXNAME@__ lexicon is used for adjectives on {{–e}} and {{–e}}  In attributes and predicatives.With EVEN-NOCOMP.

 
!! {{{
       :e     ATTR_0 ;   !!= @CODE@
!              eCASE ;   !!= @CODE@
       :e   EVENCOMP ;   !!= @CODE@
!! }}}

!D:1 E_stamme
LEXICON e_E_EVENNOCOMP1!!≈ !!!@CODE@ 
!! The __@LEXNAME@__ lexicon is used for adjectives on {{–e}} and {{–e}} stem. In attributes and predicatives.With EVEN-NOCOMP.

!! {{{
       :e     ATTR_0 ; !!= @CODE@
               eCASE ; !!= @CODE@
!! }}}


!D:1  A_stamme
LEXICON a_A_EVEN1!!≈ !!!@CODE@ 
!! The __@LEXNAME@__ lexicon is used for adjectives on {{–a}} and {{–a}}  In attributes and predicatives.With EVEN-COMP.
!! {{{
       :a       ATTR_0 ; !!= @CODE@
!                 aCASE ; !!= @CODE@
       :a     EVENCOMP ; !!= @CODE@ 
!! }}}

LEXICON as_AS_EVEN1!!≈ !!!@CODE@ ! Disse har opprinnelig vært as_a_EVEN_adjektiv
!! The __@LEXNAME@__ lexicon is used for adjectives on {{–as}} and {{–as}}  In attributes and predicatives.With EVEN-COMP. 
!! {{{   
            :a     ATTR_S ;    !!= @CODE@
                 aCASEOBL ;    !!= @CODE@
            :a   EVENCOMP ;    ! Tidligere klasse-3
  +A+Sg+Nom:as     FINAL1 ;    !!= @CODE@
+A+SgNomCmp:as          R ;    !!= @CODE@
!! }}}

!!D:1
LEXICON a_A_EVEN1_NOCOMP!!≈ !!!@CODE@ 
!!! The __@LEXNAME@__ lexicon is used for adjectives on {{–a}} and {{–a}}  In attributes and predicatives.With EVEN-NOCOMP.
!!! {{{
       :a       ATTR_0 ; !!= @CODE@
                 aCASE ; !!= @CODE@
!!! }}}


!D:1
LEXICON ie_IE_EVEN1!!≈ !!!@CODE@ 
!! The __@LEXNAME@__ lexicon is used for adjectives on {{–ie}} and {{–ie}}  In attributes and predicatives.With EVEN-COMP. 
!! {{{
       :ie      ATTR_0 ;  !!= @CODE@
                ieCASE ; !!= @CODE@
       :ie    EVENCOMP ;  !!= @CODE@
!! }}} 


!D:1
LEXICON ie_IE_EVENNOCOMP1!!≈ !!!@CODE@ 
!! The __@LEXNAME@__ lexicon is used for adjectives on {{–ie}} and {{–ie}}  In attributes and predicatives. With EVEN-COMP.
!! {{{ 
       :ie      ATTR_0 ; !!= @CODE@
!               ieCASE ; !!= @CODE@
!! }}} 

!D:1      
LEXICON es_ES_EVEN!!≈ !!!@CODE@ 
!! The __@LEXNAME@__ lexicon is used for adjectives on {{–es}} and {{–es}}  In attributes and predicatives. With EVEN-COMP.
!! {{{ 
       :e       ATTR_S ; !!= @CODE@
       :e       PRED_S ; !!= @CODE@
   !  :es   ODDCASEOBL ; !!= @CODE@
       :e     EVENCOMP ; !!= @CODE@
!! }}}


!D:1 Denne er en avledning fra klasse 2: e_ES
LEXICON es_ES_EVENNOCOMP1 
!! The __@LEXNAME@__ lexicon is used for adjectives on {{–es}} and {{–es}}  In attributes and predicatives. With EVEN-NOCOMP.

!! {{{ 
       :e  ATTR_S ;      !!= @CODE@
       :e  PRED_S ;      !!= @CODE@
       :es ODDCASEOBL ;  !!= @CODE@
!! }}}

!D:1

LEXICON ies_IES_EVEN1 
!! The __@LEXNAME@__ lexicon is used for adjectives on {{–ies}} and {{–ies}}  In attributes and predicatives. With EVEN-COMP.
!! {{{
           ies_IES_EVENNOCOMP1  ; !!= @CODE@
       :ie EVENCOMP ;            !!= @CODE@ 
!! }}}

!D:1

LEXICON ies_IES_EVENNOCOMP1
!! The __@LEXNAME@__ lexicon is used for adjectives on {{–ies}} and {{–ies}}  In attributes and predicatives. With EVEN-NOCOMP.
!! {{{ 
       :ie   ATTR_S ;  !!= @CODE@
       :ie   PRED_S ;  !!= @CODE@
!        ieCASEOBL ;   !!= @CODE@
!! }}}

LEXICON as_AS_ODD1 ! del opp denne i -oes og -as-stamme
 as_AS_ODD1NOCOMP ;
  :as ODDCOMP ;

LEXICON as_AS_ODD1NOCOMP ! del opp denne i -oes og -as-stamme
 :a    ATTR_S ;
 :a    PRED_S ;
!  aCASE ;

LEXICON oes_OES_ODD1 
oes_OES_ODD1NOCOMP  ;
   :oes ODD_ÅBPOE ;
   
   
LEXICON ah_AH_ODD1   !XXX mangler CASE - hva er riktig - kanskje EVEN?  NB!
:a    ATTR_H  ;
:a    PRED_H  ;
:ahk  ODDCOMP ; ! Wait: gaastoe - oe-NOUN  ah 
 !     aCASE ;

LEXICON AH_&_OEH_ODD1
:oeh ATTR_0 ;
:ah ATTR_0 ;
:oehk ODD_ÅBPOE ;
:ahk ODD_ABPA ;
:oeh PRED_0 ;
:ah PRED_0 ;

LEXICON oeh_OEH_ODD ! tidl -oe h_H_ODD
 :oehk ODD_ÅBPOE ; 
 oeh_OEH_ODDNOCOMP1 ;

LEXICON oeh_OEH_ODDNOCOMP1
 :oe ATTR_H ;
 :oe PRED_H ;
!+Use/Sub: oeCASE_OBL ;


 
! VOKALUTVIDELSE på 2-stavelsesadjektiv
LEXICON BAERIES
!! !!!BÅERIES LEXICON @LEXNAME@
!! UNEVEN adjective, attr = pred. Comporation uneven syllable.
!! Presentlly only used for "båeries" adjective.
!! {{{ 
 :båerie ATTR_S ; !!= @CODE@
 :båerie PRED_S ; !!= @CODE@
! :båaras aCASEOBL ; !!= @CODE@
 ! båarasam (LK),? egentl. 'voeres’
 :båaras ODDCOMP ; !!= @CODE@
 !! }}}
LEXICON ÅEHPIES 
:åehpies ATTR_0 ;
:åehpie PRED_0 ; 
!:åehp ieCASE ;
:åahpetje PRED_0 ;
:åahpatj ODDCOMP ;! mere kjent om personer, mindre vanlig
:åahpag ODDCOMP ;! mere kjent om ting, saker, tema etc.
:åahpahk ODDCOMP ; !mere kjent om ting, saker, tema etc.
! VOKALUTVIDELSE i 2.stavelse på 2-stavelsesadjektiv

LEXICON AAREH
    ATTR_H ;
    PRED_H ;
 :e    ATTR_H ;
 :e    PRED_H ;
!     eCASEOBL ; ! check! this is not right
 :e    EVENCOMP ;
 :eg  ODDCOMP ;  
 :ehk ODDCOMP ;    

LEXICON GIERIES !    åehpies- skal også hit- UMLAUT!
!! !!!GEAREHKE LEXICON @LEXNAME@
!! Umlaut from attr--> pred. Comporation uneven syllable.
!! Presentlly only used for "gieries-gearehke" adjective.
!! This lexicon covers the ies - ehke + umlaut change.
 !! {{{
 :gierie ATTR_S ;      !!= @CODE@
 :gearehke PRED_0 ;    !!= @CODE@
 :gearahk ODDCOMP ;    !!= @CODE@
 :gearahtj ODDCOMP ;    !!= @CODE@
 :gearahg ODDCOMP ;    !!= @CODE@

! :gearahk ODDCASEOBL ; !!= @CODE@
 !! }}} 

!   ****slutt Vokalutvidelse i 2.stavelse av 2-stems-adjectives *****  


!   !***UMLAUT FOR e(3)-stammen, KLASSE-1- ADJEKTIVENE
     
              !    ***UMLAUT FOR e(3)-stammen

!UMLAUT 3 = e(3)-stamme

LEXICON BUERIE_UMLAUT_IE_STAMME
!! !!!BUERIE LEXICON @LEXNAME@
!! EVEN adjective with EVEN-UMLAUT Comporation for -ie-stems.
!! {{{
 :buer ie_IE_EVENNOCOMP1    ; !!= @CODE@ 
 :buerie EVENCOMPONLY ;       !!= @CODE@ 
 :bööre MES ;                 !!= @CODE@ 
 :buerie DIMCOMP ;            !!= @CODE@ 
!! }}}
!Suppletivisme 
LEXICON NAAKE
 :nåak e_E_EVEN  ;  
 :vïerre EVENCOMPONLY ;
 :vearra EVENCOMPONLY ;
 :vïerre MES ;
 :vearra MES ;
 :veerre MES ;
 :vïerre DIMCOMP ;  
 :vearra DIMCOMP ; 


LEXICON LEEVLES
 :leevle ATTR_S  ;
+Use/NG:leevle PRED_S  ;
+Use/NG:leevle EVENCOMP  ;
+Use/NG:lyövl es_ES_EVEN  ;
+Use/NG:lïevl es_ES_EVEN  ;
+Use/NG:löövl es_ES_EVEN  ;
+Use/NG:lïevle EVENCOMPONLY ;
+Use/NG:löövle MES ;
+Use/NG:lïevle DIMCOMP ;  
! UMLAUT 3 = e(3)-stamme
LEXICON SOOKES
 :söök es_ES_EVENNOCOMP1  ;
 :suekie EVENCOMPONLY ;
 :sööke MES ;
 :suekie DIMCOMP ;  
! UMLAUT 3 = e(3)-stamme 
LEXICON SOOJMES
 :sööjme ATTR_S  ;
 :sööjme PRED_S  ;
 :sööjme EVENCOMP  ;
+Use/NG:seejm es_ES_EVEN  ;
+Use/Sub:suejme EVENCOMPONLY ; !
! UMLAUT 3 = e(3)-stamme, som får EVEN-Komparasjon
LEXICON VEELKES
 :veelke ATTR_S  ;
+Use/NG:veelke PRED_S  ;
+Use/NG:vielkie EVENCOMPONLY ;
 :vielkie DIMCOMP ;
+Use/NG:veelke MES ;
!ODDCASE ;
! UMLAUT 3 = e(3)-stamme 
LEXICON ROOPSES
+Use/NG:reeps es_ES_EVENNOCOMP1  ;
 :rööps es_ES_EVENNOCOMP1  ;
 :ruepsie EVENCOMPONLY ;
 :ruepsie DIMCOMP ;
 :rööpse MES ;
+Use/NG:rååpsehk HKE_ODD_NGCOMP ;
! UMLAUT 3 = e(3)-stamme
LEXICON SNEEHPES ! burde GEEHPES -> SNEEHPES ? ikke dokumentert geehpebe. s_S_ODD  bort?
 :sneehpe ATTR_S  ; !  
+Use/NG:sneehpe PRED_S  ; !  
+Use/NG:sneehp es_ES_EVENNOCOMP1 ; ! pga sneehpebe
+Use/NG:sneehpe EVENCOMP  ; ! pga sneehpebe
+Use/NG+Use/Sub:snaehpie EVENCOMPONLY ;
 +Use/Sub:snaehpie DIMCOMP ;
 +Use/Sub:snååhpehk HKE_ODD_COMP ;
:sneehpes ODDCOMP ;
! UMLAUT 3 = e(3)-stamme
LEXICON GEEHPES
! :geehpes ODDCASEOBL ;
 :geehpe ATTR_S ;
+Use/Sub:geehpe s_S_ODD  ; 
+Use/NG:giehpie EVENCOMPONLY ; ! -ie- i paradigmet for giehpies
 :giehpie DIMCOMP ;  ! -ie- i paradigmet for  giehpies
+Use/NG:geehpe MES ;
 +Use/Sub:gyöhpehk HKE_ODD_COMP ;
+Use/NG:geehpe PRED_S ;
! UMLAUT 3 = e(3)-stamme
LEXICON GEERVE
 :geerv e_E_EVEN  ;
+Use/NG:giervie EVENCOMPONLY ;
+Use/NG:giervie MES ;
 :giervie DIMCOMP ;  

               !    **SLUTT**UMLAUT FOR e(3)-stammen     **SLUTT**! 
               
               
LEXICON s_S_ODD ! HUFF, del opp adjektivene i dette leksikonet i jamn, odde- og loan-adjektiver!!
     ATTR_S ;
     PRED_S ;
!  +Use/Sub:s  ODDCASEOBL ;
  :s ODDCOMP ;
 
LEXICON s_S_ODDEVEN ! Dette er klasse 3-adjektiv, del opp adjektivene i dette leksikonet i jamn, odde- og loan-adjektiver!!
     ATTR_S ;
     PRED_S ;
!  +Use/Sub:s  ODDCASEOBL ;  ! LA la til -s
  :s ODDCOMP ;
  EVENCOMP ;

LEXICON oes_OES_ODD1EVEN !Dette er klasse 3-adjektiv,  del opp adjektivene i dette leksikonet i jamn, odde- og loan-adjektiver!!
     ATTR_S ;
     PRED_S ;
!  +Use/Sub:s  ODDCASEOBL ;  ! LA la til -s
  :s ODD_ÅBPOE ;
  EVENCOMP ;
 
LEXICON es_ES_ODD1 ! Kommer fra e(1)-stamme og er derivasjon av e_ES_ODDEVEN2
 :es    ATTR_S ;
 +A+SgNomCmp:es R ;
 :e    PRED_S ;
 :es    ODDCASEOBL ;
 :es    ODDCOMP ;


LEXICON s_S_ODDNOCOMP ! HUFF, del opp adjektivene i dette leksikonet i jamn, odde- og loan-adjektiver!!
     ATTR_S ;
     PRED_S ;
!+Use/Sub:s ODDCASEOBL ; !LA la til -s


LEXICON oes_OES_ODD1NOCOMP 
   :oe  ATTR_S ;
!   :oes ODDCASEOBL ; ! låapsoesem XXX check
+A+Sg+Nom:oes         FINAL1 ;
+A+SgNomCmp:oes            R ;

LEXICON oes_OES_ODD1ahk 
   :oe  ATTR_S ;
   :oe  PRED_S ;
   :ahk ODDCOMP ;
    
LEXICON oes_OES_ODD1as 
   :oe  ATTR_S ;
   :oe  PRED_S ;
   :as ODDCOMP ;
              

!  4-stavelser 
LEXICON ihks_IHKS_igs_IGS_EVENNOCOMP 
!! !!!LEXICON@LEXNAME@
!! Adjective with no comp. 

!!€gt-norm: adjectives 
!!¥ isvelihks+A+Attr:   [isvelihks, isveligs]
!!¥ isvelihks+A+Sg+Nom: [isvelihks, isveligs,isvelihke, isveligke, isveligie]
!! {{{
        :ihk  ATTR_S ;    !!= @CODE@
        :ig   ATTR_S ;    !!= @CODE@
        :ihk  PRED_S ;    !!= @CODE@
        :ihk  PRED_S ;    !!= @CODE@
        :ihke PRED_0 ;    !!= @CODE@
        :igke PRED_0 ;    !!= @CODE@
        :igie PRED_0 ;    !!= @CODE@
!              eCASEOBL ;  !!= @CODE@    
!              ieCASEOBL ; !!= @CODE@  
!! }}}


LEXICON n_N_ODDNOCOMP1  ! attr=n, pred= n
: ATTR_N ;
: PRED_N ;
 !  sliegrie;sleegre, slehtjie/-m,-n-an,-esne,-este,
 


!*SLUTT* KLASSE 1, ie,oe,e og a-stammer med 1) attribut og prediaktiv -s, 2) uten attr/pred-s  *SLUTT*!



                !***  KLASSE 2, e(1) attribut og prediaktiv -es   ****!
                                

LEXICON e_ES_EVENODDNOCOMP2 
!! !!!LEXICON@LEXNAME@
!! EVEN/ODD adjective with Comporation.

!!€gt-norm: adjectives 
!!¥ guektiengïele+A+Attr:   [guektiengïele, guektiengïeles]
!!¥ guektiengïele+A+Sg+Nom: [guektiengïeles] 

!! {{{
 :e      ATTR_0  ;
 :e      PRED_S  ;
 :es     ATTR_0  ;  ! Avlednings-s 
!:es   ODDCOMP ;   ! Komparasjon til avlednings-s
        eCASEOBL ;
        
 :es  ODDCASEOBL ;  
!! }}}

LEXICON e_ES_EVENNOCOMP2 
!! !!!LEXICON@LEXNAME@
!! This is for the adjective "jaame" 

!!€gt-norm: adjectives 
!!¥ jaame+A+Attr:   [jaame]
!!¥ jaame+A+Sg+Nom: [jaames] 

!! {{{ 
         :e    ATTR_0  ;    !!= @CODE@ 
         :e    PRED_S  ;    !!= @CODE@ 
              eCASEOBL ;    !!= @CODE@ 
!! }}}

LEXICON e_ES_ODDEVEN2 
!! !!!LEXICON@LEXNAME@
!! This one gives EVEN and ODD  Comporation.

!!€gt-norm: adjectives 
!!¥ galme+A+Attr:   [galme, galmes]
!!¥ galme+A+Sg+Nom: [galmes]
!!¥ galme+A+Comp+Sg+Nom: [galmebe, galmesåbpoe]
!!¥ galme+A+Superl+Sg+Nom: [galmemes,galmesommes]


!! {{{ 
           :e     ATTR_0 ;       !!= @CODE@           
                eCASEOBL ;       !!= @CODE@
           :es  ODDCASEOBL ;
           :e   EVENCOMP ;       !!= @CODE@
              es_ES_ODD1 ;       !!= @CODE@
!! }}}

   !***UMLAUT FOR e(2)-stammen, Klasse-2 

! UMLAUT 2 = e(1)-stamme
LEXICON SNAARE2 
 :snaar e_ES_EVENNOCOMP2  ;
 :snaerie EVENCOMPONLY ;
 :sneere MES ;
 :snaerie DIMCOMP ;  
! UMLAUT 2 = e(1)-stamme, men med en underliggende ie-stamme som bidrar til umlaut
LEXICON ORRE2
       :orr e_E_EVEN  ;
       :urr e_E_EVEN  ;
       :urre EVENCOMPONLY ;
       :urre DIMCOMP ;
       :urre MES ;
!+Use/Sub:orr e_ES_ODDEVEN ; !comment from Aajege, MLK: bør subbes. orresistie? - THIS IS HOPELESS,skulle subbe denne, men jeg forstår ikke hvorofr det ikke er mulig? 

! UMLAUT 2 = e(1)-stamme, men med en underliggende ie-stamme som bidrar til umlaut
LEXICON NOERE_STOERE2
 :oer e_E_EVENNOCOMP1   ;! stoere-noere-ebe/emes
 :uerie EVENCOMPONLY ;
 :ööre MES ;
 :oer e_E_EVEN  ; ! oere-oerebe-oeremes
! UMLAUT 2 = e(1)-stamme, men med en underliggende ie-stamme som bidrar til umlaut
LEXICON GISSE2
 :gïss e_E_EVENNOCOMP1   ;
 :gissie EVENCOMPONLY ;
 :gissie MES ;
+Use/Sub:gïssie EVENCOMPONLY ;
+Use/Sub:gïssie MES ;
 :gissie DIMCOMP ; 
+Use/NG:gïssege PRED_0 ;  ! eller er dette et annet lemma? jfr. -hke og gabtege ,MLK: Iflg. bergsland 1994, s 116 pkt. 5 er dette en 'særtilfelle (Predikatformen for det meste foreldet)'
! UMLAUT 2 = e(1)-stamme, men med en underliggende ie-stamme som bidrar til umlaut
LEXICON GAMTE2
:te ATTR_0  ;
:tege PRED_0  ;
!+Use/NG:t eCASE  ;
:%>%^DISIMPtie EVENCOMP ;
:te EVENCOMP ; 
! UMLAUT 2 = e(2)-stamme, men med en underliggende ie-stamme som bidrar til umlaut  
LEXICON GIEVTE2
 :gïevt e_E_EVENNOCOMP1   ;
 :geavte EVENCOMPONLY ;
 :gievte EVENCOMPONLY ;
 :gïevte MES ;
 :gievte DIMCOMP ;  
 :geavte DIMCOMP ;  
! UMLAUT 2 = e(1)-stamme, men med en underliggende ie-stamme som bidrar til umlaut
LEXICON TJOEVKES2
 :tjoevke s_S_ODD  ; ! ODD ikke i LMM, ikke i corp. 
 :tjuevkie EVENCOMPONLY ; 
 :tjuevkie DIMCOMP ; 
 :tjoevke MES ;
! UMLAUT 2 = e(1)-stamme, men med en underliggende ie eller a-stamme som bidrar til umlaut                   
LEXICON KRUANA2  
 :kruan a_A_EVEN1_NOCOMP ;
+Use/NG:kruene EVENCOMPONLY ;
+Use/NG+Use/Sub:kroene EVENSUPONLY ;  
+Use/Sub:kruane EVENCOMPONLY ;
:kråane EVENCOMP ; 

                     ! *SLUTT* KLASSE 2  *SLUTT*   !

             !!! ** KLASSE 3, -s i attributt, vokal i predikativ** !!!          
             
LEXICON es_E_EVEN3    
 :e     ATTR_S ;
         eCASE ;
 :e     PRED_S ; !avl. eller spr.utv.  
 :e   EVENCOMP ;


LEXICON as_oes_A_OE_EVEN3 
:a    ATTR_S ;
:oe   ATTR_S ;
       aCASE ;
       oeCASE ;
:oe   PRED_0 ;
:a    PRED_0 ;
:oe   EVENCOMP_oe ;
:a    EVENCOMP ; 

LEXICON oeh_ah_OE_A_EVEN3  
:oe   ATTR_H ;
:a    ATTR_H ;
      oeCASE ;
      aCASE ;
:oe   PRED_0 ;
:a    PRED_0 ;
:oe   EVENCOMP_oe ;
:e    EVENCOMP ; 
        
LEXICON ies_IE_EVEN3   
 :ie    ATTR_S ;
        ieCASE ;
 :ie    PRED_0 ;
 :ie  EVENCOMP ;
LEXICON ies_IE_EVEN3NOCOMP  
 :ie    ATTR_S ;
        ieCASE ;

!AVLEDNINGSLEKSIKONET -s i prediaktiv
! Avlednigsleksikon til ies-stammevokalgruppen - s  ---->   går til klasse 1 som har ies-ies i attr/pred.
LEXICON ies_IES_IE_EVEN3  ! Attr=s, Pred. = s jih Ø, Comp jih caese= even (ebe/emes/esne) 
 :ie    ATTR_S ;! 
 :ie    PRED_S ;! Avlednings-s eller språkutvikling
 :ie    PRED_0 ;
        ieCASE ;
 :ie  EVENCOMP ;
! Avlednigsleksikon til ies-stammevokalgruppen - s  ---->   går til klasse 1 som har ies-ies i attr/pred.
LEXICON ies_IES_IE_EVEN3NOCOMP  ! Attr=s, Pred. = s jih Ø, Comp jih caese= even (ebe/emes/esne) 
 :ie   ATTR_S ;! 
!        ieCASE ;
+A+Sg+Nom:ies         FINAL1 ;
+A+SgNomCmp:ies            R ;

     !!! ** KLASSE 4, -s og -h  i attributt, UMLAUT til -an predikativ** !!!

    !**** UMLAUT 4 = ie-stamme er i 4-gruppen, hvor umlauten bare er fra attr -pred. ****!

LEXICON SAETNIES4
!! !!!UMLAUT LEXICON @LEXNAME@
!! 20 ie-stem adjectives has umlaut from attr--> pred, and have no comparation.
!! Theese adjectives is in the 4. group of the southsámi adjectives!! This group which covers the ies - an/ as-an and oes-an + umlaut change, is a small undergruppe of the 4.group, where the second oeh-an umlautadjectivesgroup are.

!! {{{
 :ies ATTR_0 ;      !!= @CODE@
 :%^1UMLan PRED_0 ; !!= @CODE@
!+Use/NG:a PRED_0 ;
 : ieCASE ;		    !!= @CODE@
!! }}}
LEXICON as_AN_ODDNOCOMP4
!! !!!UMLAUT LEXICON @LEXNAME@
!! Theese 6 adjectives is in the 4. group of the southsámi adjectives, The group which contains all 
!! umlaut-adjectives. Theese adjectives whivh have -as  as attributeform and an as predicativeform, is south-southsámi adjectives, and they dont have any comparation.
!! This group which covers the ies - an/ as-an and oes-an + umlaut change, is a small undergruppe of the 4.group 
!! {{{
:a  ATTR_S ; !!= @CODE@
:a  PRED_AN ; !!= @CODE@
!! }}}

LEXICON oes_AN_ODDNOCOMP4
!! !!!UMLAUT LEXICON @LEXNAME@
!! Theese 5 adjectives is in the 4. group of the southsámi adjectives, The group which contains all 
!! umlaut-adjectives. Theese adjectives which have -oes  as attributeform and -an as predicativeform, is north-southsámi adjectives, and they dont have any comparation.
!! This group which covers the ies -> an/ as-> an and oes-> an + umlaut change, is a small undergruppe of the 4.group 
!! {{{ 
    :oe        ATTR_S  ; !!= @CODE@
    :%^3UML    PRED_AN ; !!= @CODE@
!! }}}
 
LEXICON oeh_OEN_ODD
!! {{{ 
 :oe ATTR_H ;      !!= @CODE@
 :oe PRED_N ;      !!= @CODE@
 :oehk  ODD_ÅBPOE ;!!= @CODE@   
!! }}}

LEXICON oeh_AN_ODD4 !
!! {{{  
:oe         ATTR_H ;   !!= @CODE@
:%^3UMLa    PRED_N ;   !!= @CODE@
:%^3UMLahk  ODD_ÅBPOE ;!!= @CODE@
!! }}}

LEXICON ah_AN_ODD 
:a    ATTR_H ;
:a    PRED_N ;
:ahk  ODDCOMP ;

 
LEXICON oeh_ah_AN_ODD_NOCOMP4 
 :%^3UMLa    ATTR_H ;
 :oe   ATTR_H ;      
 :%^3UML PRED_AN ; 
!+Use/NG:a PRED_0 ;
! : oeCASE ;		    
! +Use/Sub: aCASEOBL ;  
     
LEXICON oeh_OEH_AN_ODD  ! Attr=oeh, Pred=oeh og an 
      oeh_OEH_AN_ODDNOCOMP  ;
:ahk  ODDCOMP ;
:oehk ODD_ÅBPOE ; 
!Mange stammer
LEXICON oeh_OEH_AN_ODDNOCOMP  ! Attr=oeh, Pred=oeh og an 
:oe ATTR_H ;
:oe PRED_H ;
    PRED_AN ; 
!+Use/Sub: oeCASE_OBL ;


LEXICON ah_oeh_OEN_AN_ODE_ODDNOCOMP !!≈ !!!@CODE@
!! The __@LEXNAME@__ lexicon is used for adjectives on {{–ah}} and {{–an}} with an
!! exceptional predicative on {{–ode/–ede}}. This lexicon is only used for
!! three adjectives: ''båadtsoeh, båajhtoeh, skåalvoeh''. ''båadtsoeh'' is also
!! used with the stem ''båadtj–'', although that stem is probably southern.
!! It might be that we overgenerate forms as ''båadtjoeh'' and ''båadtjode''.
!! This must be checked further, possibly with informants.

!!€gt-norm: adjectives ony ah/oeh an/oen/ode - båadtsah
!!¥ båadtsoeh+A+Attr:   [båadtsah, båadtsoeh, båadtsas, båadtsoes, båadtjah, båadtjoeh, båadtjas, båadtjoes]
!!¥ båadtsoeh+A+Sg+Nom: [båadtsan, båadtsoen, båadtsode, båadtsede, båadtjan, båadtjoen, båadtjode, båadtjede]

!!€gt-desc: adjectives on ah/oeh an/oen/ode - båadtsah
!!¥ båadtsoeh+A+Attr:   [båadtsah, båadtsoeh, båadtsas, båadtsoes, båadtjah, båadtjoeh, båadtjas, båadtjoes]
!!¥ båadtsoeh+A+Sg+Nom: [båadtsan, båadtsoen, båadtsode, båadtsede, båadtjan, båadtjoen, båadtjode, båadtjede]
         :a   ATTR_H     ;
         :oe  ATTR_H     ;
         :a   ATTR_S     ;
         :oe  ATTR_S     ;
         :a   PRED_N     ;
         :oe  PRED_N     ;
         :ode PRED_0     ;
         :ede PRED_0     ;
!           aCASEOBL     ;
!           oeCASE_OBL   ;

 !**SLUTT ** UMLAUT 4 = ie-stamme er i 4-gruppen, hvor umlauten bare er fra attr -pred. **SLUTT **!

                                            !****!

                          !  **** ODD-stavelses-adjektiv   ****   !
                            ! ODD-stems (odd stem declension)
                              ! ===============================



LEXICON Ce_CE_ODD  !bårreske
 :%^DISIMPe     ATTR_0 ;
 :%^DISIMPe     PRED_0 ;
          ! ODDCASEOBL ;
    ODDCOMP ;

LEXICON Ce_CE_ODDNOCOMP 
 :%^DISIMPe     ATTR_0 ;
 :%^DISIMPe     PRED_0 ;
          ! ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPe            R ;

    
LEXICON oeCe_OECE_ODD  !bårreske
 :%^DISIMPe     ATTR_0 ;
 :%^DISIMPe     PRED_0 ;
          ! ODDCASEOBL ;
    ODD_ÅBPOE ;

LEXICON hth_HTH_ODD   ! NB!
 :%^DISIMP    ATTR_H ;
 :%^DISIMP    PRED_H ;
! ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPh            R ;
    ODDCOMP ;
LEXICON oehth_OEHTH_ODD   !NB!
 :%^DISIMP    ATTR_H ;
 :%^DISIMP    PRED_H ;
! ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPh            R ;
    ODD_ÅBPOE ;

LEXICON hth_HTH_ODDNOCOMP   ! NB!
 :%^DISIMP    ATTR_H ;
 :%^DISIMP    PRED_H ;
! ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPh            R ;

LEXICON Ces_CES_ODD
 ODDCOMP ;
 Ces_CES_ODDNOCOMP ;

LEXICON Ces_CES_ODDNOCOMP  
:%^DISIMPe     ATTR_S ;
:%^DISIMPe     PRED_S ;
! ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPe            R ;
   
LEXICON les_LES_ODD  
:%^DISIMPe     ATTR_S ;
:%^DISIMPe     PRED_S ;
! ODDCASEOBL ;
   ODDCOMP ;

LEXICON oeles_OELES_ODD  
:%^DISIMPe     ATTR_S ;
:%^DISIMPe     PRED_S ;
! ODDCASEOBL ;
   ODD_ÅBPOE ;

LEXICON Cs_CS_CE_ODD 
 Cs_CS_CE_ODDNOCOMP  ;
 ODDCOMP ;

LEXICON oeCs_OECS_OECE_ODD  
 Cs_CS_CE_ODDNOCOMP  ;
 ODD_ÅBPOE ;

LEXICON Cs_CS_CE_ODDNOCOMP 
+Use/Sub:%^DISIMPe   ATTR_S ; 
 :%^DISIMP   ATTR_S ;
+Use/NG:%^DISIMP   PRED_S ;
 :%^DISIMPe     PRED_0 ;
 ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPe            R ;
     

LEXICON Cs_CS_ODD 
  :%^DISIMP   ATTR_S ;
  :%^DISIMP   PRED_S ;
!  ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPe            R ;
     ODDCOMP ;

LEXICON  oeCs_OECS_ODD  
  :%^DISIMP   ATTR_S ;
  :%^DISIMP   PRED_S ;
!  ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPe            R ;
     ODD_ÅBPOE ;
 
LEXICON Cs_CS_ODDNOCOMP 
  :%^DISIMP   ATTR_S ;
  :%^DISIMP   PRED_S ;
!  ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPe            R ;

LEXICON hts_HTS_ODD 
hts_HTS_ODDNOCOMP  ;
  :s  ODDCOMP ;
LEXICON oehts_OEHTS_ODD 
hts_HTS_ODDNOCOMP  ;
  :s  ODD_ÅBPOE ;

LEXICON hts_HTS_ODDNOCOMP 
! This lexicon is in reality hts_HTS_HTE
! We have info on pred -e for two adjs:
! heijjehts (corpus) and bieljehts (marit) - LA: fjerner -e, jeg kan ikke se at vi har slik dokumentasjon
  :%^DISIMP   ATTR_S ;
  :%^DISIMP   PRED_S ;
! :%^DISIMPe     PRED_0 ; 
!  ODDCASEOBL ; !LA tok vekk -s
+A+SgNomCmp:%^DISIMPs            R ;

LEXICON hts_HTS_ODDhtCOMP 
 hts_HTS_ODDNOCOMP  ;
 ODDCOMP ; ! joekehtåbpoe
 
LEXICON oehts_OEHTS_ODDhtCOMP 
 hts_HTS_ODDNOCOMP  ;
 ODD_ÅBPOE ; ! joekehtåbpoe

LEXICON MAST 
!! !!!LEXICON@LEXNAME@
!! The __@LEXNAME@__ lexicon is used for adjectives on {{–masten}} and {{masth}} with an
!! used with the stem ''masten''
!! {{{
            ATTR_S ;   !!= @CODE@
  +Use/NG:e ATTR_S ;   !!= @CODE@
  +Use/NG:  ATTR_H ;   !!= @CODE@
  +Use/NG:e ATTR_N ;   !!= @CODE@
         :e PRED_N ;   !!= @CODE@
             eCASE ;   !!= @CODE@  
!! }}} 



LEXICON s_E_ODD ! SE på denne - unødvendig..?
 :%^DISIMP ATTR_S ;
! ODDCASE ; 
  ODDCOMP ;



!  **** LOAN     *****  !

LEXICON IJVEadj
!! !!!IJVE_LOAN_ADJ LEXICON @LEXNAME@
!! EVEN adjective EVEN Comporation.
!! Used for all loan-adjectives "ijve".
!! {{{ 
:ijv e_E_EVEN  ;        !!= @CODE@ 
:ïjv e_E_EVEN  ;        !!= @CODE@ 
+Use/Sub:iv e_E_EVEN  ; !!= @CODE@ 
!! }}}

LEXICON IJAALE_A_LOAN
+Use/Sub:aal AALEFORMS ;
+Use/Sub:al AALEFORMS ;
+Use/Sub:jal AALEFORMS ;
        :jaal AALEFORMS ;
+A+SgNomCmp:al    R ;

LEXICON AALE
                :al AALEFORMS ; ! vitaale = norm
 +Use/Sub:l  AALEFORMS ; ! vitale = descriptive
 +N+SgNomCmp:l    R ;

LEXICON AALEFORMS
! merging spinaale e_E_EVEN  and spinaales es_ES_EVEN 
! they were set up as doublets in the lexicon and had 
! identical comparatives and superlatives.
! The new solution analyses the same wordforms as before,
! but operates with a single lemma spinaale etc.
! Todo: look into eCASE / ODDCASE -variation (double case paradigm?)
  :e   ATTR_0 ; ! vitaale
  :e   ATTR_S ;
!       eCASE ;
  :e   PRED_S ; 
  :e   EVENCOMP ; 


LEXICON JELLE
!! !!!LEXICON@LEXNAME@
!! The __@LEXNAME@__ lexicon is used for loanadjectives on {{jelle}} and {{–jelle}} with an
!! used with the stem ''jelle'' This one should be 'jeelle'? SGM?

!!€gt-norm: adjectives 
!!¥ potensijelle+A+Attr:   [potensijelle]
!!¥ potensijelles+A+Sg+Nom: [potensijelles]
!! {{{
 +Use/Sub:^ell e_ES_LOAN ; !!= @CODE@
         :jell e_ES_LOAN ; !!= @CODE@
!! }}} 
LEXICON UELLE
!! !!!LEXICON@LEXNAME@
!! {{{
        :^ell e_ES_LOAN ; !!= @CODE@  
+Use/Sub:vell e_ES_LOAN ; !!= @CODE@
!! }}} 

LEXICON e_ES_LOAN  !LOAN  - fjerna comp LA - vi kan vurdere om noen av disse bør ha komp.  
 :    e_ES_EVENNOCOMP2  ;
+Use/Sub:e    PRED_0 ; ! forslag. OK! hvorfor det? interesant forslag, men må drøftes.For; hvis vi skal dette, betyr det at vi "godtar" at adj-systemet går fra 4-system til 2-system. Sørsamisk adj-system er i full fart fra 5 og 4-system til 2-system, men dette er ikke divvuns bord til å bestemme. 

LEXICON ÆRE  !LOAN
:ær e_E_EVEN  ;
+Use/Sub:r e_E_EVEN  ;

LEXICON ENTE  !LOAN  
:ent e_E_EVENNOCOMP1   ; ! eller skal det være -e-es ? 
+Use/Sub:eent e_E_EVENNOCOMP1   ;

LEXICON IJLE_LOAN
  :ijl e_ES_ODDEVEN2  ; !XXX check
 +Use/NG:ïjl e_ES_ODDEVEN2  ;
 +Use/Sub:il e_ES_ODDEVEN2 ;
 +Use/Sub:ïl e_ES_ODDEVEN2 ;


LEXICON LES ! Should contain only loanwords (?)
:%>%^DISIMPes ATTR_0 ; 
+A+Sg+Nom:%>%^DISIMPes          FINAL1 ;
+A+Sg+Acc:asse%>m       FINAL1 ;
+A+Sg+Gen:asse%>n       FINAL1 ;
+A+Sg+Ill:asse%>se      FINAL1 ;
+A+Sg+Ine:asse%>sne     FINAL1 ;
+A+Sg+Ela:asse%>ste     FINAL1 ;
+A+Sg+Com:ass%>ine      FINAL1 ;
+A+Pl+Nom:%>%^DISIMP%>ass%>h        FINAL1 ;
+A+Pl+Acc:%>ass%>ide      FINAL1 ;
+A+Pl+Gen:%>%^DISIMP%>ass%> IGENSUFF ;
+A+Pl+Ill:%>ass%>ide      FINAL1 ;
+A+Pl+Ine:%>ass%>ine      FINAL1 ;
+A+Pl+Ela:%>ass%>ijste    FINAL1 ;
+A+Pl+Com:%>ass%>igujmie  FINAL1 ;
+A:ass            EVEN_ESS ;
+A+SgNomCmp:%>%^DISIMPes    R ;
+A+SgGenCmp:asse%>n R ;
+A+PlGenCmp:ass%>i  R ;


LEXICON AABELE
        :aabel Ce_CES_ODD  ; ! akseptaabele
+Use/Sub:abel Ce_CES_ODD  ; ! akseptabele


LEXICON IJBELE
        :ijbel Ce_CES_ODD  ; ! fleksijbele
+Use/Sub:ibel Ce_CES_ODD  ; ! fleksibele



! ******   LOAN END ********

!4-stavelses
LEXICON LES_LASSE_ie_Comp
:al ODDCOMP ;
LES_LASSE_ie ;

LEXICON LES_LASSE_ie
LES_LASSE_NOM_ie ;
LES_LASSE_OBL_ie ;

LEXICON LES_LASSE_NOM_ie
:eles ATTR_0 ; ! should redirect to case paradigm
+A+Sg+Nom:eles          FINAL1 ;
+A+Pl+Nom:elass%>h        FINAL1 ;
+A+Pl+Gen:elass%> IGENSUFF ;
+A+SgNomCmp:eles    R ;


LEXICON LES_LASSE_OBL_ie
+A+Sg+Nom:alasse       FINAL1 ;
+A+Sg+Acc:alasse%>m       FINAL1 ;
+A+Sg+Gen:alasse%>n       FINAL1 ;
+A+Sg+Ill:alasse%>se      FINAL1 ;
+A+Sg+Ine:alasse%>sne     FINAL1 ;
+A+Sg+Ela:alasse%>ste     FINAL1 ;
+A+Sg+Com:alass%>ine      FINAL1 ;
+A+Pl+Acc:al%>ass%>ide      FINAL1 ;
+A+Pl+Ill:al%>ass%>ide      FINAL1 ;
+A+Pl+Ine:al%>ass%>ine      FINAL1 ;
+A+Pl+Ela:al%>ass%>ijste    FINAL1 ;
+A+Pl+Com:al%>ass%>igujmie  FINAL1 ;
+A:alass            EVEN_ESS ;
+A+SgGenCmp:alasse%>n R ;
+A+PlGenCmp:alass%>i  R ;

LEXICON Ce_CES_ODD 
:e     ATTR_0 ;
:e     PRED_S ;
:e     PRED_0 ; ! jfr. leksikonnamnet, TT
       ODDCASEOBL ;
       +A+SgNomCmp:%^DISIMPe R ; ! dïhte komfortaabelegåetie ??
      ! ODDCOMP ;

!LEX JEASIEGOELKIJE - går ut pg.a dette ernomen agentis
! :%^DISIMPe     ATTR_0 ;
! :%^DISIMPe     PRED_0 ;
!        ije_ODDCASEOBL ; 
!+A+SgNomCmp:%^DISIMPe            R ;
       

LEXICON jes_js_JES_JS_ODD !to adj: bualijes, fååmijes
+Use/NG:%^DISIMP  ATTR_S ;
  :%^DISIMPe  ATTR_S ;
+Use/NG:%^DISIMP  PRED_S ;
 :%^DISIMPe  PRED_S ;
!   ije_ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPe            R ;
     ODDCOMP ;


LEXICON ijes_ijs_IJE_ODD ! GUALIJES
ijes_ijs_IJE_ODDNOCOMP ; ! attr -es, -s, pred -e
 ODDCOMP ;

LEXICON ijes_ijs_IJE_ODDNOCOMP ! DÅAJMIJES
:e   ATTR_S ;     ! LMM: dåajmijes
+Use/NG:  ATTR_S ;     ! LMM: dåajmijs, KB: gualijs 
     ije_ODDCASE ;
+A+SgNomCmp:%^DISIMPe            R ;

! type 3



LEXICON Cs_CE_ODD 
  :%^DISIMP    ATTR_S ;
  :%^DISIMPe   PRED_0 ;
! ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPe            R ;
       ODDCOMP ;

LEXICON oeCs_OECE_ODD 
  :%^DISIMP    ATTR_S ;
  :%^DISIMPe   PRED_0 ;
! ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPe            R ;
       ODDCOMP ;

LEXICON Cs_CE_ODDNOCOMP 
:%^DISIMP     ATTR_S ;
:%^DISIMPe   PRED_0 ;
!       ODDCASEOBL ; !LA
+A+SgNomCmp:%^DISIMPe            R ; !LA

LEXICON Ces_CE_ODD 
   Ces_CE_ODDNOCOMP ; 
   ODDCOMP ;


LEXICON Ces_CE_ODDNOCOMP 
 :%^DISIMPe     ATTR_S ;
 :%^DISIMPe     PRED_0 ;
! ODDCASEOBL ;
 +A+SgNomCmp:%^DISIMPe            R ;


LEXICON Ces_Ce_CES_CE_ODD   !as Ces_CES_CE_ODD, but with ATTR_0
 Ces_Ce_CES_CE_ODDNOCOMP ;
 ODDCOMP ;
+Use/Sub:%^DISIMP ODDCOMP ;

LEXICON Ces_Ce_CES_CE_ODDNOCOMP
+Use/NG:%^DISIMPe ATTR_0 ;
 :%^DISIMPe ATTR_S ;
+Use/NG:%^DISIMPe PRED_S ;
! ODDCASE ;
       
LEXICON Ce_Ces_CES_CE_ODD  
 Ce_Ces_CES_CE_ODDNOCOMP ;
 ODDCOMP ;
+Use/Sub:%^DISIMP ODDCOMP ;

LEXICON Ce_Ces_CES_CE_ODDNOCOMP
 :%^DISIMPe ATTR_0 ;
+Use/NG:%^DISIMPe ATTR_S ;
 :%^DISIMPe PRED_S ;
! ODDCASEOBL ;

       
LEXICON Ces_CES_CE_ODD 
:%^DISIMPe     ATTR_S ;   !<=====  gir Attr -s
+Use/NG:%^DISIMPe     PRED_S ;
!   ODDCASE ;
!   ass eCASE ;! <-- EVEN CASE!!
+A+SgNomCmp:%^DISIMPe            R ;
   ODDCOMP ;
+Use/Sub:%^DISIMP   ODDCOMP ;


LEXICON Ces_CES_CE_ODDNOCOMP 
:%^DISIMPe     ATTR_S ;
+Use/NG:%^DISIMPe     PRED_S ;
:%^DISIMPe     PRED_0 ;
! ODDCASEOBL ;
+A+SgNomCmp:%^DISIMPe            R ;

LEXICON hks_hke_HKS_HKE_ODD 
:%^DISIMP     ATTR_S ;
+Use/NG:%^DISIMPe    ATTR_0 ;
:%^DISIMP     PRED_S ;
+Use/NG:%^DISIMPe    PRED_0 ;
!   ODDCASEOBL ;
! :%^DISIMPe     PRED_0 ;
+A+SgNomCmp:%^DISIMPe            R ;
   ODDCOMP ;

LEXICON oehks_oehke_OEHKS_OEHKE_ODD 
:%^DISIMP     ATTR_S ;
+Use/NG:%^DISIMPe    ATTR_0 ;
:%^DISIMP     PRED_S ;
+Use/NG:%^DISIMPe    PRED_0 ;
!   ODDCASEOBL ;
! :%^DISIMPe     PRED_0 ;
+A+SgNomCmp:%^DISIMPe            R ;
   ODD_ÅBPOE ;




!LEX s_N_ODDNOCOMP 
!  ATTR_S ;
! PRED_N ;
! kasusparadigme ?
!
!LEX s_N_ODD 
!   ATTR_S ;
!   PRED_N ;
! :s ODDCOMP ; 


!LEX LOANADJ
!+LOAN: ø_S_ODDEVEN   ; ! the idea here is to get both -e and -es
!+LOAN: ATTR_0 ;


!LEX ISKADJ
!+LOAN: ATTR_0 ;
!+LOAN:sk ø_S_ODDEVEN  ; 
!+LOAN:hk e_ES_EVEN  ;


LEXICON HKE_ODD_NGCOMP
+Use/Sub:%^DISIMPe     ATTR_0 ;
 :%^DISIMPe     PRED_0 ;
! ODDCASEOBL ;
+Use/NG:  ODDCOMP ;

LEXICON HKE_ODD_COMP
+Use/Sub:%^DISIMPe     ATTR_0 ;
 :%^DISIMPe     PRED_0 ;
! ODDCASEOBL ;
  ODDCOMP ;






! Odd syllabic stems - adjectives
! - - - - - - - - - - - - - - - -

!LEX ODDCASE
! ODDCASENOM ;
! ODDCASEOBL ;
!
!LEX ODDCASENOM
!+A+Sg+Nom:e%>%^DISIMP       FINAL1 ;

LEXICON ODDCASEOBL
+A+Sg+Acc:e%>%^DISIMPm      FINAL1 ;
+A+Sg+Gen:e%>%^DISIMPn      FINAL1 ;
+A+Sg+Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
+A+Sg+Ine:%>isnie           FINAL1 ;
+A+Sg+Ela:%>istie           FINAL1 ;
+A+Sg+Com:%>inie            FINAL1 ;
+A+Pl: N_ODD_PL ;
+A:    N_ODD_ESS ;
+A+SgGenCmp:e%>%^DISIMPn        R ;
+A+PlGenCmp:%>%^DISIMPi R ;

LEXICON ije_ODDCASE
ije_ODDCASENOM ;
ije_ODDCASEOBL ;

LEXICON ije_ODDCASENOM
+A+Sg+Nom:e%>%^DISIMP       FINAL1 ;

LEXICON ije_ODDCASEOBL
+A+Sg+Acc:e%>%^DISIMPm      FINAL1 ;
+A+Sg+Gen:e%>%^DISIMPn      FINAL1 ;
+A+Sg+Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
+A+Sg+Ine:%>isnie           FINAL1 ;
+A+Sg+Ela:%>istie           FINAL1 ;
+A+Sg+Com:%>inie            FINAL1 ;
+A+Pl+Nom:%>h       FINAL1 ;
+A+Pl+Acc:%>idie            FINAL1 ;
+A+Pl+Gen:%>i       FINAL1 ;
+A+Pl+Ill:%>idie            FINAL1 ;
+A+Pl+Ine:%>inie            FINAL1 ;
+A+Pl+Ela:%>ijstie          FINAL1 ;
+A+Pl+Com:%>%^DISIMPigujmie FINAL1 ;
+A+Pl+Com+Use/Sub:%>%^DISIMPigyjmie FINAL1 ; !

! Even syllabic stems - adjectives
! - - - - - - - - - - - - - - - - 
LEXICON aCASE
aCASENOM ;
aCASEOBL ;

LEXICON eCASE
eCASENOM ;
eCASEOBL ;

LEXICON eCASENOM
+A+Sg:           NIEJTESGNOM  ;

LEXICON eCASEOBL
+A:           NIEJTEOBL  ;

LEXICON aCASENOM
+A+Sg: MAANASGNOM ;

LEXICON aCASEOBL
+A+Pl: MAANA_PL ;
+A:     EVEN_ESS ;
+A+SgNomCmp:a    R ;
+A+SgGenCmp:a%>n R ;
+A+PlGenCmp:a%>j R ;
!!! +A+Der1+Der/Dimin+N:%^aLATUS%»tj GÅATETJE ;
!! TT: Added +Sg to the Px entries here, cf. MAANASGOBL.
! +A+Sg+Nom+PxSg1+Use/Sub:%>eme FINAL1 ;
! +A+Sg+Nom+PxSg2+Use/Sub:%>edh FINAL1 ;
! +A+Sg+Nom+PxSg3+Use/Sub:e%>se FINAL1 ;
! +A+Sg+Acc:a%>m       FINAL1 ;
! +A+Sg+Acc+PxSg1+Use/Sub:%>eme FINAL1 ;
! +A+Sg+Acc+PxSg1+Use/NG+Use/Sub:%>emem FINAL1 ;
! +A+Sg+Acc+PxSg2+Use/Sub:%>emdh FINAL1 ;
! +A+Sg+Acc+PxSg3+Use/Sub:e%>mse FINAL1 ;
! +A+Sg+Gen:a%>n       FINAL1 ;
! +A+Sg+Gen+PxSg1+Use/Sub:%>ene FINAL1 ;
! +A+Sg+Ill:e%>se      FINAL1 ;
! +A+Sg+Ill+PxSg1+Use/Sub:%>asanne FINAL1 ;
! +A+Sg+Ill+PxSg2+Use/Sub:%>asadth FINAL1 ;
! +A+Sg+Ine:e%>sne     FINAL1 ;
! +A+Sg+Ela:e%>ste     FINAL1 ;
! +A+Sg+Ela+PxSg1+Use/Sub:%>astanne FINAL1 ;
! +A+Sg+Ela+PxSg1+Use/NG:%>astadth FINAL1 ;
! +A+Sg+Com:%>ine      FINAL1 ;
! +A+Sg+Com+PxSg2+Use/Sub:%>anadth FINAL1 ;

LEXICON ieCASE
+A+Sg: N_IE_SG_NOM ;
         ieCASEOBL ;

LEXICON ieCASEOBL
  +A+Sg: N_IE_SG_OBLadj ;
  +A+Pl: N_IE_PL ;
  +A:    N_IE_ESS ;
  +A+SgNomCmp:ie              R ;
  +A+SgGenCmp:ie%>n           R ;
  +A+PlGenCmp+NoUml:ie%>j           R ;
  +A+PlGenCmp+Uml:%>%^iUMLi R ;




LEXICON oeCASE
oeCASE_NOMSG ;
oeCASE_OBL ;

LEXICON oeCASE_NOMSG
+A+Sg+Nom:oe          FINAL1 ;

LEXICON oeCASE_OBL
 +A+Sg: A_OE_SGILL_UML ; ! Causes Umlaut
        A_OE ;

LEXICON A_OE_SGILL_UML
 +Ill+Uml:e%>%^2UMLse FINAL1      ; ! Causes Umlaut

LEXICON A_OE
  +A+Sg: A_OE_SG  ;
  +A+Pl: A_OE_PL  ;
  +A: A_OE_ESS ;
  +A+SgNomCmp:oe    R ;
  +A+SgGenCmp:oe%>n R ;
  +A+PlGenCmp:oe%>j R ;

LEXICON A_OE_SG
  +Acc:oe%>m       FINAL1 ;
  +Gen:oe%>n       FINAL1 ;
  +Ill:o%>se       FINAL1 ;
  +Ine:o%>sne      FINAL1 ;
  +Ela:o%>ste      FINAL1 ;
  +Com:o%>jne      FINAL1 ;

LEXICON A_OE_PL
  +Nom:oe%>h       FINAL1 ;
  +Acc:o%>jde      FINAL1 ;
  +Gen:oe%>j       FINAL1 ;
  +Ill:o%>jde      FINAL1 ;
  +Ine:o%>jne      FINAL1 ;
  +Ela:o%>jste     FINAL1 ;
  +Com:oe%>jgujmie FINAL1 ;

LEXICON A_OE_ESS
  +Ess:o%>jne         FINAL1 ;



! Comparative forms
! -----------------

LEXICON EVENCOMP
EVENCOMPONLY ;
EVENSUPONLY ;

LEXICON EVENCOMPONLY
+A+Comp+Attr:%>%^COMPDISIMPbe%^DISIMP ATTRCONT ;
! EVENCOMPCASE ;
 DIMCOMP ;
LEXICON EVENSUPONLY
+A+Superl+Attr:%>%^COMPDISIMPmes%^DISIMP ATTRCONT ;
+A+Superl+Sg+Nom:%>%^COMPDISIMPmes%^DISIMP FINAL1 ;
   MES ;  
   

LEXICON EVENCOMP_oe
+A+Comp+Attr:%>%^COMPDISIMPbe%^DISIMP ATTRCONT ;
+A+Comp+Attr+Use/NG:%>%^COMPDISIMP2be%^DISIMP ATTRCONT ;
!! EVENCOMPCASE_oe ;
+A+Superl+Attr:%>%^COMPDISIMPmes%^DISIMP ATTRCONT ;
+A+Superl+Attr+Use/NG:%>%^COMPDISIMP2mes%^DISIMP ATTRCONT ;
 ! MES_oe ;
 DIMCOMP ;

LEXICON ODDCOMP
ODD_ÅBPOE ;
ODD_ABPA ;
LEXICON ODD_ÅBPOE
+A+Comp+Attr:%>åbpoe ATTRCONT ; ! båajoesåbpoe, *båajasåbpoe
+A+Comp:%>åbp ÅBPOE ;
:%>ommes OMMES ;
+A+Comp+Der1+Der/Dimin+Attr:%>obpe%»tje ATTRCONT ;
+A+Comp+Der1+Der/Dimin:%>obpe%»tj  GÅATETJEadj ;
LEXICON ODD_ABPA
+A+Comp+Attr:%>abpa ATTRCONT ;  ! båajasabpa,   *båajoesabpa
+A+Comp:%>abp ABPA ;
:%>ammes OMMES ;
+A+Comp+Der1+Der/Dimin+Attr+Use/NG:%>abpe%»tje ATTRCONT ;
+A+Comp+Der1+Der/Dimin+Use/NG:%>abpa%»tj  GÅATETJEadj ;
LEXICON OMMES
+A+Superl+Sg+Nom:%^DISIMP         FINAL1 ;
+A+Superl+Sg+Acc:e%>%^DISIMPm      FINAL1 ;
+A+Superl+Sg+Gen:e%>%^DISIMPn      FINAL1 ;
 +A+Superl+Sg+Ill:%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
+A+Superl+Sg+Ine:%>isnie           FINAL1 ;
+A+Superl+Sg+Ela:%>istie           FINAL1 ;
+A+Superl+Sg+Com:%>inie            FINAL1 ;
+A+Superl+Pl: N_ODD_PL ;
+A+Superl: N_ODD_ESS ;
+A+Superl+SgNomCmp:            R ;
+A+Superl+SgGenCmp:e%>n        R ;
+A+Superl+PlGenCmp:%>%^DISIMPi R ;
+A+Superl+Attr:%^DISIMP ATTRCONT ;


!LEX EVENCOMPCASE
!+A+Comp+Sg+Nom:%>%^COMPDISIMPbe%^DISIMP         FINAL1 ;
!+A+Comp+Sg+Acc:%>%^COMPDISIMPbe%>%^DISIMPm      FINAL1 ;
!+A+Comp+Sg+Gen:%>%^COMPDISIMPbe%>%^DISIMPn      FINAL1 ;
!+A+Comp+Sg+Ill:b%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
!+A+Comp+Sg+Ine:b%>isnie           FINAL1 ;
!+A+Comp+Sg+Ela:b%>istie           FINAL1 ;
!+A+Comp+Sg+Com:b%>inie            FINAL1 ;
!+A+Comp+Pl+Nom:%>%^COMPDISIMPb%>%^DISIMPh       FINAL1 ;
!+A+Comp+Pl+Acc:b%>idie            FINAL1 ;
!+A+Comp+Pl+Gen:%>%^COMPDISIMPb%>%^DISIMP IGENSUFF ;
!+A+Comp+Pl+Ill:b%>idie            FINAL1 ;
!+A+Comp+Pl+Ine:b%>inie            FINAL1 ;
!+A+Comp+Pl+Ela:b%>ijstie          FINAL1 ;
!+A+Comp+Pl+Com:b%>%^DISIMPigujmie FINAL1 ;
!+A+Comp:b  N_ODD_ESS ;
!+A+Comp+SgNomCmp:%>%^COMPDISIMPbe%^DISIMP            R ;
!+A+Comp+SgGenCmp:%>%^COMPDISIMPbe%>%^DISIMPn        R ;
!+A+Comp+PlGenCmp:%>%^COMPDISIMPb%>%^DISIMPi R ;

!!LEX EVENCOMPCASE_oe
!!+A+Comp+Sg+Nom:%>%^COMPDISIMPbe%^DISIMP         FINAL1 ;
!!+A+Comp+Sg+Acc:%>%^COMPDISIMPbe%>%^DISIMPm      FINAL1 ;
!!+A+Comp+Sg+Gen:%>%^COMPDISIMPbe%>%^DISIMPn      FINAL1 ;
!!+A+Comp+Sg+Nom+Use/NG:%>%^COMPDISIMP2be%^DISIMP         FINAL1 ;
!!+A+Comp+Sg+Acc+Use/NG:%>%^COMPDISIMP2be%>%^DISIMPm      FINAL1 ;
!!+A+Comp+Sg+Gen+Use/NG:%>%^COMPDISIMP2be%>%^DISIMPn      FINAL1 ;
!!+A+Comp+Sg+Ill:b%>asse            FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
!!+A+Comp+Sg+Ine:b%>isnie           FINAL1 ;
!!+A+Comp+Sg+Ela:b%>istie           FINAL1 ;
!!+A+Comp+Sg+Com:b%>inie            FINAL1 ;
!!+A+Comp+Pl+Nom:%>%^COMPDISIMPb%>%^DISIMPh       FINAL1 ;
!!+A+Comp+Pl+Nom+Use/NG:%>%^COMPDISIMP2b%>%^DISIMPh       FINAL1 ;
!!+A+Comp+Pl+Acc:b%>idie            FINAL1 ;
!!+A+Comp+Pl+Gen:%>%^COMPDISIMPb%>%^DISIMP IGENSUFF ;
!!+A+Comp+Pl+Gen+Use/NG:%>%^COMPDISIMP2b%>%^DISIMP IGENSUFF ;
!!+A+Comp+Pl+Ill:b%>idie            FINAL1 ;
!!+A+Comp+Pl+Ine:b%>inie            FINAL1 ;
!!+A+Comp+Pl+Ela:b%>ijstie          FINAL1 ;
!!+A+Comp+Pl+Com:b%>%^DISIMPigujmie FINAL1 ;
!!+A+Comp :b  N_ODD_ESS ;
!!+A+Comp+SgNomCmp:%>%^COMPDISIMPbe%^DISIMP R ;
!!+A+Comp+SgGenCmp:%>%^COMPDISIMPbe%>%^DISIMPn R ;
!!+A+Comp+PlGenCmp:%>%^COMPDISIMPb%>%^DISIMPi R ;
!!+A+Comp+SgNomCmp:%>%^COMPDISIMP2be%^DISIMP R ;
!!+A+Comp+SgGenCmp:%>%^COMPDISIMP2be%>%^DISIMPn R ;
!!+A+Comp+PlGenCmp:%>%^COMPDISIMP2b%>%^DISIMPi R ;

 
LEXICON DIMCOMP
+A+Comp+Der1+Der/Dimin+Attr:%>b%»adtje          FINAL1 ;
+A+Comp+Der1+Der/Dimin+Attr:%>b%»odtje       FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Nom:%>b%»adtje          FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Acc:%>b%»adtje%>m       FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Gen:%>b%»adtje%>n       FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Ill:%>%^COMPDISIMPb%»adtje%>se      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Ine:%>%^COMPDISIMPb%»adtje%>sne     FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Ela:%>%^COMPDISIMPb%»adtje%>ste     FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Com:%>%^COMPDISIMPb%»adtj%>ine      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Ill:%>%^COMPDISIMP2b%»adtje%>se      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Ine:%>%^COMPDISIMP2b%»adtje%>sne     FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Ela:%>%^COMPDISIMP2b%»adtje%>ste     FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Com:%>%^COMPDISIMP2b%»adtj%>ine      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Nom:%>b%»adtj%>h        FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Nom:%>b%»adtj%>eh        FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Acc:%>%^COMPDISIMPb%»adtj%>ide      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Acc:%>%^COMPDISIMP2b%»adtj%>ide      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Gen:%>b%»adtj%> IGENSUFF ;
+A+Comp+Der1+Der/Dimin+Pl+Ill:%>%^COMPDISIMPb%»adtj%>ide      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Ine:%>%^COMPDISIMPb%»adtj%>ine      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Ela:%>%^COMPDISIMPb%»adtj%>ijste    FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Ill:%>%^COMPDISIMP2b%»adtj%>ide      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Ine:%>%^COMPDISIMP2b%»adtj%>ine      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Ela:%>%^COMPDISIMP2b%»adtj%>ijste    FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Com:%>b%»adtj%>igujmie  FINAL1 ;
+A+Comp+Der1+Der/Dimin+Ess:%>%^COMPDISIMPb%»adtj%>ine         FINAL1 ;
+A+Comp+Der1+Der/Dimin+Ess:%>%^COMPDISIMP2b%»adtj%>ine         FINAL1 ;
+A+Comp+Der1+Der/Dimin+SgNomCmp:%>b%»adtje    R ;
+A+Comp+Der1+Der/Dimin+SgGenCmp:%>b%»adtje%>n R ;
+A+Comp+Der1+Der/Dimin+PlGenCmp:%>b%»adtj%>i  R ;
+A+Comp+Der1+Der/Dimin+Sg+Nom:%>b%»odtje          FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Acc:%>b%»odtje%>m       FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Gen:%>b%»odtje%>n       FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Ill:%>%^COMPDISIMPb%»odtje%>se      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Ine:%>%^COMPDISIMPb%»odtje%>sne     FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Ela:%>%^COMPDISIMPb%»odtje%>ste     FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Com:%>%^COMPDISIMPb%»odtj%>ine      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Ill:%>%^COMPDISIMP2b%»odtje%>se      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Ine:%>%^COMPDISIMP2b%»odtje%>sne     FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Ela:%>%^COMPDISIMP2b%»odtje%>ste     FINAL1 ;
+A+Comp+Der1+Der/Dimin+Sg+Com:%>%^COMPDISIMP2b%»odtj%>ine      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Nom:%>b%»odtj%>h        FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Nom:%>b%»odtj%>eh        FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Acc:%>%^COMPDISIMPb%»odtj%>ide      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Acc:%>%^COMPDISIMP2b%»odtj%>ide      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Gen:%>b%»odtj%> IGENSUFF ;
+A+Comp+Der1+Der/Dimin+Pl+Ill:%>%^COMPDISIMPb%»odtj%>ide      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Ine:%>%^COMPDISIMPb%»odtj%>ine      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Ela:%>%^COMPDISIMPb%»odtj%>ijste    FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Ill:%>%^COMPDISIMP2b%»odtj%>ide      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Ine:%>%^COMPDISIMP2b%»odtj%>ine      FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Ela:%>%^COMPDISIMP2b%»odtj%>ijste    FINAL1 ;
+A+Comp+Der1+Der/Dimin+Pl+Com:%>b%»odtj%>igujmie  FINAL1 ;
+A+Comp+Der1+Der/Dimin+Ess:%>%^COMPDISIMPb%»odtj%>ine         FINAL1 ;
+A+Comp+Der1+Der/Dimin+Ess:%>%^COMPDISIMP2b%»odtj%>ine         FINAL1 ;
+A+Comp+Der1+Der/Dimin+SgNomCmp:%>b%»odtje    R ;
+A+Comp+Der1+Der/Dimin+SgGenCmp:%>b%»odtje%>n R ;
+A+Comp+Der1+Der/Dimin+PlGenCmp:%>b%»odtj%>i  R ;

! doublet fix stops here so far Trond.

LEXICON ÅBPOE
 +Sg: ÅBPOE_SGILL_UML ; ! Causes Umlaut
           ÅBPOE_N_OE ;

LEXICON ÅBPOE_SGILL_UML
 +Ill+Uml:e%>%^2UMLse FINAL1      ; ! Causes Umlaut

LEXICON ÅBPOE_N_OE
  +Sg: ÅBPOE_N_OE_SG  ;
  +Pl: ÅBPOE_N_OE_PL  ;
!  : ÅBPOE_N_OE_ESS ;
  +SgNomCmp:oe    R ;
  +SgGenCmp:oe%>n R ;
  +PlGenCmp:oe%>j R ;
LEXICON ÅBPOE_N_OE_SG
  +Nom:oe          FINAL1 ;
  +Acc:oe%>m       FINAL1 ;
  +Gen:oe%>n       FINAL1 ;
  +Ill:o%>se       FINAL1 ;
  +Ine:o%>sne      FINAL1 ;
  +Ela:o%>ste      FINAL1 ;
  +Com:o%>jne      FINAL1 ;
  +Ess:o%>jne      FINAL1 ;
LEXICON ÅBPOE_N_OE_PL
  +Nom:oe%>h       FINAL1 ;
  +Acc:o%>jde      FINAL1 ;
  +Gen:oe%>j       FINAL1 ;
  +Ill:o%>jde      FINAL1 ;
  +Ine:o%>jne      FINAL1 ;
  +Ela:o%>jste     FINAL1 ;
  +Com:oe%>jgujmie FINAL1 ;
  +Ess:o%>jne      FINAL1 ;

LEXICON ABPA
 +Sg: ABPA_SG ; ! Causes Umlaut
  +Pl: ABPA_PL  ;
  +Ess:%>ine         FINAL1 ;


LEXICON ABPA_SG
  +Nom:a          FINAL1 ;
  +Acc:a%>m       FINAL1 ;
  +Gen:a%>n       FINAL1 ;
  +Ill:e%>se      FINAL1 ;
  +Ine:e%>sne     FINAL1 ;
  +Ela:e%>ste     FINAL1 ;
  +Com:%>ine      FINAL1 ;

LEXICON ABPA_PL
  +Nom:a%>h       FINAL1 ;
  +Acc:%>ide      FINAL1 ;
  +Gen:a%>j       FINAL1 ;
  +Ill:%>ide      FINAL1 ;
  +Ine:%>ine      FINAL1 ;
  +Ela:%>ijste    FINAL1 ;
  +Com:a%>jgujmie FINAL1 ;

LEXICON MES
+A+Superl+Attr:%>%^COMPDISIMPmes%^DISIMP          ATTRCONT ;
+A+Superl+Sg+Nom:%>%^COMPDISIMPmes%^DISIMP          FINAL1 ;
+A+Superl+Sg+Acc:masse%>m       FINAL1 ;
+A+Superl+Sg+Gen:masse%>n       FINAL1 ;
+A+Superl+Sg+Ill:masse%>se      FINAL1 ;
+A+Superl+Sg+Ine:masse%>sne     FINAL1 ;
+A+Superl+Sg+Ela:masse%>ste     FINAL1 ;
+A+Superl+Sg+Com:mass%>ine      FINAL1 ;
+A+Superl+Pl+Nom:%>%^COMPDISIMP%>mass%>%^DISIMPh        FINAL1 ;
+A+Superl+Pl+Acc:%>mass%>ide      FINAL1 ;
+A+Superl+Pl+Gen:%>%^COMPDISIMP%>mass%>%^DISIMP IGENSUFF ;
+A+Superl+Pl+Ill:%>mass%>ide      FINAL1 ;
+A+Superl+Pl+Ine:%>mass%>ine      FINAL1 ;
+A+Superl+Pl+Ela:%>mass%>ijste    FINAL1 ;
+A+Superl+Pl+Com:%>mass%>igujmie  FINAL1 ;
+A+Superl:mass            EVEN_ESS ;
+A+Superl+SgNomCmp:%>%^COMPDISIMPmes%^DISIMP    R ;
+A+Superl+SgGenCmp:masse%>n R ;
+A+Superl+PlGenCmp:mass%>i  R ;

!AdverbLexicons
LEXICON LAAKAN
+Der1+Der/laakan:#laakan adv ;
+Der1+Der/laakan+Use/Sub:-laakan adv ;

LEXICON LEEJNES
 :#leejnes ATTR_0 ;
 :#leejns ATTR_0 ;
 +Use/Sub:-leejnes ATTR_0 ;
 +Use/Sub:-leejns ATTR_0 ;


         

