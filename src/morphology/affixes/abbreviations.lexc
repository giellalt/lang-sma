! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!  ====================================
!! !!!Abbreviation affixes & contlexes
!  ====================================


! Now splitting according to POS, and according to dot or not
! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

! First collecting POS info, *-noun, *-adv, etc.
! Also splitting when in doubt: -noun-adj => -noun and -adj
! Then pointing to two contlexes, a dot-one and a non-dot-one.

!! !!Abbreviations written both with and without a following full stop
LEXICON ab-noun-itrab   +N+ABBR+Gram/IAbbr:    ab-noun ;
LEXICON ab-noun-trab    +N+ABBR+Gram/TAbbr:    ab-noun ;
LEXICON ab-noun-trnumab +N+ABBR+Gram/TNumAbbr: ab-noun ;
LEXICON ab-noun
 +Use/NG: ab-nodot-noun ;
          ab-dot-noun   ;

LEXICON ab-adj-itrab +A+ABBR+Gram/IAbbr: ab-adj ;
!LEXICON ab-adj-trab +A+ABBR+Gram/TAbbr: ab-adj ;
LEXICON ab-adj
 +Use/NG: ab-nodot-adj ;
          ab-dot-adj   ;

LEXICON ab-adv-itrab +Adv+ABBR+Gram/IAbbr: ab-adv ;
LEXICON ab-adv-trab  +Adv+ABBR+Gram/TAbbr: ab-adv ;
LEXICON ab-adv
 +Use/NG: ab-nodot-adv ;
          ab-dot-adv   ;

LEXICON ab-num-itrab +Num+ABBR+Gram/IAbbr: ab-num ;
LEXICON ab-num
 +Use/NG: ab-nodot-num ;
          ab-dot-num   ;

!! !!Abbreviations written without a following full stop
!! Most abbreviations point to various variants of {{ab-nodot-noun}}.

LEXICON ab-nodot-noun-itrab   +N+ABBR+Gram/IAbbr:    ab-nodot-noun ;
LEXICON ab-nodot-noun-trab    +N+ABBR+Gram/TAbbr:    ab-nodot-noun ;
LEXICON ab-nodot-noun-trnumab +N+ABBR+Gram/TNumAbbr: ab-nodot-noun ;
LEXICON ab-nodot-noun
 abbr-nominal-infl ;
 RHyph             ;

LEXICON ab-nodot-adj
 abbr-nominal-infl ;
 RHyph             ;

LEXICON ab-nodot-adv-itrab +Adv+ABBR+Gram/IAbbr: ab-nodot-adv ;
LEXICON ab-nodot-adv-trab  +Adv+ABBR+Gram/TAbbr: ab-nodot-adv ;
LEXICON ab-nodot-adv
 # ; ! Adv without case, consider this.

LEXICON ab-nodot-num
 +Sg+Nom: #     ; ! no period, that is.
          RHyph ;

!! !!Abbreviations written with a following full stop
LEXICON ab-dot-noun-adj-trab
  ab-dot-noun-trab ;
  ab-dot-adj-trab  ;

LEXICON ab-dot-noun-itrab   +N+ABBR+Gram/IAbbr:    ab-dot-noun ;
LEXICON ab-dot-noun-trab    +N+ABBR+Gram/TAbbr:    ab-dot-noun ;
LEXICON ab-dot-noun-trnumab +N+ABBR+Gram/TNumAbbr: ab-dot-noun ;
LEXICON ab-dot-noun
!! @LEXNAME@ is the lexicon for noun abbrs that must have a period.
    +Attr: DOTwBACKTRACK ;
! +N+ABBR+Sg+Nom: DOTwBACKTRACK ;

LEXICON ab-dot-adj-itrab   +A+ABBR+Gram/IAbbr:    ab-dot-adj ;
LEXICON ab-dot-adj-trab    +A+ABBR+Gram/TAbbr:    ab-dot-adj ;
LEXICON ab-dot-adj-trnumab +A+ABBR+Gram/TNumAbbr: ab-dot-adj ;
LEXICON ab-dot-adj
!! @LEXNAME@ is the lexicon for adjectival abbrs that must have a period.
   +Attr: DOTwBACKTRACK ;
 +Sg+Nom:%. # ;

LEXICON ab-dot-adv-itrab +Adv+ABBR+Gram/IAbbr: ab-dot-adv ;
LEXICON ab-dot-adv-trab  +Adv+ABBR+Gram/TAbbr: ab-dot-adv ;
LEXICON ab-dot-adv
!! @LEXNAME@ is the lexicon for adverbial abbrs that must have a period.
 DOTwBACKTRACK ; ! Adv without case.

LEXICON ab-dot-num-itrab +Num+ABBR+Gram/IAbbr: ab-dot-num ;
LEXICON ab-dot-num
!! @LEXNAME@ is the lexicon for numerical abbrs that must have a period.
 +Sg+Nom: DOTwBACKTRACK ;

! LEXICON ab-dot-cc-itrab ab-dot-cc ;
LEXICON ab-dot-cc-trab +CC+ABBR+Gram/TAbbr: ab-dot-cc ;
LEXICON ab-dot-cc
 DOTwBACKTRACK ;

! LEXICON ab-dot-verb-itrab ab-dot-verb ;
LEXICON ab-dot-verb-trab    ab-dot-verb ;
LEXICON ab-dot-verb
 +V+ABBR+TV+Imprt: DOTwBACKTRACK ; ! Period.

!! !!Case suffixes and analysis tags
LEXICON abbr-nominal-infl
   +Attr:        # ;
 +Sg+Nom:        # ;
 +Sg+Acc:%>%:m   # ;
 +Sg+Gen:%>%:n   # ;
 +Sg+Ill:%>%:se  # ;
 +Sg+Ine:%>%:sne # ;
 +Sg+Ela:%>%:ste # ;
 +Sg+Com:%>%:ine # ;
    +Ess:%>%:ine # ;

LEXICON DOTwBACKTRACK
! This should create a new backtrack-point before the dot, but no sub-reading:                                                                                                                          
   +Use/-PMatch:%. # ;
< "+Use/PMatch":0 "@P.Pmatch.Backtrack@" 0:%. > # ; ! This creates a split point                                                                                                         

! Contlexes not in use, kept for the future.

!LEXICON ab ! No POS, don't know what pos
!ab-nodot ;
!ab-dot ;

! Some of these POS
!LEXICON ab-noun-adj
! ab-noun ;
! ab-adj ;

!LEXICON ab-cc
!ab-nodot-cc ;
!ab-dot-cc ;

!LEXICON ab-nodot-cc
! +ABBR+CC: # ; ! no period, that is.

! noun the default POS
!LEXICON ab-nodot ! default
!+N+ABBR+Attr: # ; ! no period, that is.
!+N+ABBR+Nom: # ; ! no period, that is.
!+N+ABBR: RHyph ;

!LEXICON ab-dot ! Default
! +ABBR:%. # ;
!+N+ABBR+Attr:%. # ;
!+N+ABBR+Nom:%. # ;

! Then, as an afterthought, come our two verbs, gč. and vrd.

!LEXICON ab-verb-trab ab-verb ;
!LEXICON ab-verb
! A lexicon for "gč." and perhaps also other abbreviated verbs.
! ab-dot-verb ;
! ab-nodot-verb ;
! Not in use

!LEXICON ab-nodot-verb-trab ab-nodot-verb ;
!LEXICON ab-nodot-verb
! All entries pointing to this contlex are commented out, thus itself as well
! +ABBR+V+TV+Imprt: # ; ! No period.
! Not in use.
