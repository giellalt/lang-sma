! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ############################## !
!This file contains the numerals !
! ############################## !




! Numerals
! ========

! The initial lexica
! ------------------

LEXICON Numeral
 MILLIJOVNE ; ! a noun of its own
 OVERTÅVSENE ; ! for generator over 1000
 JUSTTÅVSENE ; ! going via 1000
 UNDERTÅVSENE ; ! for generator under 1000
 OLD ; ! for "thirteen hundred, etc. 
!num-imprecise ; 
 COLLECTIVE ; 
+Use/Circ: ARABIC ; ! for the arabic numerals !
+Use/Circ: ROMAN ; ! for the roman numerals!
+Use/Circ: NUM-PREFIXES ; ! for §34 etc.!
+Use/Circ: ISOLATED-NUMEXP ; ! for ½ etc.!
 NUMBERLIKE ; ! a dustbin







! We now run through the alphabetic numerals, ending up with the case
! inflection of the final number.

! Million, a noun of its own
! --------------------------

LEXICON MILLIJOVNE
millijovne:millijovn NUMTAGe_NOTakte ;
millijovnede+A+Ord:millijovned ORDTAGODD-ord ;
millijarde:millijard NUMTAGe_NOTakte ;! Num or N?
millijardede+A+Ord:millijarded ORDTAGODD-ord ;

nulle:null NUMTAGe_NOTakte ;


! OVERTÅVSENE, for the numerals over 1000
! --------------------------------------

LEXICON OVERTÅVSENE
 O-AKTEF ; ! 1000
 O-2TO9F ; ! 2000, 3000, ...
 O-JUSTLUHKIEF ; ! 10000
 O-2TO9LUHKIE ; ! 20000, 30000, ...
 O-TJUETIE ; ! 100000
 O-2TO9TJUETIE ; ! 200000, 300000, ...

! All the child lexica of OVERTUVSÁN have the prefix O-.
! They are directed via their respective numerals to the lexicon JUSTTÅVSENE.


! Thousands

LEXICON O-AKTEF
akte:akte# JUSTTOVSENE ;
 JUSTTOVSENE ;

LEXICON O-X1000
akte:akte# JUSTTÅVSENE ;

LEXICON O-2TO9F 
göökte:göökte# JUSTTÅVSENE ; 
golme:golme# JUSTTÅVSENE ; 
njieljie:njieljie# JUSTTÅVSENE ; 
njieljie+Use/NG:njielje# JUSTTÅVSENE ; !Saemesth amma
njieljie+Use/NG:nieljie# JUSTTÅVSENE ;
njieljie+Use/NG:nielje# JUSTTÅVSENE ; 
vïjhte:vïjhte# JUSTTÅVSENE ; 
govhte:govhte# JUSTTÅVSENE ; 
tjïjhtje:tjïjhtje# JUSTTÅVSENE ;
gaektsie:gaektsie# JUSTTÅVSENE ; 
uktsie:uktsie# JUSTTÅVSENE ; 

! Tens of thousands

LEXICON O-JUSTLUHKIEF
luhkie:luhkie# JUSTTÅVSENE ;
luhkieakte:luhkie#akte# JUSTTÅVSENE ;
luhkie:luhkie# O-2TO9F ;
! This lexicon is for the number 10000 only. it is separated from the
! rest to avoid forms like *luhkieluhkietåvsene, etc.


LEXICON O-LUHKIEF 
luhkie:luhkie# JUSTTÅVSENE ;
luhkie:luhkie# O-X1000 ;
luhkie:luhkie# O-2TO9F ;
! this lexicon is accessed only via other O-lexica, and not directly 
! from OVERTUVSÁN. Thus, *luhkieluhkietåvsene, etc. is avoided.

LEXICON O-LUHKIE123456789
luhkieakte:luhkie#akte# JUSTTÅVSENE ;
luhkie:luhkie# O-2TO9F ;

LEXICON O-2TO9LUHKIE 
göökte:göökte# O-LUHKIEF ;
golme:golme# O-LUHKIEF ;
njieljie:njieljie# O-LUHKIEF ;
njieljie+Use/NG:njielje# O-LUHKIEF ;
njieljie+Use/NG:nieljie# O-LUHKIEF ;
njieljie+Use/NG:nielje# O-LUHKIEF ; 
vïjhte:vïjhte# O-LUHKIEF ;
govhte:govhte# O-LUHKIEF ;
tjïjhtje:tjïjhtje# O-LUHKIEF ;
gaektsie:gaektsie# O-LUHKIEF ;
uktsie:uktsie# O-LUHKIEF ;




! Hundreds of thousands

LEXICON O-TJUETIE
tjuetie:tjuetie# JUSTTÅVSENE ; ! 100000
tjuetie:tjuetie# O-X1000 ; ! 101000
tjuetie:tjuetie# O-2TO9F ; ! 102000, 103000, ...
tjuetie:tjuetie# O-LUHKIEF ; ! 110000, 113000, ...
tjuetie:tjuetie# O-LUHKIE123456789 ; !100011, 100012, ...
tjuetie:tjuetie# O-2TO9LUHKIE ; ! 120000, 123000, ...
nïmme:nïmme# JUSTTÅVSENE ; ! 100000
nïmme:nïmme# O-X1000 ; ! 101000
nïmme:nïmme# O-2TO9F ; ! 102000, 103000, ...
nïmme:nïmme# O-LUHKIEF ; ! 110000, 113000, ...
nïmme:nïmme# O-LUHKIE123456789 ; !100011, 100012, ...
nïmme:nïmme# O-2TO9LUHKIE ; ! 120000, 123000, ...
nymme:nymme# JUSTTÅVSENE ; ! 100000
nymme:nymme# O-X1000 ; ! 101000
nymme:nymme# O-2TO9F ; ! 102000, 103000, ...
nymme:nymme# O-LUHKIEF ; ! 110000, 113000, ...
nymme:nymme# O-LUHKIE123456789 ; !100011, 100012, ...
nymme:nymme# O-2TO9LUHKIE ; ! 120000, 123000, ...
nomme:nomme# JUSTTÅVSENE ; ! 100000
nomme:nomme# O-X1000 ; ! 101000
nomme:nomme# O-2TO9F ; ! 102000, 103000, ...
nomme:nomme# O-LUHKIEF ; ! 110000, 113000, ...
nomme:nomme# O-LUHKIE123456789 ; !100011, 100012, ...
nomme:nomme# O-2TO9LUHKIE ; ! 120000, 123000, ...
stoereluhkie:stoere#luhkie# JUSTTÅVSENE ; ! 100000
stoereluhkie:stoere#luhkie# O-X1000 ; ! 101000
stoereluhkie:stoere#luhkie# O-2TO9F ; ! 102000, 103000, ...
stoereluhkie:stoere#luhkie# O-LUHKIEF ; ! 110000, 113000, ...
stoereluhkie:stoere#luhkie# O-LUHKIE123456789 ; !100011, 100012, ...
stoereluhkie:stoere#luhkie# O-2TO9LUHKIE ; ! 120000, 123000, ...
stoereluhkie+Use/NG:stoerre#luhkie# JUSTTÅVSENE ; ! 100000
stoereluhkie+Use/NG:stoerre#luhkie# O-X1000 ; ! 101000
stoereluhkie+Use/NG:stoerre#luhkie# O-2TO9F ; ! 102000, 103000, ...
stoereluhkie+Use/NG:stoerre#luhkie# O-LUHKIEF ; ! 110000, 113000, ...
stoereluhkie+Use/NG:stoerre#luhkie# O-LUHKIE123456789 ; !100011, 100012, ...
stoereluhkie+Use/NG:stoerre#luhkie# O-2TO9LUHKIE ; ! 120000, 123000, ...


LEXICON O-TJUETIE2
tjuetie:tjuetie# JUSTTÅVSENE ; ! 200000
tjuetie:tjuetie# O-X1000 ; ! 201000
tjuetie:tjuetie# O-2TO9F ; ! 202000, 203000, ...
tjuetie:tjuetie# O-LUHKIEF ; ! 210000, 213000, ...
tjuetie:tjuetie# O-LUHKIE123456789 ; !200011, 200012, ...
tjuetie:tjuetie# O-2TO9LUHKIE ; ! 220000, 223000, ...
nïmme:nïmme# JUSTTÅVSENE ; ! 200000
nïmme:nïmme# O-X1000 ; ! 201000
nïmme:nïmme# O-2TO9F ; ! 202000, 203000, ...
nïmme:nïmme# O-LUHKIEF ; ! 210000, 213000, ...
nïmme:nïmme# O-LUHKIE123456789 ; !200011, 200012, ...
nïmme:nïmme# O-2TO9LUHKIE ; ! 220000, 223000, ...
nymme:nymme# JUSTTÅVSENE ; ! 200000
nymme:nymme# O-X1000 ; ! 201000
nymme:nymme# O-2TO9F ; ! 202000, 203000, ...
nymme:nymme# O-LUHKIEF ; ! 210000, 213000, ...
nymme:nymme# O-LUHKIE123456789 ; !200011, 200012, ...
nymme:nymme# O-2TO9LUHKIE ; ! 220000, 223000, ...
nomme:nomme# JUSTTÅVSENE ; ! 200000
nomme:nomme# O-X1000 ; ! 201000
nomme:nomme# O-2TO9F ; ! 202000, 203000, ...
nomme:nomme# O-LUHKIEF ; ! 210000, 213000, ...
nomme:nomme# O-LUHKIE123456789 ; !200011, 200012, ...
nomme:nomme# O-2TO9LUHKIE ; ! 220000, 223000, ...
stoereluhkie:stoere#luhkie# JUSTTÅVSENE ; ! 200000
stoereluhkie:stoere#luhkie# O-X1000 ; ! 201000
stoereluhkie:stoere#luhkie# O-2TO9F ; ! 202000, 203000, ...
stoereluhkie:stoere#luhkie# O-LUHKIEF ; ! 210000, 213000, ...
stoereluhkie:stoere#luhkie# O-LUHKIE123456789 ; !200011, 200012, ...
stoereluhkie:stoere#luhkie# O-2TO9LUHKIE ; ! 220000, 223000, ...
stoereluhkie+Use/NG:stoerre#luhkie# JUSTTÅVSENE ; ! 200000
stoereluhkie+Use/NG:stoerre#luhkie# O-X1000 ; ! 201000
stoereluhkie+Use/NG:stoerre#luhkie# O-2TO9F ; ! 202000, 203000, ...
stoereluhkie+Use/NG:stoerre#luhkie# O-LUHKIEF ; ! 210000, 213000, ...
stoereluhkie+Use/NG:stoerre#luhkie# O-LUHKIE123456789 ; !200011, 200012, ...
stoereluhkie+Use/NG:stoerre#luhkie# O-2TO9LUHKIE ; ! 220000, 223000, ...


LEXICON O-2TO9TJUETIE
akte:akte# O-TJUETIE ; ! #one# hundred...
göökte:göökte# O-TJUETIE2 ;
golme:golme# O-TJUETIE2 ;
njieljie:njieljie# O-TJUETIE2 ;
njielje:njielje# O-TJUETIE2 ;
nieljie:nieljie# O-TJUETIE2 ;
nielje:nielje# O-TJUETIE2 ; 
vïjhte:vïjhte# O-TJUETIE2 ;
govhte:govhte# O-TJUETIE2 ;
tjïjhtje:tjïjhtje# O-TJUETIE2 ;
gaektsie:gaektsie# O-TJUETIE2 ;
uktsie:uktsie# O-TJUETIE2 ;


LEXICON JUSTTOVSENE
tåvsene:tåvsene# UNDERTÅVSENE ;
tåvsene+Num:tåvsan ORDTAGODD ; ! Case infl of tåvsene
tåvsanede+A+Ord:tåvsaned ORDTAGEVEN-ord ;
tovsene:tovsene# UNDERTÅVSENE ;
tovsene+Num:tovsen ORDTAGODD ; ! Case infl of tovsene
tovsenede+A+Ord:tovsened ORDTAGEVEN-ord ;
stoeretjuetie:stoere#tjuetie# UNDERTÅVSENE ;
!stoere#tjuetie+Num+Sg+Nom:stoere#tjuetie FINAL1 ;
stoeretjuetie:stoere#tjuet NUMTAGie ; ! Case infl of tåvsene
stoerretjuetie:stoerre#tjuetie# UNDERTÅVSENE ;
!stoerretjuetie+Num+Sg+Nom:stoerre#tjuetie FINAL1 ;
stoerretjuetie:stoerre#tjuet NUMTAGie ; ! Case infl of tåvsene
stoerrenïmme:stoerre#nïmme# UNDERTÅVSENE ;
stoerrenïmme:stoerre#nïmm NUMTAGe_NOTakte ; ! Case infl of tåvsene
stoerrenymme:stoerre#nymme# UNDERTÅVSENE ;
stoerrenymme:stoerre#nymm NUMTAGe_NOTakte ; ! Case infl of tåvsene
stoerrenommestoerre#nomme# UNDERTÅVSENE ;
stoerrenomme:stoerre#nomm NUMTAGe_NOTakte ; ! Case infl of tåvsene
stoerenïmme:stoere#nïmme# UNDERTÅVSENE ;
stoerenïmme:stoere#nïmm NUMTAGe_NOTakte ; ! Case infl of tåvsene
stoerenymme:stoere#nymme# UNDERTÅVSENE ;
stoerenymme:stoere#nymm NUMTAGe_NOTakte ; ! Case infl of tåvsene
stoerenomme:stoere#nomme# UNDERTÅVSENE ;
stoerenomme:stoere#nomm NUMTAGe_NOTakte ; ! Case infl of tåvsene

! Justtåvsene, for numerals going via 1000
! -------------------------------------

LEXICON JUSTTÅVSENE
tåvsene:tåvsene# UNDERTÅVSENE ; ! Thousand-and-something
tåvsene+Num:tåvsan ORDTAGODD ; ! Case infl of tåvsene
tåvsanede+A+Ord:tåvsaned ORDTAGEVEN-ord ;
tovsene:tovsene# UNDERTÅVSENE ; ! Thousand-and-something
tovsene+Num:tovsen ORDTAGODD ; ! Case infl of tovsene
tovsenede+A+Ord:tovsened ORDTAGEVEN-ord ;
stoeretjuetie:stoere#tjuetie# UNDERTÅVSENE ;
!stoere#tjuetie+Num+Sg+Nom:stoere#tjuetie FINAL1 ;
stoeretjuetie:stoere#tjuet NUMTAGie ; ! Case infl of tåvsene
stoerretjuetie:stoerre#tjuetie# UNDERTÅVSENE ;
!stoerre#tjuetie+Num+Sg+Nom:stoerre#tjuetie FINAL1 ;
stoerretjuetie:stoerre#tjuet NUMTAGie ; ! Case infl of tåvsene
stoerrenïmme:stoerre#nïmme# UNDERTÅVSENE ;
stoerrenïmme:stoerre#nïmm NUMTAGe_NOTakte ; ! Case infl of tåvsene
stoerrenymme:stoerre#nymme# UNDERTÅVSENE ;
stoerrenymme:stoerre#nymm NUMTAGe_NOTakte ; ! Case infl of tåvsene
stoerrenomme:stoerre#nomme# UNDERTÅVSENE ;
stoerrenomme:stoerre#nomm NUMTAGe_NOTakte ; ! Case infl of tåvsene
stoerenïmme:stoere#nïmme# UNDERTÅVSENE ;
stoerenïmme:stoere#nïmm NUMTAGe_NOTakte ; ! Case infl of tåvsene
stoerenymme:stoere#nymme# UNDERTÅVSENE ;
stoerenymme:stoere#nymm NUMTAGe_NOTakte ; ! Case infl of tåvsene
stoerenomme:stoere#nomme# UNDERTÅVSENE ;
stoerenomme:stoere#nomm NUMTAGe_NOTakte ; ! Case infl of tåvsene


! Old, for the old counting thirteen hundred etc. 
! -----------------------------------------------

LEXICON OLD
luhkieakte:luhkie#akte GTJUETIE ;      
luhkiegöökte:luhkie#göökte GTJUETIE ;     
luhkiegolme:luhkie#golme GTJUETIE ; 
luhkienjieljie:luhkie#njieljie GTJUETIE ;         
luhkienjielje:luhkie#njielje GTJUETIE ;     
luhkienieljie:luhkie#nieljie GTJUETIE ;   
luhkienielje:luhkie#nielje GTJUETIE ;  
luhkievïjhte:luhkie#vïjhte GTJUETIE ;     
luhkiegovhte:luhkie#govhte GTJUETIE ;     
luhkietjïjhtje:luhkie#tjïjhtje GTJUETIE ; 
luhkiegaektsie:luhkie#gaektsie GTJUETIE ;     
luhkieuktsie:luhkie#uktsie GTJUETIE ;         






LEXICON UNDERTÅVSENE
 AKTEF ; ! 1
 2TO9F ; ! 2-9
 LUHKIEF ; ! 10
!LÅGENAN ; ! 11-19 
 2TO9LUHKIE ; ! 20-99
 TJUETIE ; ! 100-199
 2TO9TJUETIE ; ! 200-999

LEXICON AKTEF
akte:akt NUMTAGe ; 
akte+Num+Sg+Acc:aktem FINAL1 ;
voestes+A+Ord+Sg+Nom:voestes FINAL1 ; !XXX check this
voestes+A+Attr:voestes FINAL1 ;

LEXICON 2TO9F
göökte+Use/NG:guekt NUMTAGie ;
göökte:göökt NUMTAGe_NOTakte ;
golme:golm NUMTAGe_NOTakte ;
!njieljie+Num+Sg+Nom:njieljie FINAL1 ;
!njieljie+Num+Sg+Acc:njieljie FINAL1 ;
njieljie:njielj NUMTAGie ;
njielje:njielj NUMTAGe_NOTakte ; !Saemesth amma
!nieljie+Num+Sg+Nom:nieljie FINAL1 ;
!nieljie+Num+Sg+Acc:nieljie FINAL1 ;
nieljie:nielj NUMTAGie ;
nielje:nielj NUMTAGe_NOTakte ; 
vïjhte:vïjht NUMTAGe_NOTakte ;
govhte:govht NUMTAGe_NOTakte ;
tjïjhtje:tjïjhtj NUMTAGe_NOTakte ;
!gaektsie+Num+Sg+Nom:gaektsie FINAL1 ;
!gaektsie+Num+Sg+Acc:gaektsie FINAL1 ;
gaektsie:gaekts NUMTAGie ;
!uktsie+Num+Sg+Nom:uktsie FINAL1 ;
!uktsie+Num+Sg+Acc:uktsie FINAL1 ;
uktsie:ukts NUMTAGie ;
mubpie+A+Ord:mubp MUBPIE ;
nubpie+A+Ord:nubp MUBPIE ;
gåalmede+A+Ord:gåalmad ORDTAGODD-ord ;
njieljede+A+Ord:njieljied ORDTAGODD-ord ;
njealjede+A+Ord:njealjad ORDTAGODD-ord ;
nealjede+A+Ord:nealjad ORDTAGODD-ord ;
nieljede+A+Ord:nieljied ORDTAGODD-ord ;
vïjhtede+A+Ord:vïjhted ORDTAGODD-ord ;
govhtede+A+Ord:govhted ORDTAGODD-ord ;
tjïjhtjede+A+Ord:tjïjhtjed ORDTAGODD-ord ;
gaaktsede+A+Ord:gaaktsad ORDTAGODD-ord ;
åktsede+A+Ord:åktsad ORDTAGODD-ord ;
gööktesh:gööktes COLLTAG ;
golmesh:golmes COLLTAG ;
njealjesh:njealjas COLLTAG ;
vïjhtesh:vïjhtes COLLTAG ;
govhtesh:govhtes COLLTAG ;
tjïjhtjesh:tjïjhtjes COLLTAG ;
gaaktsesh:gaaktsas COLLTAG ;
åktsesh:åktsas COLLTAG ;

! Case forms of the basic digits
! ------------------------------

LEXICON NUMTAGe_NOTakte ! akte:akt
+Num+Sg+Acc:e FINAL1 ;
NUMTAGe ;

LEXICON NUMTAGe ! akte:akt
+Num+Sg+Nom:e FINAL1 ;
+Num+Sg+Gen:%>en FINAL1 ;
+Num+Sg+Ill+Attr:%>en FINAL1 ;
+Num+Sg+Ill:%>ese FINAL1 ;
+Num+Sg+Ine:%>esne FINAL1 ;
+Num+Sg+Ine+Attr:%>ene # ; !XXX Attr forms
+Num+Sg+Ela+Attr:%>ede # ; !XXX Attr forms
+Num+Sg+Ela:%>este FINAL1 ;
+Num+Sg+Com:%>ine FINAL1 ;
+Num+Ess:%>ine FINAL1 ;
+Num+Pl+Nom:%>h FINAL1 ;
+Num+Pl+Acc:%>ide FINAL1 ;
+Num+Pl+Gen:%>i FINAL1 ;
+Num+Pl+Com+Attr:%>i FINAL1 ;
+Num+Pl+Ill:%>ide FINAL1 ;
+Num+Pl+Ine:%>ine FINAL1 ;
+Num+Pl+Ela:%>ijste FINAL1 ;
+Num+Pl+Com:%>igujmie FINAL1 ;


LEXICON NUMTAGie !njieljie:njielj
+Num+Sg+Nom:ie FINAL1 ;
+Num+Sg+Acc:ie FINAL1 ;
+Num+Sg+Gen:ie%>n FINAL1 ;
+Num+Sg+Ill+Attr:ie%>n FINAL1 ;
+Num+Sg+Ill+Uml:%>%^1UMLan FINAL1 ;
+Num+Sg+Ine:%>esne FINAL1 ;
+Num+Sg+Ine+Attr:%>ene # ;
+Num+Sg+Ela+Attr:%>ede # ;
+Num+Sg+Ela:%>este FINAL1 ;
+Num+Sg+Com:%>ine FINAL1 ;
+Num+Ess:%>ine FINAL1 ;
+Num+Pl+Nom:ie%>h FINAL1 ;
+Num+Pl+Acc:%>ide FINAL1 ;
+Num+Pl+Gen:ie%>j FINAL1 ;
+Num+Pl+Com+Attr:ie%>j FINAL1 ;
+Num+Pl+Ill:%>ide FINAL1 ;
+Num+Pl+Ine:%>ine FINAL1 ;
+Num+Pl+Ela:%>ijste FINAL1 ; !
+Num+Pl+Com:ie%>jgujmie FINAL1 ;

LEXICON LUHKIE-
luhkie:luhkie# AKTEF ;
luhkie:luhkie# 2TO9F ;
låhkede+A+Ord:låhkad ORDTAGODD-ord ;
låhkesh:låhkas COLLTAG ;
!luhkie+Num+Sg+Nom:luhkie FINAL1 ;
!luhkie+Num+Sg+Nom+Use/Sub:-luhkie FINAL1 ;
luhkie:luhk NUMTAGie ;

LEXICON LUHKIEF
!luhkie+Num+Sg+Nom:luhkie FINAL1 ;
luhkie:luhk NUMTAGie ;
luhkie:luhkie# AKTEF ;
luhkie:luhkie# 2TO9F ;
låhkede+A+Ord:låhkad ORDTAGODD-ord ;
låhkesh:låhkas COLLTAG ;










! Ordinal numbers
! ---------------

LEXICON MUBPIE
+Sg+Nom:ie FINAL1 ;
+Sg+Acc:ie%>m FINAL1 ;
+Sg+Gen:ie%>n FINAL1 ;
! +Sg+Ill:%>an FINAL1 ; !SUB - is this possible? IllSg without Uml in -ie?
+Sg+Ill+Uml:%>%^1UMLan FINAL1 ;
+Sg+Ine:e%>sne FINAL1 ;
+Sg+Ine+Attr:e%>ne # ;
+Sg+Ela+Attr:e%>de # ;
+Sg+Ela:e%>ste FINAL1 ;
+Sg+Com:%>ine FINAL1 ;
+Sg+Com+Uml:%>%^iUMLine FINAL1 ;
+Pl+Nom:ie%>h FINAL1 ;
+Pl+Acc:%>ide FINAL1 ;
+Pl+Acc+Uml:%>%^iUMLide FINAL1 ;
+Pl+Gen:ie%>j FINAL1 ;
+Pl+Gen+Uml:%>%^iUMLi FINAL1 ;
+Pl+Ill:%>ide FINAL1 ;
+Pl+Ill+Uml+Use/NG:%>%^iUMLide FINAL1 ;
+Pl+Ine:%>ine FINAL1 ;
+Pl+Ine+Uml+Use/NG:%>%^iUMLine FINAL1 ;
+Pl+Ela:%>ijste FINAL1 ;
+Pl+Ela+Uml+Use/NG:%>%^iUMLijste FINAL1 ;
+Pl+Com:ie%>jgujmie FINAL1 ;
+Pl+Com+Uml+Use/NG:%>%^iUMLigujmie FINAL1 ;
+Ess:%>ine FINAL1 ;
+Ess+Uml+Use/NG:%>%^iUMLine FINAL1 ;

LEXICON ORDTAGODD-ord
+Attr:e%^DISIMP FINAL1 ;
 ORDTAGODD ;

LEXICON ORDTAGODD
+Sg+Nom:e%^DISIMP FINAL1 ;
+Sg+Acc:e%>%^DISIMPm FINAL1 ;
+Sg+Gen:e%>%^DISIMPn FINAL1 ;
+Sg+Ill:%>asse FINAL1 ; !Rule to delete Jesuse%>asse > Jesusasse
+Sg+Ine+Attr:%>inie # ;
+Sg+Ela+Attr:%>ihtie # ;
+Sg+Ine:%>isnie FINAL1 ;
+Sg+Ela:%>istie FINAL1 ;
+Sg+Com:%>inie FINAL1 ;
+Pl+Nom:%>%^DISIMPh FINAL1 ;
+Pl+Acc:%>idie FINAL1 ;
+Pl+Gen:%>%^DISIMPi FINAL1 ;
+Pl+Ill:%>idie FINAL1 ;
+Pl+Ine:%>inie FINAL1 ;
+Pl+Ela:%>ijstie FINAL1 ;
+Pl+Com:%>%^DISIMPigujmie FINAL1 ;
+Pl+Com+Use/Sub:%>%^DISIMPigyjmie FINAL1 ; !
+Ess:%>inie FINAL1 ;

LEXICON ORDTAGEVEN-ord
+Attr:e FINAL1 ;
 ORDTAGEVEN ;


LEXICON ORDTAGEVEN
+Sg+Nom:e FINAL1 ;
+Sg+Acc:%>em FINAL1 ;
+Sg+Gen:%>en FINAL1 ;
+Sg+Ill:%>ese FINAL1 ;
+Sg+Ine:%>esne FINAL1 ;
+Sg+Ine+Attr:%>ene # ;
+Sg+Ela+Attr:%>ede # ;
+Sg+Ela:%>este FINAL1 ;
+Sg+Com:%>ine FINAL1 ;
+Ess:%>ine FINAL1 ;
+Pl+Nom:%>h FINAL1 ;
+Pl+Acc:%>ide FINAL1 ;
+Pl+Gen:%>i FINAL1 ;
+Pl+Ill:%>ide FINAL1 ;
+Pl+Ine:%>ine FINAL1 ;
+Pl+Ela:%>ijste FINAL1 ;
+Pl+Com:%>ijgujmie FINAL1 ; 
+Pl+Com+Use/Sub:%>igyjmie FINAL1 ;



!  Collection numbers
! ---------------



LEXICON COLLTAG
+N+Coll+Pl+Nom:%>%^DISIMPh FINAL1 ;
+N+Coll+Pl+Acc:%>idie FINAL1 ;
+N+Coll+Pl+Gen:%>%^DISIMPi FINAL1 ;
+N+Coll+Pl+Ill:%>idie FINAL1 ;
+N+Coll+Pl+Ine:%>inie FINAL1 ;
+N+Coll+Pl+Ela:%>ijstie FINAL1 ;
+N+Coll+Pl+Com:%>%^DISIMPigujmie FINAL1 ;
+N+Coll+Pl+Com+Use/Sub:%>%^DISIMPigyjmie FINAL1 ;
+N+Coll+Ess:%>inie FINAL1 ;

! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES
§+Use/Circ:§ ARABIC ;       ! §24
§§+Use/Circ:§§ ARABIC ;     ! §§24
§% +Use/Circ:§%  ARABIC ;   ! § 24
§§% +Use/Circ:§§%  ARABIC ; ! §§ 24
%-+Use/Circ:%- ARABIC ;     ! -24 U+002D / regular hyphen
%‒+Use/Circ:%‒ ARABIC ;     ! -24 U+2012 / figure dash
%–+Use/Circ:%– ARABIC ;     ! -24 U+2013 / en dash
%—+Use/Circ:%— ARABIC ;     ! -24 U+2014 / em dash
%―+Use/Circ:%― ARABIC ;     ! -24 U02015 / horizontal bar
%++Use/Circ:%+ ARABIC ;     ! <plus>24
%-% +Use/Circ:%-%  ARABIC ; ! - 24
%‒% +Use/Circ:%‒%  ARABIC ; ! - 24 U+2012
%–% +Use/Circ:%–%  ARABIC ; ! - 24 U+2013
%—% +Use/Circ:%—%  ARABIC ; ! - 24 U+2014
%―% +Use/Circ:%―%  ARABIC ; ! - 24 U+2015
%+% +Use/Circ:%+%  ARABIC ; ! <plus> 24
%*+Use/Circ:%* ARABIC ;     ! *24
%$+Use/Circ:%$ ARABIC ;     ! $24
%€+Use/Circ:%€ ARABIC ;     ! €24
%<+Use/Circ:%[%<%] ARABIC ; ! <24
%>+Use/Circ:%[%>%] ARABIC ; ! >24
s%.+Use/Circ:s%. ARABIC ;   ! s.24

LEXICON ARABIC
< [1|2|3|4|5|6|7|8|9|%0] >   ARABICLOOP ;
< [1|2|3|4|5|6|7|8|9|%0] >   ARABICLOOPORD ;
< [1|2|3|4|5|6|7|8|9|%0] >   ARABICLOOPCOLL ;

LEXICON ARABICLOOP
+Use/Circ: ARABIC ;
%-+Use/Circ:%- ARABIC ;            ! 1-2    multipart numbers
%–+Use/Circ:%– ARABIC ;            ! 1–2    multipart numbers
%—+Use/Circ:%— ARABIC ;            ! 1—2    multipart numbers
%.%-+Use/Circ:%.%- ARABIC ;        ! 1.-2   multipart numbers
%.%–+Use/Circ:%.%– ARABIC ;        ! 1.–2   multipart numbers
%.%—+Use/Circ:%.%— ARABIC ;        ! 1.—2   multipart numbers
%-% +Use/Circ:%-%  ARABIC ;        ! 1- 2   multipart numbers
%–% +Use/Circ:%–%  ARABIC ;        ! 1– 2   multipart numbers
%—% +Use/Circ:%—%  ARABIC ;        ! 1— 2   multipart numbers
% %-+Use/Circ:% %- ARABIC ;        ! 1 -2   multipart numbers
% %–+Use/Circ:% %– ARABIC ;        ! 1 –2   multipart numbers
% %—+Use/Circ:% %— ARABIC ;        ! 1 —2   multipart numbers
% %-% +Use/Circ:% %-%  ARABIC ;    ! 1 - 2  multipart numbers
% %–% +Use/Circ:% %–%  ARABIC ;    ! 1 – 2  multipart numbers
% %—% +Use/Circ:% %—%  ARABIC ;    ! 1 — 2  multipart numbers
% %-%-% +Use/Circ:% %-%-%  ARABIC ; ! 1 -- 2 multipart numbers
% %–%–% +Use/Circ:% %–%–%  ARABIC ; ! 1 –– 2 multipart numbers
% %—%—% +Use/Circ:% %—%—%  ARABIC ; ! 1 —— 2 multipart numbers
% %- ARABICDELIMITER ;             ! 1 -    multipart numbers
% %– ARABICDELIMITER ;             ! 1 –    multipart numbers
% %— ARABICDELIMITER ;             ! 1 —    multipart numbers
% %-%- ARABICDELIMITER ;           ! 1 --   multipart numbers
% %–%– ARABICDELIMITER ;           ! 1 ––   multipart numbers
% %—%— ARABICDELIMITER ;           ! 1 ——   multipart numbers
%.+Use/Circ:%. ARABIC ;
%,+Use/Circ:%, ARABIC ;
% +Use/Circ:%  ARABIC ; ! Allowing for "23 500".
 +Use/Circ:   ARABIC ; ! Allowing for "23 500" w/nbsp (initial char is alt-space)
%/+Use/Circ:%/ ARABIC ; ! 24/12
!%'             ARABIC ; ! Since ' denotes cons.grad. This is not optimal either. 
!+A+Ord:%. # ;
,%-+Num:,%- # ;   ! 10,-
,%–+Num:,%– # ;   ! 10,–
,%—+Num:,%— # ;   ! 10,—
%%+Num+Nom:%% # ;  ! 50%. It is wrong, but 75 % of us write "75%".
% %%+Num+Nom:% %% # ; ! We must analyse the correct ones as well... 50 % etc.
ARABICDELIMITER ; ! list of number-case delimiters
+Num: RHyph  ;          ! Hyphen and +Use/Circ added appropriately in RHyph

LEXICON ARABICLOOPORD
+A+Ord:%. # ;
+A+Ord: ARABICDELIMITERORD ;

LEXICON ARABICLOOPCOLL
+N+Coll: ARABICDELIMITERCOLL ;

LEXICON ARABICDELIMITER
+Num:           ARABICCASE0 ;   ! This is the 1984 case (ie nominative).
+Num:%:         ARABICCASE ;    ! This is the 1984:s case.
+Num+Use/Sub:%> ARABICCASE ;    ! This is the 1984s case.
+Num+Use/Sub:%' ARABICCASE ;    ! This is the 1984's case.
+Num+Use/Sub:%- ARABICCASE ;    ! This is the 1984-s case.
              
LEXICON ARABICDELIMITERORD
:%:         ARABICCASEORD ; ! This is the 1984:s case.
+Use/Sub:%> ARABICCASEORD ; ! This is the 1984s case.
+Use/Sub:%' ARABICCASEORD ; ! This is the 1984's case.
+Use/Sub:%- ARABICCASEORD ; ! This is the 1984-s case.

LEXICON ARABICDELIMITERCOLL
:%:         ARABICCASECOLL ; ! This is the 1984:s case.
+Use/Sub:%> ARABICCASECOLL ; ! This is the 1984s case.
+Use/Sub:%' ARABICCASECOLL ; ! This is the 1984's case.
+Use/Sub:%- ARABICCASECOLL ; ! This is the 1984-s case.

                     
LEXICON ARABICCASE0
+Sg+Nom: # ;

LEXICON ARABICCASE
f+Sg+Nom+Use/Sub:f  # ; ! s. 123f.
ff+Sg+Nom+Use/Sub:ff # ; ! s. 123ff.
b+Sg+Nom+Use/Sub:b  # ; ! § 123b
c+Sg+Nom+Use/Sub:c  # ; ! § 123c
d+Sg+Nom+Use/Sub:d  # ; ! § 123d
e+Sg+Nom+Use/Sub:e  # ; ! § 123e
+Sg+Gen:n          # ;
+Sg+Acc:         # ;
+Sg+Ill:se         # ;
+Sg+Ine:sne        # ; ! NRK:sne
+Sg+Ela:ste        # ; ! NRK:ste
+Sg+Com:ine        # ;
+Ess:ine           # ; ! NBR:ine
+Pl+Nom:h          # ;
+Pl+Gen:j          # ; 
+Pl+Acc:ide        # ; 
+Pl+Ill:ide        # ; 
+Pl+Ine:ine        # ; 
+Pl+Ela:jste       # ; 
+Pl+Com:jgujmie    # ; 
!ordinals

LEXICON ARABICCASEORD
+Sg+Nom:%>de          # ;
+Sg+Gen:%>den         # ;
+Sg+Acc:%>dem         # ;
+Sg+Ill:%>dasse       # ;
+Sg+Ine+Attr:%>dinie  # ;
+Sg+Ela+Attr:%>dihtie # ;
+Sg+Ine:%>disnie      # ;
+Sg+Ela:%>distie      # ;
+Sg+Com:%>dinie       # ;
+Sg+Ess:%>dinie       # ;
+Pl+Nom:%>dh          # ;
+Pl+Gen:%>di          # ;
+Pl+Acc:%>didie       # ;
+Pl+Ill:%>didie       # ;
+Pl+Ine:%>dinie       # ;
+Pl+Ela:%>dijstie     # ;
+Pl+Com:%>digujmie    # ;

LEXICON ARABICCASECOLL
+Pl+Nom:%>sh       # ;
+Pl+Acc:%>sidie    # ;
+Pl+Gen:%>si       # ;
+Pl+Ill:%>sidie    # ;
+Pl+Ine:%>sinie    # ;
+Pl+Ela:%>sijstie  # ;
+Pl+Com:%>sigujmie # ;
 +Ess:%>sinie    # ;


! The Roman numerals !
! ------------------ !

LEXICON ROMAN!
+Use/Circ: ROM-THOUSAND ; !
+Use/Circ: ROM-HUNDRED ; !
+Use/Circ: ROM-TEN ; !
+Use/Circ: ROM-ONE ; !

LEXICON ROM-THOUSAND !
M+Use/Circ:M ROM-THOUSAND-TAG; !
MM+Use/Circ:MM ROM-THOUSAND-TAG; !
MMM+Use/Circ:MMM ROM-THOUSAND-TAG; !
MMMM+Use/Circ:MMMM ROM-THOUSAND-TAG; !
MMMMM+Use/Circ:MMMMM ROM-THOUSAND-TAG; !

LEXICON ROM-THOUSAND-TAG !
+Use/Circ: ROMNUMTAG ; !
+Use/Circ: ROM-HUNDRED ; !
+Use/Circ: ROM-TEN ;!
+Use/Circ: ROM-ONE ;!
+Use/Circ: ROM-SPLIT ; !

LEXICON ROM-HUNDRED !
C+Use/Circ:C ROM-HUNDRED-TAG; !
CC+Use/Circ:CC ROM-HUNDRED-TAG; !
CCC+Use/Circ:CCC ROM-HUNDRED-TAG; !
CD+Use/Circ:CD ROM-HUNDRED-TAG; !
D+Use/Circ:D ROM-HUNDRED-TAG; !
DC+Use/Circ:DC ROM-HUNDRED-TAG; !
DCC+Use/Circ:DCC ROM-HUNDRED-TAG; !
DCCC+Use/Circ:DCCC ROM-HUNDRED-TAG; !
CM+Use/Circ:CM ROM-HUNDRED-TAG; !

LEXICON ROM-HUNDRED-TAG !
+Use/Circ: ROMNUMTAG ; !
+Use/Circ: ROM-TEN ; !
+Use/Circ: ROM-ONE ;!
+Use/Circ: ROM-SPLIT ; !

LEXICON ROM-TEN!
X+Use/Circ:X ROM-TEN-TAG; !
XX+Use/Circ:XX ROM-TEN-TAG; !
XXX+Use/Circ:XXX ROM-TEN-TAG; !
XL+Use/Circ:XL ROM-TEN-TAG; !
L+Use/Circ:L ROM-TEN-TAG; !
LX+Use/Circ:LX ROM-TEN-TAG; !
LXX+Use/Circ:LXX ROM-TEN-TAG; !
LXXX+Use/Circ:LXXX ROM-TEN-TAG; !
XC+Use/Circ:XC ROM-TEN-TAG; !

LEXICON ROM-TEN-TAG !
+Use/Circ: ROMNUMTAG ; !
+Use/Circ: ROM-ONE ; !
+Use/Circ: ROM-SPLIT ; !

LEXICON ROM-ONE!
I+Use/Circ:I ROM-ONE-TAG; !
II+Use/Circ:II ROM-ONE-TAG; !
III+Use/Circ:III ROM-ONE-TAG; !
IV+Use/Circ:IV ROM-ONE-TAG; !
V+Use/Circ:V ROM-ONE-TAG; !
VI+Use/Circ:VI ROM-ONE-TAG; !
VII+Use/Circ:VII ROM-ONE-TAG; !
VIII+Use/Circ:VIII ROM-ONE-TAG; !
IX+Use/Circ:IX ROM-ONE-TAG; !

LEXICON ROM-ONE-TAG !
+Use/Circ: ROMNUMTAG ; !
! +N: ROMNUMTAG ; ! !The Olav viđeš fix (Roman V now gets A)
+Use/Circ: ROM-SPLIT ; !

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the
! trick.

LEXICON ROM-SPLIT !
+Use/Circ:%- 2ROMAN ; ! II-VI, etc. !


! Here goes loop 2.

LEXICON 2ROMAN!
+Use/Circ: 2ROM-THOUSAND ; !
+Use/Circ: 2ROM-HUNDRED ; !
+Use/Circ: 2ROM-TEN ; !
+Use/Circ: 2ROM-ONE ; !

LEXICON 2ROM-THOUSAND !
M+Use/Circ:M 2ROM-THOUSAND-TAG; !
MM+Use/Circ:MM 2ROM-THOUSAND-TAG; !
MMM+Use/Circ:MMM 2ROM-THOUSAND-TAG; !
MMMM+Use/Circ:MMMM 2ROM-THOUSAND-TAG; !
MMMMM+Use/Circ:MMMMM 2ROM-THOUSAND-TAG; !

LEXICON 2ROM-THOUSAND-TAG !
+Use/Circ: ROMNUMTAG ; !
+Use/Circ: 2ROM-HUNDRED ; !
+Use/Circ: 2ROM-TEN ; !
+Use/Circ: 2ROM-ONE ;!

LEXICON 2ROM-HUNDRED !
C+Use/Circ:C 2ROM-HUNDRED-TAG; !
CC+Use/Circ:CC 2ROM-HUNDRED-TAG; !
CCC+Use/Circ:CCC 2ROM-HUNDRED-TAG; !
CD+Use/Circ:CD 2ROM-HUNDRED-TAG; !
D+Use/Circ:D 2ROM-HUNDRED-TAG; !
DC+Use/Circ:DC 2ROM-HUNDRED-TAG; !
DCC+Use/Circ:DCC 2ROM-HUNDRED-TAG; !
DCCC+Use/Circ:DCCC 2ROM-HUNDRED-TAG; !
CM+Use/Circ:CM 2ROM-HUNDRED-TAG; !

LEXICON 2ROM-HUNDRED-TAG !
+Use/Circ: ROMNUMTAG ; !
+Use/Circ: 2ROM-TEN ; !
+Use/Circ: 2ROM-ONE ;!

LEXICON 2ROM-TEN !
X+Use/Circ:X 2ROM-TEN-TAG; !
XX+Use/Circ:XX 2ROM-TEN-TAG; !
XXX+Use/Circ:XXX 2ROM-TEN-TAG; !
XL+Use/Circ:XL 2ROM-TEN-TAG; !
L+Use/Circ:L 2ROM-TEN-TAG; !
LX+Use/Circ:LX 2ROM-TEN-TAG; !
LXX+Use/Circ:LXX 2ROM-TEN-TAG; !
LXXX+Use/Circ:LXXX 2ROM-TEN-TAG; !
XC+Use/Circ:XC 2ROM-TEN-TAG; !

LEXICON 2ROM-TEN-TAG !
+Use/Circ: ROMNUMTAG ; !
+Use/Circ: 2ROM-ONE ; !

LEXICON 2ROM-ONE !
I+Use/Circ:I 2ROM-ONE-TAG; !
II+Use/Circ:II 2ROM-ONE-TAG; !
III+Use/Circ:III 2ROM-ONE-TAG; !
IV+Use/Circ:IV 2ROM-ONE-TAG; !
V+Use/Circ:V 2ROM-ONE-TAG; !
VI+Use/Circ:VI 2ROM-ONE-TAG; !
VII+Use/Circ:VII 2ROM-ONE-TAG; !
VIII+Use/Circ:VIII 2ROM-ONE-TAG; !
IX+Use/Circ:IX 2ROM-ONE-TAG; !

LEXICON 2ROM-ONE-TAG
+Use/Circ: ROMNUMTAG ;

LEXICON ROMNUMTAG !
+Num+Nom: # ;
+A+Ord:%. # ; ! !III. as ordinal.
+A+Ord:   # ;! !Looking for trouble...
+Num:%:         ROMNUMTAGOBL ; ! IV:se
+Num+Use/Sub:'7 ROMNUMTAGOBL ; ! IV'se
+Num+Use/Sub:%- ROMNUMTAGOBL ; ! IV-se
+Num+Use/Sub:%´ ROMNUMTAGOBL ; ! IV´se
+Num+Use/Sub:%> ROMNUMTAGOBL ; ! IVse

LEXICON ROMNUMTAGOBL !
+Gen:n # ; ! But here 123a 123
+Acc: # ;
+Ill:se # ;
+Ine:sne # ;
+Ine+Attr:ne # ;
+Ela+Attr:de # ;
+Ela:ste # ;
+Com:ine # ;
+Ess:ine # ;

LEXICON NUMBERLIKE
gellie+Num+Sg:gell muvhtiesg ;
gellie+Num+Pl:gell muvhtiepl ;

!gellie+Num+Sg:gell N_IE_SG ; ! as sme máŋga
!gellie+Num+Sg+Ine+Attr:gelle%>ne # ;
!gellie+Num+Sg+Ela+Attr:gelle%>de # ;
!gellie+Num+Pl+Nom:gellie%>h # ; ! are there other cases of Pl? 


! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !


LEXICON ISOLATED-NUMEXP
½+Num:½ # ;
¹+Num:¹ # ;
²+Num:² # ;
³+Num:³ # ;
¼+Num:¼ # ;
¾+Num:¾ # ;


LEXICON COLLECTIVE !Some collective numbers that do not fit in anywhere else
gööktege+N+Coll:göökteg ORDTAGODD ;
guaktege+N+Coll:guaktag ORDTAGODD ;
golmege+N+Coll:golmeg ORDTAGODD ;
gåalmege+N+Coll:gåalmag ORDTAGODD ;
njealjege+N+Coll:njealjag ORDTAGODD ;
vïjhtege+N+Coll:vïjhteg ORDTAGODD ;
govhtege+N+Coll:govhteg ORDTAGODD ;
tjïjhtjege+N+Coll:tjïjhtjeg ORDTAGODD ;
gaaktsege+N+Coll:gaaktsag ORDTAGODD ;
åktsege+N+Coll:åktsag ORDTAGODD ;
låhkege+N+Coll:låhkag ORDTAGODD ;

LEXICON 2TO9LUHKIE
göökte:göökte# LUHKIE- ;
golme:golme# LUHKIE- ;
njieljie:njieljie# LUHKIE- ;
njielje:njielje# LUHKIE- ;
nieljie:nieljie# LUHKIE- ;
nielje:nielje# LUHKIE- ;
vïjhte:vïjhte# LUHKIE- ;
govhte:govhte# LUHKIE- ;
tjïjhtje:tjïjhtje# LUHKIE- ;
gaektsie:gaektsie# LUHKIE- ;
uktsie:uktsie# LUHKIE- ;

LEXICON 2TO9TJUETIE
göökte:göökte# GTJUETIE ;
golme:golme# GTJUETIE ;
njieljie:njieljie# GTJUETIE ;
njielje:njielje# GTJUETIE ;
nieljie:nieljie# GTJUETIE ;
nielje:nielje# GTJUETIE ;
vïjhte:vïjhte# GTJUETIE ;
govhte:govhte# GTJUETIE ;
tjïjhtje:tjïjhtje# GTJUETIE ;
gaektsie:gaektsie# GTJUETIE ;
uktsie:uktsie# GTJUETIE ;

LEXICON GTJUETIE
tjuetie LUHKIEF ;
tjuetie:tjuetie# 2TO9LUHKIE ;
tjuetie:tjuetie# AKTEF ;
tjuetie:tjuetie# 2TO9F ;
!tjuetie+Num+Sg+Nom:tjuetie FINAL1 ;
tjuetie:tjuet NUMTAGie ;
tjuatede+A+Ord:tjuatad ORDTAGODD ;
nïmme LUHKIEF ;
nïmme:nïmme# 2TO9LUHKIE ;
nïmme:nïmme# AKTEF ;
nïmme:nïmme# 2TO9F ;
nïmme:nïmm NUMTAGe_NOTakte ;
nymme LUHKIEF ;
nymme:nymme# 2TO9LUHKIE ;
nymme:nymme# AKTEF ;
nymme:nymme# 2TO9F ;
nymme:nymm NUMTAGe_NOTakte ;
nomme LUHKIEF ;
nomme:nomme# 2TO9LUHKIE ;
nomme:nomme# AKTEF ;
nomme:nomme# 2TO9F ;
nomme:nomm NUMTAGe_NOTakte ;
stoereluhkie:stoere#luhkie# LUHKIEF ;
stoereluhkie:stoere#luhkie# 2TO9LUHKIE ;
stoereluhkie:stoere#luhkie# AKTEF ;
stoereluhkie:stoere#luhkie# 2TO9F ;
!stoere#luhkie+Num+Sg+Nom:stoere#luhkie FINAL1 ;
stoereluhkie:stoere#luhk NUMTAGie ;
stoereluhkie+Use/NG:stoerre#luhkie# LUHKIEF ;
stoereluhkie+Use/NG:stoerre#luhkie# 2TO9LUHKIE ;
stoereluhkie+Use/NG:stoerre#luhkie# AKTEF ;
stoereluhkie+Use/NG:stoerre#luhkie# 2TO9F ;
!stoerre#luhkie+Num+Sg+Nom:stoerre#luhkie FINAL1 ;
stoerre#luhkie:stoerre#luhk NUMTAGie ;

LEXICON TJUETIE
!tjuetie+Num+Sg+Nom:tjuetie FINAL1 ;
tjuetie:tjuet NUMTAGie ;
tjuetie:tjuetie# LUHKIEF ;
tjuetie:tjuetie# LUHKIE- ;
tjuetie:tjuetie# 2TO9LUHKIE ;
tjuetie:tjuetie# AKTEF ;
tjuetie:tjuetie# 2TO9F ;
tjuatede+A+Ord:tjuatad ORDTAGODD ;
!akte#tjuetie+Num+Sg+Nom:akte#tjuetie FINAL1 ;
aktetjuetie:akte#tjuet NUMTAGie ;
aktetjuetie:akte#tjuetie# LUHKIEF ;
aktetjuetie:akte#tjuetie# LUHKIE- ;
aktetjuetie:akte#tjuetie# 2TO9LUHKIE ;
aktetjuetie:akte#tjuetie# AKTEF ;
aktetjuetie:akte#tjuetie# 2TO9F ;
nïmme:nïmm NUMTAGe_NOTakte ;
nïmme:nïmme# LUHKIEF ;
nïmme:nïmme# LUHKIE- ;
nïmme:nïmme# 2TO9LUHKIE ;
nïmme:nïmme# AKTEF ;
nïmme:nïmme# 2TO9F ;
nymme:nymm NUMTAGe_NOTakte ;
nymme:nymme# LUHKIEF ;
nymme:nymme# LUHKIE- ;
nymme:nymme# 2TO9LUHKIE ;
nymme:nymme# AKTEF ;
nymme:nymme# 2TO9F ;
nomme:nomm NUMTAGe_NOTakte ;
nomme:nomme# LUHKIEF ;
nomme:nomme# LUHKIE- ;
nomme:nomme# 2TO9LUHKIE ;
nomme:nomme# AKTEF ;
nomme:nomme# 2TO9F ;
!stoere#luhkie+Num+Sg+Nom:stoere#luhkie FINAL1 ;
stoereluhkie:stoere#luhk NUMTAGie ;
stoereluhkie:stoere#luhkie# LUHKIEF ;
stoereluhkie:stoere#luhkie# LUHKIE- ;
stoereluhkie:stoere#luhkie# 2TO9LUHKIE ;
stoereluhkie:stoere#luhkie# AKTEF ;
stoereluhkie:stoere#luhkie# 2TO9F ;
!stoerreluhkie+Num+Sg+Nom:stoerre#luhkie FINAL1 ;
stoerreluhkie:stoerre#luhk NUMTAGie ;
stoerreluhkie:stoerre#luhkie# LUHKIEF ;
stoerreluhkie:stoerre#luhkie# LUHKIE- ;
stoerreluhkie:stoerre#luhkie# 2TO9LUHKIE ;
stoerreluhkie:stoerre#luhkie# AKTEF ;
stoerreluhkie:stoerre#luhkie# 2TO9F ;



!LEXICON NUMERALCOMPOUNDS ! numeral as first part 
! NAMÁK ; ! tuvsánjienat, logigielat, etc. 
! SASJ ; 
! NounSecond ;! golmmačiegahas
! geardásaš:geardás DABALAS ; 
! !tuvsán#geardásaš 
! jahkásaš:jahkás DABALAS ; 


! Imprecise numbers
! -----------------

!LEXICON num-imprecise
