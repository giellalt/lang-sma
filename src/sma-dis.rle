# Distributed under the terms of the GNU General Public License version 2 
# or any later version.

# =================================================================== #
# =================================================================== #
#            S O U T H   S Á M I   D I S A M B I G U A T O R          #
# =================================================================== #
# =================================================================== #

            # =========================================== #
            #            Short table of contents          #
            # =========================================== #

            # Delimiters
            # Tags and sets
	        # Mapping rules
            # Disambiguation rules
              # One-cohort disambiguation - cycle 0
              # Local disambiguation - cycles 1 and 2
              # Cycle 2: Other local disambiguation         
              # Cycle 3: Global disambiguation 
	          # Cycle 4: Syntactic disambiguation 
	          # Cycle 5: Post-syntactic morphological disambiguation


# ========== #
# Delimiters #
# ========== #


DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>";


# ============= #
# Tags and sets #
# ============= #



LIST BOS = (>>>) (<s>); 
LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.

# Tags declared as single-membered LISTs 
# ======================================

LIST N = N ;
LIST Sg = Sg ;
LIST Pl = Pl ;
LIST Nom = Nom ;
LIST Acc = Acc ;
LIST Gen = Gen ;
LIST Ine = Ine ;
LIST Ela = Ela ;
LIST Ill = Ill ;
LIST Com = Com ;
LIST Ess = Ess ;

LIST PxSg1 = PxSg1 ;
LIST PxSg2 = PxSg2 ;
LIST PxSg3 = PxSg3 ;

LIST PxDu1 = PxDu1 ;
LIST PxDu2 = PxDu2 ;
LIST PxDu3 = PxDu3 ;

LIST PxPl1 = PxPl1 ;
LIST PxPl2 = PxPl2 ;
LIST PxPl3 = PxPl3 ;

LIST V = V ;
LIST Prs = Prs ;
LIST Prt = Prt ;

LIST Sg1 = Sg1 ;
LIST Sg2 = Sg2 ;
LIST Sg3 = Sg3 ;

LIST Du1 = Du1 ;
LIST Du2 = Du2 ;
LIST Du3 = Du3 ;

LIST Pl1 = Pl1 ;
LIST Pl2 = Pl2 ;
LIST Pl3 = Pl3 ;

LIST Inf = Inf ;
LIST Neg = Neg ;
LIST ConNeg = ConNeg ;
LIST Foc/gan = Foc/gan ;

LIST PrfPrc = PrfPrc ;
LIST VGen = VGen ;
LIST Ger = Ger ;

LIST Ind = Ind ;

LIST Imp = Imp ;
LIST ImpII = ImpII ;

LIST Pot = Pot ;
LIST Cond = Cond ;
LIST Imprt = Imprt ;

LIST Opt = Opt ;

LIST Actor = Actor ;
LIST Clt = Clt ;
LIST A = A ;
LIST Ord = Ord ;
LIST ABBR = ABBR ;
LIST ACR = ACR ;

LIST Comp = Comp ;
LIST Superl = Superl ;

LIST Attr = Attr ;

LIST Pron = Pron ;
LIST Pers = Pers ;
LIST Dem = Dem ;
LIST Interr = Interr ;
LIST Rel = Rel ;
LIST Adp = Adp ;
LIST Po = Po ;
LIST Pr = Pr ;
LIST Adv = Adv ;
LIST Interj = Interj ;
LIST Indef = Indef ;
LIST Num = Num ;
LIST Prop = Prop ;

LIST Mal = Mal ;
LIST Fem = Fem ;
LIST Sur = Sur ;

LIST CS = CS ;
LIST CC = CC ;
LIST Pcle = Pcle ;
LIST CLB = CLB ;
LIST PUNCT = PUNCT ;
LIST LEFT = LEFT ;
LIST RIGHT = RIGHT ;
LIST COMMA = "," ;
LIST Der1 = Der1 ;
LIST Der2 = Der2 ;
LIST Der3 = Der3 ;

LIST REAL-TITLE = "prof" "cand.philol" ;
LIST OFFICE = "gånka" ;
SET TITLE = REAL-TITLE OR OFFICE ; 

# SET TITLE = REAL-TITLE OR TITLE-LIKE OR PROFESSION OR OFFICE ; 


LIST CASES = Nom Acc Gen Ill Ine Ela Com Ess ;
LIST NUMBER = Sg Pl ;
LIST NOMEN-NOT-PRON = N A Indef Num Ord ;  # they can have Dem as attr

LIST WORD = N A V Adv Pron Interj Adp Num CS CC Pcle Pr Po ;
SET REALWORD = WORD - Num - Ord ;
SET REALWORD-NOTABBR = WORD - Num - Ord - ABBR ;

LIST NOT-ADV-INT-PCLE = N A Num Pron A V CC CS Adp ;

SET NOT-ADV = WORD - Adv ;

SET NP-HEAD = Num OR N OR Pron ;

SET PRE-NP-HEAD = (Prop Attr) OR (Prop @>N) OR (A Attr) OR (ABBR Attr) OR (Pron Pers Gen) OR (N Gen) OR Num OR (Cmpnd) OR CC OR (Pron Dem) OR (Pron Refl Gen) OR (Pron Indef) OR (PrfPrc @>N) OR (PrfPrc @>N) OR (PrsPrc) OR (A Ord) OR Attr OR ("gånka") ;
         # The strict version of items that can only be premodifiers, not parts of the predicate - copied from sme-dis.rle
         
SET NPNH = WORD - PRE-NP-HEAD OR (ABBR) ;       # NPNH  = "NOT-PRE-NP-HEAD" 


# Noun sets

SET PX = PxSg1 OR PxSg2 OR PxSg3 OR PxDu1 OR PxDu2 OR PxDu3 OR PxPl1 OR PxPl2 OR PxPl3 ;

LIST FAMILY = "aahka" "aahkove" "aajja" "aajjove" "aajkoehkadtjh" "aajkohke" "aajnehke" "aarpije" "aehtjaahka" "aehtjaajja" "aehtjie" "baernie" "boelve" "daktere" "eejhtegh" "elkie" "fijrie" "fuelhkie" "gaalla" "geelle" "geellege" "gåeskie" "gåeskuve" "gåetehke" "gåmma" "gåmmebe" "ietnie" "jiekie" "jiekuve" "jijmie" "jijmuve" "jyöne" "jöömmetje" "krist’aehtjie" "krist’ietnie" "laahkoe" "laevie" "laeviehkadtjh" "maadteraahka" "maadteraajja" "maadtoe" "maake" "maaketje" "maana" "meata" "moehrele" "muahra" "månnja" "mænnja" "neapede" "neebne" "nååte" "nïejte" "onnevïelle" "onneåabpa" "seasa" "slïekte" "stoerrevïelle" "stoerreåabpa" "sïesele" "tjidtjaahka" "tjidtjaajja" "tjidtjie" "tjietsie" "tjietsuve" "voehpe" "voehpealmetjh" "vuanove" "vïelle" "vïjve" "åabpa" "åabpetjh" "åerpele" "åerpene" "åerpienadtjh" "vïelletjh" ;





# Verb sets


 # The set REALCOPULAS is smaller than COPULAS, made for verbs with PrfPrc complements: Seammás REALCOPULAS son dovdan iežas...
LIST REALCOPULAS = "lea" ;

# The set COPULAS is for predicative constructions
LIST COPULAS = "lea" ;

SET V-NOT-COP = V - COPULAS ;

LIST MOD-ASP = "galkedh" "tjoeveridh" "tjoeredh" "sååjhtedh" ;

 # Verbs that never have arguments of their own 
 
LIST AUX-OR-MAIN = "aelkedh" "edtjedh" "maehtedh" "sïjhtedh" "sjïdtedh" "båetedh" ;

 # These verbs can take arguments, so they do not belong in the AUX group, 
 # but they are nevertheless mapped to (@+FAUXV). 
 
SET AUX = COPULAS OR Neg OR MOD-ASP OR AUX-OR-MAIN ;


LIST VFIN = Ind OR Imprt ;


LIST INFV = "aavoedidh" "aelkedh" "buektiehtidh" "provhkedh" ; 
# These are often followed by an infitive.

LIST ILLV = "båetedh" "juhtedh" "ringkedh" ;
LIST COMV = "budtedh" "pruvredh" ;

LIST STV = Der/ehte ;

LIST ILLADV = "bæjjese" "davvese" "daase" "dahkoe" "diekie" "doekoe" "dohkoe" "jallese" "mænngese" "noerhtese" "olkese" "vueliebasse" "våålese" "åarjese" "åvtese" ;

LIST INEADV =  "daebpene" "debpene" "duebpene" "dubpene" "daagkoe" "dagkoe" "doegkoe" "dogkoe" "daajkoe" "dajkoe" "doejkoe" "dojkoe" ;

LIST ELAADV = "daebpede" "debpede" "duebpede" "dubpede"  "dåvvese" ;


# BOUNDARY SETS

SET GUKTIE = ("guktie" Adv)  ;

SET S-BOUNDARY = (Pron Interr) OR (Pron Rel) OR ("men") OR ("mohte") OR ("\;") OR (":") OR ("-") OR ("–") OR CS OR GUKTIE OR BOS OR EOS ;

# The following set contains the one-letter abbrs from abbr-sma-lex.txt
LIST SOMESMALLABBR = "a" "r" "c" "d" "e" "f" "f" "g" "h" "i" "l" "m" "b" "j" "s" ;
LIST SOMECAPABBR = "A" "R" "C" "D" "E" "F" "F" "G" "H" "I" "L" "M" "B" "J" "S" ;

# Der sets
LIST V-DER = Der/didh Der/htidh Der/alle Der/stidh Der/ehte Der/lidh Der/htalle Der/lgidh Der/PassS Der/PassL;

LIST N-DER = Der/NomAct Der/NomAg Der/Dim ;

LIST A-DER = Der/ldihkie Der/ijes ;

LIST PASS = Der/PassS Der/PassL ;

SET LEX-V = V - V-DER;

SET LEX-N = N - N-DER;

SET LEX-A = A - A-DER;

LIST VERB-FORMS = Inf Sg1 Sg2 Sg3 ;

# ============== #
# Disambiguation #
# ============== #




SECTION

# Cycle 0

REMOVE:R_Da ("Da") OR ("Dan") (0 Pers);
REMOVE:R_Num Num (0 Neg);
REMOVE:R_ACD ACR (0 CC);


#foreløpig regel: 
REMOVE:R_Interj Interj (NEGATE *0 BOS OR EOS) ; #NOTUSED


REMOVE:testA A-DER (0 LEX-A);

#REMOVE:testV1 Der1 IF (0 Der3 OR Der2);
#REMOVE:testV2 Der2 IF (0 Der3);
#REMOVE:testV3 Der1 ;

#REMOVE:testV4 V-DER (0 LEX-V) ;  ## should be restriction
#REMOVE:testN N-DER (0 LEX-N LINK NOT 0 PX) ;


# Possessive suffix
# Probably exists only for Refl and for kinship terms

REMOVE:PrfPrc_Not_PxSg1 PxSg1 IF (0 PrfPrc);
	## Mijjieh saemieh daarah aaj aalkeme gærjah lohkedh.
	
# Short Pronouns

REMOVE:dan Pers + Sg2 + Nom IF (NOT -1 V + Sg2)(0 ("dïhte" Pron Dem Sg Gen))
	## dan
	# short pronouns are placed after the finite verb.

# Proper nouns

#REMOVE: Nom IF (0 ABBR + Attr)(1 Prop OR ABBR);
REMOVE:R_Nom_ABBR Nom IF (0 ABBR + Attr)(1  Prop OR ABBR);
REMOVE:R_Nom_Prop Nom IF (0 Prop + Attr)(1 Prop OR ABBR);
REMOVE:R_Attr_ABBR Attr IF (0 ABBR + Nom)(NOT 1 Prop OR ABBR);
	## Trond Trosterud

REMOVE:CAPABBR ("<[a-zäöåøæï]>"r) IF  (0 SOMECAPABBR); #NOTUSED does not work

#REMOVE: ("j") IF (0 ("J"));
	
#REMOVE: ("<[a-zäöåøæï]>"r) IF (0 ("<[A-ZÄÖÅØÆÏ]>"r));
	## J. A.	
# Trivialia

REMOVE:Daa ("Daa") IF (0 ("daate"));
REMOVE:Daae ("Daae") IF (0 ("daate"));
	## Daah
	# Problem: Person names Daa, Daae coded as placenames
	
# agreement:
# Dem Sg Nom + N Sg Nom			Dem Pl Nom + N Pl Nom
# Dem Sg Acc + N Sg Acc			Dem Pl Acc + N Pl Acc
# Dem Sg Gen + N Sg Gen			Dem Pl Gen + N Pl Gen
# Dem Sg Ill Attr + N Sg Ill	Dem Pl Ill + N Pl Ill
# Dem Sg Ine + N Sg Ine			Dem	Pl Ine + N Pl Ine
# Dem Sg Ela + N Sg Ela			Dem Pl Ela + N Pl Ela
# Dem Sg Com + N Sg Com			Dem	Pl Com Attr + N Pl Com

SELECT:DemIll (Dem Ill Attr)(*1 (N Sg Ill) BARRIER NPNH) ;
SELECT:DemCom (Dem Com Attr)(*1 (N Pl Com) BARRIER NPNH) ;

SELECT:unifDem $$CASES (0C Dem OR Indef LINK *1 $$CASES BARRIER NPNH LINK 0 NOMEN-NOT-PRON) ;
	## Klaasen tjirrh olkese goevli doj veelkes baeniej gåajkoe, ...
REMOVE:NotPersWhenDem Pers IF (0 Dem + $$NUMBER + $$CASES LINK *1 N + $$NUMBER + $$CASES BARRIER NPNH);
REMOVE:NotPersWhenDemSg Pers IF (0 Dem + Sg3 + $$CASES LINK *1 N + Sg + $$CASES BARRIER NPNH);
REMOVE:NotPersWhenDemPl Pers IF (0 Dem + Pl3 + $$CASES LINK *1 N + Pl + $$CASES BARRIER NPNH);

REMOVE:NotDem Dem IF (0 Pers)(*1 NOMEN-NOT-PRON BARRIER NPNH)(NEGATE 0 $$CASES LINK *1 NOMEN-NOT-PRON LINK 0 $$CASES);
	## Dihte fievsesne jih mov govse. 

REMOVE:NotDem2 Dem IF (0 Pers)(NEGATE *1 NOMEN-NOT-PRON BARRIER NPNH);
	## Dihte joekoen lustes jalhts ajve akten jaepien båeries.


#REMOVE:PronIfNotAgree (Dem Nom) IF (0 Pers)(NEGATE *1 Nom BARRIER NPNH);

SELECT:NGen (N Gen)(-1C (Dem Gen)) ;
SELECT:PronPers (Pron Pers Gen) (0 Pron LINK 1C Po) ;
#SELECT: (Pron Dem Gen) (0 Pron LINK 1C (Ord Gen) OR (Num Gen)) ;
#SELECT: (Pron Dem Pl) (1 (Indef Pl)) ;

#REMOVE: Pron IF (-1 S-BOUNDARY)(0 (Dem Gen))(1 N OR Num OR A);
	

# Verbs

IFF:IsItConNeg ConNeg (*-1 Neg BARRIER ConNeg); 

REMOVE:PrtInNeg Prs IF (0 Neg)(*1C ConNeg + Prt BARRIER NOT-ADV);

REMOVE:PrsInNeg Prt IF (0 Neg)(*1C ConNeg BARRIER NOT-ADV LINK NOT 0 Prt);

SELECT:PersonInNegSg3 Sg3 IF (0 Neg + Prt)(*1C ConNeg + Sg3 BARRIER NOT-ADV);
SELECT:PersonInNegDu1 Du1 IF (0 Neg + Prt)(*1C ConNeg + Du1 BARRIER NOT-ADV);
SELECT:PersonInNegDu2 Du2 IF (0 Neg + Prt)(*1C ConNeg + Du2 BARRIER NOT-ADV); #NOTUSED
SELECT:PersonInNegDu3 Du3 IF (0 Neg + Prt)(*1C ConNeg + Du3 BARRIER NOT-ADV);
SELECT:PersonInNegPl1 Pl1 IF (0 Neg + Prt)(*1C ConNeg + Pl1 BARRIER NOT-ADV);
SELECT:PersonInNegPl2 Pl2 IF (0 Neg + Prt)(*1C ConNeg + Pl2 BARRIER NOT-ADV);
SELECT:PersonInNegPl3 Pl3 IF (0 Neg + Prt)(*1C ConNeg + Pl3 BARRIER NOT-ADV);
REMOVE:NegPrt (Neg Prt)(NEGATE 1 ("lea" ConNeg Prt)) ;
## this is for removing the 'ij' etc. 


# Imperative
#REMOVE:NotImpIfWord Imp IF (*-1 NOT-ADV-INT-PCLE BARRIER CS OR CC);
#REMOVE:NotIndIfImpAndExcl Ind IF (NOT *-1 Neg)(0 Imp)(*1 ("excl") BARRIER Ind);
       ## 

# There can be Interj, VOC, 
SELECT:Imprta Imprt (-1 BOS OR (":") OR Interj)(0 (V Sg2))(NOT *1 (Pers Sg2 Nom) BARRIER S-BOUNDARY) ;
SELECT:Imprtb Imprt (-1 BOS OR (":") OR Interj)(1 COMMA) ;
REMOVE:Imprtc Imprt (-1 Interr OR Adv OR (Pers Sg2))(0 (Prs Sg2)) ;
REMOVE:Imprtd Imprt (*-1 WORD BARRIER CC OR COMMA OR BOS)(NOT 0 Neg)(NEGATE -1 CC LINK *-1 Imprt BARRIER S-BOUNDARY OR V) ;
SELECT:Imprte (Imprt Sg2) (-1 CC LINK *-1C (Imprt Sg2) BARRIER S-BOUNDARY OR V) ;



LIST @CNP = @CNP ;
LIST @CVP = @CVP ;
LIST @+FAUXV = @+FAUXV ; 
LIST @+FMAINV = @+FMAINV ; 
LIST @-FAUXV = @-FAUXV ;
LIST @-FMAINV = @-FMAINV ;
SET MAINV = @-FMAINV OR @+FMAINV ;

	# CC- and CS-Mapping
	# -------------------
# CASES

MAP (@CNP) TARGET CC (0 ("gon")) ;
MAP (@CNP) TARGET CS (0 ("goh") LINK *1 N OR Pers BARRIER NPNH LINK *1 S-BOUNDARY BARRIER V) ;
MAP (@CNP) TARGET CS (0 ("goh") LINK *1 TITLE BARRIER NPNH LINK 1 N LINK *1 S-BOUNDARY BARRIER V) ;
MAP (@>CC) TARGET CC (0 ("dovne")) ;

MAP (@CNP) TARGET CC (-1 $$CASES LINK 2 $$CASES) ;
MAP (@CNP) TARGET CC (-1C Inf LINK 2C Inf) ;
MAP (@CNP) TARGET CC (*-1C (Pl Gen) BARRIER WORD)(1 (Pl Com)) ;

MAP (@CVP) TARGET CS ;
MAP (@CVP) TARGET CC (0 ("mohte") OR ("men")) ;

MAP (@CVP) TARGET CC (*-1 MAINV BARRIER S-BOUNDARY)(*1 MAINV BARRIER S-BOUNDARY);


#
MAP (@+FAUXV) TARGET VFIN (0 Neg OR MOD-ASP) (*1 V BARRIER S-BOUNDARY);
MAP (@+FAUXV) TARGET VFIN (0 COPULAS) ((*1 PrfPrc OR Ger BARRIER S-BOUNDARY OR CC) OR (-1 PrfPrc));
MAP (@+FMAINV) TARGET VFIN (NOT 0 COPULAS) (1 VGen);

MAP (@+FAUXV) TARGET VFIN (0 AUX LINK NOT 0 COPULAS) (*1 V BARRIER S-BOUNDARY OR CC);
MAP (@+FMAINV) TARGET VFIN (0 COPULAS)(NOT *1 PrfPrc OR Ger BARRIER S-BOUNDARY);
MAP (@+FAUXV) TARGET VFIN (0 MOD-ASP OR Neg);
MAP (@-FAUXV) TARGET AUX (0 Inf OR ConNeg)(*1 Inf BARRIER S-BOUNDARY OR CC);

MAP (@+FMAINV) TARGET VFIN IF (NOT 0 AUX) ;
MAP (@+FMAINV) TARGET VFIN IF (0 AUX LINK NOT *1 Inf OR PrfPrc BARRIER S-BOUNDARY) ;

MAP (@-FMAINV) TARGET Ger OR ConNeg OR PrfPrc OR Inf ;
#


# PrfPrc
#SELECT: PrfPrc (*-1 ("lea") BARRIER V OR S-BOUNDARY) ;
SELECT:PrfPrs_DerNomAct PrfPrc (0 (Der/NomAct) LINK *-1 S-BOUNDARY BARRIER V)(NEGATE *1 V BARRIER S-BOUNDARY) ;
	## Disse maahtam tjaeledh gosse sijhtem, jih aaj lohkedh gosse båarasåbpoe  sjidteme.





# Person
# leah Prs Sg2 = Pl3
SELECT:VSg2IfPersSg2a (V Sg2) (*-1 (Pers Nom Sg2) OR (Rel Sg Nom) BARRIER V OR Nom);
SELECT:VSg2IfPersSg2b (V Sg2) (1 (Pers Nom Sg2));
SELECT:Pl3WhenPlSubj (V Pl3) IF (*-1 (N Pl Nom) OR (Num Sg Nom) OR (Pers Nom Pl3) BARRIER Sg2 OR S-BOUNDARY)(0 ("lea" V));
SELECT:Pl3WhenSubjCC (V Pl3) IF (-1 (N Sg Nom) LINK -1 CC)(0 ("lea" V));
REMOVE:Pl3 (V Pl3) (NOT *0 Nom BARRIER S-BOUNDARY) ;
SELECT:Sg3WhenRelSg (V Sg3) (-1 (Rel Sg Nom));
SELECT:Sg3WhenSg3toLeft (V Sg3) (-1 CC LINK *-1C (V Sg3) BARRIER S-BOUNDARY OR V);


SELECT:InfIfInfv (V Inf) IF (*-1 INFV OR AUX BARRIER S-BOUNDARY)(0 Ind) ; ; 
# Should be BARRIER V OR CS OR CC # It doesn´t work...
	# Manne båatam sagke buerebe datneste jieledh, Karijuse.

SELECT:PrfPrcIfLea PrfPrc (*-1 ("lea") BARRIER V);


SELECT:PlIfPlSubj Pl3 IF (*-1 (Pl Nom) BARRIER NOT-ADV);

REMOVE:FocGan Foc/gan (0 (V Du3)) ;
SELECT:Ger Ger (0 (Der/NomAct) LINK *-1 REALCOPULAS BARRIER S-BOUNDARY)

# Contract or not -- removed, we have removed the sma contract verb analysis
# REMOVE:NotContrIfEven (".*ijidh"r) IF (0 (".*[oe]dh"r));		
# This rule might be problematic in light of the empirical work.		     

# Span sentences

"<lea>" REMOVE:lea Pl3 IF (<*-1 Sg1 OR Sg2)(NOT *-1 (N Pl Nom));

# Nomen

REMOVE:No_Px PX IF (0 N)(NOT 0 FAMILY);


#SELECT: Attr IF (0 Prop)(1 Prop); # Too hard.
REMOVE:PropAttr (Prop Attr) IF (NOT 1 Prop);

SELECT:GenToAdp Gen IF (1 Adp);
REMOVE:NotNomToAdp Nom IF (1 Adp);

SELECT:GenIfLeaNom Gen (1 ("lea" V) LINK 0 Pl3 OR Sg3)(*1 (N Nom) BARRIER NPNH) ; 
	# Dan lin baenieh njaelmesne.


SELECT:AccIfSVOV (Acc Sg) IF (*-1C INFV BARRIER NPNH)(1C V); # consider VFIN for INFV


# Verb or Noun

REMOVE:NotNomIfInf (N Pl Nom) IF (0 Inf)(*-1 Ind or Imp LINK *-1 Nom);
       ## jieledh.

# Dem

	
# CC and CS or Adv
IFF:GoNotCs Pcle (-1 Neg OR ConNeg OR ("sån"))(0 ("goh")); 
	## Ih goh måjhtijh ahte månnoeh daesnie sagke seadtoesåbpoe årroen. # Pcle
	## Dallah goh Jeesuse tjaetseste tjuedtjele, dellie vuajna Elmie rihpesåvva jih Voejkene altasasse goh ledtie suaja. # CS

REMOVE:CSNotVfin CS IF (NOT *-1 VFIN OR ("jis" CS))(0 ("dellie") OR ("dle"));
	##

REMOVE:NotNomIfCs (N Nom) (0 CS)(1 (N Nom)) ;
	# Guktie vielle Baktuse, giehtelh maa!


SELECT:CCdovne CC IF (0 ("dovne"))(*1 ("jïh" CC) BARRIER VFIN);
	## … almetjistie gieh maehtieh saemien dovne tjaaleldh jïh njaalmeldh barkoesijjide tjïeltesne.
	
REMOVE:CsIfNotFuncword Pcle (NEGATE *-1 NOT-ADV-INT-PCLE)(0 CS) ;
	# Jaa, jis damth seekere.....	

REMOVE:NotCsIfCc CS (-1 CC OR ("mij" Nom))(0 Adv) ;
	# Jih dellie jeehti...

REMOVE:NotCsIfBOS CS (NEGATE -1 WORD)(0 Adv) ;

# Adj or Adv
REMOVE:AdvNotA A (0 Adv)(1C V) ;
REMOVE:AdvNotA_Cop A (0 Adv)(-1C V LINK NOT 0 COPULAS)(NEGATE 1 A OR N) ;

SELECT:AAttr (A Attr) (*1C N BARRIER NPNH) ;
REMOVE:NotAAttr (A Attr) (NOT 1 N OR A) ;


# grammatisk ord eller N


# "<altese>" N Sg Nom  Pron Pers Pl3 Gen  Pron Pers Sg Gen PxSg3  Pron Pers Sg3 Gen 

# Remove the "skotunge" reading if not in typical N position
REMOVE:altese_Noun N IF (0 ("altese"))(NOT (1 VFIN + Sg3));
	## Mov altese lea…
	# Bad context, but the philosophy is ok.

# "<daelie>" Adv  N Sg Nom 
REMOVE:daelie N (0 ("daelie" Nom) LINK 1 Nom) ;
	## Daelie Tåamma riejries dåakteren luvnie. 

SELECT:CCCS CC OR CS (*-1 VFIN BARRIER S-BOUNDARY)(*1 VFIN BARRIER S-BOUNDARY);
	## Tåamma gylje juktie bååktjehke dorje.


REMOVE:IllNotVfin VFIN (0 ILLADV)(1 VFIN) ;
REMOVE:IlNotNom Nom (0 ILLADV)(1 VFIN) ;
	## Dah aaj naan aejkien diekie båetieh, mohte eeremes tjidtjie dihte dahkoe vualka.


# N or V

# "<aejkien>" N Sg Gen  V IV Ind Prs Du1 @+FMAINV 
REMOVE:aejkie1 VFIN (0 ("aejkie" Gen) LINK -1 ("naan") OR (Indef Gen) OR ("aerebi") OR (Num Gen) OR Ord);


REMOVE:aejkie2 VFIN (0 ("aejkie" Gen) LINK 1 VFIN);
	## Naan aejkien leah kruana jih jeatjebh aejkien provne.



SELECT:Vfin VFIN (0 (N Gen) LINK 1 S-BOUNDARY)(NEGATE -1 Pr)(NEGATE *-1 VFIN BARRIER S-BOUNDARY);
	## Tjidtjie diekie juhtieji goh aehtjine pruvri.
	
SELECT:golme (Num Gen)(0 ("golme"))(1 (N Gen)) ;
	## Jih die libriem dijpebe, gulhkine tjaeliestibie golmen aejkien jih doeltehtibie.	


# Ger or Der/NomAct
SELECT:GerNotNomAct Ger (NEGATE *-1 V BARRIER S-BOUNDARY)(0 (Der/NomAct) LINK 1 S-BOUNDARY) ;
	## Mov leah aajkohkh, muahrah jih jyønh gieh desnie årroeminie.


# Adj or Indef

SELECT:AdjNotIndef (A Pl) (0 (Indef))(*1 (N Pl) BARRIER S-BOUNDARY) ;
REMOVE:AdjBeforeV A (NEGATE 0 Nom)(1 MAINV) ;
	## Mov aehtjie jijnjem maahta. 

# Num

SELECT:NumAndNum Num IF (-1 CC)(*-2C Num BARRIER V);
SELECT:NumAndNumIne (Num Ine) (0 ("akte") LINK *1 (N Ine) BARRIER NPNH);
SELECT:NumAndOrdNum (Ord Nom)(0 Num LINK 1 N);
	## Manne skuvlesne vijhtede klaassesne.

# Putative contract verb


# Rel or Interr
SELECT:InterrNotRel Interr (0 Rel LINK NEGATE *-1 Interr)(*1 ("?")) ; 
REMOVE:RelNotInterr Interr (0 Rel LINK -1 WORD LINK NOT 0 Interj OR CC OR CS) ;
REMOVE:RelNotInterrQ Interr (NEGATE *1 ("?")) ;

SELECT:RelGen (Rel Gen) (*-1 VFIN BARRIER S-BOUNDARY)(*1 N BARRIER NPNH);
    ## Jaepien 1877 die pruvri aktine man nomme Jonas Mortensen.

# Interj


# Po or Pr
SELECT:PoGena Po (0 Pr)(-1 Gen) ;
REMOVE:Pr Pr (NEGATE *1 N BARRIER V OR S-BOUNDARY) ;
REMOVE:PoGenb Po (NEGATE *-1 Gen BARRIER V OR S-BOUNDARY) ;



# Adv or Po/Pr
SELECT:AdvNotPo Adv (0 Po)(NEGATE -1 Gen) ;
    ## Desnie lim dam mietskem jaepien 1941, vaaksjoeminie ektine mov gåmmine, Henny Bergsland, mij vuelieh tjeeli mejtie Jonetta joejki.


# Illative or genetive
SELECT:IllNotGena Ill (0 Gen)(*1 ILLV BARRIER NOT-ADV);
REMOVE:IllNotGenb Gen (0 Ill)(NOT *-1 Pr BARRIER NPNH)(1 EOS OR S-BOUNDARY);
	## Tåamma fuehpesne gåatan.

SELECT:Ill Ill (*0 ILLV BARRIER S-BOUNDARY OR ILLADV) ;


# Com
SELECT:ektine (N Com)(*-1 ("ektine") BARRIER NPNH) ;
    ## Desnie lim dam mietskem jaepien 1941, vaaksjoeminie ektine mov gåmmine, Henny Bergsland, mij vuelieh tjeeli mejtie Jonetta joejki.

SELECT:ComIfComV Com (*0 COMV BARRIER S-BOUNDARY) ;

# Accusative or illative
SELECT:AccNotIll (N Acc) (0 (N Ill) LINK *1 STV BARRIER NOT-ADV) ;
	## Mov aaj aehtjie gie båanta. Dihte fievsesne fierhten biejjien, govside båhtja, jih gaajhkide juvride biepmedahta.


# special lemmas
SELECT:nomme N (0 ("nomme") LINK 1 Prop) ;
    ## Sara jaepien 1889 Röörosen dajvese bööti jih jaepien 1893 die pruvri aktine man nomme Anders Paulsen
SELECT:giesie ("giesie" Gen)(1 Num);


# Verb person vs. Inf -- moved here in order to have the pronouns disambiguated first.

REMOVE:Inf_Not_Sg2 (V TV Der1 Der/didh Ind Prs Sg2) IF (*-1 VFIN BARRIER Pron + Sg2 + Nom);
REMOVE:Inf_Not_Pl2  (V TV Ind Prt Pl2) IF (*-1 VFIN BARRIER Pron + Pl2 + Nom);

# These rules are too weak. I flip them around:
REMOVE:Sg2_Not_Inf Inf IF (0 (V TV Der1 Der/didh Ind Prs Sg2)) (*-1 Pron + Sg2 + Nom BARRIER VFIN);
REMOVE:Pl2_Not_Inf Inf IF (0 (V TV Ind Prt Pl2))(*-1 Pron + Pl2 + Nom BARRIER VFIN);

# Even stronger. No pro-drop. ## WARNING! This is probably too strong.
REMOVE:Inf_Not_Sg2_ProDrop (V TV Der1 Der/didh Ind Prs Sg2) IF (NOT *-1 Pron + Sg2 + Nom);
REMOVE:Inf_Not_Pl2_ProDrop (V TV Ind Prt Pl2) IF (NOT *-1 Pron + Pl2 + Nom);



    
    
            AFTER-SECTIONS              #
# ======================================== #
   
#SUBSTITUTE (A) (A <sma>) WORD ;
#SUBSTITUTE (N) (N <sma>) WORD ;
#SUBSTITUTE (Adv) (Adv <sma>) WORD ;
#SUBSTITUTE (V) (V <sma>) WORD ;
#SUBSTITUTE (Num) (Num <sma>) WORD ;
#SUBSTITUTE (Interj) (Interj <sma>) WORD ;
#SUBSTITUTE (Po) (Po <sma>) WORD ;
#SUBSTITUTE (Pr) (Pr <sma>) WORD ;
#SUBSTITUTE (Pron) (Pron <sma>) WORD ;
#SUBSTITUTE (CC) (CC <sma>) WORD ;
#SUBSTITUTE (CS) (CS <sma>) WORD ;
#SUBSTITUTE (Pcle) (Pcle <sma>) WORD ;


